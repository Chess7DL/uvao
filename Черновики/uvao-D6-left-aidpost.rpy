# Д6-левая ветка - разборки в медпункте с Леной
#
# используется флаг Полной Информированности alt_uvao_D6_CS_vetrov
# Используется флаг Дежурства в столовой alt_day3_duty
#
label alt_day6_uvao_left_aidpost:
    scene bg ext_aidpost_sunset with fade
# на время отладки
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
# /на время отладки
    show mt angry panama pioneer at cleft with dissolve
    "К тому времени, как мы добрались до медпункта, я успел наконец отдышаться."
    "Выслушавшая мой сбивчивый рассказ Ольга, наоборот, раскраснелась и пыхтела как паровоз."
    "Взбежав на крыльцо, она рывком распахнула дверь и вбежала вовнутрь."
    play sound sfx_open_door_strong
    hide mt with dissolve
    cs "Вообще-то, принято стучать, когда..."
    "Донёсся до меня недовольный голос Виолы, но вожатая не дала ей закончить:"
    mt "Вообще-то, я с самого начала предупреждала тебя, что это добром не кончится! Скажи, предупреждала?!"
    stop ambience fadeout 1
    window hide
    scene int_aidpost_sunset with fade
    window show
    show cs doubt2 glasses_over at center
    show mt angry panama pioneer at left
    with dissolve
    play ambience ambience_medstation_inside_day fadein 2
    "Слегка притормозив, я вошёл следом за Ольгой и, прикрыв за собой дверь, деликатно прислонился к стене возле входа, стараясь не слишком отсвечивать."
    play sound sfx_close_door_1
#    play sound sfx_close_door_campus_1
    mt "Вот вам, пожалуйста, снова ЧП! Сначала один пионер попал под воздействие, а теперь ещё Тихонова!{w} Нет, ты скажи, я говорила?"
    "Виолетта окинула её холодным взглядом с ног до головы."
    show cs normal glasses with dissolve
    cs "Да, говорила. А я тебе тогда ещё посоветовала не путать обычный пионерский лагерь и полевой спецобъект."
    show cs normal3 with dissolve
    cs "У нас имеет место активная фаза эксперимента, ради которого всё это и затевалось, между прочим."
    cs "Произошли некоторые события, не предусмотренные первоначальным планом исследований, но ситуация находится под контролем, серьёзно пострадавших нет."
    "Ольга всплеснула руками с явным намерением что-то ответить, но Виола остановила её резким взмахом руки."
    show cs important with dissolve
    cs "А ЧП, моя дорогая, это когда вожатую с физруком застанут на пляже за э-э-э... действиями аморального характера. Вот тогда это, несомненно, ЧП на территории пионерского лагеря."
    show mt surprise panama pioneer with dissolve
    show cs normal3 with dissolve
    "Я удивлённо заморгал."
    th "Вот это номер! Но откуда она узнала? Получается, кто-то ещё, кроме Юли, их видел и Виоле заложил?" 
    "На слова Виолы вожатая ничего не ответила, только дёрнула головой, словно ей вдруг стал тесен воротник."
    show mt sad panama pioneer with dspr
    "Помолчав несколько секунд, она произнесла напряжённым голосом, ни на кого не глядя:"
    mt "Да, Виолетта, ты совершенно права. Извини пожалуйста."
    cs "Ничего страшного, нервное напряжение, я всё понимаю. Ты присядь."
    "Она махнула в сторону кушетки и Ольга послушно уселась на неё, всё так же ни на кого не глядя."
    show mt sad panama pioneer far at fleft with dissolve
    "Виола наконец обратила внимание на меня."
    show cs doubt3 with dspr
    cs "Очень хорошо, Семён, что ты ввёл Ольгу в курс дела. Это сэкономит нам кучу времени."
    "Возможно, мне показалось, но в её голосе прозвучала нотка ехидства."
    # какой-нибудь страшный грохот, hpunch и прочие спецэффекты
    play sound sfx_open_door_kick
    with hpunch
    show un shocked pioneer at fright
    show mt shocked panama pioneer far at fleft
    show cs fear2 far at cleft
    with dissolve
    "Дверь с грохотом распахнулась, чуть не пришибив меня, и в медпункт ворвалась Лена."
    show cs doubt2 far
    show un shocked pioneer at cright
    with dspr
    "Споткнувшись о порог, она едва не растянулась на полу, но каким-то чудом удержала равновесие, разом очутившись посреди комнаты."
    "Похоже, весь путь до медпункта, и путь немалый, она проделала бегом. Прижимая руки к груди и полусогнувшись, девушка выдохнула, задыхаясь:"
    un "Где... Где он?!"
    show mt surprise panama pioneer far
    show cs doubt3 far
    with dissolve
    "Виола и вскочившая на ноги вожатая уставились на неё в изумлении."
    mt "Лена, что случилось? О ком ты говоришь?"
    show un cry pioneer with dissolve
    un "Где... Семён?.. Что... с ним?"
    me "Э-э-э... Лена?"
    show un cry_smile pioneer with dissolve
    "Девочка обернулась и наконец заметила меня."
    show un cry_smile pioneer close with dissolve
    "Судорожно всхлипнув, она на нетвёрдых ногах сделала шаг ко мне. Я почувствовал, что невольно вжимаюсь спиной в стену."
    un "Ты... Ты не ранен?"
    "Окончательно сбитый с толку, я только и смог выдавить:"
    me "Н-нет...{w=0.8} Что случилось?"
    "Тут ноги у Лены подкосились, и она бы упала, наверное, если бы её не подхватила вожатая и не усадила слабо всхлипывающую девочку на кушетку. Та по-прежнему задыхалась, плечи её ходили ходуном."
    show un sad pioneer far at cleft
    show mt sad panama pioneer far at center
    show cs normal2 far at fleft
    with dissolve2
    "Виола подошла к стеклянному шкафчику с лекарствами. Покопавшись в нём немного, достала пузырёк с бело-синей этикеткой."
    "По комнате поплыл полузнакомый резковатый запах."
    "В каком-то оцепенении я следил, как капли падают на дно стеклянной мензурки, а медсестра неслышно шевелит губами, считая."
    "Досчитав до двадцати, она покосилась на Лену и добавила ещё с десяток капель, разбавила водой из графина и протянула девочке."
    cs "Вот, выпей."
    "Лена выставила перед собой руку:"
    un "Н-не... н-не надо."
    "Ольга, дурного слова не говоря, забрала мензурку и залпом проглотила содержимое, поморщившись. Сунув опустевший стаканчик Виоле в руки, она села на кушетку рядом с Леной и, полуобняв её, зашептала на ухо что-то успокаивающим голосом."
    show mt normal panama pioneer far
    show cs doubt2 far
    with dissolve
    cs "Однако!"
    "Только и выговорила Виола, озадаченно глядя на мензурку, а потом на меня."
    "Я наконец сумел отклеиться от стенки. Подойдя к кушетке, я наклонился к девочке."
    show mt normal panama pioneer at right
    show un sad pioneer close at center
    with dissolve
    me "Лена, послушай меня. Почему ты решила, будто со мной что-то случилось?"
    "Девочка подняла на меня совершенно круглые глаза."
    un "Мне Алиса сказала, что..."
    "Она всхлипнула. Обычно бледное лицо её шло красными пятнами."
    me "Лена, не волнуйся. Всё хорошо. Всё в порядке, я тебе точно говорю.{w} Так что тебе сказала Алиса?"
    un "Она... {w=0.8}Она сказала, что ты в медпункте, и что мне... {w=1.2}что мне скорее надо туда придти."
    un "Я подумала... {w=0.8}я подумала, что ты... {w=0.8}что с тобой..."
    show un cry pioneer close
    show cs normal3 far
    with dissolve
    "Вздрогнув всем телом, она снова судорожно всхлипнула и спрятала лицо в ладонях."
    show un cry pioneer
    with dissolve
    "Я выпрямился. Об остальном и так несложно было догадаться."
    "Она, конечно, не стала дальше слушать Алису, а всё бросила и помчалась сюда, в медпункт."
    th "Безусловно, женская логика штука сложная, но это уже явный перебор. Совершенно ясно, что с головой у неё неладно."
    dreamgirl "Или она отчаянно в тебя влюблена, что тоже, впрочем, вовсе не свидетельствует в пользу её душевного здоровья."
    "Виолетта, молча следившая всё это время за происходящим, подала наконец голос:"
    show cs important with dissolve
    cs "Семён, вот что - сбегай-ка ты в столовую и принеси нам всем поужинать."
    show cs normal3 with dissolve
    "Лена вскинулась было, но Виола заявила решительно:"
    show cs important with dissolve
    cs "Успокойся.{w=1.5} Никуда он не денется, и ничего с ним на территории лагеря не случится, это я тебе обещаю!"
    show cs normal3 with dissolve
    "Выудив откуда-то пару судков солидных размеров и сетчатую сумку-авоську, она всучила всё это мне и подтолкнула к выходу."
    cs "Давай-ка, пионер, быстренько. Одна нога здесь, а другая уже в столовой.{w} И да, по дороге можешь не торопиться!"
    "Старательно делая вид, что не замечаю отчаянного взгляда Лены, я кивнул и поспешил освободить помещение."
    hide cs
    hide un
    hide mt
    with dissolve
    #
    play ambience ambience_camp_center_day fadein 3
    scene bg ext_aidpost_sunset with fade
    "Оказавшись снаружи, я перевёл дух."
    th "{i}\"Быстренько и можешь не торопиться\"{/i}. Ясное дело, хочет пообщаться с Леной в спокойной обстановке."
    th "Не представляю себе, как они будут с ней объясняться!{w} {i}\"Знаешь, Лена, тебе всё показалось! Вовсе ты ни в кого не влюблена, а просто подверглась воздействию визуализации с хвостом. Не переживай, дело-то житейское!\"{/i}"
    th "Или попытаются свести всё к гормонам, возрасту и жаркой погоде?{w} {i}\"Но это всё ненастоящее, и скоро ты обязательно поправишься, а пока что надо быть умницей и взять себя в руки{/i}\"?"
    "Я попытался поставить себя на место бедной девочки и меня передёрнуло."
    dreamgirl "Если уж ты ей так сочувствуешь - вернись и прими участие в беседе!"
    th "Нет уж, спасибо. Виола с Ольгой специалисты - им и карты в руки.{w} Я и в мирной-то жизни обясниться с девушками толком не мог никогда, а тут..."
    "Я решительно повернулся спиной к двери."
    th "Но Олька! Как она это лекарство залихватски тяпнула!"
    "Я невольно хмыкнул было, но вдруг осёкся.{w} Вспомнил, почему запах показался мне знакомым."
    $ set_mode_nvl()
    "Поздний вечер, на улице темно."
    "Родители снова поругались."
    "Я, глупый мальчишка, ничего тогда толком понимал и не мог знать того, что глиняные ноги колосса уже подмыла вода, что Советскому Союзу осталось год или два до краха.{w} Да и мало кто из взрослых что-то понимал, я думаю."
    "Вот слово \"дефицит\" я знал. И что слипшаяся в бесформенный буроватый ком масса в бумажном кульке называется \"клюквенная пастила\" - тоже. А стоять в очереди лучше вместе с кем-нибудь, потому что - одно кило в одни руки."
    "И что если {i}добытая{/i} в двухчасовой очереди курица окажется {i}с душком{/i}, то можно её так приготовить, что ничего не будет заметно. Ну почти ничего."
    "Вот про то, что на нескольких сотках земли можно самим вырастить картошку, и потом практически одну только эту самую картошку и есть весь год - до следующего урожая - я тогда ещё не знал."
    "И ещё не знал, что скоро двухсотграммовый кусок сыра или пачка масла станут отличным подарком на день рожденья. А вот талоны на продукты тогда уже начали появляться, кажется. Серые, напечатанные на обёрточной бумаге."
    "Но вот про то, что купить что-то всё сложнее и сложнее, а родителям всё чаще задерживают зарплату - про это я знал."
    "Родители, когда ссорились, про это часто... говорили.{w} Наверное, поэтому они и ссорились всё чаще."
    nvl clear
    "Поздний вечер, на улице темно."
    "Родители снова поругались."
    "Отец хлопнул входной дверью и теперь, наверное, курит внизу у подъезда."
    "Мама, с покрасневшими глазами, всё ещё всхлипывая, слегка дрожащей рукой капает себе \"валокордин\" в маленький  дымчатого стекла стаканчик."
    "Сквозь узорный цветной плафон просвечивает лампочка, отбрасывая на стены причудливые тени. По кухне плывёт щекочущий ноздри запах.{w} Я тихонько сижу в уголке."
    nvl clear
    "Мне вдруг стало как-то не по себе."
    th "А я ведь как попал сюда, в этот лагерь, так ни разу про родителей и не вспомнил. Из автобуса тогда попытался до них дозвониться - и всё."
    dreamgirl "{i}Посему оставит человек отца своего и мать свою и прилепится к{/i}...{w=1.5} Слушай, а к кому бы тебе прилепиться? Несолидно как-то!"
    th "Ты мне ещё из Корана процитируй что-нибудь. Для солидности."
    # "Огрызнулся я."
    dreamgirl "Из Корана я не могу, ты же его не читал. А память-то у нас общая, к моему огромному сожалению."
    "Я промолчал. Можно было продолжить ругаться с Шизой, но это не могло изменить главного."
    "Я вдруг понял, что про \"оставит человек\" всё уже решено.{w} Точно так же, как было решено бессчётное количество раз до меня, и как будет решено после."
    $ set_mode_adv()
    play sound eat_horn fadein 2
    "Горн напомнил мне, что быстро или медленно, но за едой идти всё-таки придётся. Сунув наконец судки в авоську и постаравшись выкинуть из головы лишние мысли, я поплёлся в столовую."
    stop sound fadeout 2
    scene bg ext_square_sunset with fade
    dreamgirl "Кстати сказать, на дворе 89-й год, если нам не соврали. Вдруг и здешнему Союзу осталось пару лет существовать? Только ты обживёшься, а оно всё р-р-раз, и вдребезги пополам!{w} Что ты тогда делать будешь?"
    "Стесняясь собственного малодушия, я робко предположил:"
    th "Ну... Может быть, ещё и обойдётся как-нибудь? Мелких-то отличий здесь хватает - глядишь, и крупные найдутся?"
    if alt_uvao_D6_CS_vetrov: #ЛБ: может быть, и какой-то другой флаг поставить. Или убрать нерешительную вилку совсем
        th "Хотя... пожалуй, мне-то как раз легче будет выкрутиться, чем им. Это они здесь дети тепличного социализма, а я..."
        dreamgirl "...А ты матёрая акула дикого капитализма, ага!"
        th "Ничего, прорвёмся!"
        "Решительно отрезал я. Внутренний голос хмыкнул, но промолчал."
    else:
        "Внутренний голос скептически хмыкнул, но ничего не сказал."
    #
    scene bg ext_dining_hall_near_sunset with fade
    show dv sad pioneer with dissolve
    "На входе в столовую меня перехватила выскочившая откуда-то Алиса. Секунду поколебавшись, она спросила:"
    dv "С Леной... с Леной всё в порядке?"
    me "Ну... да, в общем."
    show dv shy pioneer with dissolve
    "TODO Алиса беспокоится о судьбе Лены - очень уж та резво убежала. ГГ успокаивает, как может"
    # TODO Алиса беспокоится о судьбе Лены - очень уж та резво убежала. ГГ успокаивает, как может
    # или не TODO... не беспокоится...
    #
    stop ambience fadeout 1
    hide dv with dissolve
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_sunset with fade
    "В столовой было людно, как и за обедом. Порадовавшись мимоходом, что хотя бы место за столами мне искать сейчас не придётся, я собрался было пробиться на раздачу без очереди, прикрываясь больничными судками, но потом вспомнил, что меня ждёт по возвращении в медпункт."
    "Торопиться разом расхотелось, и я смирно пристроился в хвост очереди за каким-то пионером из второго отряда.{w} Или третьего - кто их разберёт."
    # проверить, как у Автора в дежурстве в столовой отображается voice поварихи
    if alt_day3_duty:
        "Впрочем, через несколько минут я предстал перед знакомой уже по дежурству в столовой поварихой."
        "Увидев меня, она заулыбалась:"
        voice "А, помощничек! А чтой-то ты с судками? В медпункт, да? А поднос где? Сам-то есть не будешь разве?" #ЛБ: напоминаю на всякий случай, что данная гражданка имеет своеобразный говор
        "Услышав, что мне нужно отнести четыре порции ужина в медпункт, она кивнула."
        voice "Тебе, сынок, из какого бачка? Со вкусным или с невкусным?"
        "Посмотрев на мою упавшую челюсть, она гулко захохотала, заколыхавшись всем своим дородным телом."
        "Отсмеявшись и утирая слёзы, она наконец сжалилась надо мной:"
        voice "Тебе общий стол надо, или доктор диету какую велела?"
        me "Ага... общий, да."
        "На ужин оказались тефтели и какая-то сомнительного вида сероватая каша - не то перловая, не то овсяная, не разобрать. Щедро наполнив судки, повариха выудила откуда-то из-за спины чистые тарелки и вилки-ложки."
        voice "На, держи. У доктора тарелок-то нет, я знаю."
    else:
        "Впрочем, через несколько минут я уже предстал перед дородной тёткой в белом поварском костюме."
        "Услышав, что мне нужны четыре порции ужина в медпункт, она с подозрением окинула меня взглядом с ног до головы, но судки в моих руках, похоже, убедили её."
        "Недовольно осведомившись у меня, обычную мне еду надо, или диетическую какую-нибудь, она плюхнула в один судок какую-то сомнительного вида кашу - не то перловую, не то овсяную, не разобрать даже, а в другой - вполне симпатичные на вид тефтели."
        "Поколебавшись немного, повариха сунула мне несколько тарелок и вилки-ложки, проворчав:"
        voice "На вот... У доктора-то тарелок нет, я знаю."
    voice "Только смотри, до завтрака чтоб принёс тарелки назад! Вы завтра съедете отсюда, а посуду помыть ещё надо!"
    "Покорно кивнув, я сгрёб всё в авоську и двинулся на выход."
    stop ambience fadeout 1
    # сцена - медпункт снаружи вечером
label alt_day6_uvao_left_aidpost_battle:
# на время отладки
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
# /на время отладки
    scene bg ext_aidpost_sunset with fade
    play ambience ambience_camp_center_evening fadein 2
    "Как ни старался я оттянуть момент своего возвращения в медпункт, но чему быть, того не миновать.{w} Потоптавшись обречённо на крыльце, я наконец постучал и заглянул внутрь."
    stop ambience fadeout 1
    # scene int_aidpost_sunset with fade
    scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.2)) with fade
    # "1"
    # scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.1))
    # "2"
    # scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.1) * im.matrix.tint(1.0, 0.9, 0.9))
    # "3"
    # scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.2) * im.matrix.tint(1.0, 0.8, 0.8))
    # "4"
    # scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.3) * im.matrix.tint(1.0, 0.7, 0.7))
    # поиграться с освещением - как в scene expression Noir("bg int_dining_hall_people_sunset", brightness = -0.4, saturation = -0.4) at zentercenter
    show un shy pioneer at fleft
    show cs normal2 at cleft
    show mt sad pioneer at right
    with dissolve
    play ambience ambience_medstation_inside_day fadein 2
    cs "Заходи, Семён, заходи."
    "Обстановка в медпункте была, мягко говоря, напряжённой.{w} Лена сидела всё там же, на кушетке, с лицом белым, как алебастр."
    "Когда я вошёл, она на долю секунды подняла на меня глаза и тут же уставилась на руки, сцепленные на коленях."
    th "Похоже, мозги ей уже промыли, бедолаге. Как же ей не повезло, всё-таки..."
    "Напротив неё вполоборота расположилась на стуле Виолетта Церновна. Ольга примостилась на краешке стола с таким угрюмым выражением лица, словно ей сообщили о разжаловании в октябрята."
    "Пауза затягивалась. Кашлянув, я промямлил наконец:"
    me "Я здесь вот... ужин принёс."
    "В подтверждение своих слов я зачем-то продемонстрировал авоську с судками."
    cs "Что же, давайте ужинать."
    show cs normal3 at center
    show mt sad pioneer at fright
    with dissolve
    "Поднявшись и согнав со стола Ольгу, Виола принялась освобождать на нём место для судков и тарелок."
    "При виде каши Виола слегка скривилась, но всё-таки вылоложила понемногу на тарелки и всучила одну из них Лене, всё так же упорно не поднимающей головы."
    "Девочка покорно взяла тарелку и ложку, но не сделала ни малейшей попытки притронуться к еде, целиком сосредоточившись теперь, судя по всему, на разглядывании содержимого тарелки."
    show cs normal2
    with dissolve
    "Со стоическим выражением на лице Виола взяла свою порцию, жестом приглашая нас с Ольгой последовать её примеру и вновь уселась на стул."
    "Раздался звон. Обернувшись, я увидел, как Лена поспешно подобрала с пола ложку и вновь замерла неподвижно."
    dreamgirl "Упавший нож - к визиту мужчины, вилка - женщины. Интересно, а упавшая ложка к чему?"
    "Впрочем, даже обычно самоуверенный голос Шизы прозвучал в голове как-то натужно. Отвечать что-либо сил у меня не было."
    "Чувствуя, что при одном взгляде на еду меня начинает мутить, я распахнул дальнее окно, и уселся на подоконник, прижавшись затылком к стеклу открытой створки и отставив тарелку подальше от себя."
    hide un
    hide cs
    hide mt
    with dissolve
    scene expression im.MatrixColor(ImageReference("int_aidpost_sunset"), im.matrix.brightness(-0.3) * im.matrix.tint(1.0, 0.8, 0.8))
    with dissolve2
    "Большого облегчения, однако, это не принесло. Воздух в кабинете казался словно бы наэлектризованным - кажется ещё чуть-чуть, и с лёгким потрескиванием волосы встанут дыбом. Начинающиися сумерки ещё больше усиливали ощущение навалившейся тяжести, заставляющей сутулить плечи и не дающей вздохнуть."
    "Пробормотав что-то Виолетта встала и щёлкнула выключателем."
    stop ambience fadeout 1
    scene bg int_aidpost_night with dissolve
    # play ambience ambience_camp_center_night fadein 1
    play ambience ambience_medstation_inside_night fadein 1
    show un shy pioneer far at fleft
    show cs normal2 far at cleft
    show mt sad pioneer far at cright
    with dissolve
    # возможно, подыскать гул люминесцентного светильника в фон.
    "Под потолком с показавшимися мне оглушительными гулкими ударами и жужжанием ожили светильники и залили медпункт ярким белым светом. Сумрак на улице тут же словно бы сгустился, и разглядеть что-либо за оконными стёклами стало невозможно."
    th "Весело, как в покойницкой."
    "Подумал я, наблюдая за Ольгой, которая меланхолически помешивала кашу в своей тарелке, не делая, впрочем, никаких попыток её попробовать. Аппетита у неё явно было не больше, чем у меня."
    $ meet('uv','Голос')
    # возможно, здесь страшный звук. или нет.
    play sound sfx_scary_sting
    hide un
    hide cs
    hide mt
    with vpunch
    uv "Вот ты где!"
    show uv upset close at fright with dspr
    "Чуть не свалившись от неожиданности с подоконника, я обернулся. С улицы на меня практически в упор смотрела Юля. Как она подошла я не слышал - впрочем, чего ещё ожидать от кошки?"
    $ meet('uv','Юля')
    uv "Я тебя везде ищу..."
    "Начала было она недовольно и вдруг замолчала, принюхиваясь."
    show uv normal close with dissolve
    uv "А что это у тебя?"
    "При виде тарелки глаза её восхищённо раскрылись."
    show uv laugh close with dissolve
    uv "Котлета?!"
    "Через мгновенье лежавший там одинокий тефтель исчез у неё во рту."
    uv "М-м-м! А у тебя там ещё есть котлеты, я чую!"
    "Прежде, чем я успел сказать хотя бы слово, она легко перемахнула через подоконник и спрыгнула на пол медпункта."
    # show mt, un, cs surprise (или shocked) at left
    show un shocked pioneer far at fleft
    show cs fear2 far at left
    show mt surprise pioneer far at cleft
    show uv shocked at fright
    with dissolve
    "Для Юли наличие у меня компании явно стало полнейшей неожиданностью. Широко раскрыв шлаза, она замерла на месте."
    "Впрочем, Виолетта с вожатой и Леной тоже не были готовы к появлению перед ними чуда с кошачьими ушами и хвостом. Остолбенев, они во все глаза таращились на Юлю."
    "Несколько секунд наступившую тишину нарушали только гудение ламп под потолком и доносящийся из окна отдалённый шум лагеря."
    "Однако сладкие запахи манили, а моё присутствие придало Юле смелости. Прижав уши и косясь на меня, она, тем не менее, бочком-бочком двинулась по направлению к столу."
    # mt scared, cs normal
    show uv upset at right
    show cs normal3 far
    show mt scared pioneer far #надо поклянчить у Автора оригинал спрайта mt feared и отмасштабировать под far
    with dissolve
    "Тихо ойкнув, Ольга осела на кушетку рядом с Леной и почему-то поджала под себя ноги, словно по полу вдруг пробежала мышь."
    "Виолетта медленно поднялась со стула и так же медленно сделала шаг назад, загораживая собой Лену. Та не двинулась с места, лишь выпрямилась и судорожно вдохнула воздух, всё так же завороженно глядя на Юлю круглыми глазами." #Кажется, она даже забыла на время о своих переживаниях."
    "Подобравшись к столу, Юля ещё раз оглянулась на меня и протянула руку к судку с тефтелями{w}... Но замерла внезапно, пристально глядя Виолетте в лицо."
    "Пару секунд они смотрели друг на друга.{w=2} Потом Юля вдруг кивнула, что-то пробормотала и, осторожно подтянув к себе стул, уселась за столом подальше от людей."
    show uv normal with dissolve
    "Посмотрев ещё раз на Виолу, она неуверенно взяла со стола вилку и, зажав её в кулаке, неловко попыталась подцепить ей тефтель.{w} Со второго или третьего раза ей это удалось."
    "Открыв рот, я смотрел, как ушастая уплетает тефтели, периодически поглядывая на Виолу - Ольгу с Леной она игнорировала напрочь. В какой-то момент я заметил, что Юля уже не зажимает вилку в кулаке, а довольно уверенно держит её \"по-культурному\"."
    th "Обалдеть можно... Ещё пять минут, и она салфетку потребует, не иначе!"
    "На Виолетту было любо-дорого посмотреть. Лицо её сейчас выражало безмятежное спокойствие, и лишь цепкий взгляд слегка прищуренных глаз подсказал мне, что она {i}работает{/i}, пристально наблюдая за происходящим, не упуская ничего, ни малейшей детали."
    "Наоборот, одного взгляда на Ольгу хватило, чтобы понять - она в любую секунду может грохнуться в обморок. {w}Чувствуя, что Виолетта не собирается пока вмешиваться в происходящее, я решил хоть как-нибудь разрядить обстановку и осторожно прочистил горло:"
    me "Кхм... Вот... Познакомьтесь, пожалуйста, это Юля."
    "Все четверо мельком взглянули на меня. Потом Юля вернулась к тефтелям, а остальные вновь уставились на неё."
    "Чувствуя себя полным идиотом, я, тем не менее, попытался закончить церемонию знакомства:"
    me "Юля, это Виолетта Церновна, Ольга..."
    uv "Ням знавю!"
    "Перебила меня Юля, не переставая жевать."
    uv "Я всех вдесь зням."
    "Прикончив тефтели, она брезгливо потыкала вилкой в кашу на ближайшей тарелке и со вздохом облизнулась."
    show uv smile with dissolve
    uv "Жалко, что котлеты кончились. Вкусные!"
    "Посмотрев ещё раз Виолу, она поёрзала на стуле и вдруг добавила:"
    show uv normal with dissolve
    uv "Спасибо!"
    show cs doubt3 far with dspr
    cs "Пожалуйста."
    "Ответила Виолетта так непринуждённо, словно к ней каждый день на ужин заходят девочки с кошачьим хвостом."
    $ volume(0.25, 'music')
    play music music_list["lightness_radio_bus"] fadein 1
    show cs normal3 far with dspr
    "С улицы вдруг грянула музыка."
    show uv laugh with dissolve
    th "Ну да, дискотека же, наверное! Ужин-то давно уже закончился."
    show uv laugh close at fright with dissolve
    "Юля вскочила со стула, заставив всех вздрогнуть и подбежала ко мне."
    uv "Ой, Семён, там опять музыка! Там опять коне... концерт, да?"
    me "Нет, это дискотека. Ну, танцы, знаешь?"
    uv "Знаю! Пойдём скорее, я хочу послушать! И посмотреть!"
    show uv upset close with dissolve
    uv "А то здесь все такие грустные! Мне не нравится! Совсем-совсем не нравится!"
    "Заявила она вдруг обвиняющим тоном, обернувшись к остальным.{w} Схватив за руку, она потащила меня к окну."
    show uv laugh close at fright with dissolve
    uv "Пойдём же, ну!"
    show un normal pioneer
    show cs normal3 far behind un
    with dissolve
    un "Я тебя знаю!"
    "Тихо сказала вдруг Лена, всё это время сидевшая словно в каком-то ступоре. Она встала и сделала шаг вперёд."
    stop music fadeout 7
    show un serious pioneer
    show cs doubt2 far
    show mt surprise pioneer far behind un
    show uv surprise close
    with dissolve
    un "Я тебя видела там, на площади...{w} Нет, я не видела, но ты там была, я помню! Вечером, когда Семён ко мне подошёл!"
    $ volume(0.7, 'music')
    play music music_list["revenga"] fadein 10
    stop ambience fadeout 10
    show un angry2 pioneer with dissolve
    "Лицо Лены вдруг пошло красными пятнами. Уже громче она продолжила:"
    un "Я сидела, читала книжку, а он подошёл и заговорил со мной, а потом... потом..."
#    "Виолетта попыталась было удержать девушку, но та резко дёрнула плечами, сбрасывая её руки." #ЛБ: как бы это по-людски выразить... 
    #"Виолетта сделала было попытку удержать девушку, но та резко рванулась вперёд, не позволяя(ив) медсестре этого сделать." Ленофаг
    "Виолетта попыталась было удержать девушку за плечо, но та резким движением сбросила её руку." 
    show un angry pioneer with dissolve
    "Лицо Лены вдруг исказила гримаса гнева, и она хрипло закричала, уже не сдерживаясь:"
    un "Это ты со мной сделала! Ты! Я знаю!!!"
    show uv dontlike
    show un rage pioneer at center
    with dissolve
    with hpunch
    play sound sfx_broken_dish
    show cs fear2 far
    show mt scared pioneer far
    with dissolve
    "Она резко шагнула к Юле, сжимая кулачки. Вдребезги разлетелась чья-то тарелка, мимоходом сброшенная на пол - девушка даже не заметила этого."
    $ renpy.music.set_volume(1.0, delay=2, channel='music')
    un "Это всё ты!!!"
    show uv rage at fright with dissolve
    cs "Лена, стой!"
    menu:
        "Не мешать Виоле.": # катапульта.
            # текст на весь экран
            $ set_mode_nvl()
            hide cs
            hide mt
            hide uv
            hide un
            with dissolve
            "Не в силах больше выносить это безумие, я отвернулся и закрыл глаза, с силой прижавшись лбом к оконному стеклу."
            cs "Лена, стой! Ольга, да помоги же мне!"
            un "Пустите! Пустите меня!!!"
            cs "Да помоги же!!!"
            uv "Сёмён, она не пугается! Никак не получается!"
            un "Это всё ты!!!"
            uv "Семён! Семён, я боюсь!"
            "Кто-то - наверное, Юля - принялся трясти меня за плечо, но я лишь крепче прижался к стеклу."
            th "Как же я устал от всего этого! Не могу больше!"
            "На меня вдруг дунуло свежим ветром и я с наслаждением подставил ему лицо, чувствуя, как он уносит с собой и яростные крики Лены, и Юлино нытьё...{w} Ухватившиеся за моё плечо настырные пальцы разжались, и я полностью отдался всё крепчающему бризу."
            stop music fadeout 2
            # Может быть, согласующая связка с нейтралом
            # jump в нейтрал "сон Сыча".
            jump scenario_uvao_debug # заглушка - выход в меню отладки
        "Вмешаться.":
            pass
    "Плохо понимая, что делаю, я шагнул навстречу девушке{w}... и обхватил её руками, крепко прижимая к себе."
    hide cs
    hide mt
    hide uv
    show un angry pioneer close at center
    with dissolve
    dreamgirl "Эй, ты что творишь, псих!"
    show un shocked pioneer close with dissolve
    me "Тише, Лена, тише!"
    "Она забилась в моих руках, словно вытащенная из воды рыба."
    un "П-пусти.{w=0.8} П-пусти!{w=0.8} Отпусти меня, с-слышишь!"
    "Последние слова она выкрикнула она мне в лицо и вдруг, разом перестав вырываться, обняла, прижавшись всем телом, дрожа и всхлипывая:"
    stop music fadeout 5
    show un cry pioneer close with dissolve
    un "Отпусти-и, слышишь... Пож-жалуйста, отпусти."
    $ set_mode_nvl()
    play music music_list["torture"] fadein 5 # ЛБ: или ещё какую душещипательную музыку. Как будто кошки поют.
    dreamgirl "Ага, молодец. Зафиксировал.{w} И что ты дальше будешь делать, интересно?"
    "Краем глаза я видел, как рядом молча и деловито двигается Виола. Блеснул металл в свете лампы, что-то звякнуло, пару раз хрустнуло стекло вскрываемых ампул."
    "Всё, что мне оставалось, это выиграть немного времени, ещё крепче прижимая к себе вздрагивающую девочку, чуть поглаживая её по спине и шепча негромко на ухо:"
    me "Тише, хорошая, тише... Всё в порядке. Всё уладится, тише."
    nvl clear
    "Запахло спиртом и раздался негромкий голос Виолы:"
    cs "Лена, девочка, дай мне руку, пожалуйста."
    "Лена взрогнула, и ещё крепче прижалась ко мне."
    th "Так, сейчас одними губами, на выдохе, тихо-тихо - чтобы не спугнуть, в самое ушко:"
    me "Лена, хорошая моя, пожалуйста...{w=1.5} Я тебя очень прошу...{w=1.5} Доверься мне."
    "Помедлив секунду, она не глядя протянула Виоле левую руку."
    cs "Хорошо.{w=1.0} Семён... покрепче."
    "Снова краем глаза я увидел, как блеснул шприц. Не делая ни малейшей попытки вырваться, Лена лишь до боли притянула меня к себе свободной рукой, словно пытаясь вжать своё тело в моё, и уткнулась лицом мне в плечо."
    cs "Потерпи, девочка, сейчас всё пройдёт, сейчас."
    "Мне показалось, что прошла целая вечность, прежде чем звякнул пустой стеклянный шприц, и я услышал:"
    cs "Ну вот и всё, девочка. Теперь всё будет хорошо, две минуты потерпеть ещё - и всё."
    nvl clear
    "Вновь обняв меня обеими руками, Лена едва заметно кивнула. Она больше не всхлипывала, но я чувствовал её неровное прерывистое дыхание."
    "Только дыхание и выдавало, что она всё ещё плачет. Дыхание, и то, что рубашка у меня на плече была мокрой и горячей."
    "Я стоял, не смея шевельнуться, изо всех сил стараясь ни о чём не думать."
    "Стоял, и чувствовал омерзение {w=1.2}- я до смерти сейчас был омерзителен себе.{w} Но дело нужно было довести до конца, и я стоял, обнимая и тихонько поглаживая."
    "..."
    "Виола не обманула - через пару минут девушка задышала ровнее и глубже, бившая её дрожь ослабла."
    $ set_mode_adv()
    show un cry pioneer close with dissolve
    "Сковывавшие меня объятья ослабли и разжались.{w} Она с трудом подняла голову. Черты её лица словно бы сгладились, глаза были полузакрыты."
    un "Семён..."
    hide un with dissolve
    "Прошептала она непослушными губами и вновь уронила голову мне на плечо. Тело в моих руках безвольно обмякло, став вдруг пугающе тяжёлым."
    show cs normal3 at cleft
    show mt sad pioneer far at fleft
    with dissolve
    cs "Семён, сюда её. Сможешь?"
    "Оглядевшись, я обнаружил, что Виолетта уже успела застелить кушетку - ту же самую, куда вчера вечером мы с Элом сгружали пленного Шурика. Молча подхватив Лену на руки, я перенёс её на несколько шагов, хрустя попадающимися под ноги осколками тарелки, и как мог бережно уложил на простыню."
    show cs normal3 far at cleft with dissolve
    "Пока Виола хлопотала над девушкой, ослабляя ворот рубашки, проверяя пульс, зрачки и укрывая её, я вспомнил, как вчера мы с Шизой переругивались - мол, намного приятнее было бы нести на руках Лену, а не Шурика."
    dreamgirl "Наверное, сейчас стоило бы сострить что-нибудь на тему моей правоты, но что-то настроения нет, так что извини."
    stop music fadeout 10
    "Я знал, что всё, что сделано - сделано правильно, так было нужно{w}, но на душе было погано, словно я только что совершил какой-то бессмысленно жестокий, и оттого ещё более мерзкий поступок."
    play ambience ambience_medstation_inside_night fadein 10
    $ volume(0.25, 'music')
    play music lonesome_shepherd fadein 10
    show uv normal far at fright
    show mt scared pioneer far
    with dissolve
    uv "Мне так больше нравится. Когда она спит."
    "Юля сидела на подоконнике, болтая ногами. За своими переживаниями я успел уже забыть, что кроме нас с Леной и Виолой здесь есть кто-то ещё."
    show uv guilty far with dissolve
    uv "Я тебя, Семён, поэтому и искала.{w} Она очень грустная была, мне не нравилось. Больше, чем когда другие грустят."
    show cs normal far with dissolve
    cs "А почему тебе не нравилось, что она грустит?"
    "Подала внезапно голос Виола. Секунду поколебавшись, словно раздумывая, стоит ли вообще с ней говорить, Юля всё-таки проворчала:"
    uv "Так это же плохо, когда грустят!{w} Когда ушибётся кто-нибудь, или поранится - ничего. А когда кто-то сильно-сильно расстраивается - это плохо. Неправильно.{w=1.0} Мне не нравится."
    cs "А почему ты сказала, что тебе не нравится, что Лена грустит, больше, чем когда другие грустят?"
    show uv upset far with dissolve
    "Юля досадливо махнула ушами."
    uv "Я не знаю. Она не такая, как остальные.{w} Пусть она больше не грустит так сильно, мне не нравится!"
    show cs normal3 far with dissolve
    "Решительно повернувшись к Виоле спиной, она заявила мне:"
    show uv smile far with dissolve
    uv "Я с вами совсем про дикотеку... дискотеку забыла!{w} Пойду посмотрю!"
    hide uv with easeoutright
    "С этими словами Юля ловко вскочила на подоконник и через мгновение исчезла в темноте."
    show mt normal pioneer far
    show cs normal2 far
    with dissolve
    "Виолетта опустилась на стул и устало провела рукой по лицу. Похоже, ей последние пятнадцать минут тоже дались нелегко."
    cs "Семён, ты как...{w=0.8} Как себя чувствуешь?"
    "Я послушно прислушался к своим ощущениям."
    "Кружилась голова, а комната никак не могла определиться со своими размерами - стены всё время то приближались, то отдалялись, из-за чего меня слегка подташнивало. Отчётливо ныл правый висок." # ЛБ: Ещё какие-нибудь жалостливые факты из жизни тела ГГ. Или нет. #Ленофаг: Плечо, куда Лена упиралась головой, затекло и теперь постреливало ледяными иголочками
    "Подведя итог, я честно признался:"
    me "Неплохо, бывало и хуже."
    "Виола кивнула в ответ:"
    cs "Вот и славно, я была уверена в твоих силах... {i}пионер{/i}."
    cs "В общем-то на сегодня это всё. Шёл бы ты отдыхать, наверное. Хлопотный день выдался."
    "Прежде чем я успел оценить все плюсы этого предложения, Виола добавила:"
    cs "Сходи на дискотеку, что ли, развейся.{w} Заодно присмотришь, чтобы там всё спокойно прошло, мало ли что Юле вздумается..."
    th "Вот же ведь!.."
    show cs normal far with dissolve
    cs "...А мне здесь пока Оля поможет прибраться.{w} {i}Да, Оля{/i}?"
    show cs normal3 far
    show mt sad pioneer far
    with dissolve
    "Последние слова Виолетта произнесла таким тоном, что вожатая, после ухода Юли разом успокоившаяся и расслабленно сидевшая теперь на свободной кушетке, вскочила и отчаянно покраснела."
    mt "Да-да, Виолетта! Конечно!"
    dreamgirl "Ну что, ответственный ты наш, на тебя вся надежда! Похоже, что теперь без тебя они, как без рук."
    "Подавив рвущиеся на свободу неучтивые слова, я молча кивнул и потопал выполнять."
    hide mt
    hide cs
    with dissolve
    "Я уже открыл дверь, когда вслед мне донеслось:"
    cs "Семён... Спасибо тебе большое.{w} Ты мне очень помог сегодня."
    cs "И сделай одолжение, воздержись от лишних угрызений совести.{w} Ты всё сделал правильно. Иногда приходится делать людям больно, чтобы потом им жилось хорошо."
    "Не оборачиваясь, я шагнул в темноту улицы."
    stop ambience fadeout 1
    $ renpy.music.set_volume(1.0, delay=2, channel='music')
    scene bg ext_aidpost_night with fade
    play sound sfx_close_door_1
    play ambience ambience_camp_center_evening fadein 3
    dreamgirl "Как вовремя."
    th "Что, прости?"
    dreamgirl "Я говорю, как вовремя она тебя поблагодарила. Ни секундой раньше, ни секундой позже.{w} Да ещё такие хорошие напутственные слова сказала."
    th "Вечно ты всё опошлишь! Теперь буду думать, искренне это она сказала, или выполняла профессиональный долг.{w} Впрочем, эти её \"напутственные слова\" мне и впрямь что-то напоминают. Где я мог это слышать?"
    dreamgirl "Слышал-слышал, я тебе точно говорю. Сам вспоминай - это тебе не со смарта гуглить." # ЛБ: Фильм "Ирония судьбы, или С лёгким паром!" вышел в 1975 году и к концу 80-х гарантированно известен всему населению страны. 
    "Искренние или нет, но слова Виолы всё равно чем-то меня зацепили." #ЛБ: префразировать. # Ленофаг "Пусть я и не был до конца уверен в искренности слов Виолы, они меня всё равно чем-то зацепили."
    th "В конце концов, что ещё я мог сделать? Отвлечь Лену простым невинным вопросом?{w} Оглушить судном по голове?{w} Начать жонглировать тарелками?"
    dreamgirl "Ладно, ладно, уговорил! Тем более, что и совестью-то я работаю только на полставки."
    dreamgirl "Лучше скажи - ты что, и в самом деле собираешься сейчас идти на площадь, ногами дрыгать?"
    # Здесь и далее вспоминаем позорную сыч-дискотеку в Д3. Проводим параллели, подчёркивающие склонность нынешнего ГГ к социализации.
    th "Ну... Пойду, наверное. Поприсутствую на всякий случай - вдруг, и в самом деле, Юля заявится на радость пионерам."
    "Да и вообще... развеюсь немного. Глядишь и втянусь..."
    dreamgirl "Главное, не забудь в какой стороне туалет - куда бежать, если тебя вдруг пригласят потанцевать."
    "Тут я попытался вспомнить, сколько раз за день обещал себе больше не краснеть."
    th "Ладно уж тебе... Кто старое помянет... {w=1.2} Того из головы вон!"
    th "Тем более, если уж я собираюсь здесь осесть, то надо втягиваться в местный социум. Иначе какой смысл менять шило на мыло?{w} Хикканом был - хикканом и останусь? Дудки!"
    dreamgirl "Подожди, так ты серьёзно решил..."
    th "Слушай, и какая же после этого ты к чёрту интуция?"
    "Внутренний голос обиженно фыркнул и замолчал."
    "Невольно усмехнувшись, я одёрнул на себе пионерскую форму и, пока не растаяла моя решимость, решительно направился к площади."

    stop music
    stop ambience
    jump scenario_uvao_debug # заглушка - выход в меню отладки
