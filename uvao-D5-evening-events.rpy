label alt_day5_uvao_ev_beach_event:
#
# Д5-вечер - разговариваем на пляже со Славей
#
# Используется флаг alt_uvao_true
# устанавливает флаг встречи со Славей alt_uvao_D5_evening_sl
#    
    scene bg ext_dining_hall_away_sunset with fade
    "После такого напряжённого дня мысль искупаться казалась весьма здравой."
label alt_day5_uvao_ev_beach_event_direct:
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    stop sound_loop fadeout 3
    scene bg ext_beach_sunset with dissolve
    "Я не спеша дошёл до вотчины Ольги Дмитриевны - хвала рандому, сейчас её тут не было, и некому было устроить мне внеочередную выволочку за… да за что угодно!"
    "Я нашёл подходящий пятачок земли и уже начал развязывать осточертевший галстук, как вдруг заметил кого-то в дальнем углу пляжа.{w} Знакомый силуэт… Славя?"
    play music music_list["trapped_in_dreams"] fadein 5
    "Она сидела, уткнувшись лицом в колени, косы безвольно стелились по земле."
    show sl sad pioneer far at fleft with dissolve
    "Такой Славю я еще не видел. Она что, плачет?"
    # обрывки фраз - что-нибудь про "последний день" "сплошная выдумка"?
    dreamgirl "И ты, как рыцарь на белом коне, должен немедленно утешить печальную деву. Преисполненная благодарности, она падёт тебе на широкую грудь и доверит все свои тайны."
    th "А почему бы и нет? Не проходить же мимо. Я от неё ничего, кроме хорошего, не видел, может, смогу помочь чем-то?"
    show sl sad pioneer far at cleft with dissolve
    "Чувствуя себя донельзя неловко, я направился к ней, старательно шурша ногами по песку - чтобы она не подумала, будто я подкрадываюсь."
    show sl sad pioneer at center with dissolve
    me "Эм… Славя?"
    sl "Семён. Что тебе?"
    "Она так и не подняла голову, голос её звучал глухо и неприветливо."
    me "Да в общем-то ничего… Я просто искупаться собирался…"
    sl "Одному нельзя. Не положено. Надо, чтоб кто-то из вожатых присматривал."
    me "Вот как… Жаль…"
#    hide sl with dissolve
#    "Я сел рядом. Не зная чем занять руки, я сорвал какую-то травинку и стал обрывать с неё семена. Славя так и сидела спиной ко мне, не шевелясь."
    show sl sad pioneer close with dissolve
    "Я сел рядом. Не зная чем занять руки, я сорвал какую-то травинку и стал обрывать с неё семена. Славя так и сидела, не поднимая лица и не шевелясь." # Всё-таки если совсем уж спиной к нам - непонятно, как быть со спрайтами. Лучше не акцентировать
    dreamgirl "Кажется, с утешением и падением на грудь возникли проблемы. Может быть, она сценарий не читала?"
    me "Славя… У тебя все в порядке?"
    sl "Нет. Но это неважно."
    "Что-то мне подсказывает, что я тут нежеланный гость."
    me "Ты извини, что я навязываюсь, но… Может, я смогу помочь?"
    sl "Вряд ли…"
    "Да, так и есть. Наверное, сейчас стоит встать и уйти… Да, вот сейчас… Сейчас встану, извинюсь и пойду… "
    sl "Помнишь день, когда ты приехал, Семен?"
#    "Она так и сидела, спиной ко мне."
    "Славя произнесла это, по-прежнему не поднимая головы."
    sl "Ты еще спрашивал, какой сейчас год."
    me "Да, а ты так и не ответила."
    sl "Тысяча девятьсот восемьдесят девятый. Сегодня двадцать второе июля." # кажется такая дата в лена-руте
    me "Спасибо, но… Почему ты вдруг это вспомнила?"
    "Славя подняла наконец голову и провела ладонями по лицу, стирая слезы… "
    th "Действительно плакала!"
    show sl normal pioneer close at center with dissolve
    "…потом повернулась ко мне, нацепив своё обычное оптимистичное выражение лица."
    sl "Да так, ерунда.{w} Странное место этот лагерь, правда ведь?"
    th "Да не то слово!"
    me "Странное? О чем ты?"
    sl "Здесь кажется, будто возможно всё.{w} Будто бы тут можно исполнить любую мечту - или, по крайней мере, сделать первый шаг к ней…{w} Так ведь?"
    "Вопрос застал меня врасплох. Впрочем, с момента появления здесь я и впрямь чувствовал себя так, будто получил второй шанс."
    me "Ну… наверное. Да."
    sl "А чего бы ты хотел? Какую мечту исполнить?"
    th "Разобраться во всех этих тайнах, это уж точно. Докопаться до сути происходящего!"
    me "Так сразу сложно сказать…{w} Не знаю. Стать примерным пионером?"
    show sl smile pioneer close at center with dissolve
    "Она фыркнула."
    sl "Это у Ольги Дмитриевны такая мечта - чтобы все стали примерными пионерами. {w} Ну не хочешь говорить - как хочешь."
    show sl normal pioneer close at center with dissolve
    sl "Просто интересно стало…{w} зачем ты сюда приехал."
    th "Как будто меня кто-то спросил…"
    me "Не знаю… Чем занимаются в лагере?{w} Приезжают, отдыхают, заводят друзей, набираются впечатлений."
    sl "И уезжают."
    me "Ну… да. А как иначе?"
    sl "Значит, еще два дня - и все закончится?"
    me "Два дня?! Почему?"
    sl "Так смена же заканчивается послезавтра. Завтра вечером итоговый концерт и прощальная дискотека. Ты что, забыл?"
    th "Да я и не знал никогда… Два дня! А я так ничего и не узнал!"
    me "Да, как-то вылетело из головы. Очень уж быстро смена закончилась…"
    sl "А ты еще позже всех приехал. Ничего не успел, наверное?"
    "Разговор уходил куда-то в непонятные дебри. Славя, похоже, преследовала своими туманными расспросами какую-то цель, но вот какую?"
    me "Ну почему… Кое-что успел."
    "Впрочем, выкладывать ей все начистоту я не собирался."    
    if alt_uvao_true:
        extend " Достаточно того, что Виола в курсе, что я \"не от мира сего\"."
    me "Ладно, пойду я, пожалуй. Доброй ночи!"
    show sl sad pioneer at center with dissolve
    sl "Доброй…"
    show sl sad pioneer far at cleft with dissolve
    "Уходя с пляжа, я оглянулся. Славя так и сидела неподвижно у кромки воды."
    th "Странная она сегодня какая-то…"
    stop ambience fadeout 3
    "Не-е-ет, все, хватит на сегодня тайн. Пора в кроватку, утро вечера мудренее."
    $ alt_uvao_D5_evening_sl = True
    play ambience ambience_camp_center_evening fadein 3
    jump alt_day5_uvao_evening_headlong