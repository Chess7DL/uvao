# Д6 разговор с ОД после обеда.
# Черновик
#
# Используется флаг alt_uvao_D6_sl_pickup
# Используется флаг alt_uvao_D5_hentai
# Используется флаг alt_uvao_true
# Используется флаг alt_day2_beach_done
# Используется флаг alt_day2_phys_done
#

label alt_day6_uvao_mt_day_talk:
    scene bg ext_house_of_mt_day 
    "Подойдя наконец к своему жилищу, я зачем-то остановился на пороге в нерешительности."
    if alt_uvao_D6_sl_pickup:
        th "Всё-таки не хотелось бы наткнуться на Ольгу."
        if alt_uvao_D5_hentai:
            extend " Хамить ей вчера было совсем необязательно."
        else:
            extend " Не люблю я все эти лишние вопросы."
        th "С другой стороны, узнаю хотя бы, работают ли душевые."
        if alt_uvao_true:
            "Только по пути сюда я понял, что за шесть дней так и не сподобился нормально помыться: тело было отвратительно липким, а о сальных волосах даже думать не хотелось."
            "Вроде бы, я и был на пляже перед самым обедом, но от купания остались одни воспоминания."
            th "Всё-таки, река плохая замена нормальному душу. Да и без мыла…"
        else:
            "Только по пути сюда я понял, что за шесть дней так и не сподобился нормально помыться: тело было отвратительно липким, на ногах кое-где была засохшая грязь после вчерашнего похода к Юле, а о жирных волосах даже вспоминать не стоило."
            th "За все дни даже в реке не искупался."
        th "Удивительно, что Славя на меня вообще внимание обращает."
        th "Голову я, конечно, помыть не успею, но лучше хоть что-то…"
        dreamgirl "А со всем остальным что делать будешь, чумазоид? Да и рубашка сама собой от травы и бункерной пыли не очистится."
        th "Что есть - то есть. Славю искать ради нового комплекта формы не буду."
    else:
        "С одной стороны, от Слави я до сих пор ничего, кроме добра, не видел, так что кляузничать на неё вожатой было как-то даже не по-человечески."
        dreamgirl "Ку-ку. Ты разве уже забыл, что она в столовой вытворяла?"
        th "Да нет, но…"
        dreamgirl "Никаких «но»! Сейчас она тебя соблазнять взялась, а что потом ей в голову ударит, можешь сказать?"
        dreamgirl "Мне мёртвый носитель не нужен."
        th "А вдруг она и в самом деле того… испытывает ко мне искренний интерес. Как к парню. {w}Я же полным идиотом буду тогда выглядеть."
        dreamgirl "Неподдельный, говоришь? Ты в зеркале-то давно себя видел?"
        "Наверное смотреть на меня со стороны сейчас было забавно: красный как рак, с озадаченным лицом и нервно бегающими глазами пионер в умеренно драных шортах и перепачканной травой рубашке."
        "На секунду я представил себе эту картину, и последние сомнения отпали."
        th "Всё-таки Славе я не для дел амурных нужен."
        th "Лучше и правда расскажу всё Ольге - может быть, сама со Славей как-нибудь вопрос решит?"
    mt "Семён, хватит уже топтаться на пороге. Заходи!"
    "Донёсся изнутри голос Ольги, и мне ничего не оставалось, как наконец войти."
    window hide
    play sound sfx_open_dooor_campus_2
    pause(1)
    scene bg int_house_of_mt_day
    show mt smile pioneer at cleft
    with dissolve
    "Вожатая сидела за столом и аккуратно записывала что-то в свой ежедневник."
    "На столе перед ней я заметил крохотный фарфоровый чайник с олимпийской символикой. Тут же рядом лежал и кипятильник."
    play music lazy_olga fadein 3
    mt "Явился, значит, Сём Сёмыч."
    th "\"Сём Сёмыч\"? Однако, это что-то новенькое."
    "Она вдруг улыбнулась мне мягко-мягко, будто старому доброму знакомому, с которым связаны какие-то далёкие и приятные воспонимания."
    if alt_day2_beach_done or alt_uvao_true:
        dreamgirl "Значит, не только на пляже наша Ленивовна предаётся гедонизму. Гляди какая морда довольная."
    else:
        dreamgirl "Так вот как наша вожатка готовится к концерту. Как всегда, без продыха."
    "По комнате между тем разносился от чайника чуть уловимый аромат мяты."
    me "Не знал, что вы чай любите."
    mt "А хочешь со мной? В такую погоду глотнуть свежего зелёного чая с мятой - то что нужно."
    th "А с вожатой-то что не так?!"
    me "Ольга Дмитриевна… А почему вы меня не ругаете?"
    show mt surprise pioneer at cleft
    mt "А с чего бы это?"
    me "Ну… Я ведь на репетиции наверное должен быть, или к концерту помогать готовиться, или ещё что-нибудь полезное делать."
    show mt smile pioneer at cleft
    mt "И без тебя там вполне справятся. А пока время есть - садись давай."
    if alt_uvao_D6_sl_pickup:
        me "Я вообще-то только на минутку зашёл."
        if alt_day2_phys_done:
            me "Ольга Дмитриевна, а душевые работают?"
            show mt normal pioneer at cleft
            mt "Да, конечно. Вода, правда, только холодная, но это обычное дело, к сожалению."
        else:
            me "Ольга Дмитриевна, а где тут помыться можно?"
            show mt normal pioneer at cleft
            mt "В душевых около спортзала."
        mt "А ты что? Разве ещё ни разу не ходил?"
        me "Да вот как-то не довелось. А сейчас решил сбегать - всё-таки чистым быть приятнее."
        "Я не хотел в деталях рассказывать, с чего бы это мне вдруг вздумалось привести себя в порядок."
        show mt smile pioneer at cleft
        "Ольга улыбнулась."
        mt "Молодец, Сёмчик. Давно пора - мне на тебя такого грязнулю уже смотреть противно. Пионер ведь должен быть чист и опрятен."
        mt "У тебя все банные принадлежности есть?"
        me "Да… Наверное."
        "Я замялся и скромно потупил взгляд. Не говорить же ей, что я пользовался своим комплектом пару раз от силы."
        mt "Если что, в шкафу ещё есть."
        "Продолжение любезностей с раздобревшей не к месту вожатой не входило мои планы, и я решил плавно заканчивать этот разговор."
        me "А сколько у меня есть времени? Не хочу на концерт опоздать."
        show mt laugh pioneer at cleft
        "Ольга Дмитриевна лукаво посмотрела на меня, негромко хихикнула и совсем расплылась в улыбке." 
        "Похоже, сегодня я впервые видел не зануду-комендантшу вся лагеря, а самую обыкновенную и весьма приятную девушку, которая так до конца и не научилась быть серьёзной."
        mt "Ох, Семён Семёныч. Какой ты сознательный юноша, оказывается. И откуда что взялось?"
        show mt smile pioneer at cleft
        mt "Ну да ладно. У тебя ещё почти час. Так что давай не задерживайся."
        show mt smile pioneer at cleft
        mt "И на всякий случай я проверю, чтобы на концерте ты был вовремя."
        "Ольга хитро подмигнула мне, давая понять, что не верит до конца в моё духовное перерождение."
        me "Обязательно буду."
        "Свёрток с мылом и полотенцем по-прежнему лежал в глубине моей тумбочки, и, быстро достав его, я выскочил на улицу."
        scene bg ext_house_of_mt_day
        "Я конечно рад был таким переменам в поведении вожатой, но упускать возможность побыть вместе со Славей совершенно не хотелось."
        th "Если я по-быстрому со всем управлюсь, то даже на репетицию к ней успею, пожалуй."
        #ЛБ: дальше, видимо, согласующая связка наподобие "Через каких-нибудь полчаса, буквально хрустящий от чистоты, я уже выходил на площадку перед сценой."
        jump alt_day6_uvao_concert_with_sl
    else:
        "Я не стал спорить с Ольгой и молча уселся напротив, прикидывая, с чего бы начать разговор."
        "Вожатая тем временем прикрыла свою тетрадь и убрала её в тумбочку."
        me "На самом деле, Ольга Дмитриевна, я с вами поговорить хотел."
        "Ольга между тем достала гранёный стакан и налила в него немного кипятка."
        mt "Ты рассказывай-рассказывай. А я пока чай тебе сделаю."
        "Я собрался с духом и решил начать с самой сути."
        me "Ольга Дмитриевна, вам Славя не кажется странной?" 
        mt "Славя?! Нет. А что случилось?"
        me "Да вот сейчас на обеде мы вместе сидели, и… это самое… разговаривали, и…"
        "Самая суть никак не хотела оформляться в слова."
        mt "Ну, и? Говори уж."
        me "Знаете, у нас там… ну откуда я к вам сюда попал… Есть такое понятие - харрасмент. Вот, кажется, это было оно. Сексуальные домогательства, если перевести." 
        mt "Что? Ерунда какая-то, что ты несёшь?"
        me "Я бы и сам не поверил. Но серьёзно, приглашение заняться вечером кое-чем {i}интересным{/i} - как это можно понять иначе?"
        th "Или, может…" 
        me "Или, может, это на самом деле ваша идея?! Чтобы, так сказать, понадёжнее заякорить \"попаданца\" в вашей реальности?" 
        show mt rage pioneer at cleft
        mt "Семён! Ты совсем о…?!"
        "Она с заметным усилием проглотила едва не вырвавшееся слово, совсем не подобающее вожатой пионерского отряда." 
        mt "Как ты мог подумать вообще? Разумеется, я не просила Славю ни о чем таком!"
        mt "Во-первых, это настолько неэтично, что почти противозаконно!"
        mt "Во-вторых, ты о себе слишком много возомнил! Тоже мне, брильянт короны, соблазнять его ещё!!!" 
        "Возмущена она была искренне, на мой взгляд."
        me "А Виола? Могла она такое задумать?"
        mt "Не могла! Уж точно не стала бы просить Славю!!!"
        th "Да, скорее, сделала бы всё самостоятельно…" 
        me "Извините…"
        "Я нехотя выдавил из себя полагающееся."
        me "Но что же тогда это было?" 
        show mt angry pioneer at cleft
        mt "Просто твоя паранойя! Наверняка ты что-то не так понял, и навыдумывал потом."
        me "Но она же…"
        mt "Что? Сделала неясный намёк на что-то, что показалось тебе неприличным?"
        mt "Может, она так пытается добиться от тебя хоть какой-нибудь реакции?"
        mt "Почему бы тебе не перестать искать во всём подвох, а просто общаться с людьми, как обычный человек?"
        show mt serious pioneer at cleft
        if alt_uvao_true:
            mt "Серьёзно, это было бы гораздо полезнее - и для успешной ассимиляции, и вообще для тебя!"
        else:
            mt "Серьёзно, это было бы для тебя гораздо полезнее, чем замыкаться в себе!"
        mt "Сходить на концерт, на танцы, да даже просто погулять - но только в пределах лагеря!"
        mt "И вообще, приведи-ка ты себя в порядок. Пионер должен быть - что?{w} Правильно, опрятен! В душевых вода уже тёплая, наверное. Воспользуйся!" 
        me "Все мои доводы она, кажется, уже выкинула прочь из головы. \"Этого не может быть, потому что не может быть никогда.\" И - ни шагу за предписанное инструкциями." 

        "Кивнув и буркнув что-то утвердительное, я вышел." 
        #sfx хлопания дверью
        scene bg ext_house_of_mt_day 
        dreamgirl "И что теперь? В душевые?" 
        th "Нет. К Славе." 
        dreamgirl "Прямо так сразу?" 
        th "Я должен разобраться! Хоть в чём-то я должен тут разобраться, в конце концов?!"
        # врезка - беседа со Славей
        jump alt_day6_uvao_prufung_sl
        