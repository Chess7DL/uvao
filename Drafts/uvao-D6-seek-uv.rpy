#Установлен флаг посещения старого лагеря alt_uvao_D6_oldcamp = True
#Установлен флаг осведомлённости в историю Эла и Жужи alt_uvao_D6_el_mz_story = 0
#Установлен километражеметр uv_km == 0 
#Используется флаг alt_uvao_D5_sh_mines
#Используется флаг alt_uvao_D5_hentai
#Используется флаг танцев в день №3 alt_day3_dancing == 0
#Используется флаг миниджека alt_day2_loki_minijack
#Используется флаг записи в кружок кибернетиков alt_day2_club_join_cyb
label alt_day6_uvao_seek_uv:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ routetag = "uv_true"
    $ alt_chapter(6, u"Юля. Поиски.")
    play music music_list["silhouette_in_sunset"] fadein 3
    $ disable_all_zones_alt2()
    $ disable_all_chibi_alt2()
    $ set_zone_alt2('sandpit_alt2', 'alt_day6_uvao_map_sandpit') #Леночка
    $ set_chibi_alt2('sandpit_alt2', '?') 
    $ set_zone_alt2('old_house_alt2', 'alt_day6_uvao_map_old_house') #Старый лагерь
    $ set_chibi_alt2('old_house_alt2', '?')
    $ set_zone_alt2('forest_alt2', 'alt_day6_uvao_map_forest') #Не моем трубочиста
    $ set_chibi_alt2('forest_alt2', '?')
    $ set_zone_alt2('wash_house_alt2', 'alt_day6_uvao_map_wash_house') #Баня
    $ set_chibi_alt2('wash_house_alt2', '?')
    $ set_zone_alt2('clubs_alt2', 'alt_day6_uvao_map_clubs') #Клубы
    $ set_chibi_alt2('clubs_alt2', '?')
    $ set_zone_alt2('boat_station_alt2', 'alt_day6_uvao_map_boat_station') #Пристань
    $ set_chibi_alt2('boat_station_alt2', '?')
label alt_day6_uvao_mapseek:
    scene black with fade
    if uv_km >= 22:
        jump alt_day6_uvao_map_dining_hall
    if (uv_km >= 10) and not alt_uvao_D6_oldcamp:
        if alt_uvao_D6_el_mz_story == 0:
            $ set_zone_alt2('court_alt2', 'alt_day6_uvao_map_court') #Эл, Женя и Алисхен - начало
            $ set_chibi_alt2('court_alt2', '?')
    elif (uv_km >= 10) and alt_uvao_D6_oldcamp:
        if alt_uvao_D6_el_mz_story == 0:
            $ set_zone_alt2('me_mt_house_alt2', 'alt_day6_uvao_map_me_mt_house') #Родной дом
            $ set_chibi_alt2('me_mt_house_alt2', '?')
    if (alt_uvao_D6_el_mz_story != 0) and (alt_uvao_D6_el_mz_story != 3):
        $ set_zone_alt2('sl_mz_house_alt2', 'alt_day6_uvao_map_sl_mz_house') #Эл и Женя - разборки
        $ set_chibi_alt2('sl_mz_house_alt2', '?')
    if (uv_km > 12) and alt_uvao_D6_oldcamp:
        $ reset_zone_alt2('sandpit_alt2')
        $ reset_chibi_alt2('sandpit_alt2')
        $ reset_zone_alt2('wash_house_alt2')
        $ reset_chibi_alt2('wash_house_alt2')
    elif uv_km > 12 and not alt_uvao_D6_oldcamp:
        $ reset_zone_alt2('old_house_alt2')
        $ reset_chibi_alt2('old_house_alt2')
    $ show_map_alt2() #вызываем альтернативный widget map    
    
label alt_day6_uvao_map_me_mt_house: #Дома с Оленькой
    if uv_km > 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_house_of_mt_sunset
        play ambience ambience_forest_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_house_of_mt_day
        play ambience ambience_forest_day fadein 3
    $ disable_current_zone_alt2()
    "На всякий случай я решил заглянуть к себе"
    $ alt_uvao_D6_el_mz_story = 1
    if not uv_km >= 10:
        extend ", перед таким дальним походом стоило прихватить свой любимый рюкзак."
        dreamgirl "Вчера он тебе не особо помог. Или ты с ночевкой решил свалить из лагеря?"
        th "Вдруг поиски затянутся? Так хоть что-то полезное под рукой будет."
        if alt_uvao_D5_sh_mines:
            th "Консервный нож вон как вчера пригодился."
        "Пока все были на концерте у меня был очень неплохой шанс спокойно сделать всё как надо: без лишних свидетелей и суеты."
    else:
        extend ", ведь именно здесь я смог спервые увидеть Юлю вблизи."
        th "Вдруг она тоже пошла меня искать?"
        dreamgirl "Рассказать новые весёлые истории о вожатой с физруком или булочек поклянчить?"
        th "Да иди ты."
        "Судя по солнцу, приближавшемуся к закату, концерт уже закончился - а значит, бравая пионерия сейчас должна была стучать ложками и вилками, а не шнырять где попало."
    "Однако, моему плану не суждено было сбыться."
    "Уже стоя на самом пороге домика, я услышал чьи-то громкие шаги и промеж веток сирени, росшей на другой стороне улочки, увидел силуэт быстро приближающейся женской фигуры в панамке."
    play music music_list["eat_some_trouble"] fadein 3
    dreamgirl "Как пить дать, по твою душу.{w=.2} Беги."
    with vpunch
    "Мне ничего не оставалось, как кинуться за пожарный щит у соседнего домика и надеяться, что разглядеть кого-то через пышно цветущий куст вожатой будет трудно."
    show mt angry panama pioneer far at cright with easeinright
    "Ольга быстро заскочила в домик, и, не застав там никого, сквозь зубы прошипела:"
    mt "Опять шатается неизвестно где. А там со Славей такое..."
    mt "Сказала же русским языком: не верю. Так он довёл и сбежал, паскудник."
    mt "Пусть только попадётся мне."
    hide mt with easeoutleft
    "Не вглядываясь особо по сторонам, Шляпа с крейсерской скоростью полетела в строну домика Лены и Мику."
    stop music fadeout 6
    th "Кажется, пронесло."
    "Решив, что лимит везения на сегодня скорее всего исчерпан, я решил поскорее свалить отсюда."
    if alt_uvao_D6_oldcamp and uv_km == 12:
        $ uv_km += 5
    else:
        $ uv_km += 1
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_boat_station: #Пристань
    if uv_km > 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_boathouse_sunset
        play ambience ambience_lake_shore_night fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_boathouse_day
        play ambience ambience_lake_shore_day fadein 3
    $ disable_current_zone_alt2()
    "Непонятно зачем я вышел к берегу озера."
    "Очевидно, что Юли тут нет и быть не могло: не стала же она из кошкодевочки русалкой?"
    "Вариант показался весьма занятным, но впадать в очередной раз в ступор в планы мои не входило."
    if alt_uvao_D6_oldcamp and uv_km == 12:
        $ uv_km += 4
    else:
        $ uv_km += 2
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_clubs: #Клубы
    if uv_km > 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_clubs_sunset
        play ambience ambience_camp_center_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_clubs_day
        play ambience ambience_camp_center_day fadein 3
    $ disable_current_zone_alt2()
    "Проходя мимо логова кибернетиков, меня привлёк какой-то шум доносившийся из здания."
    if not uv_km >= 10:
        th "Сейчас же концерт - значит, никого из этих деятелей тут быть не должно."
    else:
        th "Концерт уже должен был закончиться. А поскольку другой дармовой мужской силы нет, то к переноске тяжестей этих деятелей явно признают годными."
    if alt_uvao_D6_el_mz_story > 1:
        th "Не пришёл же этот Гриб работать с горя, в конце концов?"
    "Дверь оказалась слегка приоткрытой."
    play sound sfx_open_door_clubs
    $ renpy.pause(1.0)
    if uv_km >= 10:
        scene bg int_clubs_male_sunset
    else:
        scene bg int_clubs_male_day
    with dissolve
    play ambience ambience_clubs_inside_day fadein 3
    show sh surprise pioneer at right with dissolve
    sh "А! Кто здесь?"
    "Шурик. Без присмотра. Копошится в каком-то приборчике." 
    th "Надеюсь, он не станет бросаться на меня с каким-нибудь паяльником."
    me "А ты чего тут делаешь?"
    show sh normal pioneer at right with dissolve
    sh "Это ты..."
    "Кажется, он успокоился."
    sh "Умножитель для нашего проекта сам себя не сделает."
    sh "Завтра же отъезд: не хочу время терять на этих припевочках у японки."
    sh "А Максимычу лишь бы народ нагнать да промариновать почём зря." 
    sh "Что бы они делали без успехов нашей науки, певуны эти!"
    th "А он тот ещё технофашист. Надеюсь, до опытов по евгенике ещё не додумался."
    show sh serious pioneer at right with dissolve
    if alt_day2_club_join_cyb:
        sh "Ты помочь мне пришёл?"
        me "Эм, я..."
        th "Время терять на тебя я точно не хочу."
        sh "Сам к нам записался - так что время не ждёт."
        me "Я тут вообще-то за другим..."
        "Нужно было что-то срочно ляпнуть, как хорошую отмазку."
        me "Меня Ольга прислала. На сцене что-то с аппаратурой случилось."
        show sh normal pioneer at right with dissolve
        sh "И чего? Мне туда идти теперь?"
        "Понимать нужды других Шурику было не дано."
        me "Да нет. Я и сам справлюсь. Нам всего-то нужен какой-то разъём для динамика."
        show sh serious pioneer at right with dissolve
        if alt_day2_loki_minijack:
            "Он подошёл к уговому ящику с какими-то деталями и спросил:"
            sh "А тот что я тебе давал не годится?"
            me "Нет, там другой нужен."
            "Он слегка усмехнулся."
            sh "Логично. Это же училитель."
        $ renpy.pause(1.5)
        "Спустя пару минут он вручил мне странный кабель с пятью контактами и моток изоленты."
        sh "Серёга вроде там где-то ошивается. Он знает как подсоединить."
        me "Да, спасибо. Ты очень выручил."
    else:
        sh "Может, ты мне поможешь раз пришёл?"
        th "Так и знал, что вляпаюсь."
        me "Да нет, знаешь."
        me "Я вообще в электронике не силён."
        show sh upset pioneer at right with dissolve
        "Шурик тяжело и расстроено вздохнул."
        sh "А чего заходил тогда?"
        me "Да так. Мимо проходил.{w} Удачи тебе."
    $ renpy.pause(1.0)
    if alt_uvao_D6_oldcamp:
        scene bg ext_clubs_sunset with dissolve
        play ambience ambience_camp_center_evening fadein 3
    else:
        scene bg ext_clubs_day with dissolve
        play ambience ambience_camp_center_day fadein 3
    "Я кивнул и быстро вышел из здания клубов."
    "Лучше тут не задерживаться."
    if alt_uvao_D6_oldcamp and uv_km == 12:
        $ uv_km += 4
    else:
        $ uv_km += 2
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_wash_house: #Баня
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        play ambience ambience_forest_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        play ambience ambience_forest_day fadein 3
    scene bg ext_bathhouse_day 
    $ disable_current_zone_alt2()
    "Ещё около эстрады я заметил небольшую тропинку уходящую куда-то в ближайший лес."
    "Стараясь не привлекать к себе внимание и пробравшись среди кустов, я вышел к странной избушке."
    "Она существенно отличалась от всех мест, где мне удалось побывать за эти шесть дней, и скорее походила на домик лесника, чем на что-то связанное с пионерским лагерем."
    "На двери висел здоровенный амбарный замок. Делать тут было нечего."
    $ uv_km += 1
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_campfire: #Костровая
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_polyana_sunset
        play ambience ambience_forest_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_polyana_day
        play ambience ambience_forest_day fadein 3
    $ disable_current_zone_alt2()
    "Что-то определённо тянуло меня в лес - то ли какие-то воспоминания, то ли подспудные ощущения чего-то упущенного."
    "Так что, решив послоняться ещё немного, ноги сами вывели на какую-то поляну."
    "Несколько костровищ с булыжником, брёвна, разбросанный мусор - это что костровая?"
    "В воздухе ощутимо пахло дымом - похоже, тут ещё вчера были какие-то посиделки, иначе дождь бы ни оставил и следа этого запаха."
    th "Неужели был лагерный поход?"
    dreamgirl "Если бы ты чаще смотрел в распорядок, то помнил бы это."
    dreamgirl "Как раз после вчерашнего ужина и отправились все."
    "Наблюдательность как всегда подвела меня."
    th "Странно, что Ольга не подняла шум из-за моего отсутствия."
    if not alt_uvao_D5_sh_mines:
        "Даже с учётом всех моих заслуг по переноске Шурика, с трудом верилось, что она так просто позволила мне прогулять."
    else:
        "Весь день пропадал неизвестно где, а потом ещё нахамил ей - должно же последовать хоть какое-то наказание?"
    dreamgirl "Думаю, ты уже понял, что без предварительной подготовки сюда не устроиться вожатой?"
    dreamgirl "К тому же, тащить тебя в лес, где поджидает хвостатое нечто, об опасности которого тебе сегодня с самого утра твердят, означало бы поставить под удар всех кто был бы в походе."
    dreamgirl "А ты ещё так удачно вздремнуть вчера на шезлонге решил. Неудивительно, что на тебя рукой махнули."
    $ renpy.pause(1.5)
    "Юли тут тоже не оказалось."
    th "Только зря тащился в такую даль."
    $ uv_km += 4
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_sl_mz_house: #Домик Слави
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_houses_sunset
        play ambience ambience_camp_center_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_houses_day
        play ambience ambience_camp_center_day fadein 3
    $ disable_current_zone_alt2()
    $ alt_uvao_D6_el_mz_story = 3 #Новый флажок2
    if alt_uvao_D6_el_mz_story == 1:
        "Ольга окончательно скрылась из виду - можно было ещё раз хорошенько осмотреться у собственного жилища."
        "Как оказалось, расслабился я преждевременно."
        "Осторожно вырулив на соседнюю от своего дома улочку, в глаза тут же бросились двое спещащих сюда пионеров."
        th "Твою ж дивизию. Чего им на эстраде не сидится-то всем!"
        "На план спасения времени совершенно не было, и я от безысходности шмыгнул за старую чугунную скамейку."
    else:
        "Я и сам не понял, что привело меня сюда: то ли отчаянное желание найти Юлю хоть где-то, то ли захотелось убедиться всё ли в порядке со Славей."
        "Заходить в сам домик я точно не собирался, но уже по привычке обшарить кусты - всё же стоило."
        "Ещё с соседней улочки я услышал какую-то ругань: скрипучий голос и невнятное мычание в ответ."
        "Не устояв перед любопытством, я быстренько спрятался за старой чугунной скамейкой."
    th "Весь день сегодня прячусь, как зверёк какой-то."
    dreamgirl "Не забывай: Ольга тебя зачем-то искала - а значит, если уж ты решился найти Юлю, то не стоит показываться всем на глаза."
    "Место, конечно, я выбрал не самое удачное - от жары металл хорошо разогрелся, и прислоняться к нему сейчас было даже горячо."
    "Кое-как выбрав удобное положение, я наконец взглянул в строну откуда доносился шум."
    if uv_km >= 10:
        scene bg ext_house_of_sl_sunset
    else:
        scene bg ext_house_of_sl_day
    play music clueless_hope fadein 5
    show mz angry glasses pioneer far at cleft
    show el upset pioneer far at right
    mz "Чего ты бегаешь за мной?"
    el "Женя, я…"
    mz "Что «я»? Какая часть фразы «пошёл на хрен» тебе не понятна?"
    "Электроник что-то чуть слышно проблеял себе под нос."
    mz "Чего простить? Ты же вроде нашёл наконец, что тебе надо - так и иди туда."
    mz "Я тебе зачем сдалась?"
    "Сегодня библиотекарша явно была в ударе: такой сварливой физии я даже у бабок в метро не смог вспомнить."
    "Казалось, она скоро взлетит от свобственной злости - ядовито жёлтые глаза с ненавистью смотрели на растерянного Эла, а неуложенный хохолок на голове трясся от каждого свежего ругательства."
    show el normal pioneer far
    "Вдруг отчаянно сжав кулаки, он набрал воздуха в грудь и на последнем издыхании выпалил:"
    el "Женя, я люблю тебя."
    show el upset pioneer far at right
    "Смелости на большее у него не хватило, и Электроник тут же уставился виновато в пол."
    show mz bukal glasses pioneer far
    dreamgirl "Хорошее кинцо. Попкорном бы ещё похрустеть."
    th "Тебе бы всё шуточки. Парень в русскую рулетку почти играет."
    dreamgirl "Любовь зла. Не мешай - «Санта-Барбару» тут всё равно не показывают."
    mz "Ты чего сморозил? Опять дурой меня делаешь?"
    el "Женя, я люблю тебя."
    "Голос его с каждым новым словом терял в решительности."
    mz "Словестный понос что ли открылся? Надоел."
    mz "Катись уже отсюда или придумай какую-нибудь чушь поумнее, ксилофонист несчастный."
    el "Но это правда..."
    mz "Что правда?"
    el "Это..."
    show mz shy glasses pioneer far
    "Я чуть не поперхнулся от удивления - Жужелица покраснела!"
    hide mz
    extend " И тут же зашла в дом."
    play sound sfx_close_door_1
    th "Так у этого очкастого недоразумения тоже есть чувства?!"
    dreamgirl "У любого человека есть чувства, если ты успел забыть это за время пока пялился в своего ненаглядного жидкокристаллического друга."
    "Эл ошарашенно бросился за ней и стал стучать в дверь."
    el "Женя! Женя! Открой!"
    th "Чего она взъелась на этого горе-кибениматика тогда?"
    show el sad pioneer far
    dreamgirl "Такой много не надо. Она в себе уверена не больше, чем какая-нибудь Лена."
    dreamgirl "Только реакция на стресс немного другая."
    "А Сыроежкин тем временем всё выстукивал морзянку по окну."
    "Неожиданно дверь открылась"
    play sound sfx_open_door_1
    show mz bukal glasses pioneer far
    show el smile pioneer far at cright with moveinright
    extend ", Жужелица выползла на порог"
    show mz angry glasses pioneer far
    play sound sfx_face_slap
    show el pioneer slap at fleft with moveinleft
    extend "и отвесила Элу смачного леща, что тот тут же отбежал метров на пять."
    el "Но почему?!"
    mz "Не смей сюда приходить больше! Нашёл себе курву и дуй к ней!"
    mz "Скатертью дорога!"
    hide mz
    play sound sfx_close_door_1
    "На Сыроежкина было тошно смотреть: ему как будто только что скормили ведро помоев и хорошо отлупили за это же."
    "Немного отдышавшись, он подошёл ещё раз к двери"
    show el pioneer slap at center with moveinright
    extend ", но, так и не решившись на второй сеанс оплёвывания, побрёл уныло по тропинке с трудом разбирая дорогу."
    hide el with moveoutright
    th "Ну что за дура такая! Кошек на старости лет собирать по помойкам хочет, что ли?!"
    dreamgirl "Прям как ты?"
    "Я не стал отвечать на колкость."
    th "Ему да и себе самой сейчас столько комплексов наплодит, что потом ни один психолух не сможет этот гордиев узел размотать."
    dreamgirl "Всё у них будет хорошо - вот увидишь. Может, ему так нравится?"
    if not alt_uvao_D5_sh_mines:
        dreamgirl "Вчера он точно с такой же отметиной расхаживал, и ничего ведь!"
    dreamgirl "Представь - Женя в обтягивающем кожаном костюме с плёткой{w=.2}, а рядом Эл, выполняющий приказы госпожи."
    dreamgirl "Море страсти и полного доминирования.{w=.2} Завидуй."
    th "Свят-свят. И вообще я Юлю ищу."
    if alt_uvao_D6_el_mz_story == 1:
        $ uv_km += 1
    else:
        $ uv_km += 2
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_court: #Корт с Алисхен
    if uv_km > 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_playground_sunset
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_playground_day
    $ disable_current_zone_alt2()
    "Я изрядно взмок за время прогулки до карьера, а поскольку желание переться до самых умывальников отсутствовало напрочь, мне ничего не оставалось как умыться в ближайших душевых."
    "Ещё на подходе к спортплощадке я заметил парочку пионеров, украдкой пробиравшихся за корты."
    "Концерт по идее должен был давно закончиться, а после него никаких мероприятий в распорядке до самой дискотеки на сегодня не значилось."
    "Любопытство взяло верх и, отложив свои гигиенические процедуры, я последовал за ними."
    "От увиденного я тут же выпал в осадок - здесь были"
    show dv shy pioneer2 at left with dissolve
    extend " Алиса"
    show el normal pioneer with dissolve
    extend " и Электроник."
    "То что они были {i}вместе{/i} и {i}наедине{/i} уже казалось по меньшей мере странным."
    dv "Серёг, ну ты можешь быть не таким тормознутым, а?"
    el "Я стараюсь. Понимаешь..."
    "Он замялся."
    show el sad pioneer with dissolve
    el "Я никогда раньше девочку даже... не держал."
    dv "Пффф. Ты только ей этого не говори, Ромео."
    if not alt_uvao_D5_sh_mines:
        dv "Повторяю для ущербных. Нужно быть решительным или опять по лагерю с оплеухой бегать будешь."
        th "Это она про вчерашнее что ли?"
    dv "Ты конечно хорош: нашёл кого выбрать в помощники. Меня от всех этих сюсюканий с танцульками просто тошнит."
    dv "Если бы я тебя не первый год знала, прописала сразу бы в бубен за всё это и не морочилась тут."
    "Такой Алису я ещё не видел никогда - оказывается, даже она может быть отзывчивой."
    th "Интересно, какая она - ДваЧе настоящая?"
    dreamgirl "Сходил бы, когда тебя приглашали послушать её концерт, да и узнал."
    dv "...Тебе ведь это надо?"
    el "Да."
    dv "Ну так не тупи. Давай живее, наконец."
    "Электроник сделал пару шагов в сторону Алисы и встал близко-близко к ней."
    dv "Одну руку сгибаешь в локте..."
    dv "Второй аккуратно обнимаешь талию..."
    "Эл вытянувшись по стойке старательно повторял все указания и в один момент со всей силы прижал Алису к себе."
    "Как оказалось ненадолго."
    #движ спрайтов
    "Алиса тут же вырвалась из его объятий."
    show el surprise pioneer with dissolve
    dv "Ты б ещё меня за задницу приобнял. Выше надо."
    el "Алис, я..."
    dv "Зачем я вообще согласилась..."
    "Рыжевская была прекрасна. Сочетать одновременно смущение, недовольство и соучастие.{w} Выглядит обворожительно."
    th "Ей бы поменьше этой мужиковатости и..."
    dreamgirl "Что «и»?"
    th "Проехали."
    dv "Попытка номер два. Пошёл."
    show el normal pioneer with dissolve
    "Судя по всему, кибениматик сделал всё так как и было нужно, потому что Алиса продолжила:"
    dv "Вооот. Можешь же когда хочешь."
    dv "А теперь начинаешь медленно делать шаги в одну сторону и считай «раз-два-три»."
    "Со страшным запозданием в голове стрельнула мысль:"
    th "Так это он для Жужелицы что ли старается?!"
    "Краем глаза я заметел приближающуюся со стороны столовой фигуру."
    th "Жужелица. Легка на помине."
    show mz bukal glasses pioneer far at fleft with dissolve
    dreamgirl "Сейчас начнётся."
    "Женя спокойно поправила очки и невозмутимым тоном сказала:"
    mz "Молодец, наконец-то от меня отстанешь."
    show el sad pioneer with dissolve
    #спрайтус Алисхен
    "Электроник и Алиса как два бильярдных шара отлетели друг от друга и синхронно покраснели - похоже подобная ситуация была в новинку для обоих."
    el "Женя. Всё не так, как ты думаешь. Мы это..."
    mz "Да, вы {i}это{/i}.{w} Совет да любовь."
    mz "Горько!"
    "На её лице библиотекарши по-прежнему не дрогнул ни один мускул, чего было нельзя сказать об Алисе - спрятала взгляд, смутилась и заливалась всё дальше краской."
    show mz laugh glasses pioneer far at fleft with dissolve
    mz "Ну чего ж вы. Го{w=.2}-рько!{w=.2} Го{w=.2}-рько!"
    show mz bukal glasses pioneer far at fleft with dissolve
    "Чуть вздрагивавший и сгоравший от глупости всего происходящго Эл неожиданно решил уцепиться за последнюю соломинку."
    show el upset pioneer with dissolve
    el "Прекрати. Алиса ну скажи ей!"
    "Двачевская тоже как-то пришла в себя и тут же выпалила:"
    dv "Разбирайтесь сами. Меня только не впутывайте."
    hide dv with moveoutleft
    "И оттолкнув от себя несчастного кибениматика, на котором и лица не было, быстро отправилась куда-то в сторону столовой."
    show el sad pioneer with dissolve
    el "Алиса, но как же..."
    mz "Беги-беги, не упусти своё счастье."
    "Противно проскрипело библиотечное насекомое."
    el "Женя..."
    "Ядовито жёлтые глаза не выражали ничего кроме внутренней желчи и глубокого безразличия."
    mz "А я тоже пойду. Финита ля комедия, Сыроежкин."
    "Эл что было сил кинулся к Жене, но та как заправский вахтёр лишь рявкнула на него:"
    mz "Не лезь ко мне. Я всё сказала."
    hide mz
    "И развернувшись, грузными и размашистыми шагами с раздражённым видом ушла."
    "Сказать по правде, я не ожидал другого исхода этого фарса - Сыроежкин бросился за ней вдогонку."
    hide el
    th "Даже жалко его как-то. Стараться ради этой грымзы..."
    dreamgirl "Ты вообще ни для кого не старался, умник."
    th "Да лучше бы с Алисой что-то попробовал - они вроде даже как друзья."
    dreamgirl "Сам же знаешь, что френдзона куда хуже, чем ничего."
    dreamgirl "А другим тут и не пахнет. Рыжей другой типаж нужен, который её тараканов придушить сможет."
    dreamgirl "Тебе кстати не пора? Цигель цигель, дружок - Юля сама себя не найдёт."
    th "Да, пойду-ка я."
    $ alt_uvao_D6_el_mz_story = 2 #Новый флажок2
    $ uv_km += 4
    if alt_uvao_D6_oldcamp:
        $ set_zone_alt2('dining_hall_alt2', 'alt_day6_uvao_map_dining_hall') #Столовая
        $ set_chibi_alt2('dining_hall_alt2', '?')
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_forest: #Умывальники
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_washstand_sunset
        play ambience ambience_camp_center_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_washstand_day
        play ambience ambience_camp_center_day fadein 3
    "Я вспомнил, что видел небольшой лес около умывальников."
    th "У Юли наверняка много убежищ по всей территории лагеря"
    if alt_uvao_D6_oldcamp:
        extend ", и - чем чёрт не шутит - вдруг сейчас она именно там."
    else:
        extend ", и - чем чёрт не шутит - вдруг не придётся лишний раз тащиться в тот жутковатый дом."
    "У самих умывальников ожидаемо никого не оказалось."
    "Я проверил на всякий случай все соседние кусты, но не нашёл ничего кроме нескольких окурков и одной разбитой бутылки."
    th "Похоже, и тут пионеры не блещут порядочностью."
    th "Впрочем, с подходом нашей вожатой - ничего удивительного."
    if uv_km >= 10:
        scene bg ext_path_sunset
        play ambience ambience_forest_evening fadein 3
    else:
        scene bg ext_path_day
        play ambience ambience_forest_day fadein 3
    "Вглубь леса уходила исхоженная тропа, и я решил пройтись по ней"
    if uv_km >= 10:
        scene bg ext_path2_sunset with dissolve
    else:
        scene bg ext_path2_day with dissolve
    show fence bricks with fade
    $ renpy.pause(1.5)
    extend ", пока через несколько минут не упёрся в кирпичный забор."
    me "Ю{w=.1}-л{w=.1}-я!"
    $ renpy.pause(1.5)
    "Ответом здесь была лишь тишина."
    th "Этого и следовало ожидать."
    $ disable_current_zone_alt2()
    if alt_uvao_D6_oldcamp and uv_km == 12:
        $ uv_km += 5
    else:
        $ uv_km += 2
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_sandpit: #Карьер и Леночка
    $ disable_current_zone_alt2()
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_path2_sunset2
        play ambience ambience_forest_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_path2_day
        play ambience ambience_forest_day fadein 3
    if alt_uvao_D6_oldcamp:
        "То что я не нашёл Юлю в старом корпусе, в который раз за этот день поставило меня в крайне затруднительное положение."
        "Сейчас она могла быть абсолютно в любой точке около лагеря, и оставалось лишь гадать, а стоила ли свеч вся эта затея с поисками."
    else:
        "После всего случившегося было совершенно неудивительно, что я поплёлся в ближайший лес: идти сейчас в старый лагерь у меня попросту не было никакого желания."
        "За день вчерашний я прошагал, наверное, больше, чем за пару месяцев своей обыденной питерской жизни, а к новым сверхнагрузкам готовности вовсе не ощущалось."
    "Понадеявшись, что концерт привлечёт внимание одной крайне любопытной кошечки, я решил прочесать ближайший к эстраде лесок."
    "И уже спустя полчаса поисков я понял насколько сильно ошибался."
    "Это был не лесок, а самый настоящий лесище - совсем не похожий на наш Левашовский, куда я выбирался раз в год из своей берлоги на дополнительный сеанс одиночества от всего мира людей."
    "От жары и большой влажности, оставшейся со вчерашнего дождя, дышать было довольно тяжело."
    "Тропинка, уводившая меня всё дальше в глубины чащи, то и дело перемежалась с рытвинами и поваленным ельником, так что идти дальше было тяжело, и как бы я не старался, треск от моей поисковой операции разносился гулким эхом на всю округу."
    th "Оно, наверное, и к лучшему. Если Юля где-то рядом, услышит меня наверняка."
    dreamgirl "Тебя и так за версту слышно. Можно было давным давно назад разворачиваться - нет тут её."
    if alt_uvao_D6_oldcamp:
        scene bg ext_path2_sunset
    "Однако, через некоторое время за деревьями показался просвет и я несколько ускорил шаг."
    #Карьер, которого так и нет. Автор - редиска, принял бы хоть ленофагово творение.
    play music music_list["dance_of_fireflies"] fadein 3
    "Как оказалось, нелёгкая вывела меня к самому настоящему песчаному карьеру, уходившему вниз почти отвесным глиняным склоном метров на десять."
    "Вокруг то и дело сновали трескучие стрекозы, чьи крылья поблёскивали на лету перламутром, а немного взмокшее лицо приятно обдувал тёплый сухой ветерок."
    "Наверное, какой-нибудь художник с удовольствием коротал бы здесь время в поисках вдохновения, вдали от суеты и городского шума."
    th "Хорошее здесь место. Жаль только, что Юлю я так и не нашёл."
    "Немного отдохнув в тишине, я решил напоследок крикнуть разок от души:"
    me "Ю{w=.2}-ю{w=.2}-л{w=.2}-я{w=.2}-я!"
    play sound sfx_hiding_in_bush
    $ renpy.pause(3)
    with vpunch
    th "Мне ведь не послышалось?"
    "Не став медлить, я тут же сунулся в кусты, откуда, как показалось, донёсся звук."
    if uv_km >= 10:
        scene bg ext_path2_sunset2
    else:
        scene bg ext_path2_day
    me "Юля!"
    "Под одним из деревьев вдруг что-то ярко блеснуло в солнечном свете."
    "Приглядевшись, я увидел небольшой аккуратный перочинный ножик, совсем непохожий на тот, что вчера я нашёл в своём «Гриззли»."
    th "Откуда он здесь?"
    dreamgirl "А ты посмотри по сторонам."
    "На уровне моих глаз на дереве, под которым лежал ножичек, под слоем соскобленной коры виднелось сердечко{w} с вырезанными буквами «С.П. + Лена»."
    "Сама надпись похоже была совсем не новая: её края уже успели задеревенеть, но в паре мест виднелась свежая ещё жидкая смола - кто-то решил обновить давнишнее признание в любви."
    th "Похоже, я вспугнул очередного малолетнего вандала."
    "Усмехнувшись я взглянул на соседний ствол, а затем на следующий..."
    "На каждом из них красовалось такое же сердце, те же инициалы, но уже с другим именем."
    th "«Алиса»... Опять «Алиса»... «Лена»..."
    th "«Ольга»...{w=.4} Это не про нашу ли Ленивовну написано? Или пионерка какая-то?"
    "Почерк всюду выглядил удивительно знакомым, как будто всё это когда-то сделал один и тот же человек."
    th "Интересно, кто такой этот «С.П.», что испортил половину местного леса."
    dreamgirl "Когда-нибудь узнаешь, кто тут такой любвеобильный."
    "Удивительно, но внутренний голос тут же умолк, так и не дав никаких более существенных объяснений."
    "Находиться в этой рощице памяти стало почему-то тягостно, и я отправился искать Юлю дальше."
    $ set_zone_alt2('campfire_alt2', 'alt_day6_uvao_map_campfire') #Костровая
    $ set_chibi_alt2('campfire_alt2', '?')
    $ uv_km += 10
    stop music fadeout 2
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_old_house: #Старый лагерь
    $ disable_current_zone_alt2()
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_old_building_sunset_red
        play ambience ambience_forest_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_old_building_day
        play ambience ambience_forest_day fadein 3
    $ alt_uvao_D6_oldcamp = True #Новый флажок1
    "Где как ни здесь после вчерашнего похода логичнее всего было встретить Юлю."
    "Дорога опять оказалось не самой приятной, но уже почти по наитию я с первого раза отыскал и нужную тропу, и даже засохшие после дождя следы Шурика, отпечатавшиеся в глине."
    th "Похоже, я проскочил вчера прямо перед тем, как разверзлись хляби небесные."
    th "Ведь ни дождевика, ни зонта не брал - повезло."
    "Хоромы моей хвостатой принцессы выглядели всё также угрюмо, но сейчас я решил осмотреться получше."
    "Никаких намёков на другие строения. Только остатки проржавевшей насквозь карусели да небольшая бетонная площадка, сквозь трещины в которой уже вовсю росла лесная осока."
    "Получается, пионеров селили в палатках? Выглядело это крайне сомнительно, но другого правдоподобного объяснения мне так и не удалось придумать."
    "Всё познаётся в сравнении. Из неудобств моего нынешнего положения можно было сразу вспомнить лишь ледяную воду в умывальниках."
    "Представлять каково это - жить почти в лесу, где любой гад может ночью заползти тебе в ухо, а во время дождя лучше всего оказаться в палатке подальше от низины - упорно не хотелось."
    "Послонявшись вокруг да около ещё минут пятнадцать никаких следов присутствия Юли найти так и не получилось."
    th "И чего я тут брожу? Она же наверняка в бункере или в самом корпусе."
    scene bg int_old_building_day_uvao with dissolve
    "Внутри также никого не оказалось. На всякий случай, я даже слазал на чердак, но и там не было ничего кроме давно отсыревшей мебели."
    play sound sfx_door_squeak_light
    pause(1)
    "Всё-таки не стоило бродить тут одному: половицы предательски скрипели под ногами, и случись что - помощи я дождался бы очень нескоро."
    scene expression Noon("cg d4_hatch_night_open")
    with dissolve
    "Вчерашний люк так и остался открытым." 
    "Даже хорошо, что поисковая группа не успела заглянуть сюда - чего доброго решили бы искать Шурика и там тоже."
    "Впредь стоило быть более осторожным."
    dreamgirl "Какое впредь? Завтра конец смены. Или ты корни решил пустить тут?"
    th "Это я образно. Осторожность нигде не помешает."
    "Лезть в жутковатые тёмные катакомбы без фонарика и спичек совсем не хотелось, но мои крики внутрь так и остались без ответа."
    me "Была ни была."
    play sound sfx_bodyfall_1
    scene bg int_catacombs_entrance
    play ambience ambience_catacombs fadein 3
    "Прыжок удался на славу - я даже не стал спускаться по лестнице."
    "Довольно быстро привыкнув к темноте, я наощупь отправился вдоль стены ко вчерашней двери бункера."
    "Довольно скоро пальцы окаменели - тут было ощутимо холоднее, чем снаружи, а бетонный короб и вовсе можно было использовать как холодильник."
    if alt_uvao_D5_sh_mines:
        th "Не удивлён, что Юля здесь запасы делает." 
    th "При желании тут и тушу мамонта можно сохранить."
    "Пол не был слишком каменистым и довольно скоро я оказался перед заветной дверью."
    scene bg int_catacombs_door
    th "Ой."
    "Я упустил одну маленькую деталь.{w} Эту дверь можно открыть только вдвоём."
    "Никаких подходящих инструментов поблизости не оказалось - ни рычага, ни даже фомки."
    dreamgirl "У меня есть один способ."
    th "Да? Какой же?"
    "Внутренний голос как будто набрал воздуха в грудь и со всей серьёзностью начал:"
    dreamgirl "Повторяй за мной."
    "Вдруг последовала странная пауза."
    me "Ну! Давай скорее."
    dreamgirl "Сим-сим, открой дверь!"
    pause(1)
    #blink
    th "Угораздило же меня заполучить такую шизофрению..."
    "В отчаянии я сняв сандалию несколько раз постучал по двери, но расслышать что-то за гермозатвором было попросту невозможно."
    "Нужно было идти обратно."
    th "И где её искать теперь?.."
    $ set_zone_alt2('camp_entrance_alt2', 'alt_day6_uvao_map_camp_entrance') #Стоянка
    $ set_chibi_alt2('camp_entrance_alt2', '?')
    $ uv_km += 12
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_camp_entrance: #Остановка
    if uv_km >= 10:
        $ persistent.sprite_time = "sunset"
        $ sunset_time()
        scene bg ext_camp_entrance_sunset 
        play ambience ambience_camp_entrance_evening fadein 3
    else:
        $ persistent.sprite_time = "day"
        $ day_time()
        scene bg ext_camp_entrance_day 
        play ambience ambience_camp_entrance_day fadein 3
    "Раз уж Юли не оказалось в старом корпусе, стоило проверить все места, где я её вообще встречал."
    "Первой на ум сразу же пришла автобусная стоянка, а воспоминания о позавчерашних булочках напомнили, что такими темпами я вполне могу остаться и без ужина."
    "Я прошёл несколько метров вдоль забора, но не нашёл никаких свежих следов."
    th "Интересно, здесь вообще часто кто-то бывает?"
    if alt_uvao_D5_sh_mines:
        th "Или после вчерашних разборок с Ольгой тут так никто и не появлялся?"
    "Сейчас всё это не имело ровным счётом никакого значения." 
    "То, что с этим лагерем что-то не так, было понятно с самого начала, но теперь мне кровь из носу нужно было отыскать мой единственный ключик к этим загадкам."
    "А здесь не было ничего - кроме трещин в асфальте да пары довольно чирикающих птиц."
    if alt_uvao_D5_sh_mines:
        if alt_uvao_D5_hentai:
            "Всё было совсем как вчера, и на меня немного накатили воспоминания от нашего запоздалого первого поцелуя."
        else:
            "Всё было совсем как вчера, и на меня немного накатили воспоминания от наших неумелых - почти детских - поцелуйчиков в щёчку."
        dreamgirl "Ау! Тебе же уже почти тридцатник, а раскис на ровном месте как повёрнутый на токсикозе подросток."
        dreamgirl "Собрался искать - ищи. Если не поторопишься, то придётся рыскать нашу хвостатую красавицу в потёмках."
        "Уже собравшись в путь, я решил проверить вход в бункер, через который мы вчера попали сюда."
        "К сожалению, он оказался заперт."
        th "Быстро Олька постаралась. Похоже, нашего бугая запрягла."
        "Присев, я потряс прутья решётки, пару раз даже стукнул по ним кулаком, но они и не думали поддаваться."
        "Я даже покричал в глубину шахты, но так и не смог рассышать ничего кроме собственного эха, бродящего по подземному лабиринту."
    th "И где её черти носят..."
    if alt_uvao_D6_oldcamp and uv_km == 12:
        $ uv_km += 4
    else:
        $ uv_km += 2
    $ disable_current_zone_alt2()
    jump alt_day6_uvao_mapseek
    
label alt_day6_uvao_map_dining_hall: #Столовая - выход
    $ disable_current_zone_alt2()
    $ persistent.sprite_time = "night"
    $ night_time()
    play ambience ambience_camp_center_night fadein 3
    scene bg ext_dining_hall_away_night
    th "Хватит с меня этих идиотских поисков. Ещё полчаса и я от голодухи еле ноги волочить буду."
    dreamgirl "Сам решился, сам и искал, упорный ты наш."
    th "Чего ж не отговорил меня? Совесть ты или только по половой части?"
    dreamgirl "И то, и другое. На полставочки."
    dreamgirl "Просто хотел посмотреть, чем выгорит ли эта затея."
    dreamgirl "Сдалась тебе эта Юля. Только ужин пропустил."
    "Вокруг столовой было довольно пустынно - зато с площади уже отчётливо слышались бодрые аккорды корифеев местной эстрады."
    "Похоже, дискотека вовсю шла уже минут пятнадцать."
    th "Пусть себе дальше конечностями дрыгают. Мне и прошлого раза по горло хватило."
    if alt_day3_dancing != 0:
        dreamgirl "А мне казалось, в прошлый раз ты неплохо потанцевал."
    else:
        dreamgirl "Верно. Ты же у нас живёшь по заветам Сыча - ни с кем и никогда."
    dreamgirl "Тебе напомнить, как Славя с тобой сегодня потанцевать напрашивалась?"
    th "Слушай, отвянь, а? У меня кишка кишке бьёт по башке - давай решать проблемы по мере их поступления."
    with vpunch
    show us smile dress with dspr
    us "Эй. Пошли танцевать."
    th "Чем я прогневал местного демиурга? Ну чем?!"
    "Остатки хоть сколько-то миролюбивого состояния были разрушены вдребезги."
    me "Во-первых, выучи наконец моё имя. Или оно в твоей пустой головушке поместиться не может?" 
    show us sad dress with dspr
    me "Во-вторых, с тобой я никуда не пойду."
    show us grin dress with dspr
    us "Тебе жалко что ли?"
    "Я одарил её самым недружелюбным взглядом, но Ульянка была совершенно непрошибаема."
    us "Пошли - весело будет. Не хочешь танцевать - не надо."
    us "Я уже пару шуточек заготовила. Поможешь мне."
    th "Прилипла как банный лист, блин."
    me "Улечка.{w=.3} Милая.{w=.3} Дорогая..."
    "Не дав мне закорнчить, она протянула:"
    show us normal dress with dspr
    us "Да-а?"
    "Незлобивая улыбка и кристально голубые глаза." 
    "Как оказалось, пронять душу закоренелого циника может и самая обычная простота ребёнка. Ведь стоит хоть иногда не искать у людей камни за пазухой, а всего лишь довериться им."
    th "Ведь не боится же она, что сейчас я могу на правах сильного толкнуть её или ударить. Не боится и доверяет мне."
    th "Быть может, и я не стану делать глупостей на ночь глядя?"
    me "Я уже еле ноги волочу с голодухи. Сейчас мне только с тобой приключений до полного счастья не хватало."
    me "Давай, разбежимся по доброму и никто не пострадает."
    "Ульяна заговорщицки улыбнулась. Кажется, маленькая бестия опять что-то придумала."
    us "Так ты голодный что ли?"
    me "Я тебе только, что это и сказал."
    us "А если я еду тебе достану, пойдёшь со мной?"
    "Наверное, стоило соврать: от пустоты в желудке в голове была полная каша, а тело с трудом выполняло простейшие манипуляции. Но эта девочка не отступила бы от своего и нашла бы методы, как заставить меня держать своё слово."
    me "Нет, не пойду. Я очень устал."
    "Вместо расстройства Ульяна почему-то улыбнулась."
    us "Жди здесь. Сейчас что-нибудь придумаю."
    hide us with dissolve
    "Энергии ей было не занимать: во мгновение ока она скрылась за углом столовой."
    $ renpy.pause(1.5)
    "Подспудно я всё же опасался очередной пакости от мелкой егозы, но всего-то через пять с небольшим минут она вернулась с двумя небольшими кульками в руках, один из которых тут же протянула мне."
    show us normal dress with dspr
    us "Держи, голодающий Поволжья."
    "В моём свертке оказалась пара давно остывших пряников, но что самое удивительное – ни одной сколопендры!"
    "Нет, других ползающих и прыгающих гадов тоже не было."
    me "Это... Спасибо, наверное."
    me "Только в чём подвох?"
    us "А ни в чём. Я всё равно хотела залезть сегодня напоследок в столовую за конфетами в дорогу."
    us "Вот и тебе прихватила, что нашла."
    "Верилось мне во всё происходящее с трудом – уж слишком легко я отделался. Стоило всё-таки расспросить её поподробнее."
    me "Я тебе ничего не должен?"
    us "С тебя прока как с козла молока, Семён."
    "Она шутливо подмигнула мне."
    us "Но если ты хочешь, можешь завтра мою постель на склад отнести."
    me "Хорошо. Договорились. "
    us "И помни мою доброту."
    hide us with dissolve
    "Улыбнувшись напоследок, она вприпрыжку побежала на танцы – даже хорошо, что вся её энергия не обратилась в мою сторону, и теперь можно было спокойно заморить червячка."
label alt_day6_uvao_prufung_uv:
    scene bg ext_dining_hall_near_night with dissolve
    "Под навесом у самого входа внутрь оказалась скамеечка. Место идеальное для спокойного приёма пищи – со стороны площади в темноте меня было бы трудно разглядеть, а показываться вожатой хотелось меньше всего."
    "Нечаянно я провёл рукой под одной из досок - удивительно, но внезапно пальцы упёрлись в какой-то небольшой прямоугольный предмет, с любовью приклеенный кем-то из местных умельцев."
    "Пара крепких движений и в моей руке оказалась пачка «Полёта» - к несчастью, пустая."
    "Я лишь разочарованно вздохнул - у меня явно были какие-то неоплаченные долги богам всеблагого рандома. Что ж, ничего удивительного." 
    th "Не очень-то и хотелось. Сейчас первостепенно - наполнение собственного желудка."
    "После сегодняшней прогулки мне за счастье была любая еда, и эти, судя по запаху, медовые пряники справедливо показались почти манной небесной."
    uv "Как вкусно пахнет!"
    "Сверху с правого края крыши на меня глядела пара кошачьих глаз – тех самых, что я так долго искал сегодня."
    me "Это пряники."
    uv "Я люблю пряники! Даже больше чем булочки. Поделишься?"
    "Скрипя сердцем, я уполовинил свою порцию и вручил драгоценную сладость в маленькие ручки с небольшими коготками."
    "Деваться было всё равно некуда – профукать последний шанс выяснить, что же происходит со Славей, и чем это может грозить мне, было бы сущей глупостью."
    "Проглотив кое-как суховатый пряник и собрав остатки рассудка в кучу, я спросил:"
    me "Где ты была днём? Я тебя везде искал."
    uv "Я запасы проверяла. Белки воруют иногда."
    uv "Потом видела, как ты от своего дома куда-то побежал."
    uv "Хотела позвать, но ты какой-то злой был. Не люблю злых."
    "Я со злобы чуть прикусил губу."
    th "Это же надо было так попасть впросак: ходил неизвестно где, а Юля в соседнем от сцены лесу сидела."
    "Хотелось грохнуть что-то о ближайшую стену - столько кругов намотано впустую. Однако, горевать о содеянном было бы попросту глупо."
    uv "Ой, а зачем ты меня искал?"
    me "Кое-что у тебя спросить хотел..."
    uv "Залезай сюда. Тут огоньки красивые."
    "Она чуть укоризненно посмотрела на меня."
    uv "Мало пряников принёс. Нехорошо."
    "Оглядев внимательнее скос крыши, я понял: высота была не слишком уж большой, но навернуться при неудачной попытке было проще простого."
    me "Может, не стоит?.."
    uv "Залезай. Тут интереснее и тебя никто не заметит."
    th "Грехи мои тяжкие..."
    "Я осторожно уцепился за край крыши и попытался подтянуться, но как и в первый день пальцы упорно отказывались слушаться."
    pause(1)
    "На третьей попытке хват предательски ослаб, и я еле удержал равновесие на перилах. Было очевидно, что продолжать эти физические нагрузки абсолютно бессмысленно."
    th "Зачем я вообще полез. Сразу же было понятно, что с этим телом у меня ничего не выйдет – это ведь даже не удобный турник."
    th "Как будто тянет меня к ней."
    th "Даже странно, что совсем не боюсь её после всех утренних рассказов Виолы."
    "А хвостатая прохвостка всё сидела и улыбалась наверху."
    uv "Ты так смешно пыхтишь, как будто спариваешься."
    "От этих слов я покраснел ещё больше."
    if alt_uvao_D5_hentai:
        dreamgirl "Нужно было лучше стараться. Смотри: даже кошечку разочаровал."
    else:
        dreamgirl "А киска у нас с фантазией – всё вчерашнее тебе не забудет."
    dreamgirl "Может, вместо допросов устроишь небольшую какофонию под луной? Сам ведь сказал, что тянет."
    "Кажется, я окончательно ушёл в ступор."
    th "Хорошо хоть Юля немногословна. Любая пионерка на её месте меня за идиота приняла."
    dreamgirl "Ну не каждый же может на пустом месте взять и потянуть кота за хвост."
    dreamgirl "Кстати, возможно наверху именно этого и хотят."
    th "Уймись, наконец. Или попрошу у Виолы три порции успокоительного."
    dreamgirl "Отличная идея. Возьми валерьянки – и если всё получится..."
    pause(1)
    "Чтобы выкинуть из головы всю пошлость, побороть естественные реакции и отдышаться, ушла ещё пара минут."
    me "Спускайся. Я к тебе не смогу залезть."
    "Не став спорить, Юля грациозно спрыгнула и в два шага встала прямо передо мной."
    show uv smile with dspr
    uv "Так что ты хотел?"
    "От лишних глаз я отвёл Юлю под навес и, разместившись на скамейке, начал:"
    me "Я сегодня тебя долго искал, чтобы поговорить о Славе. Ты ведь знаешь её?"
    "Кажется, Юля поняла, что для неё в разговоре не будет ничего интересного, потому что ответом мне стало лишь двусмысленное молчание."
    th "Она, наверное, не понимает о ком я."
    me "Светлая такая. С косами."
    uv "Знаю."
    "В её голосе прозвучали явные нотки недовольства."
    th "Похоже, всё таки существует какая-то связь между ними, и обстоятельства там явно не из приятных."
    me "Откуда?"
    uv "Видела."
    "Она старательно не смотрела в мою сторону и то и дело нервно взмахивала хвостом."
    th "Второй раз за день клещами вынимать информацию нужно. И не надоело тут всем в Зою Космодемьянскую играть?.."
    dreamgirl "Ты с ней всё-таки поаккуратнее. Не пионерка какая-то – за её реакции я не поручусь."
    me "А она тебя видела?"
    uv "Да."
    "Мои опасения только подтверждались. Нужно было продолжать."
    me "И… Ты сама к ней подошла, как ко мне?"
    uv "Да."
    me "А как это было?"
    uv "Она по вечерам выходит кормить ежей – туда, у леса, где большие дома бегают."
    me "Какие дома?"
    uv "Красные такие. После них ещё воздух плохой становится."
    th "Кажется, она про остановку и автобусы."
    uv "Странная она – увидела меня, вдруг потрогать захотела, побежала за мной."
    me "И чем всё кончилось?"
    "Смотрела Юля уже не на меня, а куда-то сквозь, будто пытаясь найти в уголках своей памяти все подробности злополучной встречи."
    #спрайтус поменять
    "Вдруг, чуть приосанившись, она неожиданно уверенно ответила:"
    uv "Я её испугала, потому что мне было страшно."
    me "Ис.. Испугала?"
    "Я недоумённо посмотрел на Юлю, но та вдруг навострилась, поджала уши и быстро шепнула мне:"
    uv "Сюда кто-то идёт. Большой."
    uv "На крыше было бы лучше."
    hide uv
    "Не дав мне произнести ни слова, она ловко прошмыгнула за стоящую у крыльца столовой «Волгу»."
    "Почти тут же за собой я услышал приминающийся хруст песка."
    ba "Куришь, Дрищ?"
    show ba em1 uniform with dissolve
    th "Принесла же нелёгкая этого медведя на мою голову."
    "В кармане предательски похрустывала глянцевой упаковкой пустая пачка, которую я зачем-то сунул себе в карман."
    if alt_day2_phys_done or alt_day3_ladder_phys == 2:
        me "Борис Александрович, я это... Просто отдыхаю."
        show ba normal uniform with dissolve
    else:
        me "Прос-стите, а как вас..."
        show ba normal uniform with dissolve
        "Немного помедлив, он раздражённо прошипел:"
        ba "Борис Александрович. Пора бы выучить, как старших зовут."
    th "Надеюсь, он не будет по карманам шарить. Нужно его заболтать. Может, и прокатит."
    me "Танцы же. Взмок немного - решил отойти."
    "Похоже, моей версией остались недовольны."
    show ba evil uniform with dissolve
    ba "Я тебя там не видел.{w} Врёшь мне, что ли?"
    me "Да я... в общем..."
    dreamgirl "Ещё никогда Штрлиц не был так близок к провалу. Кончились отмазки?"
    me "Я это... не очень танцы люблю, вот. Скучно мне там."
    show ba smile uniform with dissolve
    "Он от души расхохотался."
    ba "Что? Девки не клюют, а ты застеснялся?"
    "Для вида я потупил взгляд: качку с единственной извилиной такое объяснение могло показаться правдоподобным - а значит, у меня появлялся шанс выйти сухим из воды."
    ba "На занятия надо было ходить - я бы из тебя-оглобли нормального пацана бы сделал."
    if alt_day2_club_join_footbal or alt_day2_club_join_volley:
        ba "Почему не появлялся на тренировках?"
        ba "Сам же записывался."
    elif alt_day2_club_join_badmin:
        ba "Записался на девчачью ерунду и не появился ни разу."
    elif alt_day2_phys_done:
        ba "Ну что, хамло малолетнее, помогли тебе твои нарды?"
    else:
        ba "Я тебя даже рядом у спортплощадки не видел. Пришёл бы да позанимались. Что ты боишься, как маленький?"
    ba "Стоишь мне тут лапшу на уши вешаешь, а Олька из-за тебя переживает."
    th "И чего ей из-за меня переживать - у неё ты, амбал старый, есть."
    mt "Бориска, ты чего тут?"
    show mt smile dress far at left with dissolve
    "Легка на помине."
    ba "Охламона твоего встретил. Курит, небось, как всегда."
    show mt surprise dress at left with dissolve
    mt "Семён?! Нашёлся, наконец-то."
    "Она подошла ближе и немного нервно осмотрела меня."
    show mt serious dress at left with dissolve
    mt "Ты где пропадал с самого концерта?"
    th "Хоть сейчас меня в пляс не потащит, как на прошлой дискотеке."
    dreamgirl "Тебя ещё одно не вполне понятное нечто в кустах дожидается, или ты забыл?"
    dreamgirl "Славю она, как сама рассказала, испугала - того и гляди, своё влияние и на этих чудаков продемонстрирует."
    th "Правда твоя. Заигрался я в непонятого мальчика-одиночку."
    mt "Я тебя по всему лагерю ищу, а ты тут прохлаждаешься."
    "Галстук надоел до ужаса за весь день, но сейчас он превратился в настоящую удавку."
    me "Ольга Дмитриевна, я тут..."
    mt "Я вижу, что ты тут. Ты мне нужен был, и это не говоря о распорядке, и что ты обещался быть на концерте."
    "Я подозревал, что Юля где-то рядом и, осмотревшись боковым зрением, увидел всю ту же знакомую пару флуоресцирующих глаз у левого края «Волги»."
    th "Нужно хотя бы попытаться намекнуть ей. Всё равно здоровяк ничего кроме своих железяк и мячей не понимает."
    me "Тут кошка орала. Я её искал - думал, куда-то попала..."
    show mt angry dress at left with dissolve
    show ba normal uniform with dissolve
    "Вожатая нахмурилась и упёрла руки в боки."
    mt "Какая ещё кошка?"
    me "Ну, кошка. Я её искал-искал, а она куда-то делась..."
    ba "В лагере с роду никаких котов не было. По нормативам запрещено."
    me "Ольга Дмитриевна, вы же видели её, кошку."
    "Она глубоко вздохнула."
    mt "Сколько можно уже, Семён. Ты сегодня у Виолы был?"
    me "Конечно, был. Там интересно было - мы про всякое разговаривали. И про кошку эту тоже."
    show mt surprise dress at left with dissolve
    "А вот теперь вожатая поняла всё как надо."
    "Её глаза нервно забегали по всем окрестным кустам, а потом удивлённо остановились на мне."
    if alt_uvao_true:
        mt "А! Ты про ту {i}кошку{/i}. Шатен... ой. Бурую такую?"
        me "Да-да. Где-то тут она. Ищу, вот."
        "Ольга быстро уцепилась за запястье физрука."
        mt "Борис, пойдём."
        show ba em1 uniform with dissolve
        ba "А Дрищ?"
        "Вожатая с ненавистью посмотрела на него."
        mt "Видишь, у него дела? Чего ты к нему прицепился? Там Сидорова или Ерохин какое-нибудь ЧП нам устроят."
        mt "И дискотека скоро уже кончится - пошли."
        hide mt
    else:
        "Слегка опустив голову, она заметила слегка оттопыренный карман шорт и быстро залезла в него."
        show ba em1 uniform with dissolve
        "Не рисуясь, она достала ту самую пачку и, не найдя в ней ничего, быстро скомкала и бросила себе под ноги."
        mt "Семён, ты... Я к Виоле, а ты... Последи за кошкой. Мне к Виоле надо."
        ba "Оля, ты чего?"
        "С растерянным видом Ольга невнятно что-то буркнула себе под нос и почти бегом направилась в сторону медпункта."
        hide mt
        "Куда бы она не спешила на самом деле - вид у вожатой был крайне испуганный."
    "Саныч ещё немного потоптался, бросая взгляд то ей вслед, то на меня, и тоже отправился к площади."
    hide ba
    pause(1)
    "Убедившись, что больше никого из посторонних рядом нет, я окликнул Юлю, сидевшую уже в далёких кустах."
    me "Они ушли - можешь вылезать."
    show uv normal
    pause(1)
    "Спустя пару минут мы уже сидели на скамейке - ровно также как и до прихода бдительного физрука."
    uv "Я думала, что они тебя заберут. Испугалась."
    me "Но ведь не забрали."
    "Она тихо рассмеялась."
    uv "Захотела бы и не забрали."
    uv "Я бы их тоже испугала."
    if alt_uvao_D5_sh_mines:
        "Тут меня осенило:"
        me "А вчера Шурика ты тоже так испугала?"
        uv "Шурика?"
        me "Парня, что мы под землёй видели."
        "Она отстранилась на пару секунд и снова улыбнулась."
        uv "Да. Ты хороший, а он злой был."
    else:
        me "Слушай, а вчера ты того мальчика, что за мной по следу шёл, больше не видела?"
        uv "Видела, конечно. Он в шахтах лазил."
        "Дело начало приобретать куда как более масштабный оборот, чем я изначально предполагал."
        me "И ты его тоже испугала?"
        uv "Да. Он за ту дверь залезть хотел, потом броситься на меня решил."
    th "Значит, его поведение потом это от этого испуга? Стоп. Значит, и Славя..."
    "Пора было поставить все точки над ё."
    me "Ты говорила, что и Славю испугала. А как это?"
    "Юля опять нахохлилась."
    uv "Не знаю. Испугала, и всё."
    uv "Глупости спрашиваешь какие-то."
    "Повисло неловкое молчание: Юля не собиралась рассказывать мне подробности, да и не имело это уже практически никакого смысла."
    "Теперь, когда я узнал кое-что новое о вчерашнем происшествии, сложная мозаика сложилась в моей голове. Оставалось только ужаснуться тому, что сейчас могло произойти с ничего не подозревающими Ольгой и Санычем."
    th "Нужно расспросить Славю о том случае, может чего и вспомнит."
    th "А Юлю нужно увести куда-то подальше - пока она опять кого-нибудь не испугала."
    me "Я, наверное, пойду. Поздно уже. Меня ругать будут."
    "Она опять расстроилась."
    uv "Ругать это плохо. Хочешь, я их тоже испугаю?"
    "Перспектива выглядела настолько удручающе, что я нервно хихикнул."
    me "Не надо. Они хорошие."
    "Юля чуть слышно шмыгнула носом и вздохнула."
    uv "Мне скучно будет."
    me "Так ты это... мышей полови, вот. Сейчас ведь ночь - самое для них время."
    "Кажется, она взбодрилась."
    uv "Ой, правда." 
    uv "Тогда я к тебе завтра приду - ты ведь уезжаешь уже."
    me "Всё-то ты знаешь. Хорошо, приходи."
    hide uv
    "Пара взмахов рукой на прощание, и вот она уже скрылась в окрестном лесу."
    "А у меня ещё оставалось одно незаконченное дело на вечер."
    #jump alt_day6_uvao_sl_adultery
    jump scenario_uvao_debug
