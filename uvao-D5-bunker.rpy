label alt_day5_uvao_bunker:
# Д5 - Бункер. Начало.
#
# Используется флаг alt_uvao_true
# Устанавливает флаг alt_uvao_D5_sh_mines. Видим Шурика в шахтах.
# Устанавливает флаг alt_uvao_D5_hentai, если крыли кошочку
#
#   Все флаги стоят в дебаге, так что тут не нужны.
#

    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Бункер")
    $ night_time()
    $ persistent.sprite_time = "night"
   
    window hide
# ТЕМНО!
    scene black with fade
    play ambience ambience_catacombs fadein 3
    window show
    dreamgirl "Эй! Да здесь же темно, как в…"
    th "…Наверное, как в подземелье, да? Жаль, фонарика нет!"
    me "И куда теперь дальше?"
# Всё ещё темно!
#    show uv upset at center with dissolve
    uv "Никуда. Подождём здесь."
    uv "Когда твой Шурик уйдёт - вернёмся."
    "Юля замолчала, слышно было лишь её спокойное дыхание где-то рядом. С поверхности не доносилось ни звука."
    th "Интересно, а если он наверху решит поселиться? Будем сидеть здесь вечно?"
    "С минуту я просто стоял на месте столбом, боясь отойти от лестницы."
    "Против ожиданий, воздух в подвале, или где там мы оказались, вовсе не был затхлым, да и под ногами было сухо."
    window hide
    scene bg int_catacombs_entrance with dissolve2
    window show
    "Постепенно зрение адаптировалось, и я понял, что мрак не был вовсе беспросветным.{w} Мы явно стояли в каком-то длинном коридоре или тоннеле, с одной стороны издалека доносился слабый свет."
    show uv normal with dissolve2
    me "Юля, а куда коридор ведёт?"
    "Я показал в сторону невидимого источника света."
    uv "Никуда. Там дверь большая. Железная. Не открывается."
    dreamgirl "Вот тебе и возможность убить время. В худшем случае пройдёшься туда-сюда, разомнёшься."
    th "Ну да, а в лучшем - найду новых приключений на свою задницу."
    dreamgirl "Ну и сидел бы тогда в лагере, клинья к девочкам подбивал!{w} Глядишь, и обломилось бы чего."
    "Возразить Шизе было нечего. Действительно, стоя на месте много не узнаешь, и если уж я взялся искать ответы - упускать шанс получить хоть какую-то информацию было глупо."
    "К тому же здесь было ощутимо прохладнее, чем наверху, так что тонкая рубашка помогала слабо. Я зябко поёжился, с тоской вспоминая забытый тёплый свитер, и предложил:"
    me "Слушай, а пойдём, посмотрим эту твою железную дверь? Вдруг вместе получится её открыть?"
    "Юля не заставила себя долго упрашивать."
    show uv grin with dspr
    uv "Пойдём, попробуем открыть!{w} Узнаем, что там!"
    "Она схватила меня за руку и уверенно потащила куда-то в сторону источника света."
    "По мере того, как становилось светлее, я смог немного осмотреться."
    "Тоннель - а сейчас было уже совершенно ясно, что это никакой не коридор в подвале, а именно тоннель - имел явно заметный уклон вниз."
    show uv normal with dspr
    th "Спуск в бомбоубежище? Серьёзно у них здесь всё организовано было!"
    "Вдоль бетонных стен тянулись аккуратно уложенные кабели, на потолке через равные промежутки были закреплены забранные в сетку светильники."
    "Впрочем, источник света впереди оказался таким же светильником - то ли единственным уцелевшим, то ли это просто было дежурное освещение."
    th "Надо же, сюда питание до сих пор подаётся!"
    "Запоздало удивился я."
    th "Прочно же раньше строили!"
    "В желтоватом свете пыльной лампы хорошо было видно, что стенам уже не один десяток лет, но время практически не коснулось их - ни трещин, ни потёков воды."
    "Тоннель, освещённый редкими работающими светильниками, всё никак не заканчивался, плавно уводя куда-то вглубь."
    th "Что-то это выглядит слишком серьёзным для бомбоубежища в пионерском лагере где-то в глуши."
    "Через несколько минут впереди показался наконец тусклый блеск металла."
    "Дойдя до конца, мы оказались перед закрытой гермодверью."
    window hide
    scene bg int_catacombs_door
    play music music_list["orchid"] fadein 5
    with dissolve
    window show
    me "Ого, мощно сделано. Интересно, что там, за этой дверью?"
    uv "Не знаю. Говорю же, у меня не получалось открыть."
    "Я попробовал повернуть кремальеру, но - увы, сил не хватало."
    "Немного пошарив глазами по сторонам, я заметил кусок толстой арматуры."
    th "Хмм, а что если?…"
    "Подобрав свою находку, я вставил её в колесо кремальеры."
    me "Дайте мне точку опоры, иииии…"
    "И ни хрена. Я надавил ещё сильнее, потом навалился всем телом, надеясь сдвинуть-таки заржавевший механизм. Тщетно."
    play sound breath fadein 1
    "Переводя дух, я снова огляделся. Глаза остановились на чёрной коробочке сбоку от двери - от кремальеры не дотянуться.{w} Под откидной крышкой обнаружилась кнопка без опознавательных знаков."
    stop sound fadeout 5
    "Немного подумав, я надавил кнопку, преодолевая усилие неожиданно тугой пружины…"
    "Внутри стены что-то глухо щёлкнуло."
    "Я отпустил кнопку - снова щелчок."
    th "Возможно, здесь специально проход для двоих - для сильного и для умного?"
    dreamgirl "Всегда двое их - учитель и ученик…"
    "Я не стал уточнять у Шизы смысл намёка, а вместо этого повернулся к Юле, с интересом наблюдавшей за моими манипуляциями."
    me "Поможешь мне?"
    show uv surprise at center with dspr
    uv "А что надо делать?"
    me "Я думаю, что дверь можно будет открыть, если держать эту кнопку нажатой. Сможешь её удержать?"
    show uv smile far at fright with dissolve
    "Несмотря на хрупкое телосложение, Юля без особого труда придавила тугую кнопку."
    "Услышав щелчок, я поспешно навалился вновь всем телом на импровизированный рычаг…{w} Это было опрометчиво."
    play sound sfx_carousel_squeak
    me "Уййй!"
    dreamgirl "Сила есть - ум не поможет."
    "Кремальера провернулась неожиданно легко, арматурина сорвалась, и я крепко приложил ей сам себя по правой ноге прямо над коленом."
    show uv surprise at center with dissolve
    "Юля от неожиданности отпрыгнула на пару метров и уставилась на дверь."
    uv "Скрипит. Получилось?"
    "Шипя от боли, я выкрутил колесо до упора и потянул."
    play sound sfx_open_door_mines_metal
    "Дверь очень неторопливо провернулась на петлях."
    me "Похоже, что да."
    show uv normal with dspr
    stop music fadeout 3
    "Находясь под впечатлением от уже увиденного, я рассчитывал оказаться в тамбуре со второй гермодверью."
    window hide
    scene bg int_catacombs_living with fade
    play music music_list["waltz_of_doubts"] fadein 5
    show uv normal at center with dissolve
    window show
    "Однако, к моему удивлению, сразу за дверью оказалось что-то вроде небольшого убежища. Двухъярусная кровать, какие-то приборы - работающие, что удивительно! - шкафчики с какими-то ящиками. Похоже, именно сюда стремился Шурик."
    "Правое колено начинало выть всё сильнее, и я, стараясь не беспокоить его, допрыгал до кровати на левой ноге."
    me "Подожди, давай передохнём. Я, похоже, сильно ногу ушиб, и не смогу идти какое-то время."
    uv "Хорошо, давай."
    "Я решил посмотреть, что с ногой. На коленке красовалась небольшая ссадина поверх наливающегося синяка."
    #th "Похоже, повезло. Так можно было и инвалидом стать."
    #dreamgirl "Инвалидом ты бы стал, если бы с разбитым суставом выбрался отсюда! А до тех пор ты бы числился пропавшим без вести."
    uv "Залижи."
    me "A?"
    uv "Залижи. Я всегда так делаю. Мне очень помогает."
    show uv smile at center with dspr
    "Она задрала ногу и продемонстрировала, кося на меня жёлтым глазом."
    "Я усмехнулся. Ну и кошка!"
    dreamgirl "Может она и тебе коленку полижет, вдруг пройдёт?"
    "Шиза была как всегда в своём репертуаре."
    th "Отвали, не до этого сейчас."
    me "Нет, мы так не делаем."
    "С некой гордостью сказал я."
    me "Мне бы немного льда. Да только где его тут взять?"
    uv "Вот поэтому зализывать лучше."
    "Но тут с ней не поспоришь. Наверное, в лагере найти лёд не составило бы труда, но в каком-то подземном бункере это не такая уж и лёгкая задача."
    "Вставать с кровати не хотелось.{w} Слишком рано встал, слишком много беготни по лесам, руинам и туннелям, а также бесед с этим… существом, Юлей."
    "Должно быть, именно так себя чувствует чересчур разогнанный комп во время серьёзных вычислений - мой мозг практически кипел. Я вытянулся на кровати и закрыл глаза."
    "Толчок в плечо."
    uv "Дверь закрой. А то этот, друг твой, нас найдёт."
    me "И не друг он мне вовсе!"
    "Возмутился я."
    "Но в целом - она была права, тут же ещё Шурик шляется где-то. Не хватало ещё, чтобы он меня нашёл в подземном бункере с девочкой, которая помимо хорошей фигуры имеет пару кошачьих ушей и хвост."
    #"Застонав, я поднялся, доковылял до двери, закрыл её и свалился на кровать. Глаза закрылись сами.{w}"
    "Доковылял до двери, я закрыл её и свалился на кровать. Глаза закрылись сами.{w}"
    show blink
    "Последнее что я почувствовал - как кто-то тёплый подлез мне под руку и улёгся рядом…"
    window hide
    $ renpy.pause(2, hard=True)
    window show
    "Проснулся я от ощущения какой-то тяжести на груди."
    "\"Завалило!\" - была первая паническая мысль."
    hide blink
    show unblink
    show uv smile close at center with dissolve
    "Но нет, это была всего лишь Юля."
    "Она лежала, опираясь локтями мне на грудь, и пристально изучала меня. Какое-то время мы молча смотрели друг на друга."
    uv "Я вчера двоих ваших видела, старших. У речки. Большой, тяжёлый такой, и та, с которой ты живёшь."
    "Внезапно сообщила мне Юля."
    me "Ага… И что они делали?"
    uv "Спаривались."
    "Я вытаращил глаза."
    me "Чего-о?! {w=.4}Ольга Дмитриевна и Саныч?!"
    dreamgirl "Вот это поворот!"
    uv "Ага. Интересно было, я раньше не видела, как это. Непохоже."
    me "На что непохоже?"
    uv "На других зверей. Не как белки. Или мыши. Их тогда легко ловить."
    dreamgirl "Я же говорил, она тебе столько способов ловли мышей расскажет! Способ первый - дождись, пока папа-мышь с мамой-мышью захотят сделать мышат…"
    "Одно дело, когда у тебя в голове появляется посторонний голос - банальная шизофрения, ничего необычного. Но когда этот посторонний голос сам начинает сходить с ума…  Остаётся только вздохнуть."
    me "А на что похоже?"
    uv "Сначала они просто сидели."
# пред-хентай...
    "Она взяла мои руки и положила себе на спину."
    uv "Потом вот так."
    "Она прижалась носом к моему носу. Наверное, это должно означать поцелуй."
    uv "Потом вот."
    "Чуть повернулась боком, и прижала мою ладонь к своей груди."
    dreamgirl "Чувааааак? Кажется, нам дадут!"
    th "Но она же… не человек?"
    dreamgirl "Сиська вполне человеческая. И, кстати, совсем не маленькая. Да и все остальное, кроме хвоста и ушей - тоже."
    th "Да я не об этом! Если б такое устроила обычная девушка - все ясно на сто процентов. А вот что у неё в голове происходит… я же вам не какой-нибудь контактер-уфолог!"
    dreamgirl "И улеглась она на тебя просто так, ага.{w} Поверь мне, если её перестанет устраивать происходящее, тебе об этом сообщат. В виде огромных царапин на всю щёку."
    if alt_uvao_true:
        jump alt_day5_uvao_evade_hentai_scene
    else:
        menu:
            "Попытаться сменить тему":
                jump alt_day5_uvao_evade_hentai_scene
            "Узнать поподробней":
                $ alt_uvao_D5_hentai = True
                jump alt_day5_uvao_hentai_scene
                
label alt_day5_uvao_evade_hentai_scene:
# Д5 - Сваливаем от хентая.
#
# Используется флаг alt_uvao_true для фотографии
# Устанавливает флаг alt_uvao_D5_sh_mines, если мы спим
#
#    "Почему-то я решил не выдаваться в сравнительный анализ коитуса грызунов и людей."
    "Почему-то я не был уверен, что хочу вдаваться в дальнейший сравнительный анализ коитуса грызунов и людей."
    dreamgirl "Кажется, тебе недвусмысленно предлагают перейти от болтовни к делу."
    "…"
    dreamgirl "Не тяни, девочка ждёт!"
    "Сделав над собой героическое усилие, я заставил практически вышедшую из-под контроля руку расслабиться и деликатно переместил её с груди Юли немного повыше."
    "Юля всё ещё лежала на мне и изучающе смотрела в лицо. Чёрт, да ведь на ней из одежды только лишь эта пародия на платье!"
    "Я вдруг очень остро ощутил сквозь два слоя тонкой ткани тепло её тела, его упругую мягкость… {w}Ноздри наполнял соблазнительный запах девичьего тела…"
    "Рука, кажется, сама собой миллиметр за миллиметром поползла обратно…"
    if alt_uvao_true:
        th "Балда, меня же Виола особо предупреждала - никаких сексуальных контактов!"
        dreamgirl "А почему - не объяснила? Может быть, она это все придумала, чтоб тебя заполучить?"
        dreamgirl "Ещё денёк-другой тебя промаринует в этом рассаднике подростковых грёз, а потом предложит заночевать в медпункте…{w} А ты уже на все согласен будешь."
        th "Не, ты не просто Шиза, ты нечто наглухо психанутое. Пойди проспись!"
        "Я на несколько секунд задержал дыхание и потряс головой, пытаясь отогнать затягивающий меня сладостный дурман.{w} Бунтующее тело отчаянно отказывалось повиноваться. Ещё немного и…"
        "Впрочем, Виола мне кое-что ещё говорила. Мол, сделай снимок - и много вопросов отпадёт."
        me "Юля, А можно я тебя сфотографирую?"
        "Кажется, я нашёл наконец, как отвлечься."
        show uv surprise close at center with dspr
        "Юля навострила уши и удивлённо посмотрела на меня."
        uv "Сфотографируешь? Это как?"
        dreamgirl "… я вас сэйчас обоих сфатаграфырую!"
        me "Ну, это… Эээ…"
        th "И как ей объяснить, интересно?"
        me "Эээ, это такая штука… Делает картинку, на которой ты как настоящая."
        uv "Настоящая? Ещё одна я?"
        me "Ну… Давай я просто тебе покажу. Только сиди смирно. И отодвинься немного."
        "Нехотя Юля чуть приподнялась с меня и смирно села, ожидая чего-то неизвестного для неё."
        show uv guilty at center with dissolve
        "Я навёл камеру на Юлю, а она продолжала удивлённо смотреть на меня."
        dreamgirl "Внимание, сейчас вылетит мышка! Летучая!"
        window hide
        show frame at truecenter:
            xalign 0.5 yalign 0.5 zoom 1.0
            linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
        show cam_ui at truecenter:
            xalign 0.5 yalign 0.5 zoom 1.0
            linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
        window hide
        $ renpy.pause(2, hard=True)
        play sound snap fadein 0
        scene white with flash
        $ renpy.pause(2, hard=True)
        scene bg int_catacombs_living
        show uv surprise2 at center with dspr
        window show
        "Вспышка, Юля подскочила от неожиданности, а я скорее полез в телефон посмотреть снимок."
        window hide
        # play sound sfx_scary_sting //Дофига резкий ВНЕЗАПНЫЙ страшный звук, я только не уверен, в верное ли место я его вставил, поэтому пока в комменты - Ленофаг
        scene uv_photo_city with dissolve
        $ renpy.pause(2, hard=True)
        scene bg int_catacombs_living with dissolve
        show uv surprise2 at center with dissolve
        window show
        "…"
        dreamgirl "…"
        "Даже словоохотливая Шиза не нашлась, как прокомментировать получившееся."
        "Юля уже опомнилась от вспышки, и опять приняла свою любимую позу. На мне."
        show uv smile at center with dspr
        uv "Получилось? Покажи ещё одну меня!"
        "Я молча повернул смарт экраном к ней."
        "Юля изучила снимок и кивнула."
        uv  "Хорошо получилось. Почти как я."
        "И ни капли удивления, главное."
        dreamgirl "Чувак, если таки надумаешь её сношать - попроси у Виолы полосатый презерватив. Пусть водители твой левитирующий посреди проспекта орган за жезл регулировщика принимают. {w} Извини, нервное."
        th "Задолбал со своими шутками. То есть {b}это{/b}, по твоему, нормально?"
        dreamgirl "А я и не говорю, что это нормально. Ты думаешь Виола просто так попросила кошечку твою сфотографировать? Но, честно сказать, это для меня тоже сюрприз."
        #TODO: Текст
        #Удивиться, выйти наверх.
        #Закончить текст, опять же, на выходе из двери.
        jump alt_day5_uvao_true_back
    else:
        "Я разрывался между зовом плоти и параноидальным червячком сомнения.{w} Однако, что-то во всей этой ситуации настораживало…"
        dreamgirl "Или это кто-то уже настолько боится женщин, что ищет повод слиться."
        "Я не боюсь! Я опасаюсь. Тем более что она не вполне человек."
        dreamgirl "Там где надо - вполне человек, явно выраженного женского полу. Не ври хотя бы себе. И мне."
        show uv normal close at center with dspr
        "Юля между тем заскучала и, игриво прижав уши, стала подползать ближе. Прямо по мне."
        dreamgirl "Ну, и как ты планируешь слиться сейчас?"
        me "О, Юля. Я же тебе конфет принёс!"
        "Я открыл рюкзак и достал конфеты. Я уже хотел закрыть его, как заметил, что под конфетами лежали спички."
        th "А что, вдруг пригодятся?"
        "Не долго думая я закинул их в карман шорт, закрыл портфель и отдал своё спасение."
        show uv laugh close at center with dspr
        "Юля радостно заулыбалась. Судя по её реакции, вкусная еда для неё всегда на первом месте."
        dreamgirl "Некоторые подманивают девочек конфетами… А ты - наоборот, отвлекаешь. In Soviet Russia…"
        "Я протянул ей пакет, и та с готовностью зашуршала. Я же воспользовался шансом получше осмотреть бункер."
        hide uv with dissolve
        "Какая-то странная аппаратура - между прочим, подключённая и работающая!" 
        th "Интересно, кто и что здесь замеряет?"
        "Я попытался разобраться в хитросплетениях проводов…{w} Безрезультатно. Какие-то провода соединяли клеммы приборов, но большая часть уходила куда-то в стену."
        "Мелькнула мысль понажимать кнопки и повертеть ручки, что называется, \"от балды\", но я решил не искушать судьбу. Мало ли, еще спалю что-нибудь."
        "У другой стены выстроились шкафчики с разными необходимыми припасами."
        "Противогазы, аптечки с описью, инструменты… Но, кажется, ничего такого, что могло бы пригодиться мне прямо сейчас." 
        # Ага, фонарик есть, а блуждаем в потёмках потом.
        # Cпички же.
        #"О, фонарик! Пригодится, если придётся ещё блуждать по подземельям."
        stop music fadeout 3
        menu:
            "Продолжить расспросы":
                $ alt_uvao_D5_sh_mines = True
                jump alt_day5_uvao_asking
            "Задуматься":
                jump alt_day5_uvao_thinking
    
label alt_day5_uvao_asking:
#Д5 - Расспросы Юли
#
    # TODO: Мысли между строк
    play music music_list["trapped_in_dreams"] fadein 3
    th "Поиск улик не принёс результатов. Следовательно, стоит сосредоточиться на допросе свидетелей.{w} Тем более что вот он, свидетель, шуршит конфетами."
    show uv smile close at center with dspr
    me "Юля?{w} Ты мне так и не рассказала, кто ты."
    "Доедая свою сладкую добычу сказала она."
    uv "Я - это я. Не могу же я быть не мной? Тогда это уже была бы не я." 
    show uv normal close with dspr
    th "Мда, так мы никуда не придём."
    me "Ну вот смотри. Я - Семён."
    uv "Да, я знаю."
    me "Но я также - человек. Пионер. Я заснул в автобусе у себя, а проснулся здесь. А ты?"
    uv "Я?{w}… Я - человек… только с ушами и хвостом.{w} Я - девочка…{w} Я всегда жила здесь…{w} Ой сколько меня много! Я так потеряюсь!"
    me "Ну ладно. А чем ты тут занимаешься вообще?"
    uv "Всем! Еду ищу, чтоб запасы делать. Хожу, смотрю. Вы, люди, интересные бываете, на вас весело смотреть. Только прятаться приходится."
    me "От меня-то ты не прячешься…"
    uv "Ну я же тебя давно знаю! Я тебя видела, когда спала. Все уезжают, и я сплю. Потом я просыпаюсь - а все уже приехали! Одной тут грустно и скучно."
    me "Погоди, как это ты меня видела?"
    uv "Когда спала, говорю же. Ты все время в свой светящийся квадрат смотрел, и идти не хотел.{w} А что это за квадрат был? Он интересный?"
    me "Ну, это… Неважно, в общем, я тебе потом как-нибудь расскажу. Ты мне лучше скажи - я вот идти не хотел… а потом взял - и пришёл?"
    uv "Ага, не хотел, не хотел, а потом пришёл."
    me "А как это? Как это так вдруг получилось?"
    uv "Обычно. Взял - и пришёл. Приехал. Тут очутился."
    me "Ничего не понимаю. Я просто сел в автобус, заснул… А потом проснулся здесь! Да что за фигня-то?!"
    show uv upset close with dspr
    uv "Не кричи! Просто взял и пришёл. Ты давно уже должен был прийти, только все не хотел и не хотел. Я уже даже ждать устала."
    me "Так, получается, это ты мне снилась и спрашивала - \"Ты пойдёшь со мной?\""
    show uv normal close with dspr
    uv "Не знаю. Это же тебе снилось, а не мне. Я просто ждала, когда ты наконец придёшь."
    me "Вот не знаю, как у тебя, а мы, обычные люди, не умеем так, чтобы заснуть зимой в городе, а проснуться летом в лагере. Может, это лагерь какой-то особенный?"
    uv "Может быть, я других лагерей не видела. Я же все время здесь живу."
    me "А давно, кстати? Сколько лет прошло?"
    uv "Давно. Всегда. Тут ещё дети жили, маленькие, в старом лагере. Потом все в новом лагере стали жить, а я здесь. Наверно, их та вожатая испугала."
    me "Вожатая? Испугала?"
    uv "Ну я с ней поговорила, а потом она испугалась. А я только с ней поговорить хотела!"
    me "Погоди, а что с ней стало-то?"
    show uv rage close with dspr
    # Юля недовольно шипит и отказывается разговаривать на эту тему.
    #TODO: Тут тоже текст
    me "Ну ладно. Тогда расскажи лучше, что это за ходы тут такие под землёй?"
    show uv smile close with dspr
    uv "Очень интересные ходы! Длинные! Там дальше ещё дыра есть вниз, в другие ходы. Я в них живу и припасы храню. А теперь, когда ты мне дверь открыл, я буду здесь жить, здесь кровать удобная."
    me "А откуда они взялись-то, эти ходы?"
    show uv guilty close with dspr
    uv "Всегда были. Или нет… Те, нижние, всегда были. А эти, сверху - появились, давно. Я проснулась, а они есть."
    # TODO: ТЕКСТ
    "Расспросы ходили по кругу, не принося практически ничего. Сложно вытрясти информацию из существа, которое тебя понимает с пятого на десятое."
    "В конце концов мой живот напомнил мне, что пора бы и пообедать."
    show uv normal close with dspr
    me "Юля, а ты знаешь, как отсюда быстрее в лагерь попасть?"
    uv "Знаю. Надо вниз спуститься, и оттуда можно прямо к Генде выйти. Под землёй.{w} А если ты вон ту дверь тоже откроешь - то будет еще быстрее."
    me "Ну только если ты мне поможешь. Помнишь - надо на кнопку давить."
    uv "Хорошо!"
    show uv smile far at center with dissolve
    #Юля встала и подошла к кнопке
    "Она надавила на кнопку, я завертел колесо… С ржавым скрипом дверь распахнулась, и мы увидели точно такой же тоннель."
    jump alt_day5_uvao_mines_begin

label alt_day5_uvao_thinking:
#Д5 - Задумался, разговор с Шизой.
#
    # TODO: ТЕКСТ
    # Как вариант - это тот-же разговор с шизой, что и в хентае.
    "Задумался. Нейтрал-рут"
    "Думаю, пора жрать."
    "Идём на обед по низу!!!"
    jump alt_day5_uvao_mines_begin

label alt_day5_uvao_hentai_scene:
# Д5 - Сцена Хентая.
#
# Устанавливает флаг alt_uvao_D5_sh_mines, если мы спим.
#
    me "Да-а-а? А потом?"
    uv "А потом уже вот так!"
    "Она вскочила и ловким, по-кошачьи гибким движением избавилась от платья, под которым не было больше ничего лишнего."
    "Потом встала на четвереньки и повернулась ко мне, при этом хитро поглядывая через плечо, на манер избушки, так сказать. К стене передом, к Семёну задом."
    dreamgirl "Ну, убедился?"
    "Юлин хвост изогнулся эдаким приглашающим знаком вопроса. Не дождавшись от меня какой-нибудь реакции, она потопталась коленями и вопросительно наклонила голову."
    uv "М-рррр?"
    dreamgirl "Перевожу: Ну и долго тупить будем? Барышня готова и ждёт своего кавалера."
    "Нет смысла противиться неизбежному. Не то чтоб происходящее оставило меня равнодушным, совсем наоборот, но… Блин, это же бред какой-то! Или эротический сон."
    dreamgirl "Если сон, то тем более нечего тормозить. Короче, ты меня достал!"
    #scene bg black with fade2
    play music music_list["i_dont_blame_you"] fadein 3
    "Меня подняло с кровати каким-то внезапным импульсом - должно быть, это тот самый пресловутый \"Внутренний пендель\"."
    "Только сейчас я обнаружил, что и рубашка давно расстёгнута, и я лежу с полностью голым торсом."
    dreamgirl "А времени она зря не теряла. Поспал бы ты ещё полчасика - на тебе бы и шорт не осталось."
    "Руки сами собой легли на её талию, притянули к себе."
    uv "Мрррррра-а-у-у-у!"
    "Одобрительно протянула она и соблазнительно прогнулась."
    scene uv_bunker_hentai with dissolve
    th "Будь я котом, я бы просто взял её зубами за шкирку и хардкорно отымел…"
    dreamgirl "Как будто что-то плохое. Да и она вряд ли будет против."
    th "Цыц. Я пытаюсь не посрамить гордое звание Гомо Сапиенса в первом контакте с иной расой!"
    "Вместо этого, я наклонился и прикоснулся губами чуть выше копчика. Хвост немедля обвил мне шею, будто какой-то пушистый удав, и затрепетал кончиком у меня на груди."
    "Дальше - цепочка поцелуев вдоль позвоночника, руки, облапившие грудь… Когда я дошёл до места между лопаток…"
    uv "Мррра-а-а-у-у-а-а-у-у!"
    "Она немного приподнялась сзади и прижалась своими ягодицами ко мне."
    dreamgirl "Это означает: Ну не тяни уже! Ваш К.О. всегда рад помочь."
    "Желание девочки - закон. А уж кошкодевочки… Я в единый миг освободился от всего, что мешало мне ниже пояса, и, прицелившись…"
    dreamgirl "На ладонь ниже хвоста. Не промахнись, Ястребиный Глаз."
    uv "Мааааауууу…"
    "…единым махом скользнул внутрь."
    "Она со вздохом качнулась сначала от меня, потом обратно… И снова… И снова, вовлекая меня в движения."
    "Старая железная конструкция душераздирающе скрипела и качалась. Оставалось надеяться, что проектировщики заложили приличный запас прочности, и эта бандура не обрушится нам на головы. Впрочем, нас это мало беспокоило - мы как раз нашли подходящий ритм, и не собирались останавливаться несмотря ни на что!"
    "Юля с каждым движением старалась выгнуться все сильнее, её кошачьи стоны заглушали скрип, а я… Я так увлёкся ролью \"кота\", что стал даже подпевать ей. Кажется, мы движемся к финалу…"
    "Хвост её по-прежнему обвивался вокруг меня, щекоча и вздрагивая. Я прижал его рукой к животу, и тон её \"песен\" внезапно повысился на октаву, вместе с громкостью. Ого! Вот это находка!"
    "Я не преминул воспользоваться новым знанием эрогенных зон кошкодевочек - обхватил ладонью хвост у основания и несильно, но властно потянул к себе. Кошечка прогнулась совершенно невероятным образом, и натурально взвыла, продолжая насаживаться на меня."
    uv "А-а-а-а-х."
    "Это стало первым камешком лавины, и я совершенно потерял самоконтроль. Несколько секунд - или минут? - безумных телодвижений под дикие животные вопли, и мы оба, тяжело дыша, мокрые от пота и прочих жидкостей, повалились обратно на кровать."
    scene bg int_catacombs_living with fade2
    show uv smile close at center with dissolve
    uv "Мррррр… Да, примерно вот так и они тоже."
    "Тяжело дыша сказала она, устраиваясь головой на моём плече."
    me "Они? Кто?"
    "Мозг напрочь отказывался работать."
    uv "Тот большой и вожатая. Только у неё хвоста нет. Жалко её. С хвостом лучше."
    "Я представил Ольгу Дмитриевну с пушистым кошачьим хвостом, торчащим из-под юбки, и фыркнул. А если ещё уши, да панаму сверху… Не удержавшись, я захихикал совершенно по-дурацки."
    uv "Ты что?"
    "Удивлённо подняла голову Юля."
    me "Представил её с хвостом и ушами."
    uv "Ааа. Тогда ладно."
    "Она взяла мою ладонь и пристроила себе на голову, негласно намекая - гладь давай, не отвлекайся!"
    hide uv with dissolve
    "Так мы и лежали, я - почёсывая ей между ушами, она - уткнувшись мне в плечо и посапывая."
    dreamgirl "Поздравляю вас, многоуважаемый коллега!"
    stop music fadeout 3
    "С Шизой говорить желания не было, однако она не оставляла попытки докопаться до меня."
    dreamgirl "Видел бы ты себя со стороны, кошачий угодник."
    menu:
        "Проигнорировать":
            $ alt_uvao_D5_sh_mines = True
            "Сил не было даже на мысленную пикировку с внутренним голосом, так что я просто проигнорировал его выпад."
            "Продолжая почёсывать Юлю за ухом, я незаметно провалился в сон."
            "…"
            window hide
            show blink
            $ renpy.pause(2, hard=True)
            hide blink
            $ set_mode_nvl()
            scene bg black
            window show
            "На офисном столе мурлыкнул селектор."
            uv "Семён Семёнович, приехали от заказчика на сегодняшнее мероприятие. Примете?"
            me "Да, Юля, проводи их в шоу-рум, и позови девочек."
            th "Крупный заказчик, высшее общество, мать его. Падкое на всяческую экзотику, которую я им, собственно, и поставляю. И с того мы все имеем кусок хлеба с маслом и икрой. Так что собираемся и идём работать."
            me "Добрый вечер, господа! Сейчас, буквально через минуту, подойдут наши девочки, которых мы выбрали для вашей вечеринки. А я пока расскажу о правилах и условиях."
            voice "Короче, Склифосовский!"
            th "Шутник, блин."
            me "Если коротко - уши и хвост полностью настоящие, но не стоит за них дёргать и проверять, они этого очень не любят. Обращаться с ними нужно бережно, подчёркиваю - насилия не применять, не угрожать и не запугивать. Были случаи, закончилось все печально, увы."
            voice "В смысле?"
            me "Представляете себе атакующую рысь или пантеру? Вот примерно в таком смысле."            
            me "Лучше действовать лаской, уговорами и подкупом - отлично работают всякие пирожные."
            nvl clear
            "В дверь постучали"
            sl "Можно, Семён Семёнович?"
            me "Да, проходите, девочки. Знакомьтесь, господа, это - Славя, Лена, Алиса и Мику - помесь русской голубой и ориентальной породы. Девочки, эти господа приглашают вас потанцевать и поразвлекаться, вы не возражаете?"
            voice "Позвольте глупый вопрос, а почему они в пионерской форме?"
            me "Называйте это корпоративным стилем, или рекламным ходом. Это запоминается, создаёт имидж..."
            voice "Ещё вопрос - почему только девушки?"
            me "У нас есть и мальчики, если они вас интересуют, но обычно девушки пользуются бОльшим спросом. Мальчики более агрессивны, стараются поддерживать доминирующую роль в стае... В общем, лучше брать их отдельно, тогда все пройдёт без эксцессов."
            voice "Ну что ж, тогда не будем терять время, где подписать?"
            "Покончив с формальностями и проводив кошечек до лимузина, я вернулся в кабинет. Неплохой куш нам сегодня обломился! Я подмигнул Юрию Куклачёву, грозящему мне пальцем из фоторамки на столе, и вытянулся в удобном кресле и прикрыл глаза."
            nvl clear
            window hide
            $ set_mode_adv()
            scene bg int_catacombs_living
            show unblink
            window show
            "…"
            "Открыв глаза, я уже не увидел никакого офиса, ни Юрия Куклачёва, который, казалось, секунду назад грозил мне пальцем из фоторамки. Что за… это сон?"
            dreamgirl "Коллега. Что за херню мы только что посмотрели?!"
            "Время, наверное, подходило к обеду."
            "На мне всё в такой же позе лежала Юля, тихо посапывая. Даже моя рука, которая была у неё в волосах, осталась, кажется, в том же положении, что и перед сном."
            th "Наверное, вот оно какое, это счастье. Просыпаться с красивой девочкой. И не важно где, важно лишь с кем."
            dreamgirl "Угомонись, мечтатель. Конечно это не отменяет того факта, что вы несколько часов назад вытворяли тут \"Хау-ау\", но, чувак, у неё есть кое что, чего ты не в одной девушке не найдёшь."
            th "Её глаза?"
            "Мозг выдал первое что пришло в голову."
            dreamgirl "Какие глаза, придурок! У неё половина тела кошачья, тебя это вообще не смущает?"
            th "Ну, может совсем чуть-чуть."
            "Полюбовавшись своей кошечкой ещё минут пять - я решил что пора вставать."
            "Правой рукой я потормошил макушку Юле и немножко почесал за ушками."
            "Результат не заставил себя долго ждать и она тотчас же проснулась."
            show uv smile close at center with dissolve
            uv "Приве-е-т."
            "Она было собралась сладко потянуться, однако…"
            show uv guilty close with dspr
            uv "Ай… {w}Больно."
            "…вместо этого произошла неожиданная для кошечки встреча с верхним ярусом кровати!"
            "Состроив недовольную мордашку, она почесала ушибленное место."
            "Вышло у неё это на удивление уморительно. Да настолько, что я не выдержал и засмеялся."
            "Заметив мою реакцию, она вытаращила на меня свои глаза, полные непонимания."
            uv "Чего ты смеёшься, больно же!"
            "Она удивлялась моему поведению так искренне, как умеют удивляться только дети."
            "И эта её реакция пробудила во мне новую волну неконтролируемого смеха, от которого даже немного начало крутить в животе."
            "Юля продолжала смотреть на меня, как на больного."
            "На секунду в голове пронеслось, что если я не угомонюсь, то рискую сильно её обидеть."
            "Однако, в следующую секунду этим сомнениям суждено было развеяться."
            show uv smile close with dspr
            "На её лице появилась лёгкая улыбка, а потом она звонко прыснула, не выдержав."
            "Похоже, я смеялся слишком заразительно."
            "С трудом преодолевая хохот, я сбивчивым голосом кое-как из себя выдавил:"
            me "Че… чего смеёшься, бо…больно же!"
            "Кажется, я смог смутить её, за что был щедро вознаграждён несильным шлепком по носу."
            "Немного полежав на кровати и восстановив дыхание, мы, наконец, поднялись."
            "Тут я вспомнил, что близится обеденное время."
            "Пропускать его совсем не хотелось, так что я поспешил подняться и поскорее одеться." 
        "Побеседовать с Шизой":
            "Судя по монотонному сопению девушки, та явно уснула. Все-таки есть в ней что-то человеческое…"
            "Ко мне же сон идти не хотел. Мысли ураганом носились в моей голове."
            th "Чёрт, нехорошо как-то получилось. Развёл девушку…"
            dreamgirl "Развёл? Серьёзно? А не она тебя?"
            th "Ну она-то да… Но ты прекрасно понимаешь, что я имел в виду."
            dreamgirl "Опомнился, гусар. Раньше надо было думать."
            th "И все же, было в этом что-то неправильное."
            dreamgirl "Конечно, не мне тебя воспитывать, но стоит лишь взглянуть на её уши и хвост… В общем, зоофилией попахивает, дорогой."
            th "А кто подстрекал-то?"
            dreamgirl "Я всего-навсего голос в твоей голове. Не я же тут устроил порнографию и кошачьи ласки?"
            th "Заткнись, пожалуйста. И без тебя вопросов хватает."
            dreamgirl "Пф-ф. Больно надо общаться с зоофилами."
            "Скосив глаза, я присмотрелся к голове, лежащей у меня на плече. Уши как уши, обычные кошачьи. Вот только растут из человека… И хвост. Я прикрыл глаза, вспоминая недавние события. Хвост не выглядел как-то фальшиво. Вполне ощущался руками как обычный кошачий хвост."
            "Чтобы ещё раз в этом убедиться, я легонько потрогал её хвост, пока она спала."
            "Кто она вообще? Как такое возможно?"
            dreamgirl "Ну ко{w=.4}-неч{w=.4}-но, самое время! А ДО ты спросить не мог, неукротимый ты наш?"
            th "Отстань, я сказал."
            dreamgirl "Я-то отстану. Но если вдруг через 9 месяцев окрестные леса заполонят похожие на тебя котята… Мы оба будем знать, кто из нас виноват."
            th "Да чтоб тебя! Ты вообще на чьей стороне, скотина?"
            dreamgirl "Я твой внутренний голос, говорю же. Я озвучиваю твои потаённые желания и мысли, иногда подрабатываю совестью на полставки. И вот сейчас тебе позарез требуется потерзаться!"
            th "Мне?!"
            dreamgirl "Ну не мне же. Я тут сижу и угораю, глядя на происходящее. Зацени - встретил кошкодевочку, приманил плюшками, завёл в бомбоубежище и отодрал так, что шерсть дыбом. Одно слово - педозоофил!"
            "Не найдя слов для достойного ответа, я решил этот выпад проигнорировать."
            "В чем-то он прав, мой внутренний голос. Я всего лишь собирался разобраться, что за чертовщина происходит в этом лагере, выспросить у этой Юли все, что она знает, а дальше… дальше по обстоятельствам."
            "А вместо этого - поддался животным инстинктам!"
            dreamgirl "Девочка, девочка! А пойдём со мной в подвал! Я тебе там конфет дам!"
            "Шиза считала за должное поиздеваться надо мной, но я уже не слушал её, а лежал, просто наслаждаясь моментом."
            "Юля по-прежнему сопела, лёжа на моем уже затёкшем плече. Без одежды было неуютно, но встать я не мог. Кто знает, как она отреагирует на внезапное пробуждение?"
            "Я взглянул на неё."
            "Каштановые волосы были немного растрёпаны, ушки то и дело подрагивали, а хвост ходил из стороны в сторону. Ей явно что-то снилось."
            "А что ей может сниться? Охота на мышей или…"
            "Перед глазами вновь пронеслось наше действо, как раз в этот момент Юля замычала и сморщила носик. От удовольствия или от боли я не знаю, но хотелось верить что от первого."
            "Она заворочалась и я, изловчившись, вытащил руку из под её головы."
            "Стараясь не шуметь, я начал одеваться, изредка поглядывая на обнажённое спящее тело девушки."
            "Я опомнился не сразу, уже с полминуты наблюдая, как она мило дёргает ушками, как спящий котёнок, гоняющийся во сне за мышкой. Нога её изменила положение, обнажив пикантные места…"
            dreamgirl "Хороша, чертовка…"
            "И ведь не поспоришь."
            "Из-за двери, через которую мы пришли, донёсся какой-то шорох."
            "Я так и застыл, стоя на одной ноге, вдевая вторую в шорты."
            "Шорохи был непродолжительными, будто шаги, однако вскоре они затихли, но затем в дверь ударили."
            "Кошачье ушко дёрнулось и повернулось в сторону шума, у меня же в висках застучала кровь с такой силой, что мне даже показалось, будто кто-то продолжает барабанить в дверь бункера."
            show uv surprise close at center with dissolve
            "Юля открыла один глаз, обвела взглядом комнату, полуодетого меня, взглянула на железную дверь, откуда секунду назад доносились шорохи щебёнки. Она открыла второй глаз и потянулась."
            "Мне показалось, будто за дверью нас прекрасно слышали."
            "Я тут же жестами начал всячески призывать Юлю к тишине, но она, потупив взгляд, зевнула, обнажив слегка заострённые клыки."
            "За дверью снова начали доноситься шорохи, Юлины зрачки сузились, а хвост тут же вытянулся по струнке."
            "Шорохи удалялись."
            "Я выдохнул с облегчением."
            uv "Человек. Ушёл…"
            show uv normal close with dspr
            "Она тут же упала на кровать, снова потянувшись. Я снова напряжённо вдохнул, так как кровать громко скрипнула, но шорохов слышно не было, и я успокоился."
            "Шорты были уже застёгнуты, я потянулся к рубашке, на которой устроилась Юля. Она смотрела, не мигая на меня, её хвост ходил туда-сюда."
            "Мне даже показалось, будто она смотрит на меня как на добычу."
    me "Позволь…"
    "Юля хмыкнула и села. Она прижала руки к обнажённой груди…"
    dreamgirl "Хэй, гусар-зоофил, поаккуратнее…"
    "Я только сейчас понял, что я пытаюсь почти вслепую завязать себе галстук, ибо мой взгляд не отходил от её груди ни на секунду."
    "Выдав себе мысленного леща, я всё-же отвернулся и попытался завязать, наконец, этот грёбаный пионерский галстук."
    show uv laugh close with dspr
    "Юля хихикнула, наблюдая, как я в третий раз повязываю галстук и снова неудачно."
    uv "Не понимаю, зачем вам эта красная повязка? Ужасно неудобно."
    me "Сам не понимаю. Такой у нас строгий дресс-код."
    uv "Без одежды значительно удобней…"
    uv "Только ветки больно царапаются, и зимой холодно…"
    show uv normal close with dspr
    "Юля тоже стала натягивать свою нехитрую одёжку."
    me "Ты сказала человек. Ну, шуршал за дверью."
    uv "Ага. Наверное, тот самый… В очках."
    me "И как нам выбраться, чтобы он нас не заметил?"
    uv "Вот ещё дверь."
    "Юля ткнула пальчиком в сторону ещё одной железной двери."
    uv "Только она тоже не открывается."
    me "Ничего, сейчас что-нибудь придумаем."
    "За разговором мне удалось-таки повязать себе несчастный галстук."
    th "Хоть пальцы узлом не повязал в процессе - и то хорошо."
    "Подобрав рюкзак и перекинув его за спину, я подошёл к двери."
    me "А куда она ведёт?"
    uv "В какой-то туннель… А там спуск в шахту, где я ловлю мышей."
    uv "Я была с другой стороны, но открыть так и не смогла."
    show uv smile close with dspr
    "Она игриво облизала губы и улыбнулась."
    "Мышей… Нелегко бедной девочке."
    me "Надо успеть до обеда. Не хотелось бы попасть под разнос от вожатой."
    uv "Так туннель ведёт под площадь. Я часто там бывала, когда бегала за едой."
    dreamgirl "Созналась, воровка. Пришло время её наказать, жестоко наказать, товарищ следователь."
# Добавим про чистосердечное для увязки с alt_day4_uvao_meet_Yulia_after_lunch
    th "Оставь свои пошлые намёки. Да и к тому же, она ещё вчера днём чистосердечно во всём созналась."
    dreamgirl "Фи, какие мы серьёзные."
# Собственно, а какие проблемы открыть дверь? Тем более, сделаем вид, что изнутри кремальера крутится и без нажатия кнопки
# У товарища автора нажатие кнопки изнутри тоже требовалось - Ленофаг
#    me "Хм, как же её открыть?"
#    "Я оглядел бункер, но ничего способного мне помочь, не нашёл."
    uv "Так мы туда пойдём? Там тоннель. Я там часто хожу."
    me "Ну, ладно. Пошли."
    "Я подошёл к двери, не без труда повернул колесо и, навалившись всем весом, открыл её. Юля проворно выбралась за мной. Я закрыл дверь за собой и попробовал повернуть колесо, но то повернулось лишь на несколько градусов и его тут же заело."
    "Плюнув на бессмысленное занятие, я отряхнул ладони от ржавчины."
    "Нога уже немного отошла, хоть и ныла периодически. Тем не менее, я был в состоянии продолжать путь на своих двоих."
    jump alt_day5_uvao_mines_begin

label alt_day5_uvao_mines_begin:
# Д5 - Шахты.
#
# Используется флаг alt_uvao_D5_sh_mines. Если спали - видим Шурика в шахтах.
#
    window hide
    scene bg int_catacombs_entrance
    show uv smile at center 
    with dissolve
    window show
    "Вышли мы, вроде, без особых проблем."
    me "Ну-с, куда теперь?"
    "Юля с любопытством оглядела туннель и ткнула своим пальчиком в черноту."
    uv "Туда."
    "Мне ничего не оставалось, кроме как следовать за моим проводником."
    "Туннель был везде одинаковым, так что я не мог с полной уверенностью сказать, как долго мы по нему шли."
    "На пути я часто задумывался, из-за чего Юле приходилось меня ждать или постоянно окрикивать."
    "Я вспомнил что она сказала мне про Саныча и Ольгу Дмитриевну и невольно начал смеяться."
    dreamgirl "Это же надо, Ольга Дмитриевна и Саныч. Кто бы мог подумать."
    th "А они что, не люди что ли?"
    dreamgirl "Люди-то люди, но заниматься этим на пляже. Не каждая вожатая такое себе позволит."
    th "Ночь, полная луна и пляж выполнили свою работу на отлично."
    dreamgirl "А пока вожатая по походам шастает, он к Виоле наверное ещё наведывается. С каким-нибудь вывихом или ушибом."
    "Стоило только подумать о Виоле, как сразу побежали мурашки по всеми телу."
    th "Виола и на \"{i}пионэрах{/i}\" неплохо отрывается. Хотя это тоже не исключено."
    window hide
    scene bg int_catacombs_hole
    play ambience ambience_catacombs_stones fadein 3
    window show
    "Пока я болтал с Шизой, Юля уже куда-то пропала."
    th "Может, я отстал? Надо бы ускорится!"
    dreamgirl "Полегче, под ноги-то смотри."
    "Это предупреждение внутреннего голоса оказалось очень своевременным - взгляд выхватил впереди огромную яму."
    "Я подошёл к ней и глянул вниз, однако в темноте дна не было видно."
    uv "Давай сюда."
    "Я легонько вздрогнул от неожиданности и не сразу понял, откуда доносится голос Юли. Как оказалось - откуда-то снизу. Наверное, пока я был занят столь увлекательным диалогом с самим-собой, она уже спрыгнула."
    dreamgirl "…И я покажу тебе, наколько глубока котовья нора."
    if alt_uvao_D5_hentai:
        extend " Хотя ты в принципе уже все, что можно, у нее разведал."
    th "Отвянь, озабоченный."    
    "Впереди красовался тупик, а мой проводник канул в темноту. Выбора не было…"
    th "Надеюсь, там не высоко."
    "И я прыгнул в дыру."
    #play sound sfx_jump_into_hole_2 - опять таки в комменты
    window hide
    play music music_list["door_to_nightmare"] fadein 3
    scene bg int_mine
    show uv normal at center
    with dissolve
    window show
    "Скатившись по насыпи в шахту и отряхнув одежду, я разглядел в темноте силуэт Юли. Нежные пальцы обвили мою ладонь и легко потащили за собой."
    uv "Здесь темно. Давай лапу."
    me "Руку."
    uv "А?"
    me "Мы зовём это рукой."
    uv "Как хочешь. Давай сюда, свою эту, руку."
    scene bg black with dissolve
    "Под ноги подворачивались камни и шпалы, я то и дело спотыкался, но Юля, будто не обращая внимания, вела меня дальше. Мы шли в полной темноте."
    "Я уже начинал чувствовать себя слепым, как вдруг вспомнил одну важную вещь."
    if alt_uvao_D5_hentai:
        th "У меня же в рюкзаке спички есть!"
        me "Юля, подожди минутку."
        "Отпустив её руку и сняв свою ношу со спины, я быстро пустил в него руку и стал их искать. {w}Спустя пару мгновений заветный коробок был найден."
        "Взяв спичку, я тут же зажёг её, а коробок положил в карман."
    else:
        th "У меня же спички в кармане лежат!"
        me "Юля, подожди минутку."
        "Взяв из кармана заветный коробок, я достал из него спичку и зажёг её." 
    window hide
    play sound match_lights
    scene bg int_mine_crossroad with fade2
    show uv normal at center
    with dissolve
    window show
    th "Какой-то перекрёсток?"
    "Спичка догорела почти до конца, и я бросил ее, чтобы не обжечься."
    scene bg black with fade2
    uv "Направо."
    me "Я ведь ничего не вижу. Спички быстро сгорают."
    "Она усмехнулась."
    uv "Я тебя поведу. За ла…, ой, то есть за руку."
    dreamgirl "А она быстро учится! Только смотри плохому её не научи."
    if alt_uvao_D5_hentai:
        dreamgirl "Если уже не научил."
        "Я невольно вспомнил то, что мы вытворяли в бункере. Эх… как же она хороша."
        "Наверное, будь моя воля, я бы повторил то же самое с прямо здесь."
    else:
        dreamgirl "А ведь мог, если бы ты не был таким тормозом. У тебя был просто идеальный шанс, но и тут ты умудрился всё запороть."
        th "Отвали. Не хватало ещё что бы мой первый раз в лагере был с кошкодевочкой, которая в реальности может и не существует."
        dreamgirl "Зато вспомнил бы хоть как это делается ради приличия. Чтобы не облажаться перед другими пионерками в будущем, так сказать."
        "Разговоры с Шизой, как со старшим братом, который никогда не упустит момент чтобы подшутить надо мной, стали меня конкретно раздражать."
    "Я немного прибавил шагу, чтобы не отставать, и чуть сжал её руку."
    uv "Налево."
    "Я зажёг ещё одну спичку."
    window hide
    play sound match_lights
    scene bg int_mine_crossroad with fade2
    show uv normal at center
    with dissolve
    window show
    th "Ещё один перекрёсток? Как она вообще тут ориентируется? Ещё и в кромешной темноте."
    "К сожалению, эта спичка была точно такая же, что и предыдущая, и уже готова была погаснуть."
    scene bg black with fade2
    "Выкинув её, чтобы не обжечься, я нащупал в темноте тёплую руку своей проводницы и мы пошли дальше."
    # можно было бы зациклить этот кусок, чтобы он игрался параллельно тексту - зажечь спичку, посмотреть на перекрёсток, потушить. Кажись что-то такое видел в документации
    # Да вроде всё норм. - Ravsii
    "Так мы и ходили. То направо, то налево. Я иногда зажигал спички, но все перекрёстки были похожи как близнецы."
    "Поэтому я целиком и полностью положился на своего странного провожатого -  она каждый раз зачем-то озвучивала, в какую сторону мы поворачиваем."
    "Может быть, она хотела поговорить о чём-то, пока я молча плёлся за ней?"
    dreamgirl "Не льсти себе."
    "Очередной подкол Шизы я решил проигнорировать."
    if alt_uvao_D5_sh_mines:
        uv "Стой!"
        "Вдруг резко сказала Юля."
        "Я застыл столбом в ожидании следующих приказаний."
        uv "Там опять кто-то есть."
        play sound match_lights
        window hide
        scene bg int_mine_crossroad with fade2
        show uv normal at center
        with dissolve
        window show
        th "Я догадываюсь, кто это может быть."
        uv "Снова он, в очках. Друг твой."
        th "Похоже, это означает, что я не ошибся. {w}Хотя погодите. {w=.5}\"Друг\"?"
        me "Да не друг он мне!"
        show uv smile at center with dspr
        "Грозно сказал я, притопнув ногой. Похоже, это зрелище её очень сильно позабавило."
        th "Неужели мы и правда настолько похожи, что нас можно принять за друзей? Да ну, тупость какая-то."
        uv "Ой, да, прости."
        scene bg black with fade2
        uv "Он где-то там."
        "Я услышал взмах. Наверное она показала в какую-то сторону, да вот только я ничего не видел."
        uv "Пошли."
        jump alt_day5_uvao_mines_sh
    else:
        uv "Направо."
        "Я покорно проследовал за моим всевидящим спутником. Да и что мне оставалось, в темноте я всё-равно ничего не вижу, а тратить спички мне уже давно надоело."
        "Кроме своих указаний, в какую сторону идти, она ничего не говорила. Да и мне не очень-то и хотелось начинать разговор, так что я просто плёлся, держась за её руку."
        jump alt_day5_uvao_from_mines_to_dinner

label alt_day5_uvao_mines_sh:
# Д5 - Шахты. Развилка с Шуриком.
#
    #"Она шла быстрее, чем обычно, словно предчувствуя какую-то беду."
    play music music_list["doomed_to_be_defeated"] fadein 3
    "Она ускорила шаг и сжала мою ладонь чуть сильнее, словно предчувствуя беду."
    "Вдруг Юля остановилась, а я, продолжая идти, чуть не врезался в неё."
    uv "Стой. Не ходи дальше."
    me "Почему?"
    uv "Тихо."
    "Я не очень понял, что происходит, но решил подчиниться."
    "Юля некоторое время почти не шевелилась. Потом она выдала:"
    uv "Он там, дальше."
    "Первым делом я решил осмотреться, куда же этот Шурик притащил нас."
    window hide
    play sound match_lights
    scene bg int_mine_halt with fade2 #поворот с поваленой вагонеткой
    show uv dontlike at center
    with dissolve
    window show
    "Брошенные инструменты, какие-то разбитые ящики. Рельсы заворачивали вправо, и у стены лежала опрокинутая вагонетка."
    th "Наверно, слетела с рельсов на повороте. Катались они на ней, что ли?"
    "Я бросил спичку, чтобы не обжечь пальцы, и все погрузилось во тьму…"
    window hide
    scene int_mines_halt_spotlight with fade2 # тот же поворот, только темный и с подсвеченой стеной за углом
    show uv normal at cleft
    with dissolve
    window show
    "…но не полностью! Я заметил какой-то тусклый свет из-за поворота."
    # зум на пятно света?
    # Сделано - Ravsii
    window hide
    hide uv
    scene int_mines_halt_spotlight:
        zoom 1.0 xalign 0.5 yalign 0.4
        linear 2.5 zoom 1.7 xalign 0.8 yalign 0.4
    #show uv normal at cleft
    with dissolve
    window show
    th "Это Шурик там? Больше некому, вроде. Но какого черта ему здесь надо?"
    dreamgirl "Может, плутоний добывает?"
    th "Ка…какой плутоний?"
    dreamgirl "Оружейный, балда! Кирку взял и копает! Подкрадись и посмотри, чем он там занят, вместо того, чтоб гадать!"
    "Я воспользовался советом и стал осторожно подкрадываться, стараясь не споткнуться и не шуметь. Юля кралась рядом."
    "Вскоре мы подобрались достаточно близко, чтобы выглянуть из-за угла."
    window hide
    scene cg d4_sh_sit
    window show
    "Шурик действительно был там. С фонариком, с какой-то сумкой через плечо - он явно подготовился к походу в катакомбы."
    "Впрочем, выглядел он совершенно не как храбрый исследователь подземелий."
    "Он скорчился в какой-то нише в стене, раскачивался и что-то бормотал."
    sh "… Пришёл… {w}А тут пусто… {w}Они говорили - налево… направо…{w} Куда вели?…{w} Я правильно шёл…{w} И никого…{w} Надо искать…{w} Она где-то здесь…{w} Тогда поверят…"
    "Его поведение меня довольно сильно испугало."
    th "Да у него совсем крыша съехала… Черт, и что же с ним теперь делать? Нельзя же его тут оставить!"
    dreamgirl "А я думаю - можно. Лучше сначала самому выбраться, а за ним вернуться потом, с санитарами и успокоительным."
    th "Да хрен его потом тут найдёшь, в шахтах-то. Надо как-то увести его отсюда. Но как?"
    dreamgirl"Может, оглушить его? А потом как мешок картошки вынести."
    dreamgirl "Хлороформ бы для этого идеально подошёл. Подкрался бы сейчас к нему со спины, спросил бы, как пройти в библиотеку…"
    th "Ну ты-то хоть с ума не сходи.{w} Да и вообще, даже если удастся его оглушить, как я его понесу потом? Он же явно потяжелее мешка картошки будет."
    dreamgirl "Моё дело предложить. Давай попробуем с ним поговорить тогда, вдруг он не совсем псих?"
    window hide
    scene bg int_mine_coalface
    with dissolve
    window show
    "Я поднялся, и медленно, стараясь излучать дружелюбие, вступил в круг света."
    me "Шурик! Привет, а ты что здесь делаешь?"
    show sh scared pioneer far
    "Шурик вздрогнул, вскочил и вжался в стену."
    sh "Семён?… Это ты?…"
    me "Я, я. Не видишь что ли?"
    sh "А где… она?"
    me "Она? Кто - она?{w} Шурик, ты себя нормально чувствуешь?"
    sh "Не притворяйся! Я всё про тебя знаю! Ты - ненастоящий!"
    me "Шурик, подожди… Давай успокоимся…"
    sh "Не подходи! Говори, где она?!"
    me "Да кто она-то?"
    th "Он что, Юлю сюда пришёл искать?!"
    sh "Ты знаешь!…{w} Ты должен мне сказать…{w} Говори, где она!"
    show sh angry bar3 with dspr #Палка опущена
    "Он угрожающе шагнул ко мне, сжимая в руках неведомо откуда взявшуюся железяку."
    me "Эй, эй! Ты что творишь, псих?! Это же я, Семён!"
    "Я попятился, а он продолжал приближаться, поднимая свое орудие."
    show sh angry bar with dspr #Палка посередине
    sh "Ты…{w} мне…" 
    show sh angry bar2 close with dspr #Палка в замахе
    extend " всё…{w} расскажешь…"
    dreamgirl "Берегись!"
    window hide
    show cg d4_sh_stay
    window show
    "Он ринулся вперёд, целя дубиной мне в голову."
    "Я рефлекторно отпрыгнул назад,{w} но оступился на камнях и рухнул на спину."
    with vpunch
    window hide
    scene bg int_mine_coalface
    show sh angry bar3 far at cright #Палка опущена
    show uv rage at cleft
    with dissolve
    window show
    "Внезапно, с каким-то странным яростным мявом из-за камней выпрыгнула Юля и встала между нами, как бы защищая меня."
    show sh scared pioneer far at cright with dspr
    sh "А… а…{w} Она здесь?…{w} Я не…{w} нет…"
    "Он забормотал что-то неразборчивое, отступая в темноту."
    hide sh with easeoutright
    "Потом внезапно развернулся и со всех ног бросился прочь - туда, откуда мы пришли."
    show uv guilty close with dissolve
    "Юля наклонилась ко мне."
    uv "Вставай. Ты не ушибся?"
    me "Вроде бы нет."
    "Я поднялся и отряхнулся."
    show uv normal with dissolve
    th "И что теперь с этим полудурком очкастым делать? Искать его по всей шахте?"
    dreamgirl "Тебе железяки в голову показалось мало? Охота ещё лопатой по шее получить?"
    th "Ну блин, явно же не в себе человек! Если его бросить - навернётся, сломает себе что-нибудь и так и сгинет.{w} А потом сюда пригонят спасателей, они тут все перероют… И Юлю уже точно найдут!"
    me "Юля. Ты не знаешь, куда он побежал?"
    uv "Обратно туда. Откуда мы пришли."
    me "Проведёшь? Надо его отсюда выгнать, вернуть заблудшую душу в коллектив, так сказать."
    show uv smile with dspr
    uv "Хорошо, пошли. Я его буду выгонять."
    th "Как это она его будет выгонять?"
    dreamgirl "А ты заметил, то как только она выпрыгнула - Шурик сразу весь боевой пыл растерял и драпанул? Не так уж она проста, наша кошечка…"
    "Я подобрал фонарик Шурика - трофей, взятый в бою! Теперь не придётся спотыкаться в темноте и жечь спички."
    th "Он, получается, без света убежал? Тогда мы его быстро нагоним."
    dreamgirl "У него в сумке мог быть запасной. Он, похоже, изначально собирался лезть в подземелья, вот и подготовился."
    me "Идём скорее!"
    "Не медля, она взяла мою руку и резко направилась в ту сторону, в которую побежал Шурик."
    window hide
    play sound_loop sfx_slavya_run fadein 2
    scene bg int_mine_crossroad with fade2:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    window show
    "Идти с фонарём было гораздо быстрее, чем спотыкаться в полной темноте. Почти бегом мы пересекли несколько перекрёстков - каждый раз Юля без тени сомнения выбирала направление."
    window hide
    scene bg int_mine_crossroad with fade2:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    window show
    th "Интересно, это она на слух определяет, куда идти, или следы замечает?"
    stop sound_loop fadeout 2
    window hide
    scene bg int_mine
    show uv normal
    with dissolve
    window show
    "Кажется, мы его нагоняли - мне иногда чудился какой-то свет впереди. Все-таки у него нашёлся запасной фонарь."
    "За очередным поворотом распахнулся длинный прямой штрек, в потолке которого зияла осыпавшаяся дыра."
    me "Это мы здесь спускались?"
    "Спросил я, пытаясь не сбиться с дыхания."
    uv "Нет, это другой вход. Тот там."
    "Она махнула рукой куда-то вправо. Там - значит там, сейчас это без разницы."
    uv "Он наверх вылез."
    me "Значит и мы за ним!"
    "Нас обоих разбирал азарт погони. Я с трудом представлял, что я буду делать, когда догоню его, но это все казалось неважным - впереди добыча! И она уходит!"
    window hide
    scene bg int_catacombs_entrance
    show uv upset close
    with dissolve
    window show
    "Юля забралась первая, и это сыграло с ней злую шутку. Ей пришлось помогать мне забраться обратно наверх. Такая перспектива, похоже, её не очень радовала, но деваться ей было некуда."
    "Наверху оказался тоннель - похоже тот самый, в который вёл люк из старого корпуса."
    show uv dontlike close with dspr
    me "Это тот, первый тоннель?"
    uv "Да. Лестница там, а он побежал туда. Там тоже выход есть."
    "Она махнула вперёд, где брезжил какой-то свет."
    hide uv
    "Мы продолжили преследование, и через пару минут увидели тот самый выход - обвалившийся потолок тоннеля, выводящий на поверхность посреди леса. Я прикинул высоту - до поверхности где-то метра три."
    window hide
    scene int_catacombs_entrance_light
    show uv dontlike
    with dissolve
    window show
    uv "Он наверх вылез."
    "Констатировала Юля и, похоже, приготовилась взбираться по торчащим из стены крюкам, по которым протягивались кабели."
    me "Эй, погоди! Я тут не влезу! Как тут Шурик вообще вскарабкался?"
    th "Вроде бы навыков ниндзя я за ним не замечал…"
    dreamgirl "Когда за тобой гонится крупный хищник из рода кошачьих, навыки ниндзя проявляются у всех."
    uv "По верёвке. Пошли по лесенке тогда."
    "Мы развернулись и побежали обратно."
    window hide
    scene bg int_catacombs_entrance
    show uv normal
    with dissolve
    window show
    "Я первым заметил лестницу, вскарабкался по ней и откинул люк. Свет, бьющий через щели в забитом досками окне, ослепил меня и заставил зажмуриться."
    # засветка, потом заброшенный корпус изнутри, 
    window hide
    stop ambience
    $ meet('mt', 'Голос')
    $ day_time()
    $ persistent.sprite_time = "day"
    scene int_old_building_day_uvao
    with flash
    window show
    "Яркий свет ударил мне в глаза."
    show uv shocked with dissolve
    "Суда по всему, не мне одному."
    "Мы по немного привыкли к солнечному свету, как вдруг с улицы раздался чей-то крик."
    show uv surprise with dspr
    mt "ШУ-У-У-УРИ-И-И-ИК!"
    th "Голос знакомый. Ольга Дмитриевна?"
    dreamgirl "Не мы одни тут на психованных изобретателей охотимся."
    "Мы с Юлей подобрались к окну и одновременно заглянули в щель."
    window hide
    $ meet('mt','Ольга Дмитриевна')
    scene bg ext_old_building_day
    show mt pioneer normal veryfar:
        xalign 0.001 yalign 0.9
    show sl pioneer serious veryfar:
        xalign 0.1 yalign 0.9
    show un pioneer serious veryfar:
        xalign 0.2 yalign 0.9
    show el pioneer normal veryfar:
        xalign 0.3 yalign 0.9
    with dissolve
    window show
    "Спасательная команда в составе Ольги Дмитриевны, Слави, Лены и Электроника стояла посреди двора и озиралась." # Состав спасательной команды вроде бы такой?
    th "И как они теперь будут его искать? Наверное, придётся нам прятаться обратно в тоннелях, или в убежище…"
    if alt_uvao_D5_hentai:
        dreamgirl "Ага-ага, спрятаться, и ещё разок объездить кошечку. Ну, чтоб время скоротать."
        "Я отмахнулся и от лезущей в голову похабщины, и от моего назойливого альтер эго."
    "Пионеры тем временем продолжали звать потерянного товарища на все лады."
    mt "Шу-у-у-у-ури-и-и-ик! Шу-у-у-ури-и-и-ик!"
    sl "Ау-у-у-у, Шу-у-ури-и-и-ик!"
    el "Эге-ге-е-ей, Шу-у-ури-и-ик!"
    th "Что-то я сомневаюсь, что он сам добровольно выйдет…"
    show sh angry bar3 veryfar behind mt with dissolve:
        xalign 0.95 yalign 0.999
    "И в этот момент из зарослей появился сам виновник торжества. Чумазый, ободранный, и все с той же приснопамятной железякой в руке."
    "Не тратя времени на разговоры, он размахнулся своим рубилом и бросился на замерших в испуге спасателей."
    th "Он сейчас точно кому-нибудь голову проломит!"
    "Я уже собрался было выскочить на подмогу, но… Непонятно как спасатели повалили нашего Рэмбо и старательно закрутили ему руки."
    show sh pioneer rage veryfar behind mt:
        xalign 0.9 yalign 0.85
    show un pioneer serious veryfar behind mt:
        xalign 0.8 yalign 0.9
    with dspr
    "Лена в это время вырвала арматурину из его рук и выкинула куда подальше."
    mt "Ну-ка, переверните его!"
    show mt pioneer angry veryfar:
        xalign 0.8 yalign 0.85
    show un pioneer serious veryfar:
        xalign 0.7 yalign 0.9
    with dspr
    "Вожатая наклонилась над ним{w} и отвесила несколько смачных оплеух! А Славя с Электроником в это время взяли его под руки, чтобы не убежал."
    show mt pioneer angry veryfar:
        xalign 0.5 yalign 0.85
    show un pioneer normal veryfar:
        xalign 0.65 yalign 0.85
    show sl pioneer normal veryfar behind sh:
        xalign 0.8 yalign 0.85
    show el pioneer normal veryfar behind sh:
        xalign 0.999 yalign 0.85
    with dspr
    "Внезапно, это подействовало - Шурик перестал вырываться из рук Слави и Электроника, и в целом вроде как подуспокоился."
    dreamgirl "Вот что значит советская педагогика! А если б ногами - вообще был бы как шёлковый!"
    "Вожатая о чем-то спрашивала у Шурика, но тот явно не мог ничего объяснить, только растерянно озирался по сторонам."
    "Вскоре вся поисковая партия двинулась по направлению к лагерю. Шурика шатало туда-сюда, и шёл он только благодаря поддержке Эла и Слави."
    hide un
    hide sl
    hide sh
    hide el
    show mt pioneer normal veryfar:
        yalign 0.85
    with dspr
    "Ольга Дмитриевна зачем-то обернулась и озабоченно обвела взглядом поляну. По-видимому, не найдя ничего подозрительного, она повернулась и скрылась за деревьями, вслед за остальными."
    hide mt with dspr
    # Немного выжидаем, болтаем с Юлей, топаем в лагерь вместе.
    # Романтичная сцена прощания на стоянке, которую портит ОД
    return
    #jump событие_с_отрядом

label alt_day5_uvao_from_mines_to_dinner:
# Д5 - Идём на обед после шахт.
#
    window hide
    scene bg int_mine_door
    show uv normal at center
    with dissolve
    window show
    uv "Пришли."
    hide uv with dissolve
    "Она отпустила мою руку."
    "Пока мы ходили в полной темноте - я уже начал чувствовать себя слепым."
    "Как вдруг я вспомнил."
    if alt_uvao_D5_hentai:
        th "У меня же в рюкзаке есть спички!"
        "Сняв свою ношу со спины, я быстро пустил в него руку и стал их искать. {w}Спустя пару мгновений заветный коробок был найден."
        "Взяв из него спичку я зажёг её, а коробок положил в карман. Вдруг ещё пригодится."
    else:
        th "У меня же спички в кармане лежат!"
        "Взяв из кармана заветный коробок, я достал из него спичку и зажёг её."
    "Через пару мгновений я наконец перестал моргать с огромной скоростью. Мои глаза немного привыкли к маленькому огоньку, который исходил от спички, и я смог, наконец, осмотреться."
    "Пришли мы к какому-то подобию шахтёрского привала. Слева на стенке висели старые фонари, которые назывались в честь какого-то ночного животного." #Тут не уверен, что правильно описал, так что поправьте, если что.
    dreamgirl "\"Летучая мышь\" такие фонари называются, гений. С такими, наверное, ещё твой дед ходил."
    "Впрочем, и от Шизы бывает иногда польза."
    "По правую сторону стояли лопаты и несколько вёдер, а в самом центре красовалась большая деревянная дверь с железными ручками."
    "Раздался небольшой скрип."
    show uv normal at center with dissolve
    uv "Сюда."
    "Юля тем временем немного приоткрыла дверь и ожидала меня. Не медля я пошёл в её сторону."
    window hide
    scene bg int_mine_room
    show uv normal at center
    with dissolve
    window show
    "Под ногу подвернулась бутылка, и я мешком упал на пол."
    me "Ч-чёрт!"
    "В помещении проникало немного света и я, привыкший к темноте, разглядел обстановку. Изрисованные стены, бутылки, мусор по углам, Юля, смотрящая на меня своими светящимися глазами."
    uv "Дальше наверх, только там замок."
    "Я тут же подобрал лежащую рядом арматуру."
    me "Может этим?"
    uv "Не стоит." 
    uv "Я тут недавно кое-что нашла."
    show uv smile with dspr
    "Она держала в руке какой-то ключик."
    "Юля осмотрела комнату, нырнула за большую трубу, покопошилась немного и вынырнула прямо под свет. Она забралась по лестнице к решётке в потолке, немного поковыряла в замке и спустилась вниз."
    window hide
    scene bg int_mine_exit
    show uv normal close at center
    with dissolve
    window show
    uv "Вот. Закрой замок за собой и брось ключ мне. Не хочу, чтобы ваши лазали сюда."
    "Я кивнул и взял из протянутой руки ключик."
    me "Эм-м… Спасибо."
    show uv smile close at center with dspr
    "Юля улыбнулась и ответила:"
    uv "Принесёшь ещё булочек? Я буду ждать."
    "Я кивнул ей и полез наверх."
    "Поворот ключа и уже старый ржавый замок вновь защищал лаз от посторонних. Я просунул ключик между прутьями решётки и бросил его вниз, после чего выпрямился и огляделся. Обед должен был начаться с минуты на минуту."
    window hide
    scene bg ext_square_day with flash
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day fadein 5
    window show
    "Солнце было ярким и мне пришлось сперва привыкнуть к свету. Когда мои глаза стали различать окружающие площадь деревья, я поднялся на ноги, держась за пыльный постамент, на котором стоял загадочный Генда. Он все так же надменно следил за происходящим на площади."
    un "Где ты был?"
    show un normal pioneer far at center with dissolve
    "От неожиданности я подпрыгнул."
    "Лена сидела на лавочке, неподалёку от того места, откуда я только что вылез."
    dreamgirl "Бегал… по шахтам… с девочкой-кошкой. Так ей и скажи."
    me "А, это ты. Ну, я…"
    un "Тебя вожатая искала."
    "Она старалась не смотреть на меня, и крепко обнимала книжку, будто надеясь, что та укроет её от моего бескрайнего сумасшествия."
    un "Почему ты такой грязный?"
    "Она бегло обвела меня взглядом и снова повторила вопрос:"
    un "Где ты был?"
    me "Я… Ну, это…"
    play music eat_horn fadein 3 
    "В голове уже пролетели тысячи отмазок как вдруг меня спас сигнал на обед."
    "Фух, пронесло."
    dreamgirl "Вышло, значит, господин гусар, и рыбку съесть, и на кошечку залезть."
    me "Обед уже. Давай поторопимся."
    "Я старался придать голосу небрежность, но как мне показалось, получилось не особо удачно. Оставив недоумевающую Лену на площади, я побежал в столовую, попутно отряхивая пыльные шорты и рубашку."
    stop music fadeout 3
    jump alt_day5_uvao_lunch

label alt_day5_uvao_true_back:
# Д5 - Поход наверх. Тру рут.
#
    "Начать на выходе из бункера."
    "Возвращаемся по верху!!!"
    return