init:
    $ alt_uvao_D4_viola_morning = False
    
label alt_day4_start_uvao:
    $ routetag = "uv_unknown"
    $ alt_chapter_uv(4, u"Утро")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg int_house_of_mt_sunset
    play ambience ambience_int_cabin_day fadein 4
    window show
    "Проснулся я сам, спасая собственное сознание от какого-то дурацкого сна. Что-то про запчасти от когтистой перчатки… {w}Бред, короче. Впрочем, немудрено, после вчерашних-то событий."
    th "Так, будем рассуждать последовательно."
    th "Вчерашняя ушастая тень. Могла она мне привидеться?{w} Могла. Других свидетелей поблизости не было, подтвердить некому."
    dreamgirl "А я?"
    th "Если я привлеку тебя как свидетеля, это обеспечит мне путевку в веселый дом еще надежнее, чем кошастые галлюцинации."
    th "Как проверить, глючит меня или нет? Очевидное решение - наведаться к Виоле."
    dreamgirl "О, йа-йа, доктор Виола пропишет тебе {i}процедуры{/i}, после которых тебя перестанут волновать воображаемые девочки!"
    "Я внутренне содрогнулся - воспоминания о манере Виолы проводить обследования были еще свежи."
    th "Какие еще варианты?"
    dreamgirl "Например, поспрашивать местных, вдруг они все в курсе про эту местную достопримечательность? Просто избегают про нее упоминать.{w} Почему-то."
    th "Может быть. Тут все может быть."
    play music music_list["my_daily_life"] fadein 3
    "Я сел в кровати и принялся одеваться."
    play sound sfx_open_dooor_campus_1
    "Скрипнула дверь, и в домик вошла Ольга Дмитриевна."
    show mt normal panama pioneer with dissolve
    mt "Семен? Уже проснулся?{w} Хорошо, умойся, причешись, и марш на линейку! Сегодня все должны примерно выглядеть.{w} Включая тебя!"
    me "А по какому поводу?"
    show mt angry panama pioneer with dspr
    mt "Родительский день! Я же говорила вчера на линейке. Опять все мимо ушей пропускаешь?"
    me "А, ну да. Забыл."
    mt "То-то же!"
    "Оставив последнее слово за собой, вожатая вышла за дверь, позволяя мне одеться."
    hide mt with dissolve
    window hide
    $ renpy.pause(3)
    scene bg ext_house_of_mt_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 6
    window show
    "Я обнаружил вожатую неподалёку от домика - она терпеливо дожидалась, пока некий нерадивый пионер соизволит подняться и одеться."
    show mt normal panama pioneer with dissolve
    "Тут мне в голову пришла одна мысль, которую я не мог не проверить."
    th "Родительский день, значит? Интересно, а что она ответит на это…"
    me "А мои родители не приехали?"
    show mt smile panama pioneer with dspr
    mt "Приезжали рано с утра, оставили тебе мешок гостинцев, и уехали обратно."
    me "Мешок? А где он?"
    mt "В вожатской остался, не буду же я с ним по всему лагерю бегать! После обеда выдам, если хулиганить не будешь."
    show mt normal panama pioneer with dspr
    mt "А вообще - хватит болтовни, давай бегом к умывальникам и на площадь!"
    th "Пожалуй, между линейкой и медпунктом я выберу медпункт."
    me "Ольга Дмитриевна, можно я сначала в медпункт загляну?"
    show mt surprise panama pioneer with dspr
    mt "А что такое?"
    me "Да что-то странности какие-то со мной творятся, со вчерашнего дня. Видения…"
    show mt normal panama pioneer with dspr
    mt "Ну-ка, давай выкладывай, что стряслось? Что еще за видения?"
    menu:
        "Рассказать про тень.":
            th "А почему бы и не рассказать? Закончится-то все равно отправкой к Виоле."
            me "Помните, у меня вчера живот прихватило?"
            show mt sad panama pioneer with dspr
            mt "Конечно, помню. Как раз на мой танец!"
            dreamgirl "Ее это так цепляет? Пользуешься успехом, Семен! Но не ты ее танцевал, не тебе ее ужинать."
            me "Так вот, я потом увидел, что около столовой кто-то ходит.{w} Подошел поближе, думал, Алиса чем-нибудь поживиться решила."
            show mt normal panama pioneer with dspr
            me "Смотрю – а там девочка незнакомая. {w}С кошачьими ушами и хвостом. Как-то так…"
            play music music_list["awakening_power"] fadein 0
            show mt angry panama pioneer with dspr
            dreamgirl "Беги, Форест, беги! Судя по ее виду, тебя сейчас будут съедать заживо!"
            mt "Что за чушь? Семен, ты издеваешься?! Если решил прогулять линейку и зарядку таким образом – не выйдет!"
            mt "Или ты меня разыгрываешь, или тебя разыграли, а ты и рад поддаться!" 
            mt "Так, никакого медпункта, марш умываться! И чтобы я больше такой чепухи не слышала!"
            "Волной ее негодования меня просто снесло в направлении умывальников."
            hide mt with moveoutleft
            "А Ольга Дмитриевна с недовольным и озабоченным видом направилась в сторону площади."
            stop music fadeout 5
            window hide
            jump alt_day4_uvao_morning_wash
        "Не рассказывать":
            th "Что-то не нравится мне такой интерес к моим личным глюкам со стороны вожатой…  Отдаст еще потом в руки карательной психиатрии."
            th "Виола в день моего появления показала себя гораздо более компетентным специалистом в вопросах оценки состояния психики."
            me "Да так, в глазах периодически плывет… Ну и в таком же духе. {w}Наверное, действительно перегрелся."
            show mt normal panama pioneer with dspr
            "Ольга Дмитриевна, услышав столь банальное пояснение, сразу потеряла интерес к деталям."
            mt "Тогда к Виоле загляни обязательно." 
            mt "На зарядку можешь не идти, а то такими темпами в медпункт тебя придется нести, и до конца смены там и пропишешься."
            "Я кивнул и отправился к медпункту."
            hide mt with dissolve
            window hide
            jump alt_day4_uvao_aidpost
            
label alt_day4_uvao_morning_wash: #умываемся, беседуем с Элом про кошочку
    scene bg ext_houses_sunset with dissolve
    window show
    th "Может, действительно показалось?"
    dreamgirl "Ты знаешь, когда так кажется – тут уже и креститься бесполезно, друг мой."
    th "Ну или действительно разыграли…"
    dreamgirl "Кто, Алиска? Ты же ясно видел, фигура не та!{w} На ДваЧе не похоже, а остальные были на дискотеке… вроде бы.{w} Да и Ольга Дмитриевна как-то слишком уж взбеленилась на такую историю…"
    th "Но ведь получается как-то криво. Теория заговора и некодевочка из инкубатора? Пошло, заезженно и бывает только в аниме."
    window hide
    scene bg ext_washstand_day with dissolve
    window show
    "Я поймал себя на том, что уже давно дошел до умывальников и стою, сосредоточенно пялясь куда-то в сторону кустов." 
    "Остатки мыслей из головы выбило хлопком по плечу."
    show el smile pioneer with dissolve
    el "Чего стоишь, как будто рыжих высматриваешь? Они уже к площади идут, не бойся!"
    "Голос Электроника был настолько жизнерадостным и так контрастировал с моим утром, что хотелось его ударить."
    dreamgirl "Может, ты и ему расскажешь? Вдруг он видел, чьими стараниями тебе Ольга Дмитриевна против шерсти проехалась?"
    th "А почему бы и да? Хуже явно не будет. К тому же кошкодевочка – это не какой-нибудь многоцветный морфирующий конь."
    me "Да вожатая пропесочила…"
    me "Представляешь, я вчера около столовой во время дискотеки девочку увидел с кошачьими ушами и хвостом, думал, галлюцинации.{w} Рассказал Ольге Дмитриевне, а она на меня вызверилась так, словно я ее лично оскорбил.{w} Вот теперь обтекаю."
    show el normal pioneer with dspr
    el "Да, не повезло…{w} А вообще радуйся – легенду местную увидел."
    el "У меня друг позапрошлым летом тут отдыхал, рассказывал, что это что-то вроде местной лагерной легенды, мол, ходит иногда по лагерю девочка-кошка.{w} Сладости иногда ворует, другую мелочевку…" 
    el "Вроде даже ее кто-то видит периодически. Может, и ты сподобился.{w} Ну или действительно тебя разыграл кто-то, всякое бывает. Ты не скисай, главное!"
    "Я угрюмо кивнул и направился к умывальнику. Пытка холодной водой – действо неприятное, но неминуемое."
    window hide
    $ persistent.sprite_time = "day"
    scene bg ext_washstand2_day
    with dissolve
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    window show
    "От холодной воды ломило челюсть, руки немного онемели, но настроение явно стало лучше, да и спать уже не хотелось. Теперь надо бы держать курс на площадь, а то вожатая с меня три шкуры спустит."
    stop sound_loop
    window hide
#    scene bg ext_square_sunset with dissolve
    scene cg d2_lineup
    window show
    "Линейка и зарядка прошли в уже привычном ключе."
    "Славя оправдывала свой статус помощницы вожатой на все сто, остальные с разной периодичностью зевали и поглядывали в сторону столовой."
    play music eat_horn fadein 3
    "Вот и долгожданные звуки горна!"
    stop music fadeout 5
    window hide
    jump alt_day4_uvao_breakfast

label alt_day4_uvao_aidpost: #беседуем с Виолой
    scene bg ext_square_sunset with dissolve
    stop music fadeout 3
    $ renpy.pause(1)
    scene bg ext_aidpost_day with dissolve
    play sound sfx_knock_door7_polite
    $ renpy.pause(2)
    window show
    cs "Да-да, войдите!"
    "Я поежился, вспомнив  предыдущую встречу с медсестрой, но отступать было некуда."
    window hide
    scene bg int_aidpost_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    window show
    show cs normal with dissolve
    me " Виолетта Церновна, а я к вам с проблемой."
    play music music_list["eternal_longing"] fadein 5
    show cs smile with dspr
    cs " Просто Виола, пионер, для тебя – просто Виола…"
    dreamgirl "Если ты к ней по делу – не красней и взгляд подними выше выреза на халате, дубина! Иначе твое дело ширинку скоро рвать начнет!"
    "Я собрался с мыслями и начал объяснять."
    me "Эм-м-м… Тут так получилось… Вопрос достаточно деликатный…"
    show cs grin with dspr
    cs "В ящике стола, бери, сколько надо."
    "Под взглядом медсестры я подошел к столу, открыл ящик и увидел горку квадратных бумажных пакетиков с надписью «Изделие №2». Лицо Виолы выражало исключительно дружелюбное участие."
    show cs normal with dspr
    me "Нет-нет, не в этом дело! Я… Мне… В общем, у меня, судя по всему, галлюцинации!"
    cs "Ну, раз ты сам пришел за медицинской помощью, значит, не все еще потеряно. И что же тебе мерещилось?"
    me "Ну, вчера во время дискотеки мне живот прихватило, я и ушел…{w} А потом увидел, что около столовой тень какая-то, думал, Алиса шастает, присмотрелся – а у нее уши кошачьи. И хвост.{w} У тени, а не у Алисы, Алисы там вообще не было.{w} Я подумал — нарядился кто-то, а оно ухом дёрнуло.{w} Вот."
    th "Идиот. Полный и беспросветный. Сейчас тебя накормят галоперидолом, потом воткнут в задницу шприц с аминазином, чтобы не особо дергался, и отправят в комнату с мягкими стенами."
    "Как ни странно, Виола не выказывала никакой тревоги и волнения, скорее на ее лице читался вполне себе явный интерес."
    $ renpy.music.set_volume(0.5, delay=5, channel='music')
    cs "Девочку-кошку? Не ты первый, пионер, не ты последний… В твоем возрасте это нормально, фантазии и не такие бывают…{w} но в данном случае ты столкнулся с настоящей легендой. Не знаю, почему Ольга…"
    me "Ольга Дмитриевна? А она здесь при чём?"
    cs "Когда мы в «Совенке» работать начали, эта легенда только появилась. Ей её подопечные все уши прожужжать успели, теперь из-за каждого упоминания бесится. А потом как-то поутихли слухи понемногу."
    me "И давно вы тут? А что за легенда?"
    show cs shy with dspr
    cs "Пионер, посмотри на меня внимательно и подумай, насколько невежлив был твой первый вопрос. Я выгляжу настолько старой, чтобы это было давно?"
    "Я нечленораздельно квакнул и отрицательно мотнул головой."
    show cs normal with dspr
    cs "То-то же. А легенды как таковой нет, просто начали рассказывать, что видели девочку с кошачьими ушами и хвостом. Еще периодически из столовой всякая всячина пропадает, но тут уж скорее пионеры под шумок действуют.{w} Так говоришь, где ты ее видел?"
    me "Около столовой…"
    cs "И ты уверен, что это не Алиса была?"
    me "Не, там фигура… Не Алиса, короче."
    "В глазах Виолы блеснул интерес."
    cs "Знаешь, пионер… Ты смотри, если её еще где-нибудь заметишь – заходи, рассказывай, я буду {i}очень благодарна.{/i}"
    $ alt_uvao_D4_viola_morning = True
    "Последние слова в исполнении Виолы чисто автоматически трактовались весьма интересным, но несбыточным образом. В горле снова пересохло, но в этот раз я сумел перебороть себя."
    me "Надеюсь, благодарность будет выражена… {i}интересным образом.{/i}"
    dreamgirl "Так держать! Еще бы на полтона ниже – и звучало бы уже совсем намеком!"
    stop music fadeout 3
    show cs smile with dspr
    cs "А ты весьма неплохо держишься… для школьника."
    $ renpy.music.set_volume(1, delay=0, channel='music')
    play music eat_horn fadein 2
    "Нашу беседу прервал горн. Виола направилась к двери."
    stop music fadeout 2
    cs "Завтракать идешь, пионер? Ольге я скажу, что ты в полном порядке."
    "Я кивнул и направился к столовой."
    hide cs with dissolve
    window hide
    jump alt_day4_uvao_breakfast

label alt_day4_uvao_breakfast:
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_sunset  with fade
    window show
    "Свободных мест в столовой, как всегда, не хватало." 
    "Детальный осмотр местности показал, что сесть я могу либо с рыжим дуэтом, от близости с которым всех предостерегал здравый смысл, либо с нашей доблестной девочкой-пулеметом восточного происхождения."
    dreamgirl "Ну что, путь смертника или путь стоика?"
    th "Лучше путь стоика. Мне как раз надо крепко обдумать пару вопросов, под болтовню это явно проще, чем под риск съесть кого-нибудь не того."
    show mi normal pioneer with dspr
    "Я сел рядом с Мику, улыбнулся и начал разбираться с содержимым тарелки. Она что-то тараторила, я кивал и погружался все глубже в размышления.{w} Вернее, в пустоту, из которой должны были родиться хоть какие-то мысли."
    th "Я тут уже четвертый день, а все еще забиваю на самые важные вопросы.{w} Для начала – текущая дата, местоположение лагеря и памятник на площади, при первом же вопросе о котором Лена  только что не отпрыгнула."
    dreamgirl "Может, она тебя за начинающего пикап-мастера приняла. Мол, начал тут один шаблоны рвать. А они-с барышни стеснительные и таких подкатов не понимают, у них Митчелл и романтика."
    th "Ша, о важном думать надо. На реконструкцию это не тянет – не поверю, что столько народа так старательно будут косить под пионерлагерь и добровольно откажутся от своих девайсов."
    th "Да и как я в таком случае здесь оказался? К тому же из зимы в лето…{w} Вариантов всего два – путешествие во времени и путешествие в параллельный мир.{w} И в том, и в другом случае, раз меня не встречают с цветами или наручниками, разгребаться придется самому."
    show mi serious pioneer with dspr
    "Я заметил, что Мику встревоженно смотрит на меня, и только тогда обнаружил, что в очередной раз подношу ко рту пустую ложку."
    me "Извини, задумался!"
    show mi smile pioneer with dspr
    "Мику рассмеялась с облегчением."
    mi "Ну, Сёма, ты даешь, так задуматься. Я уж думала, тарелку процарапаешь или ложку съешь! А о чем ты таком задумался? Не выспался, что ли? Я вот если не высплюсь, тоже могу вот так вот задуматься, почти сплю тогда на ходу даже. Или что-то случилось?"
    mi "Ой, ты знаешь, Шурик вчера с крыши упал, ушибся сильно, в медпункте лежит. Но вроде бы ничего страшного. А к тебе сегодня родители приедут?" 
    "Очередной поток болтовни практически смыл все попытки определиться с планом действий. Я отрицательно мотнул головой на ее расспросы и молча отправился в сторону выхода."
    mi "… заходи в клуб, если что! Вдруг скучно будет!"
    "догнал меня голос Мику."
    hide mi with dspr
    stop ambience fadeout 3
    window hide
    $ renpy.pause(2)
    $ persistent.sprite_time = "day"
    $ day_time()
    scene bg ext_dining_hall_near_day
    play ambience ambience_camp_center_day fadein 3
    play music music_list["my_daily_life"] fadein 3
    window show
    th "Где разжиться более-менее актуальной информацией о происходящем, не вызывая подозрений?{w} Правильно, в интернете. Чем заменить при отсутствии?{w} Телевидением и газетами."
    th "Насчет ти-ви не знаю, а вот газеты… Надо бы наведаться в библиотеку."
    dreamgirl "Ну наконец-то ты пошевелил мозгами хоть как-то.{w} Попроси подшивку журналов каких-нибудь, энциклопедию, что-нибудь еще…"
    th "А ведь действительно! Попрошу, к примеру, прошлогоднего «Юного техника» - вот и год вычислил, и не спалился. Да и вообще.{w} Только Жужелица…"
    dreamgirl "А ты ее палкой по голове тюкни – она мешать и не будет.{w} Можешь даже воспользоваться беспомощностью девушки в своих мерзких целях, разрешаю.{w} Нарисовать на ней что-нибудь, например…"
    stop music fadeout 0
    play sound2 sfx_bodyfall_1 
    show mt surprise panama pioneer close at center with hpunch
    "Разговаривая с Шизой, я перестал следить за дорогой и буквально врезался в Ольгу Дмитриевну."
#
    jump uvao_D4_garlands_removal

label uvao_D4_garlands_removal_debug:
# Д4-Утро-Уборка гирлянд
#

label uvao_D4_garlands_removal:
    me "Извините…"
    show mt normal panama pioneer with dissolve
    mt "О, Семён! Ты-то мне и нужен."
    "Этот тон мне сразу не понравился."
    me "Очередное архиважное поручение?"
    show mt smile panama pioneer with dspr
    mt "Как ты догадался?"
    me "Если подумать, то создается впечатление, что я вам нужен только в качестве чернорабочего."
    show mt laugh panama pioneer with dspr
    "Ольга Дмитриевна рассмеялась."
    mt "Не паясничай. Тебе наверняка все равно нечем заняться."
    "На клеточном уровне я очень-очень занят!"
    show mt normal panama pioneer with dspr
    mt "Так вот, на линейке видел состояние нашей площади?"
    me "А что, красиво. Особенно вчера было."
    "Я понял, куда она клонит. Кажется, поиск ответов придется отложить."
    "В принципе я мог бы обойтись и без свидания с Жужелицей, но если хочется понять, что здесь происходит - придется рисковать."
    mt "Как ты уже, наверное, понял, требуется убрать площадь, снять гирлянды и отнести их на склад."
    me "Вы всерьез думаете, что один я справлюсь?"
    mt "Можешь попросить помощи у Электроника. Тем более стремянка как раз была у них."
    th "Интересно, а не та ли эта стремянка, по которой Шурик забирался?"
    me "Но…"
    show mt angry panama pioneer with dspr
    mt "Никаких но, Семён! Пионеры говорят «Всегда готов!» и не задают лишних вопросов."
    me "Да, белая госпожа…"
    "Пробубнил я себе под нос.{w} К счастью, Ольга Дмитриевна меня не услышала."
    show mt normal panama pioneer with dspr
    mt "В общем, задание получил, вперед выполнять."
    show mt smile panama pioneer with dspr
    "Она улыбнулась и твердой походкой направилась в сторону домика."
    #Я вспоминаю, тебя вспоминаю! А радость шальная легла как заряяяяя! Летящей походкой! Ты вышла из мая! Потом из июля и фееевраляяяя!
    hide mt with dissolve
    dreamgirl "Ну что, работай, нигер! Солнце еще высоко!"
    "Что же, уборку без стремянки не начать. Я направился в сторону кружка кибернетиков."
    window hide
    #$ renpy.pause(2)
#    play ambience ambience_camp_center_day fadein 1
    $ alt_chapter_uv(4, u"Всегда готов!")
    play music music_list["my_daily_life"] fadein 3
    scene bg ext_clubs_day with dissolve
    window show
    "Я постучал и дернул было ручку, но дверь оказалась заперта."
    th "Где это Электроника носит? Шурика, что ли, навещает?{w} Ладно, подожду его тут."
    show us grin sport far with dissolve:
        xalign 0.999 yalign 0.999 zoom 0.7
    "Объявился тот спустя двадцать минут, я же за это время успел заметить в кустах рыжую шевелюру местной «охотницы на жуков»."
    "Сейчас мне было ужасно жаль того, кому Ульяна захочет похвастаться своей коллекцией."
    hide us with dissolve
    me "Явился - не запылился."
    show el surprise pioneer with dissolve
    el "Привет Семён, а я это…"
    "Электроник что-то спрятал за спину."
    "Я ловко выхватил у него небольшую стопку журналов.{w}\n«Юный техник». А я уж подумал - «Playboy»…"
    show el serious pioneer with dspr
    el "Это Шурику. Скучно ему в медпункте, просил принести, вот я в библиотеку и сходил."
    "Ясно, чем занимался Электроник все эти двадцать минут. Кажется, Жужелица сегодня будет более нервной, чем обычно."
#
    show el sad pioneer with dspr
    el "Да, ты же не знаешь, наверное? Шурик вчера с крыши навернулся…"
    me "Слышал уже, слышал!"
    "Перебил я нетерпеливо.{w} Выслушивать эпическое повествование о падении с крыши у меня особого желания не было."
    me "Как у него дела?{w} Что-нибудь серьёзное?"
    el "Нет, как ни странно, по мелочи только - ушибы да ссадины. Повезло, одним словом."
    el "Виолета мне сказала, что завтра его отпустит, наверное."
    "При упоминании о медсестре Эл заметно покраснел.{w} Похоже, она не только надо мной одним любит мило пошутить…"
    me "Ладно, всё это очень хорошо, но я не для того тебя здесь ждал."
#
    me "Помощь твоя нужна."
    show el normal pioneer with dspr
    el "В чем же?"
    "Тот явно заинтересовался."
    me "Ольга Дмитриевна велела убрать гирлянды, а стремянка, кажется, у вас."
    el "Да, она тут."
    hide el with easeoutright
    "Тот оббежал здание клубов, чем-то поскрипел и выволок сложенную жестяную стремянку."
    show el normal pioneer with easeinright
    me "Отлично, пошли."
    show el upset pioneer with dspr
    el "Ну, вообще-то у меня дела."
    "Он покраснел."
    th "Опять в библиотеку собрался?"
    dreamgirl "Причем, явно не за журнальчиками… А что, руководитель на больничном, подчиненные развлекаются как умеют."
    me "Э, нет. Один я ни за что не полезу. Ольга Дмитриевна сказала, что ты можешь помочь."
    el "Ну, если надо…"
    me "Не представляешь как."
    show el normal pioneer with dspr
    "Мы направились к площади."
    window hide
    scene bg ext_houses_day
    with dissolve
    $ renpy.pause(3)
    show el normal pioneer at left with dissolve
    window show
    "Эл, кряхтя, нес стремянку, а я осматривал лагерь.{w} В голову приходили разные мысли о путешествиях во времени, параллельных мирах, экспериментах ученых…"
    show el sad pioneer with dspr
    el "Давай передохнем?"
    me "Выдохся уже? Давай сюда."
    "Я схватил стремянку и понес ее к площади."
    window hide
    scene bg ext_square_day
    with dissolve
    show el sad pioneer at left with dissolve
    window show
    "Вскоре показалась площадь, с ее лавочками, украшенными гирляндами деревьями и с ее основным атрибутом – одиноким загадочным Гендой."
    "Тот как всегда надменно взирал на происходящее вокруг.{w} На воркующую парочку, сидящую на скамейке в тени деревьев, на Славю, машущую метлой, на меня, медленно несущего стремянку."
    "Я с той же надменностью взглянул в его очки."
    th "Ну, погоди, неизвестный товарищ. Мы еще узнаем твои тайны."
    dreamgirl "Мда-а. Натуральные Авгиевы конюшни!"
    "Действительно, работы оказался практически непочатый край."
    "Я прикинул примерный вектор работы и поставил стремянку у одного из крайних деревьев."
    me "Кто полезет?"
    show el upset pioneer with dspr
    el "Э нет, только не я."
    me "Мне тоже как-то не очень хочется."
    show el normal pioneer with dspr
    "Никто из нас не хотел повторять подвиг Шурика, посему было принято решение решить вопрос «по-мужски»."
    "…"
#    play music music_list["my_daily_life"] fadein 3
#здесь можно сделать мини-развилку (Лезет Сёма/Лезет Эл)
    show el smile pioneer with dspr
    el "Ножницы режут бумагу. Я победил."
    th "Ничего не остаётся…"
    me "Держи, только крепче!"
    show el normal pioneer with dspr
    "Эл кивнул и схватился за стремянку."
    show sl smile pioneer far at fright with dissolve
    "Работа пошла."
    "Славя с улыбкой наблюдала за нашей деятельностью, пристроив на лавочке рядом с собой метлу."
    "Ну да, на что там можно смотреть вечно?" 
    me "Эл, держи крепче!" with hpunch
    show el sad pioneer
    show sl normal pioneer far at fright
    with dspr
    el "Прости, отвлекся."
    show mz bukal glasses pioneer far at right with dissolve
    "Я взглянул за плечо и заметил как Славя о чем-то разговаривает с Жужелицей."
    show el smile pioneer with dspr
    "Эл, как и ожидалось, смотрел туда же."
    "Женя, что-то сказала, покосилась в нашу сторону и, развернувшись, пошла в сторону библиотеки.{w} Отлично, значит, она будет там."
    hide mz with dissolve 
    play sound sfx_tree_branches
    with hpunch
    "Стремянка громко скрипнула, я от испуга выронил моток гирлянды и схватился за ветку дерева." 
    me "Эл!!!"
    show el upset pioneer with dspr
    el "Извини!"
    show el sad pioneer with dspr
    "Электроник тут же крепко сжал стремянку."
    "Спустя полчаса последняя гирлянда была смотана и брошена в общую кучу."
    show el normal pioneer
    show sl normal pioneer at right
    with dissolve
    sl "Вам помочь, мальчики?"
    me "Если не трудно."
    show sl happy pioneer with dspr
    "Она лучезарно улыбнулась."
    sl "Нет, не трудно. Как раз с площадью закончила."
    show sl smile pioneer with dspr
    "Я схватил большой моток проводов и понес его в сторону склада."
    "Эл, помедлив, схватил стремянку и пошел за мной. Славя взяла остатки гирлянд."
    window hide
    scene bg ext_houses_day
    with dissolve
    show el sad pioneer at right
    show sl normal pioneer at left
    with dissolve
    window show
    el "Аккуратнее, Семён. Не упади."
    $ renpy.pause(1)
    "Нести моток было неудобно, и провода то и дело попадались под ноги. С горем пополам мы дошли-таки до склада."
    window hide
    scene bg ext_shed_day
    with dissolve
    play ambience ambience_camp_entrance_day fadein 3
    $ renpy.pause(3)
    show el sad pioneer at right
    show sl normal pioneer at left
    with dissolve
    window show
    sl "Одну минутку, мальчики."
    show sl normal pioneer far at left
    show el normal pioneer
    with dspr
    "Она подбежала к дверям, звякнула ключами и ловко открыла навесной замок."
    hide sl with dissolve
    sl "Загружай!"
    "Донесся голос из помещения."
    "Я поволок гирлянды внутрь."
    stop ambience fadeout 0
    window hide
    scene int_shed_day with dissolve
    $ persistent.sprite_time = "sunset"
    $ renpy.pause(2)
    show sl normal pioneer far at left with dissolve
    window show
    me "Куда?"
    sl "В коробку, вот сюда."
    "В помещении было темно и Славю было едва видно."
    "Я бросил большой моток туда, куда указал Славин силуэт и наконец разогнулся."
    me "Ох! Как будто вагон разгрузил!"
    show sl happy pioneer far with dspr
    "Славя хихикнула."
    show el normal pioneer far at right
    show sl smile pioneer far
    with dspr
    "Рядом раздался скрип стремянки. Электроник поставил железяку в другой угол и теперь отдыхал, прислонившись к стене."
    me "Что ж, мавр сделал свое дело, мавр может уходить. Спасибо за помощь."
    show el smile pioneer far
    show sl smile pioneer far
    with dspr
    "Эл кивнул, а Славя улыбнулась."
    sl "Обращайтесь."
    window hide
    scene bg ext_shed_day
    play ambience ambience_camp_entrance_day fadein 3
    with dissolve
    $ persistent.sprite_time = "day"
    window show
    "После темноты склада солнце было необычайно ярким."
    show el normal pioneer far at right with dissolve
    "Пока я привыкал к свету, мимо меня проскользнул Эл и с деловым видом направился куда-то в сторону сцены."
    hide el with easeoutleft
    "Я же присел на крыльцо перевести дух. Шутка ли, битых два часа корячиться на верхотуре! Руки ощутимо гудели, затекшая шея ныла."
    show sl normal pioneer far at left with dissolve
    "Тем временем Славя заперла дверь и, мимолетно улыбнувшись мне, побежала куда-то по своим делам. Активистка!"
    hide sl with easeoutright
    dreamgirl "Ну что, передохнул? Ты вроде собирался приобщиться к знаниям."
    stop music fadeout 3
    "Кряхтя, я поднялся и направился к хранителю местных архивов, в надежде откопать побольше информации о мире, куда меня занесло."
    stop ambience fadeout 1
    window hide
    jump uvao_D4_library
#

label uvao_D4_library:
#Д4 посещение библиотеки
#
#
    $ alt_chapter_uv(4, u"Повелитель книг")
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day fadein 1
    scene bg ext_library_day with fade
    window show
    th "Все, пора с этим заканчивать, а то мне уже начинает казаться, что сами Высшие Силы не хотят, чтобы я узнал правду…"
    dreamgirl "Впервые на арене! Смертельный номер!{w} Укрощение безумной библиотекарши! Эх, жаль, похрустеть нечем…"
    th "Спасибо за поддержку."
    dreamgirl "Обращайся!"
    "Остановившись возле двери, я несколько раз глубоко вздохнул."
    th "Знал бы, что понадобится ее помощь, был бы хоть немного повежливее…"
    dreamgirl "Не думаю. Бешеный лев обычно хреново реагирует на комплименты…"
    "Я собрался с мыслями и повернул ручку."
    play sound sfx_door_squeak_light
    $ renpy.pause(2)
    window hide
    scene bg int_library_day with dissolve
    $ persistent.sprite_time = "day"
    play ambience ambience_library_day fadein 3
    window show
    "Шаг, другой.{w} Я еще жив? И на меня даже не наорали? Удивительно!"
    "Уютное гнездышко библиотекарши пустовало. Где же хранитель сего места?"
    "Меня напугал внезапный крик из-за соседнего стеллажа:"
#    play sound sfx_angry_ulyana
    play music music_list["glimmering_coals"] fadein 5
    mz "Да сколько можно? Двадцатый раз уже! Зачем сюда приходишь? Ты же их уже до последней буквы знаешь."
    el "Жень, я… Это…"
    th "Вот он куда так торопился, оказывается."
    "Голос Электроника немного дрожал, и я даже немного его понимал."
    mz "Я знаю, как меня зовут! Заруби себе на своем конопатом носу…"
    "Вот это подфартило! Впервые Электроник там, где нужен. Если он еще потянет резину…"
    dreamgirl "Правильно. Пусть пираний кормит другой водолаз. Давай скорее, они же не вечно будут ворковать."
    th "Сейчас найду какую-нибудь книгу вроде «Истории СССР» или что там ещё…"
    "Тут я с огорчением понял, что до картотеки-то мне не добраться - там я как раз окажусь в поле зрения препирающейся парочки."
    th "И что же делать? Смотреть все книги подряд? Так я до вечера провожусь, даже если не прогонят."
    "Я отчаянно зашарил взглядом по аккуратным бумажным наклейкам на стеллажах."
    "Разумеется, здесь должно быть полно литературы для воспитания патриотического духа среди пионеров, в том числе и книги по истории…"
    "Одно плохо - все стеллажи с книгами подходящей тематики находились в опасной близости от злобствующей Жени."
    th "Да что ты будешь делать! Ведь застукают же…"
    dreamgirl "А ты Женю попроси помочь! И Эла."
    "Тут в глаза бросилось слово «УЧЕБНИКИ». Может быть, там что-нибудь найдётся?"
    "Я тихонько направился к стеллажу с учебниками. Не в моем положении было мешать сейчас голубкам."
    "Голос Жени немного стих, но она продолжала поносить Электроника."
    mz "Каждый день! Каждый день, ты заявляешься сюда с неизвестной целью…"
    "Нужный стеллаж."
    "«Природоведение»… Не то.{w} «Геометрия»… К чёрту геометрию!"
    "«История»! Есть!"
    "Я ухватился за зажатый между другими учебник, на корешке которого красовалось «ИСТОРИЯ СССР. 10»"
    "Значит, хоть что-то я узнаю."
    mz "…прогуливайся сам! А мне и так хорошо!"
    stop music fadeout 6
    el "Но…"
    dreamgirl "Зря парень старается. Она, кажется, почкованием размножается, не иначе…"
    "А голоса меж тем приближались. Еще чуть-чуть и накроют. И шкаф угловой, не спрячешься. Что делать? Может, в подпол?"
    dreamgirl "Какой подпол? Кто тебе будет в библиотеке делать подпол?! Давай лучше запрыгивай наверх и зависни между полками, как трёхглазый ублюдок!"
    th "…"
    window hide
    scene pioner_lib_hiding
    play sound sfx_head_heartbeat fadein 2
    "Вместо этого я пригнулся и на трех конечностях - четвертая была занята книгой - шустро забежал за стеллаж."
    play music music_list["you_won_t_let_me_down"] fadein 6
    
    
    dreamgirl "…Ну или так."
    "Стук каблуков подсказывал, что Жужелица снова заняла свое место у дверей. Электроник тащился за ней."
    mz "Разговор окончен! Иди, поищи себе кого-нибудь другого, кто согласится выгулять тебя, например Шурика своего!"
    el "Но он в медпункте…"
    mz "Да плевать, ты мне здесь не нужен! Уходи."
    play sound sfx_open_door_kick
    th "Боже, какой у нее противный голос! И кто на {i}такую{/i} поведется?"
    play sound sfx_close_door_1
    dreamgirl "Вот всякие Электроники и ведутся. С другими у него немного шансов, сам пойми."
    "Я стоял на четверенках и напрягал слух, пытаясь улучить момент для побега."
    "Женя, видимо, никуда не торопилась. Не было слышно ни шагов, ни ее скрипучего голоса.{w} Может быть она ушла?"
    dreamgirl "Не дождешься! Дрыхнет небось, как всегда, на своих книжках."
    "Не ждать же мне до обеда?"
    play sound sfx_open_door_kick
    "Внезапно скрипнула дверь, и кто-то вошел."
    mz "Это кто это там?"
    un "Женя, здравствуй, это я."
    mz "Снова ты? Что ж вы ходите-то все подряд… Уже прочитала?"
    un "Угу."
    mz "Давай тогда. Та-ак. Страницы на месте, ничего не изрисовано… Что-то еще?"
    un "Мне бы еще что-нибудь…"
    "Я почти уверен, что Лена сейчас краснее знамени с Лениным, висящего за спиной библиотекарши, хотя видно лишь ее сандалии."
    mz "Опять про любовь? А ты разве не все еще прочитала?"
    "Раздался скрип отодвигаемого стула."
    mz "И что вам так нравится в бульварных романах? Я больше предпочитаю книги по естественным наукам…"
    "Лена промолчала."
    "Шаги девочек удалялись. Судя по всему, романтике жужелица отделила место в самом дальнем углу, посему…"
    dreamgirl "Ваш выход, мсье Фишер!"
    window hide
    scene bg int_library_day with dissolve
    window show
    "Я перехватил учебник поудобнее во вспотевшей ладони и выглянул из-за шкафа. Никого."
    "Отлично, теперь главное - не спеша…"
    "Мимо стеллажа, обходим столы… Дверь."
    window hide
    play sound sfx_open_door_kick
    scene bg ext_library_day with fade
    stop music fadeout 3
    $ renpy.pause(2)
    play ambience ambience_camp_center_day fadein 1
    window show
    "Весь путь к двери я даже не дышал. Но теперь, вырвавшись из лап дракона с трофеем, я просто не мог надышаться."
    "Сладкий воздух, воздух победы…"
    "Так, теперь посмотрим, что это я добыл…"
    play sound sfx_paper_bag
    "Оглавление, зажатое почему-то между приложениями, удалось найти не сразу."
    "Так… Что тут…"
    show us smile sport with dissolve
    play music music_list["i_want_to_play"] fadein 3
    us "Семён! А ты чего это тут делаешь?"
    "Я аж подскочил, пряча книгу за спину."
    show us normal sport with dspr
    me "Ульяна! Подкрадываться к людям нехорошо."
    "Я уже описывал широкую дугу вокруг девочки."
    show us dontlike sport with dspr
    us "Ты прям как Саныч, честное слово. Хотела мяч взять, футбольный, зашла, а он дрыхнет на матах. Давай его расталкивать…"
    us "Вот он мне прям то же самое сказал!"
    me "Мяч не дал, что ли?"
    show us normal sport with dspr
    us "Дал, почему же?"
    me "Тогда почему не играешь?"
    show us sad sport with dspr
    us "Ску-учно! Алиса куда-то пропала, а с младшими отрядами играть неинтересно. Пошли сыграем, Семён?"
    me "Да я сейчас занят…"
    show us grin sport with dspr
    us "И чем же? Из библиотеки книжки тыришь?"
    "Ульяна хитро взглянула на меня…"
    us "Дай поглядеть!"
    "… и выхватила учебник, который я пытался спрятать за спиной."
    me "Эй, верни!"
    show us grin sport far at fright with dissolve
    "Ульяна отбежала на безопасное расстояние и только потом посмотрела на обложку."
    show us dontlike sport far at fright with dspr
    us "История СССР? Фе. Скукотень! Я думала про индейцев, или про шпионов… Зачем он тебе?"
    dreamgirl "Ну что, теперь думай как добычу вернуть…"
    dreamgirl "Догнать мы уже пробовали, подкупить нечем, осталось только уговорить.{w} Или припугнуть. Кого она боится?"
    me "Поручение от Ольги Дмитриевны. Кстати, ты мне поможешь!"
    show us calml sport far at fright with dspr
    us "Я?! Чем это?"
    me "Про концерт слышала? Ольга Дмитриевна велела мне найти материал для сценки.{w} Что-нибудь про пионеров-героев, например, или про революцию."
    th "Надеюсь, она не в курсе, что в учебнике для 10 класса про пионеров-героев вряд ли что-то может быть…"
    show us normal sport far at fright with dspr
    me "Вот ты и будешь пионером-героем!"
    dreamgirl "Ты соображаешь чего несешь? Она же сейчас согласится!"
    me "А я - фашистом.{w} Ми тебя будем вешайт унд убивайт!"
    show us laugh sport far at fright with dspr
    us "Не, я всех из пулемета перестреляю - тра-та-та-та!!! И никаких вешайт и убивайт!"
    me "Не пойдет, они потому и герои, что геройски погибли. Придется тебе…"
    show us dontlike sport far at fright with dspr
    us "Не-е-е, тогда не хочу!{w} Найди кого-нибудь другого, Лену, например!"
    show us normal sport far at fright with dspr
    us "Забирай свою скукотень, а я пойду, может Алиса вернулась."
    show us grin sport far at fright with dspr
    us "Заодно проверю своих… э… друзей…"
    dreamgirl "Ну вроде разобрались. И совет, дружок: есть с ней не садись, а то снова в тарелке обнаружишь ее… друзей…"
    hide us with dissolve
    "Я облегченно вздохнул и, стараясь никому не попадаться на глаза, направился к домику.{w} Лучше поглощать знания в уединеном месте."
    stop music fadeout 3
    stop ambience fadeout 1
    window hide
    scene bg int_house_of_mt_noitem_day with dissolve
    play music eat_horn fadein 5
    window show
    "Стоило только открыть дверь, как раздался обеденный горн."
    stop music fadeout 2
    "В тот же момент я, как настоящая собачка Павлова, немедленно ощутил пустоту в желудке."
    play sound sfx_stomach_growl
    th "Поглощать знания лучше на сытый желудок."
    "Спрятав книгу под подушку и стряхнув пыль знаний со штанов, я направился в столовую. "
    stop music fadeout 3
    window hide
    
    jump uvao_uvao_D4_lunch
#
label uvao_uvao_D4_lunch:
# Д4-обед
#
# используется флаг посещения медпункта alt_uvao_D4_viola_morning
# устанавливает флаг ухода на концерт uvao_D4_concert #(временный для отладки)
# устанавливает флаг обеда с  Леной  uvao_D4_lunch_un
# устанавливает флаг обеда со Славей uvao_D4_lunch_sl
#
    $ uvao_D4_concert = False
    $ uvao_D4_lunch_un = False
    $ uvao_D4_lunch_sl = False
#
    $ alt_chapter_uv(4, u"Обед")
    $ persistent.sprite_time = "day"
    $ day_time()
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_day with fade
    window show
    "В столовой почему-то было просторнее, чем обычно."
    "То ли часть пионеров выбыла из строя, наевшись чего-то не того{w}, то ли их просто задержали на каком-то мероприятии, о котором я не знал."
    "Впрочем, не всё ли мне равно? Меньше народа - больше кислорода!{w} Главное, в столовой было достаточно свободных мест, что давало мне некоторую свободу выбора."
#
    "После акробатических упражнений на площади есть хотелось особенно сильно, но и любопытство, раззадоренное чтением статьи в библиотеке, тоже не давало покоя."
    th "Вообще-то, кто мешает мне совместить полезное с полезным? Можно подсесть к кому-нибудь и за едой попробовать ненавязчиво выведать что-нибудь о местных реалиях…"
    dreamgirl "«Ненавязчиво выведать» ты уже пробовал в первый день - у Лены. Гениально проведённая разведывательная операция, преисполненная тонким знанием психологии."
    "Не удостоив Шизу ответом, я взял с раздачи поднос и окинул взглядом столовую, выбирая, к кому бы из знакомых присоседиться."
    show mi normal pioneer far at fleft with dissolve
    "Ближе всего было свободное место рядом с Мику.{w} Определённо, никаких проблем с тем, чтобы её разговорить, не предвиделось - скорее уж наоборот."
    "Вопрос только в том, удастся ли направить фонтан её красноречия в интересующем меня направлении.{w} В своих способностях в этой области я не сомневался ни секунды{w} - однозначно, не справлюсь."
    hide mi with dissolve
    th "Тем более, что она, можно сказать, иностранка. Так ли уж много она знает про {i}эту страну{/i}. Лучше попробовать с аборигенами пообщаться."
    dreamgirl "Ну ты, барин, и задачи ставишь! Где же их здесь взять, аборигенов-то?{w} Хотя подожди, вот же как раз сидят две прелестных рыжеволосых аборигенки. Как раз и место рядом свободно!"
    show dv smile pioneer far at fright
    show us grin pioneer far at right
    with dissolve
    "Сидящие поодаль Алиса с Ульянкой уже некоторое время на меня ехидно поглядывали."
    show dv grin pioneer far at fright with dspr
    $ renpy.pause (1.5)
    show us laugh pioneer far at right with dspr
    "Заметив, что я смотрю в их сторону, Двачевская что-то быстро зашептала подруге на ухо, та прыснула и состроила мне гримасу."
    th "Вот уж не думал, что у меня, оказывается, латентная склонность к мазохизму!"
    th "Если уж собственное альтер эго предлагает мне поучаствовать в комедии «Девочки с рыжими волосами, или Тридцать три подзатыльника»!{w} Меня будут обливать борщом, кормить саранчой, давать пощёчины… Словом, унижать морально и физически. Это очень смешная комедия…"
    hide dv
    hide us
    with dissolve
    "Шиза изобразила демонический хохот, а я, рассудив, что обед в обществе сразу двух рыжих бандиток вряд ли принесёт какую-то пользу, а вот нервотрёпки избежать точно не удастся, огляделся в поисках более подходящей компании."
#
    menu:
        "Сесть с Леной":
#
# Исходим из того, что в Д2-Д3 с Леной общались мало (конвой в Д2 невозможен, а остальные выборы в её пользу не использовались/использовались мало, иначе не проходим в ЮВАО по ЛП)"
# Соответственно, до сих пор Лена свою симпатию к ГГ с ним не обсуждала, ибо не имела возможности/стеснялась"
#
            show un sad pioneer far at center with dissolve
            "Я почувствовал на себе чей-то взгляд и, обернувшись, увидел как Лена поспешно отвела глаза и уткнулась носом в тарелку."
            th "Пожалуй, стоит с ней поговорить.{w} Извинюсь за странные расспросы в первый день, скажу что неудачно пошутил, или что-то вроде этого.{w} А там, глядишь, и разговоримся."
            show un sad pioneer at center with dissolve
            "Решительно подойдя к столу, за которым она сидела, я кивнул на свободное место напротив."
            me "Ты не против, если я присяду?"
            show un surprise pioneer at center with dspr
            "Лена как-то странно глянула на меня, но тут же снова уставилась в стол и неловко кивнула, слегка при этом покраснев."
            show un shy pioneer close at center with dissolve
            "Решив принять этот жест за знак согласия, я уселся и составил с подноса тарелки."
            me "Приятного аппетита."
            "Она снова кивнула и, не поднимая глаз, беззвучно шевельнула губами."
            "Слегка обескураженный таким приёмом, я принялся за еду, периодически поглядывая на Лену."
            stop ambience fadeout 15
            play music music_list["lets_be_friends"] fadein 10
            "Та сидела, гоняя вилкой по тарелке несколько оставшихся макаронин, и упорно не глядя на меня."
            th "Странно, до этого она вела себя вполне нормально.{w} Насколько это возможно для такой стесняши, разумеется."
            th "Неужели я так сильно смутил её тогда расспросами про Генду?"
            th "Это даже и расспросами-то назвать нельзя, для неё это должно выглядеть как неумная шутка или, максимум, проявление лёгкого слабоумия, но не более того."
            "Пауза явно затягивалась и, заканчивая с супом, я наконец решил попробовать ещё раз завести светскую беседу."
            me "Э-э-э… как дела?{w} Я имею в виду, как прошло утро?"
            show un surprise pioneer close at center with dspr
            "Кажется, даже Шиза не нашлась, что сказать на это вступление."
            "Лена подняла на меня озадаченный взгляд, но быстро опомнилась и снова принялась сосредоточенно разглядывать опустевшую тарелку."
            show un shy pioneer close at center with dspr
            "Внутренне поморщившись от собственного идиотизма, я решил, тем не менее, давить до последнего:"
            me "Послушай, я извиниться перед тобой хотел. В тот вечер я тебя про Генду спрашивал… Ты не обращай внимания, это я так… э-э-э…"
            show un surprise pioneer close at center with dspr
            "Теперь девочка уставилась на меня уже с явным недоумением, и дальнейшие слова сами собой замерли у меня на языке."
            dreamgirl "Знаешь, здесь даже я не понимаю, что происходит. Может быть, дело не в твоих дурацких расспросах?"
            show un sad pioneer close at center with dspr
            dreamgirl "Слушай, может быть, она в тебя влюбилась? Хотя нет, вы почти и не общались."
            dreamgirl "А с другой стороны, ты мужчина хоть куда - в самом рассвете сил. Точно, влюбилась!"
            "Скрипнув мысленно зубами, я попытался продолжить выступление:"
            me "Э-э-э… Видишь ли, это у меня такое задание на лето, провести э-э-э… своего рода социологический опрос…"
            show un shy pioneer close at center with dspr
            "Тут я окончательно потерялся и понял, что единственное спасение для меня - это быстренько всё доесть и свалить отсюда поскорее."
            "С минуту всё было тихо. Я поспешно заглатывал свою порцию макарон по-флотски. Лена сидела напротив и продолжала гипнотизировать тарелку, постепенно краснея всё больше."
            "Я уже схватился за стакан с компотом, когда вдруг она отложила вилку в сторону и решительно посмотрела мне прямо в глаза:"
            show un serious pioneer close at center with dspr
            un "Семён, я хотела тебя спросить…"
            "От неожиданности я чуть компотом не захлебнулся."
            show un shy pioneer close at center with dspr
            "Лена смутилась и снова отвела было взгляд{w}, но неожиданно быстро взяла себя в руки и продолжила, пусть и запинаясь после каждого слова, но совершенно отчётливо, вместо своей тарелки гипнотизируя теперь уже меня своими бездонными зелёными глазами."
            show un sad pioneer close at center with dspr
            un "Понимаешь…{w} последние… несколько дней…{w} Вернее, с того дня…{w} как ты приехал… я…{w} Вернее, со мной…"
            show sl happy pioneer at fright
            stop music fadeout 1
            play ambience ambience_dining_hall_full fadein 1
            show un surprise pioneer close at center with dspr
            sl "А! Я смотрю, вы обед уже закончили!"
            "Раздался откуда-то сверху жизнерадостный до отвращения голос."
            "Я вперил в его обладательницу полный ненависти взгляд, но Славя этого то ли не заметила, то ли её такие мелочи не смущали."
            sl "Ну что, пойдёмте на концерт? Посмотрим, что ребята приготовили!"
            show un sad pioneer close at center with dspr
            th "Вот делать мне больше нечего, только самодеятельностью любоваться!{w} Меня мирового масштаба тайны ждут, а она…"
            me "Славя, мне бы…"
            show sl serious pioneer at fright with dspr
            sl "Нет-нет, ты и так, Семён, всё время отрываешься от коллектива, а концерт это важное общественное мероприятие, все старались, готовились, Мику там изо всех сил…"
            "Наша активисточка продолжала разливаться дальше про ответственность перед обществом и что-то ещё в том же духе, но я её уже не слушал."
            show un normal pioneer at center
            show sl normal pioneer at fright
            with dspr
            "Лена тем временем встала и, нацепив на лицо отстранённо-нейтральное выражение, покорно пошла на выход.{w} Словно и не было только что этого странного разговора."
            "Словно это не она пыталась не то спросить у меня что-то, не то рассказать о чём-то для неё очень важном."
            hide un with easeoutright
            dreamgirl "Знаешь, а вот это уже и в самом деле свинство.{w} Сидят люди, непринуждённо общаются со взаимным удовольствием, и вдруг эта Славя подкрадывается со своими глупостями."
            dreamgirl "Хотя с другой стороны - а не оказала ли тебе активисточка только что любезность?"
            th "Какую ещё любезность?!{w} Что, моя карманная Шиза тоже сошла с ума?"
            dreamgirl "Чем обзываться, лучше пораскинь мозгами.{w} Лена явно собиралась основательно тебя загрузить какими-то своими проблемами, это даже тебе должно быть понятно."
            dreamgirl "А теперь подумай - оно тебе надо?{w} Нет, я понимаю, ты немного поплыл - такая милая скромная девочка, фигурка что надо, глазища эти… Так и хочется её утешить по самое не балуйся."
            dreamgirl "Но ты, вроде бы, собирался делом заниматься, разве нет?"
            show sl angry pioneer at fright with dspr
            sl "…меня слушаешь вообще?!"
            "Вздрогнув, я отвлёкся от увлекательного диалога с самим собой и обратил внимание на Славю. Кажется, она уже успела всерьёз рассердиться."
            sl "Имей в виду, на концерт ты пойдёшь обязательно."
            sl "Если понадобится - за руку потащу! Не сможешь идти - понесём на носилках."
            sl "Ольга Дмитриевна сказала собрать всех - значит пойдут все!"
            show sl serious pioneer at fright with dspr
            "Сказала она, как отрезала."
            "Посмотрев ещё раз на Славю я понял, что у меня на выбор только два варианта - либо я иду добровольно, либо меня действительно силком отведут за руку, как маленького."
            "Обречённо вздохнув, я поплёлся за Славей."
            hide sl with easeoutright
#
            $ uvao_D4_lunch_un = True
            $ uvao_D4_concert = True
#
        "Сесть со Славей":
            show sl normal pioneer far at center with dissolve
            "Поймав мой вопросительный взгляд, Славя улыбнулась и помахала мне рукой."
            th "А что, хорошая идея!"
#
#необходимо добавить проверку на флаги ивентов со Славей в дни 1-3 (если они возможны при сычевании) и соответствующие трогательные упоминания о них."
#
            show sl normal pioneer at center with dissolve
            "Славя все эти дни относилась ко мне доброжелательно."
            "Кроме того, она наверняка назубок знает историю страны и партии и может рассказать мне что-нибудь полезное, если удастся придумать походящую отмазку для расспросов."
            me "Я так понимаю, ты не против моей компании?"
            "Спросил я, подходя к её столу."
            show sl happy pioneer at center with dspr
            sl "Ну конечно же не против! Садись!"
            "Она гостеприимным жестом указала на место напротив."
            show sl normal pioneer close at center with dissolve
            "Я уселся, сгружая посуду с подноса на стол."
            show sl smile pioneer close at center with dspr
            sl "Приятного аппетита!"
            "Улыбнулась мне Славя."
            me "И тебе приятного."
            "С удовольствием ответил я."
            play music music_list["take_me_beautifully"] fadein 10
            stop ambience fadeout 15
            show sl normal pioneer close at center with dspr
            sl "Спасибо ещё раз, что помог с уборкой на площади. В одиночку Электроник до сих пор бы с гирляндами возился."
            me "Да не вопрос. Тем более, если бы ещё и он сверзился с лестницы, то пришлось бы закрывать кружок кибернетики, а это уже серьёзный удар по общественной деятельности в лагере."
            show sl angry pioneer close at center with dspr
            "Кажется, моя шутка не слишком понравилась Славе, потому что она нахмурилась и даже есть перестала."
            me "Извини, глупость сказал. Надеюсь, что с Шуриком всё будет в порядке."
            "Поспешил исправиться я."
            show sl serious pioneer close at center with dspr
            me "Мне Электроник сегодня сказал, что забегал в медпункт. Виола ему пообещала, что Шурика ещё денёк подержит для порядка и отпустит."
            sl "Да, я знаю."
            sl "Но всё равно, не шути так больше."
            me "Не буду, обещаю. Честное пионерское!"
            show sl normal pioneer close at center with dspr
            th "А кстати…"
            me "Тем более, у меня найдётся другой повод тебя немного шокировать."
            show sl surprise pioneer close at center with dspr
            "Хитро улыбнулся я."
            me "Я тебе сейчас задам неожиданный, но действительно важный для меня вопрос, а ты постарайся не задумываясь на него ответить - если сочтёшь возможным, разумеется."
            me "Обещаю, что сразу после твоего ответа я всё объясню."
            show sl shy pioneer close at center with dspr
            "Можно было догадаться, о чём подумала Славя, потому что на мгновение она явно смутилась, но быстро взяла себя в руки и решительно кивнула."
            show sl serious pioneer close at center with dspr
            sl "Ладно, спрашивай. Не обещаю, что отвечу на {i}любой{/i} твой вопрос, но постараюсь."
            dreamgirl "Да, да! Спроси, сколько у неё было парней, какая у неё любимая поза и… и…{w} Блин, такое раздолье для фантазии, что поневоле растеряешься!"
            "С серьёзным лицом я выдержал паузу…"
            me "Не могла бы ты коротко, буквально в несколько фраз, обрисовать роль товарища Генды в становлении нашего государства?"
            show sl surprise pioneer close at center with dspr
            "Разумеется, я бил наугад, но вряд ли этот самый Генда мог быть вовсе уж незначительной личностью, раз в пионерском лагере ему памятники ставят."
            "Смотреть на Славю было одно удовольствие. Определённо, она ожидала услышать что угодно, но только не это."
            "Мне стоило немалых усилий удержаться от смеха, но я справился."
            me "Я совершенно серьёзно - именно такой вопрос. И ты обещала ответить на него, если сможешь."
            "Спокойно заявил я."
            me "Или эта тема тебя смущает?{w} Разумеется, тогда я не буду настаивать."
            "Здесь я позволил себе улыбнуться, но только самую малость. Так, намёк на улыбку."
            show sl serious pioneer close at center with dspr
            sl "Подожди, ты и вправду хочешь, чтобы я…"
            "Наконец-то пришла в себя Славя."
            me "Да, я именно прошу тебя рассказать своими словами про Генду. Потом, как обещал, объясню зачем."
            "Девочка взмахнула косами и улыбнулась."
            show sl normal pioneer close at center with dspr
            sl "Ну хорошо. Стоит постараться как минимум для того, чтобы услышать потом твоё объяснение."
            "Как я и предполагал, со знанием истории у нашей активисточки всё было в полном порядке."
            "Я слушал, затаив дыхание, хотя и пытался делать безразличное лицо, как будто речь шла о фактах, известных мне с пелёнок."
            th "Генда… Руководящая роль… Верный соратник и продолжатель дела В.И.Ленина… Играл важнейшую роль в становлении советской науки…"
            "В голове творилось чёрт знает что. Либо это часть какой-то колоссальной мистификации, жертвой которой я всё-таки стал, либо…"
            show sl serious pioneer close at center with dspr
            "Возможно, мне не слишком хорошо удалось совладать с лицом, потому что, закончив, Славя посмотрела на меня как-то странно.{w} Пожалуй, даже с подозрением."
            sl "А теперь твоя очередь."
            me "Что?"
            "Растерялся я."
            show sl serious pioneer close at center with dspr
            sl "Ты же обещал объяснить, зачем тебе это было нужно!"
            "Возмутилась Славя."
            show sl angry pioneer close at center with dspr
            sl "Я надеюсь, это не какая-то глупая шутка?"
            "Спросила она с упрёком."
            me "Да нет, что ты! На самом деле всё очень просто.{w} Даже немного скучно, если честно."
            show sl serious pioneer close at center with dspr
            "Заторопился я с ответом.{w} Что бы здесь ни происходило, совсем незачем портить отношения со Славей.{w} Да и просто не хотелось обижать хорошего человека."
            show sl normal pioneer close at center with dspr
            me "Всего лишь часть школьного проекта по истории. Своего рода небольшой социопсихологический опрос."
            me "Мы с ребятами за лето опрашиваем всех, кого сможем, на такие вот темы.{w} Потом, осенью, всё это соберём воедино, обработаем…"
            me "В общем, такой вот эксперимент."
            show sl serious pioneer close at center with dspr
            sl "Экспериментатор!"
            "С упрёком бросила мне Славя, надув губки и рассеяно обводя черенком ложки клеточки на клеёнке, слегка смахивающей на шахматную доску."
            me "Славя, извини, я совсем не хотел тебя обидеть, но здесь всё дело именно в неожиданности вопроса.{w} К тому же, всё-таки общественное поручение…"
            sl "Да ладно тебе, ничего я не обижаюсь!"
            "Буркнула Славя."
            show sl normal pioneer close at center with dspr
            sl "Но чтобы окончательно загладить свою вину, тебе придётся принять участие ещё в одном общественно важном мероприятии."
            show sl happy pioneer close at center with dspr
            "Заулыбалась вдруг она, глядя на моё насторожившееся лицо."
            sl "Мы сейчас вместе идём на концерт.{w} И не вздумай отказываться! Ольга Дмитриевна велела всех собрать, так что явка обязательна."
            show sl smile pioneer close at center with dspr
            sl "И вообще, что ты будешь один сидеть, как сыч какой-нибудь?!"
            show sl normal pioneer at center with dissolve
            stop music fadeout 3
            play ambience ambience_dining_hall_full fadein 1
            "Встав из-за стола, она выжидающе посмотрела на меня."
            "Меньше всего мне сейчас хотелось любоваться на какую-то дурацкую самодеятельность."
            "В конце концов, в домике меня под подушкой дожидался многострадальный учебник, но выбора у меня, похоже, не было."
            th "Кажется, отказаться в такой ситуации будет не только рискованно, но и попросту невежливо…"
            hide sl with easeoutright
            "Стараясь не слишком показывать своё разочарование, я встал и поплёлся следом за Славей."
#
            $ uvao_D4_lunch_sl = True
            $ uvao_D4_concert = True
#
        "Сесть одному":
            "Идея болтать с пионерами об истории страны нравилась мне всё меньше и меньше. Только время зря терять!{w} Совсем не обязательно, что я смогу узнать хоть что-то полезное, а вот за сумасшедшего очень даже могут принять. Мало мне разговора с Леной насчёт Генды!"
            th "Лучше сначала почитать добытую в библиотеке книгу, а уже потом, если приспичит, доставать окружающих вопросами.{w}\nПо крайней мере, не буду похож на совсем уж наивного дурачка."
            "Я уселся за свободный столик в углу, изобразил самое мрачное выражение лица, на какое был способен, и принялся торопливо поглощать пищу."
            "Обед прошёл довольно спокойно.{w} Пару раз в поисках места к столу подходили было незнакомые пионеры, но встретив мой взгляд, благоразумно ретировались подальше."
            "Залпом проглотив компот, я поспешно встал из-за стола и, пока меня никто не успел перехватить, выскочил вон из столовой."
            "Очень хотелось поскорее добраться до домика и наконец-то заняться учебником истории."
            stop ambience fadeout 2
            window hide            
            jump uvao_D4_meet_Yulia_after_lunch
    stop ambience fadeout 2
    window hide
#
label uvao_D4_meet_Yulia_after_lunch:
#Д4 встречаем Юлю после обеда
#
# используется флаг рассказа Слави про Генду за обедом uvao_D4_lunch_sl
#
    $ alt_chapter_uv(4, u"Идентификация")
    $ persistent.sprite_time = "day"
    scene black with dissolve
    $ renpy.pause(2)
    play sound sfx_open_door_2
    play ambience ambience_int_cabin_day fadein 1
    scene bg int_house_of_mt_noitem_day with dissolve
    window show
#Придя, Семён приоткрывает окно, потому что домик прогрелся на солнце и читать умные книжки жарко
    "Домик успел здорово прогреться на солнце, поэтому первым делом я открыл окно нараспашку."
    if uvao_D4_concert:
# Ходили на концерт
        "В ушах до сих пор звенело от адской какофонии, которую мне приходилось терпеть последние полтора часа."
        th "И дался Славе этот концерт. Точнее я на нем."
        th "Если ты видел хоть один детский утренник - считай, видел их все."
        "Вожатые рассадили отряды по предназначенным им местам, а часть пионеров сгрудилась сбоку от сцены, возле лестницы.{w} Должно быть, это - наши юные дарования."
        "Ведущая - Мику - объявила начало концерта, все захлопали, и - на-ча-лось."
        "Не попадающие ни в одну ноту певцы."
        "Музыканты, которым следовало бы прописать мануальную заборотерапию."
        "Танцоры, от природы лишенные чувства ритма."
        "И что самое удивительное - каждого провожали бурными аплодисментами!{w} Хотя, возможно, это был звук дружного фейспалма."
        "В общем, улучив момент, когда Славя отвлеклась, я благоразумно слинял с сего мероприятия.{w} Терпеть не могу самодеятельность, у меня от нее приступы мизантропии!"
        th "Столько времени потрачено зря!"
        dreamgirl "Ладно, хватит ныть об упущенных возможностях!{w} Вырваться-то ты всё равно не мог. Славя сторожила как…"
        dreamgirl "Посмотри лучше, возле кровати рюкзак валяется.{w} Не про него ли Олька говорила?"
        th "А знакомый рюкзак. Точь-в-точь мой старый «Гриззли».{w} Разве что мой-то {i}дома{/i} остался…"
        th "Так и что тут нам «родители» прислали?"
        "Конфеты…"
        dreamgirl "А это точно твое? Скорее мелкой прислали. Напутали чего-то где-то."
        "Проволока… перочинный нож… веревка… свинец… леска… ещё какая-то мелочь…"
        th "Да что за фигня? На что мне это все?"
        dreamgirl "Отлично, будем делать заграждение, отольем пуль, добудем пушку, возьмем заложника и будем требовать ответов."
        th "Да ну тебя."
        "Я задвинул рюкзак под кровать, и устало упал на неё."
        th "Что за день? Бездарно потраченное время, ненужный концерт, бесполезная посылка…{w} {i}И никаких ответов{/i}! Шикарно! Что дальше, интересно?"
        th "Ладно, посмотрю хоть учебник. Зря с риском для жизни тырил, что ли?!"
    else:
# Избежали концерта
        "Возле моей кровати обнаружился подозрительно знакомый рюкзак.{w} Можно было бы даже подумать, что это мой старый добрый «Гриззли», не будь я уверен, что прибыл сюда без рюкзака."
        th "Наверное, это про него мне Ольга утром говорила. Интересно, что там?"
        "Прямо под молнией, поверх остального содержимого, обнаружился внушительный пакет с конфетами."
        "Под пакетом - колода игральных карт, перочинный нож, моток лески, коробочка с рыболовными крючками и, почему-то, мятый кусок свинца."
        th "Странный набор. Особенно свинец этот мне прямо-таки позарез нужен… Хотя, можно из него грузил понаделать, если вдруг приспичит."
        "Отчаявшись понять причудливую логику моих «родителей», кто бы они ни были, я махнул на всё рукой и вытащил из-под подушки заветный учебник."
        th "Пора уже разобраться с этим Гендой вплотную!"
# Конец вилки с концертом №1
    window hide
    show blinking
    $ renpy.pause(5)
    window show
#    "Наверное, прошло часа два, прежде чем я отложил учебник в сторону и тупо уставился в стену."
    "Не знаю, сколько прошло времени, прежде чем я отложил учебник в сторону и тупо уставился в стену."
    "Голова была готова взорваться."
    if uvao_D4_lunch_sl:
        "По всему выходило, что Славя за обедом мне всё очень точно изложила про этого самого Генду."
        "Из книги я не узнал почти ничего принципиально нового. Так, подробности."
    else:
        th "Генда…{w} Академик из народа…{w} Верный соратник и продолжатель дела В.И.Ленина…{w} Научная работа…{w} Множество статей…{w} Важнейшая роль в становлении советской науки…{w} Беззаветная преданность идеям коммунизма…"
    play music music_list["my_daily_life"] fadein 3
    "Казалось бы, после внезапного марш-броска из промозглой зимы прямиком в середину лета я должен быть готов ко всему. {w}Оказалось, что нет, не готов."
    "Я и сам понимал, что все эти дела минувших дней меня напрямую не касаются, но ничего не мог с собой поделать. {w}Вроде бы, какое мне дело до того, кто здесь что возглавлял, но ощущение было такое, словно под ногами закачалась опора, с детства казавшаяся незыблемой."
    dreamgirl "Ой, а что тут у нас такое? Потрясение основ? Ниспровержение устоев? Чужого очкастого дядю подсовывают?"
    dreamgirl "А ну давай-ка, быстренько напомни мне основные вехи славного пути вождя мирового пролетариата {i}нашей{/i} Родины! Того самого, который Ильич!"
    th "Э-э…"
    window hide
    $ set_mode_nvl()
    window show
    "Память услужливо принялась подсовывать что-то про добрый прищур глаз и чернильницы из хлебного мякиша… {w}Шалаш в Разливе… {w}Или Разлив - это не местность, а что-то другое? {w}Ссылка, Надежда Константиновна и обручальные кольца, сделанные из монет…"
    "\nСтарший брат Александр, который покушался взорвать… этого, как его… Александра III, вот!{w}\nИли нет, это-то здесь при чём. Не помню даже, кто того царя в итоге укокошил - этот самый брат, или кто-то другой?"
    "Хотя погодите, это же Александра II взорвали! И о чём это я? При чём здесь вообще все эти Александры?!"
    "Всё это на вехи пути вождя пролетариата тянуло как-то слабо."
    "\nЯ зажмурился и попробовал ещё раз."
    "Так. Возглавлял партию большевиков, там ещё враги-меньшевики были… {w}Минуточку, так они ведь тоже были членами компартии? Вот ведь…"
#    nvl clear
    window hide
    $ set_mode_adv()
    window show
    dreamgirl "Ну и какое отношение имеет та чушь, которая тебе лезет в голову, к тому, как этот самый вождь руководил страной и что он для неё сделал? Даже неважно, плохого или хорошего."
    dreamgirl "Ну ладно, последний вопрос, на двойку - назови даты рождения и смерти. {w}\nХотя нет, это слишком сложно. Хотя бы год смерти, а?"
    th "1928-й?"
    "Шиза только вздохнула. \nКажется, я сморозил очередную глупость…"
    th "Зато я Ленина в мавзолее видел! Сам, лично!"
    dreamgirl "Тогда, да! Конечно же, это полностью опровергает существование Генды, а заодно - и учебника, который валяется рядом с тобой!"
    "Шиза насмешливо фыркнула и замолчала."
    "Я задумался. {w}Получалось, что про родной мир я знал не так уж много."
    "Пожалуй, о новейшей истории здешнего мира я теперь знаю даже больше - вон, хотя бы учебник пролистал."
    "И кстати, по всему выходит, что это именно какой-то {i}другой{/i} мир. Разве что всё вокруг - очень убедительная подделка…"
    "Я снова взял в руки учебник.{w} В меру потрёпанная книга - слегка потёртая обложка, переплёт немного перекосился. Многие страницы, особенно те, что в начале, хранили следы рук предыдущих читателей.{w} Похоже, большинство из них успевало продвинуться в изучении материала не так уж далеко."
    th "Ну если уж и {i}это{/i} подделка…"
    play sound sfx_tree_branches
    "От размышлений о множественности миров меня отвлек негромкий шелест, донёсшийся с улицы.{w} За приоткрытым окном вздрогнула ветка сирени…"
    window hide
    $ renpy.pause(1)
    window show
    if uvao_D4_concert:
# Ходили на концерт
        dreamgirl "У нас завелись гости."
        th "Ага, и я даже догадываюсь, какие именно."
        th "Мелкие и рыжие."
        dreamgirl "Как тараканы. Непонятно, ты их гоняешь или они тебя."
        "Копошение продолжилось."
        th "Сделать, что ли, себе приятное, а ближнему гадость?{w} Тем более, Мелкая Зараза там явно не просто так засела."
        dreamgirl "Карателем подрабатываем? Одобряю! Шнель, шнель, партизанен капут!"
        th "Яволь, майн фюрер!"
        "Я встал с кровати… "
        stop music fadeout 1
        play sound2 sfx_bodyfall_1
        extend "и забытый учебник плюхнулся на дощатый пол!"
        dreamgirl "Самое главное - все делать тихо, ага. Ты б еще в барабан ударил."
        "Торопясь, чтобы рыжая бестия не успела смыться, я высунулся в окно."
    else:
# Избежали концерта
        "Похоже, кто-то там есть, причём этот «кто-то» старается не афишировать своё присутствие, раз по кустам шастает, а не ходит, как нормальные люди, по дорожке."
        "Я замер, прислушиваясь.{w} Несколько секунд всё было тихо…"
        "Потом за окном промелькнуло что-то тёмное, но так быстро, что ничего толком разглядеть не получилось."
        "Стараясь не заскрипеть пружинами, я тихонько встал с кровати и, крадучись словно ниндзя, двинулся в сторону окна{w}… Чтобы немедленно споткнуться о проклятый рюкзак, так и валявшийся на полу!"
        stop music fadeout 1
        with vpunch
        play sound2 sfx_bodyfall_1
        $ renpy.pause(1)
        dreamgirl "Не расстраивайся, у тебя есть и сильные стороны! То есть, должны быть, непременно должны…"
        "Охая, я кое-как поднялся на ноги. Хорошо хоть, головой о стол не приложился, а не то был бы у них тут детективный сюжет с моим трупом в качестве главной улики."
        "Кто бы там под окном ни прятался, после такой шумной встречи он наверняка уже смылся от греха подальше."
        "С этой мыслью я высунулся в окно."
# Конец вилки с концертом №2
    $ routetag = "uv"
    window hide
    scene bg int_house_of_mt_noitem_day:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 2.0 zoom 1.6 xalign 0.7 yalign 0.35
    $ renpy.pause(2)
    stop music fadeout 1
    scene bg ext_house_of_mt_day with dissolve:
        zoom 2.5 xalign 0.7 yalign 0.55
    $ meet('uv','Существо')
    show uv dontlike close at center
    window show
    play music music_list["mystery_girl_v2"] fadein 5
    "…Где чуть ли не нос к носу столкнулся с… э-э-э?"
    "На меня в упор смотрела пара настороженных глаз необычного золотисто-янтарного цвета."
    "Бледноватое лицо, густые, изрядо всклокоченные каштановые волосы, напряжённо расставленные уши."
    th "Что?! Уши?!"
    "Я внезапно понял, что на расстоянии вытянутой руки от меня сидит, напряжённо припав к земле, странное существо, которое я с уверенностью определил бы как девочку-подростка, если бы не уши, больше всего похожие на кошачьи, и не боязливо поджатый кошачий же хвост."
    dreamgirl "Эй, не тупи! Неужели ничего не напоминает? Ну, вспомни ты вчерашний вечер!"
    th "Да помню я всё, не мешай"
    "Пауза явно затягивалась. Не сводя с меня пристального взгляда, существо осторожно попятилось."
    show uv dontlike at center with dissolve
    "Всего-то на полшага, но я почувствовал, что ещё пара секунд - и она попросту убежит."
    th "В конце концов, я всегда неплохо ладил с кошками, в отличие от собак.{w} Да и в отличие от большинства людей тоже, если уж на то пошло."
    "Опасной она мне не казалась, а если удастся наладить с ней контакт, то мало ли, что сможет рассказать такая… такое… необычное существо."
    me "Привет."
    "Негромко сказал я. Кажется, получилось не слишком уверенно, но мне было здорово не по себе."
    "Она замерла, по-прежнему глядя мне прямо в глаза."
    me "Извини, не хотел тебя напугать."
    dreamgirl "Тоже мне, мастер контакта! Тебя только с террористами договариваться посылать! Я имею в виду…"
    uv "Я думала, внутри никого нет. А тут грохот!"
    "Осуждающе сказало вдруг существо (или, всё-таки, девочка - и не поймёшь сразу, кем её считать)."
    show uv normal with dspr
    "Она наконец моргнула и, похоже, немного расслабилась."
    dreamgirl "Ты смотри, она ещё и разговаривать умеет. Надо же!"
    if uvao_D4_concert:
        #не падали
        "Стараясь не делать резких движений, я облокотился на подоконник, попытавшись принять непринуждённую позу."
        me "Понимаешь, я услышал, что под окном кто-то ходит, и хотел посмотреть кто, но уронил книжку на пол."
    else:
        "Стараясь не делать резких движений, я облокотился на подоконник, попытавшись принять непринуждённую позу, насколько это было возможно с ноющими локтями и коленями."
        me "Понимаешь, я услышал, что под окном кто-то ходит, и хотел посмотреть кто, но споткнулся и упал."
    "Сказал я, решив на всякий случай строить фразы попроще."
    uv "Вы, люди, всегда такие неуклюжие."
    show uv normal with dspr
    "Сообщила она.{w} Вздохнув, уселась на траву, поджав под себя ноги и аккуратно уложив хвост вокруг. Судя по всему, убегать она раздумала."
    "Теперь, когда она перестала сжиматься в комок, я поневоле отметил, что фигурка-то у неё очень даже ничего."
    "Изрядно оборванное короткое летнее платьице не скрывало изящный изгиб бёдер.{w} Стройные точёные голени, тонкие щиколотки, маленькие ступни…"
    dreamgirl "Да что тебе ступни, ты посмотри, какие у неё…"
    th "Цыц!"
    "Призвал я к порядку одновременно и Шизу и себя."
    "Впрочем, похоже было, что хвостатую девочку нисколько не смутило столь пристальное внимание с моей стороны."
    "Наоборот, в ответ она также разглядывала меня с самой наивной непосредственностью."
    me "Э-э-э… Послушай, а как тебя зовут?"
    show uv surprise with dspr
    uv "Меня?"
    "Искренне удивилась она."
    me "Ну да. Вот меня, например, Семён зовут."
    show uv normal with dspr
    uv "Я знаю. Слышала, как тебя ваша старшая так называла. И ещё та, с длинной косой - Славя."
    me "А у тебя есть имя?"
    show uv smile with dspr
    uv "А меня никак не зовут. Я - это просто я, зачем мне имя? И так всё ясно."
    show uv normal with dspr
    "Она немного подумала."
    uv "Хотя раньше меня Юлей называли. Наверное, и тебе можно. Если хочешь."
    $ meet('uv','Юля')
    "Мне показалось, что беседа начала ей надоедать и я поспешил сменить тему, ляпнув первое, что пришло в голову:"
    me "А ты здесь что-нибудь искала, да?"
    uv "Интересно было, что там в домике. В других я уже бывала, а тут всё время заперто."
    show uv upset with dspr
    "Она недовольно надула губки."
    uv "Еду вкусную искала."
    me "Так ты, наверное, есть хочешь?"
    "Догадался я."
    show uv normal with dspr
    uv "Не очень… В лесу всегда можно что-нибудь найти. Обычно я на мышей охочусь, только их поймать почему-то трудно. Странно, правда?"
    "Я-то как раз ничего удивительного в этом не находил - уши там, или не уши, а великовата она, мышей-то ловить.{w} Впрочем, ей этого говорить явно не стоило, поэтому я молча кивнул."
    uv "Ещё я в столовой еду беру иногда, только там вкусной еды мало остается, ваши всю съедают."
    "Она сказала это так просто, что сразу стало ясно - принципы собственности, что частной, что коллективной, для неё вовсе не пустой звук.{w} Она о них попросту не знает."
    me "Слушай, так это вчера, наверное…"
    play music eat_horn fadein 2
    "Из динамиков разнёсся по лагерю призыв к пионерам восполнить иссякшие за день силы."
    stop music fadeout 2
    play ambience ambience_int_cabin_day fadein 1
    "Я как-то сразу вспомнил, что за чтением учебника, делом пусть и полезным, пропустил полдник."
    show uv upset far with dspr
    "Юля дёрнула ушами и одним гибким движением поднялась на ноги."
    "Да-а-а, пожалуй, насчёт девочки-подростка я был неправ. Ничего детского в ней не было заметно.{w} Скорее уж, очень стройная молодая девушка, просто… компактная, что ли."
    uv "Ваши сейчас есть пойдут. Не хочу, чтобы меня кто-нибудь увидел."
    show uv upset far at fright with ease
    "Она повернулась и скользнула прочь."
    me "Подожди! Хочешь, я тебе что-нибудь вкусное с ужина принесу?"
    show uv smile far at fright with ease
    uv "Принеси! Булочку!"
    "Живо обернулась ко мне хвостатая."
    show uv grin far
    uv "Их сегодня пекли, я знаю! По запаху!"
    "С восторгом облизнулась она."
    me "Хорошо, будет тебе булочка. Где встретимся?"
    show uv normal far at fright
    "Юля задумалась на пару секунд."
    uv "За воротами, куда автобус приезжает.{w} Там вечером никто не ходит."
    me "Договорились…"
    hide uv with moveoutright
    "Сказал я её удаляющейся спине."
    dreamgirl "Нет, ты заметил, какие у неё…"
    th "Да заметил я, заметил! Не слепой.{w} И вообще, сделай милость - отстань. Здесь и так полно девчонок бегает, далась тебе эта ушастая!"
    dreamgirl "Вот то-то и оно, что девчонок полно, но хвост и уши - только у этой! Такая няшка!"
#    play music music_list["my_daily_life"]
    scene bg int_house_of_mt_noitem_day with dissolve
    "Я слез с подоконника, едва не наступив на рюкзак, и закрыл окно."
    # "Со злости я хлопнул окном.{w} Хотел было пнуть валяющийся посреди комнаты рюкзак, но потом решил всё-таки поберечь ноги."
    th "Кстати о рюкзаке!{w} Конфеты наверняка должны подойти для бартера с этим ушастым чудом природы!"
    dreamgirl "Позволь узнать, за какие же это услуги ты собираешься расплачиваться в таких объёмах?"
    th "За информацию! И заткнись уже наконец, или я сегодня же ночью заберусь в медпункт и буду есть там все таблетки подряд, пока не найду средство от шизофрении!"
    dreamgirl "Ладно, ладно, не горячись. Я нисколько не сомневаюсь, что способы охоты на мышей, которым она может научить, тебе очень пригодятся в жизни."
    "Недовольно буркнула Шиза, но всё-таки заткнулась."
    stop music fadeout 3
    window hide
    jump uvao_uvao_D4_supper
#
label uvao_uvao_D4_supper:
# Д4-Ужин
#
# используется флаг посещения медпункта alt_uvao_D4_viola_morning
# устанавливает флаг ужина с Виолой alt_uvao_D4_supper_cs
#

    $ uvao_D4_supper_cs = False
    $ alt_chapter(4, u"Юля. Ужин")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    window hide
    scene bg int_dining_hall_people_sunset
    with dissolve
    play ambience ambience_dining_hall_full fadein 3
    window show
    "Войдя в столовую я огляделся."
    if alt_uvao_D4_viola_morning and alt_prologue:
# Утром были у Виолыи в прологе выбрали "Я пойду с тобой", можем поужинать вместе
        "Свободных мест практически не было. Впрочем, за одним из столиков их было сразу три."
        "Четвёртое занимала Виолетта."
        th "Похоже, не меня одного её общество смущает…"
        "Впрочем, поговорить с ней было надо, так что совместить беседу с Виолой и ужин показалось мне хорошей идеей."
        menu:
            "Подсесть к Виолетте":
                th "По крайней мере, лучше поговорить с ней здесь, чем наедине в медпункте."
                show cs normal with dissolve
                me "Виола? Можно к вам присоединиться?"
                dreamgirl "Неплохой заход. Ужин, потом пара коктейлей - и она вся твоя!"
                "Она неторопливо оглядела меня с ног до головы."
#                $ renpy.music.set_volume(0.7, delay=2, channel='music')
                play music music_list["eternal_longing"] fadein 3
                show cs smile with dspr
                cs "Конечно, {i}пионер{/i}, присаживайся. Я никогда не против интересной компании за ужином."
                "Как обычно, в этой фразе уместился целый ворох двусмысленностей, но отступать было поздно, и я уселся напротив неё."
                show cs normal close with dissolve
                me "Эээ… На самом деле я хотел поговорить, по поводу вашего {i}утреннего приглашения{/i}."
                dreamgirl "Ты что, собрался переиграть её на её же поле?! Без шансов, чувак, без шансов…"
                "Виола понимающе усмехнулась."
                show cs shy close with dspr
                cs "Само собой, приглашение всегда в силе, в {i}любое{/i} время дня и ночи. Только предупреди заранее, чтобы я могла подготовить все {i}необходимое{/i}."
                "Да, кажется, этот противник мне не по зубам."
                show cs smile close with dissolve
                me "Виола, я про местную легенду, девочку-кошку.{w} Я её видел сегодня. И даже парой слов перекинулся."
                $ renpy.music.set_volume(0.3, delay=3, channel='music')
                show cs normal close with dspr
                "Виола поставила стакан с компотом. Никакой томности во взгляде уже не наблюдалось."
                cs "Видел? Где?"
                me "Да возле домика. Услышал треск за окном, думал, опять Ульянка жуками забрасывать собирается, высунулся. А там она."
                "Виола помолчала, о чём-то размышляя."
                cs "И о чём вы беседовали, если не секрет?"
                me "Да ни о чём практически. Сказала, что я неуклюжий. Ещё что раньше меня видела."
                cs "Интересно. Она не представилась?"
                me "Представилась. Говорит, что раньше её Юлей звали."
                "Виола как-то оценивающе на меня посмотрела."
                "Под её пристальным взглядом я невольно заёрзал на стуле и уставился в тарелку."
                me "Мне с вами идти, да?"
                cs "Зачем?"
                me "Ну, как… Аминазину покушать, или что там у вас…"
                "Похоже, что-то для себя решив, Виола смягчила вгляд и улыбнулась."
                show cs smile close with dspr
                cs "Успокойся, всё с тобой нормально. Вообще, ты неплохо держишься для четвёртого дня в новом мире."
                th "Она знает?! Откуда?"
                me "Что?"
                show cs normal close with dspr
                cs "Что слышал. Не ты первый, пионер."
                me "А… Вы тоже?…"
                cs "Я? Нет, конечно. Я из группы исследователей."
                dreamgirl "О чёрт. Парень, ты, кажется, серьёзно влип. Сейчас тебя в такое место потащат, что дурдом раем покажется."
                "Кажется, Виола поняла, почему я вдруг побледнел, и поспешила меня успокоить:"
                show cs smile close with dspr
                cs "Пионер, не нервничай ты так. Никто не будет тебя исследовать... Если сам не попросишь.{w} Изучаем мы точки проколов между реальностями, а не тех, кто сквозь них приходит."
                cs "Смысл тебя потрошить? Такой же человек, как и все. Ну, жил на двадцать пять лет в будущем, подумаешь."
                th "Уф-ф-ф! Отлегло. Может, и вывернуться получится."
                show cs normal close with dspr
                "Виола ненадолго замолчала, задумчиво покачивая стакан с остатками компота."
                cs "Насколько я могу судить, то, что представилось тебе Юлей, одно из проявлений точки прокола.{w} Визуализация, можно сказать."
                "Я сложил немногие известные мне факты в кучку."
                me "Получается, что моё появление здесь и эта… Юля как-то взаимосвязаны?"
                "Виола решительно отставила стакан в сторону."
                cs "Думаю, раз ты её уже дважды за сутки видел, то весьма вероятно, увидишь в скором времени ещё раз."
                me "Навер… Ой, да! Мы же договорились с ней после ужина на стоянке встретиться."
                "Виолетта только головой покачала на мою забывчивость."
                cs "У тебя ведь есть фотокамера в {i}смарт-фоне{/i}?"
                "Последнее слово она выговорила как-то непривычно."
                me "Откуда вы?… А, ну да. Есть, конечно."
                cs "Попробуй её сфотографировать, и многие вопросы сразу отпадут."
                me "Лад… Подождите, так мне что, к ней идти?"
                cs "А почему нет? Есть все основания полагать, для {i}тебя{/i} она должна быть безопасна, если специально злить не будешь."
                $ renpy.music.set_volume(0.6, delay=3, channel='music')
                show cs smile close with dspr
                cs "Только хочу сразу предупредить насчёт {i}чересчур{/i} близких контактов — тут тебе и изделие №2 не поможет."
                "Я снова покраснел."
                me "Да я и не думал даже!"
                show cs smile close with dspr
                cs "Ну-ну. Смотри, тут ты не котятами обзавестись рискуешь."
                stop music fadeout 5
                show cs normal close with dspr
                cs "Последствия могут быть весьма тяжёлыми, и не только для тебя. Понимаешь меня, {i}пионер{/i}?"
                me "Угу."
                show cs normal close with dspr
                cs "Вот и хорошо. Она что-нибудь еще говорила? Не просила чего-нибудь принести?"
                me "Просила. Булку просила. Мышей, говорит, сложно ловить, а булки ещё и вкуснее."
                cs "Ну так бери и иди. И постарайся побольше с ней говорить, всё-таки случай… редкий, скажем так."
                cs "Если получится к ней в гости напроситься — совсем хорошо. Ольгу я предупрежу, чтобы не искала тебя."
                me "Ольгу Дмитриевну? А она тоже… с вами?"
                cs "Да, отвечает за адаптацию.{w} Иди на своё свидание, пионер, не заставляй аномалию ждать. Потом мне все доложишь."
                me "А… Виола, а я вообще где?"
                show cs smile close with dissolve
                cs "Долгий разговор. Да и не в столовой же об этом беседовать?"
                "Покорно кивнув, я сунул в карман сдобную булку с изюмом и встал из-за стола."
                cs "Подожди-ка. Мне этот убийца фигуры ни к чему, а тебе для дела пригодится."
                "С этими словами она протянула мне свою порцию сдобы."
                dreamgirl "Да-а-а, такую фигуру грех не поберечь!"
                "Я сунул вторую булку в ощутимо раздувшийся карман и поспешил откланяться."
                hide cs
                $ renpy.music.set_volume(1, delay=0, channel='music')
                th "Всё равно, ещё немного разговоров в том же духе - и стены столовой придётся отмывать от моих мозгов."
                th "Проколы между реальностями, понимаешь, аномалии… Визуализации…"
                "Мне явно следовало взять паузу и привести содержимое головы в порядок."
                $ uvao_D4_supper_cs = True
                jump uvao_D4_supper_end
            "Сесть одному":
#                "Очень СТРАШНО!"
                th "А с другой стороны, рассказывать-то пока особенно и нечего."
                th "Вот поговорю после ужина с этой Юлей ещё раз…"
                dreamgirl "Ой, да ладно! Признайся хотя бы самому себе, ты попросту боишься, что тебя съедят.{w} АМ!"
                th "О! Как раз место свободное есть."
                "Проигнорировав насмешки Шизы, я забился в угол и принялся спешно поглощать еду."
    else:
# Утром не ходили к Виоле
        "Как и за обедом, в столовой было неожиданно много свободных мест."
        "Здраво рассудив, что поддерживать с кем-то беседу всё равно сейчас не в состоянии, я забился в угол и принялся спешно поглощать еду."
# Ужинаем в одиночестве.
    dreamgirl "Эй! Ты булочку-то не сожри, проглот!"
    dreamgirl "И вообще, мало ли, какой случай может приключиться."
    th "И правда… Случаи-то, они разные бывают."
    "На всякий случай, я сунул полагающуюся мне булочку с изюмом в карман."
    "На месте мне не сиделось, очень уж хотелось поскорее убедится, что неожиданное знакомство перед ужином - не плод моего воспалённого воображения."
    "Или, как минимум, в том, что мои галлюцинации достаточно устойчивы."
    "Наконец я решительно отложил вилку и вскочил из-за стола."
label uvao_D4_supper_end:
    th "А нюх-то у хвостатой что надо - действительно, булочки свежие, сегодняшние."
    "Неожиданно для самого себя ухмыльнувшись этой мысли и избавившись от подноса с недоеденным ужином, я вышел из столовой."
    stop ambience fadeout 3
    window hide

    jump uvao_D4_meet_Yulia_at_evening
#
#День4 встречаем Юлю после ужина за воротами
#
label uvao_D4_meet_Yulia_at_evening:
    $ alt_chapter(4, u"Юля. Вечерние встречи")
    $ persistent.sprite_time = "sunset"
    scene bg ext_dining_hall_away_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    window show
    "К воротам лагеря я шёл целеустремлённой походкой, не глядя по сторонам и постаравшись изобразить на лице чувство ответственности за выполнение очень важного дела, которое мне, несомненно, поручили."
    window hide
    scene bg ext_square_sunset with fade
    window show
    "В итоге, до ворот я добрался без помех - то ли проявились дремавшие раньше способности к лицедейству, то ли до меня попросту не было никому дела."
    stop ambience fadeout 1
    window hide
    scene bg ext_no_bus_sunset with fade
    play ambience ambience_camp_entrance_evening fadein 1
    window show
    "Быстро оглянувшись и убедившись, что за мной никто не увязался, я вышел на стоянку."
    "Здесь тоже было пусто."
    "Я постоял, прислушиваясь."
    play sound sfx_tree_branches
    show uv normal far at fleft with easeinleft
    uv "Эй, иди сюда!"
    "Из кустов слева высунулась Юля. Осмотревшись по сторонам, она выбралась на стоянку."
    show uv normal at left with dissolve
    uv "Принёс?"
    me "Конечно, как обещал!"
    "Я протянул ей прихваченную с ужина булочку."
    play music music_list["she_is_kind"] fadein 5
    show uv smile at center with dissolve
    "Юля проворно сцапала гостинец и, усевшись на поребрик, окружающий статую пионера, впилась в сдобу ровными белыми зубками."
    "Машинально отметив про себя, что клыки у неё тоньше и длиннее, чем обычно можно встретить у людей, я тоже присел чуть в стороне."
    show uv smile close at center with dissolve
    me "Давно ждёшь?"
    "Она неопределённо мотнула головой."
    "Похоже, моя новая знакомая целиком сосредоточилась на еде."
    "Она жмурилась от удовольствия и явно не собиралась отвлекаться на меня. Тем не менее, я попробовал ещё раз."
    me "А ты где живёшь? Где-то в лагере? Или в лесу?"
    uv "Нев. Тав."
    "Невнятно сказала Юля сквозь булку и неопределённо махнула рукой куда-то вдоль забора."
    "Она доела остатки и со вздохом облизнулась."
    show uv normal close at center with dspr
    uv "Вкусно. Жалко только, что мало."
    uv "У тебя с собой больше ничего нет?"
    "Я улыбнулся."
    if uvao_D4_supper_cs:
        me "А вот есть!"
        "Острые зубки вонзились во вторую булочку."
        me "А кстати, ты конфеты любишь?"
    else:
        me "Нет, с собой нет. А кстати, ты конфеты любишь?"
    dreamgirl "Девочка, девочка, пойдём со мной в подвал! Я тебе там конфет дам!"
    show uv laugh close at center with dspr
    uv "Конечно люблю! А у тебя есть конфеты?"
    "Она явно оживилась и снова облизнулась."
    me "Я могу принести, если хочешь.{w} Может, я бы в гости к тебе зашёл? Ну туда, где ты живёшь."
    show uv surprise close at center with dspr
    "Кажется, Юлю такая идея искренне удивила."
    "Впрочем, через пару секунд она улыбнулась и кивнула."
    show uv smile close at center with dspr
    uv "Приходи. И конфеты приноси. Смотри, не забудь!"
    me "Так а куда приходить-то?"
    show uv normal close at center with dspr
    uv "Так говорю же - я там живу!"
    "Сказала Юля нетерпеливо и снова махнула рукой куда-то вдоль забора."
    "Вероятно, на моём лице отражалась некоторая заторможенность мыслительных процессов, потому что она снисходительно улыбнулась и добавила:"
    show uv smile close at center with dspr
    uv "Там большой старый дом, за лесом."
    if uvao_D4_supper_cs:
        extend " По тропинке идти надо, тогда придешь."
        uv "Здесь в заборе калитка есть, она незаперта всегда. Вот от неё и иди по тропинке."
    me "А ког…"
    show uv upset at center with dissolve
    stop music fadeout 3
    "Начал было я, но Юля вдруг вздрогнула и навострила ушки."
    "Из-за ворот донеслись детские голоса, потом - приглушённый взрыв смеха."
    hide uv with easeoutleft
    play sound sfx_tree_branches
    "Я было повернулся обратно к Юле, но той и след уже простыл - только ветки кустов закачались."
    dreamgirl "И что этим пионерам спокойно не сидится?!{w} Лучше бы с умной книжкой посидеть, так нет же, шляются везде, ржут как лошади, мешают серьёзным людям устанавливать контакт с чуждыми формами разума!"
    "В кои-то веки я был согласен с Шизой - в самом деле, из-за них я так толком ничего и не выяснил про эту ушастую."
    "Хорошо хоть, я теперь хотя бы приблизительно знаю, где её искать."
    "Иначе можно было бы до старости бродить по лесу и ничего не найти…"
    "Я с досадой махнул рукой и поплёлся обратно в лагерь."
    stop ambience fadeout 3
    window hide
    jump uvao_D4_evening_business
#
label uvao_D4_evening_business:
#День4 Ходим по лагерю перед сном после второй встречи с Юлей
#
# устанавливает флаг вечернего визита к Виоле в медпункт и выхода на тру-энд alt_uvao_true
#
    $ alt_uvao_true = False
    $ persistent.sprite_time = "sunset"
    scene bg ext_square_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    window show
    "Снова вечер, снова недовольный Генда."
    "Я сел на скамейку напротив памятника и откинулся назад."
    th "Так. По крайней мере вырисовывается план действий."
    th "Завтра с самого утра отправляюсь в старый лагерь, к Юле в гости."
    th "Надеюсь, не зря придётся ходить."

    if (uvao_D4_supper_cs):
        dreamgirl "Вообще-то тебя просили доложить о результатах переговоров."
        th "И то верно."
        menu:
            "Пора навестить доктора Виолу.":
                "Я поднялся, пересек площадь и свернул на дорожку к медпункту. Алиса, сидевшая с гитарой на отдаленной скамейке, проводила меня странным взглядом. "
                window hide
                scene bg ext_aidpost_sunset with fade
                window show
                th "Что это она так смотрит? Как на психа."
                dreamgirl "А ты и есть он. Добровольно идешь в логово сексуальной маньячки, затерроризировавшей все мужское население лагеря. Тебе только букета и бутылки вина не хватает для полной картины."
                th "Да ну тебя. Я же по делу."
                dreamgirl "Она-то этого не знает, что у тебя {i}дело{/i}. Жди, теперь слухи пойдут."
                "Переругиваясь сам с собой, я неторопливо добрел до медпункта и постучал."
                play sound sfx_knock_door7_polite
                $ renpy.pause(2)
                cs "Открыто!"
                window hide
                stop ambience fadeout 2
                play sound sfx_open_door_1
                $ renpy.pause(1)
                $ persistent.sprite_time = "day"
                scene bg int_aidpost_day
                with dissolve
                play ambience ambience_medstation_inside_day fadein 3
                play music music_list["eternal_longing"] fadein 3
                show cs normal with dissolve
                me "Добрый вечер, Виола."
                "Она оторвалась от своего допотопного компьютера, в который вбивала какие-то записи с карточек."
                show cs grin with dspr
                "Входи, входи, пионер. Ты с отчетом, или так, заскучал в одиночестве?"
                dreamgirl "Держи себя в руках, прекращай думать всякие пошлости! Думаешь, я не вижу? И взгляд выше."
                me "С отчетом."
                $ renpy.music.set_volume(0.3, delay=3, channel='music')
                show cs normal with dspr
                cs "Тогда присаживайся и выкладывай."
                "Я послушно уселся на край кушетки и принялся докладывать:"
                me "Встретил снова Юлю, покормил булками, немного поболтали.{w} Сказала, что она в каком-то большом старом доме живет. Я так понял, где-то к югу отсюда."
                me "Говорит, в заборе какая-то калитка есть и от неё надо по тропинке идти."
                "Виола кивнула."
                cs "А! Это старый корпус, есть здесь такой, за пределами территории.{w} Вобщем-то мы это предполагали. Вполне подходящее место - людей нет, и от лагеря недалеко.{w} К тому же… Впрочем неважно, продолжай."
                me "Ну так вот. Она вроде бы не против, если я к ней заявлюсь в гости. С конфетами и булками, само собой - очень практичная девушка."
                cs "Практичная - вполне объяснимо… Но еще раз повторяю, она - не девушка! Не человек. Я даже не уверена, что она живая, а не какая-нибудь массовая галлюцинация."
                cs "Завтра, когда пойдешь в гости - веди себя прилично. Слишком близкие контакты закончатся очень печально. Для тебя - как минимум."
                me "Эээ… А мне обязательно идти?"
                show cs smile with dspr
                "Виола снисходительно усмехнулась."
                cs "Если не хочешь - я не заставляю. Но в твоем положении разбрасываться шансами глупо, где ты еще сможешь докопаться до самой истины?{w} Во всяком случае, интересные впечатления я тебе гарантирую."
                me "Ясно…"
                dreamgirl "Эй! Она что, на слабо тебя пытается взять?"
                show cs normal with dspr
                cs "Так что, пойдешь? Я тогда с утра Ольге передам, что у меня для тебя важное поручение. Иди, пионер, отсыпайся, пока можешь."
                th "Однако, какой решительный подход! Похоже, она за меня уже всё решила."
                th "Хотя, с другой стороны - я ведь и сам не против пойти…"
                me "Виола, так а куда идти-то? Я имею в виду, где мне эту тропинку-то искать, про которую Юля говорила?"
                cs "Ну с этим-то как раз всё просто.{w} От площади иди к реке и возьми правее от лодочной станции, по дорожке вдоль домиков. Дойдёшь до ней забора - там калитка будет."
                th "Ну хоть что-то прояснилось. А кстати…"
                me "Виола… Вы еще обещали рассказать, куда это я попал."
                cs "Если вкратце - параллельный мир. Хотя ты это, наверное, уже понял. А подробный рассказ пока подождет."
                cs "К слову сказать, ты эту Юлю сфотографировать не пробовал, как я советовала?"
                me "Да не успел я - кто-то рядом по лагерю шлялся, спугнули они её.{w} Толком поговорить не дали."
                cs "Досадно… Ну ничего, при случае - попытайся."
                show cs grin with dspr
                cs "Ступай, уже поздно."
                stop music fadeout 3
                "Она практически выставила меня за дверь."
                hide cs with dissolve
                $ renpy.music.set_volume(1, delay=0, channel='music')
                $ alt_uvao_true = True
                window hide
                play sound sfx_close_door_1
                $ night_time()
                $ persistent.sprite_time = "night"
                scene bg ext_aidpost_night with fade
                play ambience ambience_camp_center_night fadein 2
                window show    
                "До отбоя еще оставалось время, и я пошел обратно на площадь…"
                scene bg ext_square_night with dissolve
                window show
                "…Где немедленно наткнулся на Алису."
                show dv normal pioneer at center   with dissolve
                dv "Что, Семен, в медпункт ходил? Заболел?"
                me "Да нет. В гости заходил, поболтать."
                show dv grin pioneer at center   with dspr
                dv "Поболтать?"
                "Ехидно ухмыльнулась она."
                show dv laugh pioneer at center   with dspr
                dv "Судя по тому, как тебя выпнули, {i}поболтать{/i} ты ничем не успел."
                me "Успел, успел. Долго ли, умеючи-то…{w} А что, ты тоже собиралась… {i}поболтать{/i}?"
                show dv angry pioneer at center   with dspr
                dreamgirl "Молодец. Виола бы тобой гордилась!"
                "Алиса все с тем же выражением смотрела на меня - что-то среднее между «Вот придурок!» и «Ни фига себе дает чувак!»."
                "Я гордо расправил плечи и пошёл восвояси."
                hide dv with dissolve
                #плюшки не тырим
                jump uvao_D4_sleep_time
            "Не пойду я к ней!":
                th "…Но это не значит, что я собираюсь делать всё, что она мне скажет!"
                th "Раскомандовалась! Видали мы таких!"
                dreamgirl "Ну смотри, тебе решать."
    else:
        pass
    "Вечерняя площадь была безлюдной - видимо, после родительского дня большинство пионеров собиралось лечь спать пораньше."
    "Мне спать пока не хотелось, я сидел и наслаждался тихим вечером."
    "Незаметно стемнело."
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_square_night with dissolve
    play ambience ambience_camp_center_night fadein 2
    window show
    "Я решил пройтись туда-сюда перед сном, и в итоге вырулил к столовой."
    window hide
    scene bg ext_dining_hall_away_night with dissolve
    window show
    "На крыльце кто-то копошился. Опять Юля?"
    play sound sfx_alisa_picklock
    th "А, нет. На этот раз действительно Алиса. Снова пытается вскрыть замок, вполголоса ругаясь себе под нос."
    scene bg ext_dining_hall_near_night with dissolve 
    me "Опять замок ломаешь, Двачевская? Сейчас доломаешь, и завтра все без завтрака останемся."
    show dv scared pioneer at center   with dissolve
    "Она аж подпрыгнула - кажется, я подкрался совсем тихо."
    #show dv rage pioneer at center   with dspr
    dv "Заикой сделаешь, блин! Чего приперся?"
    me "Того же, чего и ты. А тебе что, из дому припасов не прислали?"
    show dv angry pioneer at center   with dspr
    "Кажется, я что-то не то сказал - Алиса набычилась."
    dv "Не хочешь помогать - так и скажи!"
    me "Да ладно, помогу, не кипятись."
    show dv normal pioneer at center   with dspr
    "Тем более что и мне для завтрашнего похода не помешает запастись."
# тырят плюшки
    if (keys_keep or keys_take == 1):
    #если есть ключи
        if dv_help == 1:
        #если помогал Алисе в 1 день
            dv "Ты те ключи еще не потерял? Открывай давай."
        else:
            "Алиса уже собралась снова ковырять замок булавкой, но я вытащил из кармана ключи и демостративно позвенел ими."
            show dv grin pioneer at center   with dspr
            dv "Фига себе! Запасливый. Ну открывай тогда."
        th "С ключами любое дело становится намного проще."
        "Я открыл двери и мы проникли в пустую столовую."
    else:
        "Алиса принялась ковырять замок булавкой, как и в тот раз, я же вспомнил, в какое окно залезала Юля, и подошел проверить."
        "Оказалось, если немного подергать створку, шпингалет сам выходит из гнезда."
        show dv surprise pioneer at center   with dspr
        me "Эй, Алиса, хватит замок ломать! Иди сюда!"
        show dv normal pioneer at center   with dspr
        "Стараясь не натоптать следов на подоконнике, мы влезли в пустую столовую."
    window hide
    scene bg int_dining_hall_night with dissolve
    window show
    "Пяток булочек на брата - неплохой результат набега на столовую."
    "Алиса еще прихватила пару пирамидок с кефиром, а я решил обойтись сухпайком."
    window hide
    scene bg ext_dining_hall_near_night with dissolve
    show dv normal pioneer at center   with dissolve
    window show
#    "Поделив добычу, мы замели все следы и разошлись в разные стороны, по направлению к домикам."
    "Поделив добычу, мы замели все следы."
    "Мы уже собирались разойтись в разные стороны, по направлению к домикам, когда мне в голову пришла неплохая, как мне показалось, мысль."
    "Пока Двачевская в хорошем настроении, этим надо воспользоваться."
    me "Алиса, а ты ничего не слышала про какое-то старое здание за пределами лагеря, в той стороне?"
    "Я неопределённо махнул рукой куда-то в сторону реки."
    show dv surprise pioneer at center   with dspr
    dv "Это ты про старый корпус что ли? Тебе-то зачем?"
    th "Бинго! Теперь главное - не спугнуть."
    "Я состроил загадочную физиономию."
    me "Зачем-зачем! Затем же, зачем и тебе!"
    me "Сама-то, небось, сто раз там бывала!"
    show dv normal pioneer at center   with dspr
    dv "Вот ещё! Делать мне нечего, только по развалинам всяким шляться."
    "Фыркнула Алиса, посмотрев, однако, на меня с некоторым даже уважением.{w} Впрочем, в темноте я мог и ошибиться."
    dv "Вообще, туда добраться несложно. От лодочной станции иди направо, по дорожке вдоль домиков."
    dv "Дойдёшь до забора - там калитка будет.{w} А дальше по тропинке иди, через лес."
    th "Ну конечно, ни разу не была там, как же! А дорогу-то знает."
    "Алиса внезапно изобразила испуганное выражение лица и перешла на зловещий шёпот:"
    show dv scared pioneer at center   with dspr
    dv "Только учти, место там проклятое, можешь и не вернуться!"
    "С этими словами она развернулась и, весьма довольная собой, направилась в сторону своего домика."
    hide dv with dissolve
    dreamgirl "Ага, запугала прямо-таки до смерти! Как бы штаны не намочить от страха."
    th "Ладно, хоть что-то прояснилось. По крайней мере понятно, куда идти."
    th "Конечно, с Двачевской станется меня отправить в кругосветное путешествие, но придётся поверить ей. Других вариантов всё равно нет."
    "Я пожал плечами и пошёл к себе домой."
    
# приходит в домик
label uvao_D4_sleep_time:
    window hide
    play sound sfx_open_dooor_campus_1
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night with dissolve
    play ambience ambience_int_cabin_night fadein 1
    window show
    "Ольги Дмитриевны в домике не было.{w} И хорошо."
    if not alt_uvao_true:
        "Сгрузив припасы в рюкзак, я разделся и занырнул под одеяло."
    else:
        "Я быстро разделся и занырнул под одеяло."
    th "Завтра рано вставать. Лучше даже до завтрака."
    if not alt_uvao_true:
        "Я выставил будильник на телефоне на 6:45."
        "Подумав немного, отключил звук, поставив на вибро - чтобы не спалиться, если вдруг вожатая к этому времени не уйдёт."
        "Сунул телефон под тощую подушку и закрыл глаза."
    else:
        pass
    stop ambience fadeout 5
    window hide

    scene bg black with fade2
    stop music fadeout 2
    $ renpy.pause (2)

    
## THE END    
    return