#
# Д5 - Возвращение в лагерь через стоянку с палевом.
#
# Используется флаг alt_uvao_D4_viola_morning (ходили к Виоле утром Д4)
# Используется флаг alt_uvao_D5_hentai (хентаили кошочку в бункере)
#

label alt_day5_parking_back:
#
# <---строки ниже удалить при сборке!>
    $ day_time()
    $ persistent.sprite_time = "day"
    scene int_old_building_day_uvao
    play ambience ambience_old_camp_outside fadein 3
    window show
#
# для справки строка из uvao-D5-mines-sh.rpy
#    "Ольга Дмитриевна зачем-то обернулась и озабоченно обвела взглядом поляну. По-видимому, не найдя ничего подозрительного, она повернулась и скрылась за деревьями, вслед за остальными."
# <---строки выше удалить при сборке!>
    show uv grin with dissolve
    "Я с облегчением перевёл дыхание и покосился на Юлю. Та стояла донельзя довольная, разве что не подпрыгивая на месте."
    th "Ну да, она же говорила, что за нами, людьми, интересно наблюдать. А тут целый спектакль разыграли, как по заказу!"
    play sound sfx_stomach_growl
    "Однако тут желудок изволил напомнить о себе, и мои мысли приобрели совершенно другое направление:"
    th "Ё-моё, время-то уже к полднику, а я сегодня даже и не завтракал!"
    dreamgirl "Вот они, вредные привычки холостяцкой жизни. Нельзя же так не любить свой организм.{w} И вообще, жениться тебе надо, барин!"
    "Похоже, Юля правильно поняла ситуацию."
    show uv laugh with dspr
    uv "А, так ты есть хочешь! Угадала?"
    "Я смущённо пожал плечами и честно признался:"
    me "Ну да… Со вчерашнего вечера ничего не ел!"
    show uv normal with dissolve
    uv "Это плохо. Есть надо обязательно! Мне вот очень не нравится, когда я голодная."
    "Юля сразу посерьёзнела - видимо, в отличие от меня, о питании своего организма она заботилась своевременно."
    "Впрочем, почти тут же она снова заулыбалась:"
    show uv smile with dspr
    uv "А давай пойдём, поедим?"
    dreamgirl "Конечно-конечно. Чем там у нас кошки питаются? Как насчёт свежепойманной мышки? Вку-у-усная! Или белку предпочитаете?"
    "Моё альтер эго как в воду глядело, потому что следом Юля предложила:"
    uv "Здесь мыши знаешь какие вкусные. Пойдём наловим."
    "Юля облизнулась, обнажив острые зубки."
    dreamgirl "Соглашайся, соглашайся! Когда у тебя будет ещё шанс попробовать такой чудный деликатес!"
    me "Да как бы, понимаешь… Мы-то, люди, мышей обычно не едим…"
    "Я замялся, боясь обидеть Юлю отказом."
    show uv surprise with dissolve
    uv "Чудные вы. Вкусно же!"
    "Кажется, на лице Юли было написано искреннее недоумение: не любить мышей, слыханное ли дело?"
    uv "А чем тогда тебя покормить? Орехи будешь? Ягоды есть. И грибы ещё."
    "Услышав про более привычную еду я несколько воспрял духом. Ну да, она же говорила, что припасы делает.{w} Хоть что-то из этого должно оказаться съедобным!"
    me "Во-во! Ягоды с орехами подойдут."
    show uv normal with dissolve
    uv "Ну пошли тогда."
    "К некоторому моему удивлению, Юля направилась не к люку в туннель, а к выходу из здания. Заметив мой недоумённый взгляд, она пояснила:"
    uv "Пойдём здесь, так ближе!"
    play sound sfx_open_metal_hatch
    "Я прикрыл на всякий случай крышку люка и поспешил за ней."
    window hide
    scene bg ext_old_building_day with dissolve
    window show
    show uv normal with dissolve
    "Выйдя на поляну, Юля уверенно двинулась по направлению к тропинке, ведущей к лагерю."
    th "Надеюсь, она знает, что делает. Не хватало ещё столкнуться со всей этой компанией."
    dreamgirl "Правильно, совершенно это незачем! Мало того, что с расспросами привяжутся, так ещё и, как пить дать, заставят Шурика носить!"
    stop ambience fadeout 3
    window hide
    scene bg ext_path2_day with dissolve
    show uv normal with dissolve
    play ambience ambience_forest_day fadein 3
    window show
    "Впрочем, очень скоро Юля уверенно свернула с тропинки"
    hide uv with easeoutleft
    extend " и нырнула в кажущиеся отсюда непроходимыми заросли кустарника."
    "Стараясь не отставать, я бросился следом" #намеренно опущен знак препинания
    show uv normal close with dissolve
    extend "… И чуть не сбил Юлю с ног."
    "Она стояла на краю какой-то ямы, казавшейся отсюда бездонной - всё внутри скрывал густой мрак."
    me "Э-э… Нам сюда что ли?"
    "Я опасливо кивнул на оскаленную пасть провала.{w} К сожалению, Юля подтвердила мои опасения:"
    uv "Ну да, здесь спустимся."
    hide uv with easeoutbottom
    "Не успел я глазом моргнуть, как она присела на корточки и ловко спрыгнула куда-то вниз, в темноту."
    "Впрочем, снизу тут же послышался её жизнерадостный голос:"
    uv "Прыгай сюда, здесь неглубоко!"
    "Мне совсем не улыбалось снова лезть в эти проглятые подземелья с риском сгинуть там навсегда, но есть хотелось сильнее, чем жить."
    "Вздохнув, я бросил прощальный взгляд на море живой зелени вокруг и сиганул вниз."
    stop ambience fadeout 3
    window hide
    scene int_catacombs_entrance_light with dissolve
    with vpunch
    play ambience ambience_catacombs fadein 3
    $ night_time()
    $ persistent.sprite_time = "night"
    window show
    "Однако всё оказалось не так уж плохо - пролетев от силы метра два или три, я довольно удачно приземлился на кучу земли вперемешку с камнями."
    "На ногах, впрочем, мне удержаться не удалось. Поднявшись и пытаясь отряхнуться, я осмотрелся."
    th "Опять туннель… Кажется, это тот самый провал, через который от нас Шурик сбежал.{w} Или другой? Здесь всё такое похожее."
    show uv normal with dissolve
    "Юля стояла рядом. В рассеянном свете, пробивавшемся через дыру в потолке, её глаза таинственно поблескивали."
    show uv smile with dspr
    uv "Пойдём!"
    "Она уверенно двинулась куда-то в темноту. Мне не оставалось ничего другого, как последовать за ней."
    "После яркого дневного света в туннеле мне показалось ещё темнее, чем раньше.{w} Буквально через несколько шагов я перестал что-либо различать вокруг."
    window hide
    scene bg black with dissolve2
    window show
    "Решив не дожидаться, пока я напорюсь на какую-нибудь железяку, я достал трофейный фонарь."
    window hide
    scene bg int_catacombs_entrance with fade
    show uv upset
    with dissolve
    window show
    uv "Не свети в глаза."
    "Юля недовольно сощурилась, и я поспешил отвести фонарь в сторону."
    uv "Не понимаю, зачем вам эти светящие штуки. Здесь и так всё хорошо видно."
    me "Ну, наверное, у людей не такое хорошее зрение."
    window hide
    scene bg black with fade
    window show
    "Тем не менее я выключил фонарь и постоял несколько секунд, слепо тараща глаза."
    me "Тогда ты веди меня за руку, пока я к темноте не привыкну, хорошо?"
    uv "Хорошо!"
    "Впрочем, на этот раз Юле совсем недолго пришлось вести меня за собой - очень скоро она остановилась и, подумав немного, заявила:"
    uv "Ладно, включай эту штуку… Фонарь, вот. А то ты так еду не увидишь."
    window hide
    scene bg int_catacombs_entrance
    show uv normal at left
    with flash
    window show
    # для кладовки бэк не меняем, поскольку не на что
    "Я огляделся. Мы стояли всё в том же туннеле, но однообразная непрерывность стен в этом месте нарушалась нешироким проходом явно рукотворного происхождения."
    "За ним оказалось какое-то небольшое помещение."
    th "Наверное, это какая-то насосная или ещё что-нибудь такое."
    "Впрочем, первоначальное назначение этой каморки сейчас угадать было непросто.{w} Всё доступное пространство занимала теперь {i}еда{/i}."
    "Связки грибов, свисающие с потолка, ровные кучки орехов и ягод… Да, запасов здесь действительно хватило бы на всю зиму."
    "Однако, очень быстро мой энтузиазм насчёт предстоящей кормёжки дал трещину. Похоже, большая часть припасов лежала здесь уже не один год…"
    me "И ты здесь все свои запасы хранишь?"
    "Поинтересовался я больше для того, чтобы прервать наступившее молчание."
    show uv grin with dissolve
    uv "Нет, что ты! У меня ещё несколько таких мест есть. Я там тоже еду запасаю."
    "Она вручила мне горсть прошлогодних орехов."
    show uv smile with dissolve
    uv "Угощайся!"
    "И сама принялась хрумкать ими, выплёвывая скорлупки."
    th "Что же, орехи - выбор настоящих укротителей кошкодевочек…"
    "Положив фонарик на пол, я обречённо принялся бороться с орехами, пытаясь не сломать зубы. Ссохшиеся за год ядра по твёрдости также мало уступали скорлупе."
    show uv surprise with dissolve
    "Вдруг Юля замерла, широко раскрыв глаза."
    me "Что такое? Что-то случилось? Ты кого-то почуяла?"
    uv "Слу-у-ушай! Раз ты придумал, как ту дверь железную открыть…"
    hide uv with easeoutleft
    "Она бросилась в угол, принявшись что-то там ожесточённо ворошить, и через несколько секунд вернулась назад."
    show uv smile at left with easeinleft
    uv "Вот! Я это в столовой взяла. Я точно знаю, что там еда внутри!"
    show uv guilty with dissolve
    uv "Вот только достать не могу…"
    "Я уставился на консервные банки в её руках."
    show uv normal with dissolve
    uv "Смотри, они тоже железные, как дверь! Открой, а?"
    "Я аккуратно положил остатки орехов обратно в кучу на полу."
    me "Знаешь, нам сказочно повезло! Я как раз крупный специалист в этой области! Разумеется, я их открою!"
    show uv laugh with dissolve
    "Тут я обратил наконец внимание, что свет от фонаря совсем пожелтел и на глазах становится всё слабее."
    show uv smile with dissolve
    me "Вот только давай отнесём их куда-нибудь, где есть свет - фонарик скоро погаснет совсем, а в темноте их открыть не получится."
    me "Пойдём к тому месту, где мы в туннель спустились? Там светло."
    "Юля согласно кивнула, и мы, сложив банки в рюкзак, отправились обратно."
    dreamgirl "А потом окажется, что консервы эти времён войны. Какие там первые симптомы ботулизма?"
    th "Тьфу на тебя! Не смей портить настроение!"
    window hide
    scene int_catacombs_entrance_light with fade
    window show
    show uv smile with dissolve
    "К моей радости, инсинуации Шизы оказались беспочвенными - у консервов даже не кончился срок годности."
    "Я кое-как открыл перочинным ножом сардины в масле для Юли, и вступил в схватку с банкой, таящей в своих недрах, если верить этикетке, тушёную говядину."
    show uv normal with dspr
    "Юля с опаской принюхалась к содержимому банки."
    uv "А это точно можно есть? Ты уверен?"
    "Я пожал плечами и, подцепив ножом кусочек рыбы, отправил его в рот."
    me "Нормальные консервы. Если тебе не понравится - я их сам с удовольствие съем."
    "Глядя на меня, Юля наконец тоже решилась."
    show uv grin with dissolve
    uv "Умм! Вкусно!"
    "Я ожесточённо заработал ножом, и следующие несколько минут мы оба были страшно заняты, уплетая содержимое всех банок подряд."
    show uv smile with dissolve
    "Наконец с едой было покончено и я сыто привалился к стене туннеля."
    th "Ну вот, поели – теперь можно и поспать…"
    # насчёт набухшего синяка сомнительно - уточнить согласованность
    dreamgirl "Ты что, сдурел? Тебя уже давно в лагере хватились! Пора ноги делать.{w} И кстати, насчёт ног…"
    "Действительно давешний синяк набух и почернел, а нога снова начала ныть. Надо будет к медсестре сходить, наверное."
    "Со страшной силой навалилась усталость, накопленная за день. Впрочем, Юля выглядела бодрой как огурчик."
    me "Юль, а здесь есть какой-нибудь короткий путь в лагерь?"
    show uv normal with dspr
    uv "Да. Пойдём."
    window hide
    scene bg black with fade
    window show
    "И снова тёмные тоннели и штольни, которые мне уже осточертели за этот день."
    "Я уже давно перестал ориентироваться и просто послушно шагал за своей проводницей в полной темноте, не решаясь лишний раз включить севший фонарик."
    # здесь выходим не в подсобку "Лена-дура", а попадаем снова в туннель, к вентшахте под стоянкой. См. схему
    # решётка запирается на ключ, найденный в туннелях и он-таки подходит, хотя и с трудом - заржавело всё (не путаем с другим ключом от вентшахты под Гендой)
    window hide
    scene bg int_catacombs_entrance
    show uv normal at left
    with fade2
    window show
    "После невообразимого количества развилок мы наконец оказались в каком-то пусть чуть-чуть, но освещённом туннеле."
    "Довольно быстро я понял, что свет проникает сюда через какое-то отверстие в потолке. Прямо под ним на стене обнаружились металлические скобы."
    uv "Здесь наверху есть люк. Только он на замок заперт. У меня есть ключ, но замок всё равно не открывается. Я уже пробовала."
    me "Ключ? У тебя есть ключ?"
    uv "Да, я в тоннеле недалеко нашла. Давно ещё."
    dreamgirl "Ключ-с, моя прелесть. Мы нашли его в тёмных тоннелях-с. Мерзские Семены-с хотят отобрать у нас нашу прелесть-с."
    uv "Но ты ведь откроешь, да? Он ведь тоже железный!"
    th "Кажется, Юля свято уверовала в мою способность открывать всё железное… Забавно."
    me "Давай я попробую."
    window hide
    scene int_mine_exit_day with dissolve
    window show
    "Поднявшись по узкой шахте, я оказался перед металлической решёткой с вваренным в неё замком."
    "Как ни странно, но ключ подошёл, хотя провернуть его до конца мне долго не удавалось."
    "Стоя на металлической скобе и упёршись спиной в стенку шахты, я принялся крутить ключ туда-сюда, одновременно тряся решётку."
    th "Эх, масла бы сюда какого-нибудь! Да хоть бы даже от тех сардин, всё равно!"
    play sound sfx_open_metal_hatch
    "Наконец замок сдался, и решётка заскрипела, поворачиваясь на ржавых петлях. Не похоже, чтобы этим ходом регулярно пользовались."
    stop ambience fadeout 3
    window hide
    scene bg ext_camp_entrance_sunset with dissolve2
    play ambience ambience_camp_entrance_evening fadein 3
    $ day_time()
    $ persistent.sprite_time = "day"
    window show
    th "Интересно, где это я?"
    "Выбравшись наружу, я обернулся…{w} И уткнулся носом с гипсовые ноги какой-то скульптуры. Похоже, оголовок вентиляцтонной шахты служил ей постаментом."
    "Я обалдело завертел головой.{w} Подозрительно знакомые ворота, ещё одна скульптура…"
    th "Да это же выход из лагеря! Ничего себе… Интересно, много ли ещё здесь таких ходов?"
    show uv smile far at fright with dissolve
    "Юля вылезла вслед за мной и теперь потягивалась в свете вечернего уже солнца."
    th "Я что, провёл в шахтах весь день?! Славно прогулялся, ничего не скажешь."
    "Заперев решётку, мы отошли к воротам."
    show uv normal at cright with dissolve
    #
    #
    #
    me "Ну, я пойду?"
    uv "Конечно."
    me "Эмм… Спасибо тебе."
    show uv smile with dspr
    "Юля улыбнулась."
    uv "Приходи ещё. Было весело."
    show uv smile close at cright with dissolve
    "Она поднялась на цыпочки и неловко потёрлась носом о мою щёку."
    th "Кажется, это ещё один поцелуй? Наверное, пора всё-таки провести небольшой урок…"
    me "Смотри, это чуть-чуть иначе делается. Давай я тебе покажу."
    show uv surprise close with dissolve
    "Я мягко прикоснулся губами к её щеке."
    if alt_uvao_D5_hentai:
        extend " Потом ещё раз, в уголок губ…{w} Смешно, мы такое устроили в бункере, а банально поцеловаться - забыли!"
        "Руки сами обвили тонкую талию, я притянул ее к себе, наши губы соединились…"
        th "Запоздалый первый поцелуй. Ну лучше уж поздно, чем никогда."
        "Не удержавшись, я провел рукой по пушистому, удивленно поднятому хвосту."
    show uv smile close with dissolve
    uv "Мм-м-р-р… Мне нравится. Подожди, я тоже попробую!"
    "Юля оказалась способной ученицей, и я уже собирался перейти к следующей части урока…"
    dreamgirl "Послушай, все эти поцелуйчики очень трогательны и романтичны, но не лучше ли заняться этим в каком-нибудь месте поспокойнее?{w} И вообще, ты куда-то там собирался идти, или я ошибаюсь?"
    "Я со вздохом остранился от Юли."
    if alt_uvao_D5_hentai:
        show uv guilty with dissolve
        me "Мне тоже очень нравится, но я боюсь, что нас кто-нибудь здесь увидит."
        uv "Жалко. А потом можно будет ещё так сделать?"
        me "Ну конечно же можно! Даже нужно!"
    else:
        me "Ну вот как-то так у нас принято."
        uv "И всё? Жалко."
    show uv normal with dspr
    uv "Принесёшь мне ещё булочек? Я буду ждать."
    th "Да, сначала булочки, а потом уже Семён.."
    me "Обязательно. Я приду к тебе завтра."
    show uv smile with dspr
    "Внезапно мне в голову пришла одна безумная идея. Решив не откладывать её на потом, я попросил Юлю:"
    me "Не двигайся. Хочу тебя сфотографировать."
    #
    # далее фрагмент почти полностью взят из бункера
    show uv surprise with dissolve
    "Юля навострила уши и удивлённо посмотрела на меня."
    uv "Сфотографируешь? Это как?"
    dreamgirl "…Я вас сэйчас обоих сфатаграфырую!"
    me "Ну, это… Эээ…"
    th "И как ей объяснить, интересно?"
    me "Эээ, это такая штука… Делает картинку, на которой ты как настоящая."
    uv "Настоящая? Ещё одна я?"
    me "Ну… Давай я просто тебе покажу. Только стой смирно."
    show uv guilty at center with dissolve
    "Я навёл камеру на Юлю, а она продолжала удивлённо смотреть на меня."
    dreamgirl "Внимание, сейчас вылетит мышка! Летучая!"
    window hide
    show frame at truecenter:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
    show cam_ui at truecenter:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
    window hide
    $ renpy.pause(2, hard=True)
    play sound snap fadein 0
    scene white with flash
    $ renpy.pause(2, hard=True)
    scene bg ext_camp_entrance_sunset
    show uv surprise2 at center with dspr
    window show
    "Вспышка, Юля подскочила от неожиданности, а я скорее полез в телефон посмотреть снимок."
    window hide
    play sound sfx_scary_sting
    scene uv_photo_galaxy with dissolve
    $ renpy.pause(2, hard=True)
    scene bg ext_camp_entrance_sunset with dissolve
    show uv surprise2 at center with dissolve
    window show
    "…"
    dreamgirl "…"
    "Даже словоохотливое обычно альтер эго не нашлось, как прокомментировать получившийся результат."
    show uv smile with dissolve
    "Юля уже опомнилась от вспышки и с любопытством заглядывала мне через плечо."
    uv "Получилось? Покажи ещё одну меня!"
    "Я молча повернул смарт экраном к ней."
    "Юля изучила снимок и кивнула."
    show uv grin with dissolve
    uv  "Хорошо получилось. Почти как я."
    show uv smile with dissolve
    th "И ни капли удивления, главное."
    # 
    # Конец заимствования из бункера
    #
    dreamgirl "Ну и что это такое было?"
    th "Вообще-то я рассчитывал, что это ты мне что-нибудь подскажешь!"
    dreamgirl "Извини, но сейчас я понимаю не больше твоего. {w} Разве что тень на силуэте кажется смутно знакомой… Нейронная сеть?{w=0.8} Разряды плазмы?{w=0.8} Звёздное небо?"
    th "Ну да, скажи ещё - карта Вселенной!"
    play music music_list["doomed_to_be_defeated"] fadein 0
    show mt rage panama pioneer far at fleft with dspr
    with hpunch
    show uv rage at right with ease
    mt "Семён!"
    "Внезапное появление вожатой стало для нас полной неожиданностью."
    hide uv with easeoutright
    play sound_loop sfx_tree_branches
    show mt rage panama pioneer at left with dissolve
    "Как и вчера, Юля мгновенно метнулась в кусты, но в этот раз она даже не пыталась сделать это тихо - сквозь кусты словно бы тигр ломился не разбирая дороги."
    stop sound_loop
    "Наконец всё стихло. Стиснув зубы, я приготовился к обороне."
    show mt angry panama pioneer at center with dissolve
    mt "Семён! Что это такое?! Я требую объяснений!"
    me "Вы о чём, Ольга Дмитриевна?"
    "Вежливо поинтересовался я."
    mt "Не придуривайся! Я всё видела! Ты с этой кошкой!…"
    "От возмущения вожатая даже начала задыхаться."
#    show mt angry panama pioneer with dspr
    "Однако, теперь уже злиться начал и я тоже."
    if alt_uvao_D4_viola_morning:
        me "Вот я как знал, вчера не стал к Вам с этим подходить!"
        me "Увидел её издали, хотел было посоветоваться, да подумал, что ничего кроме истерики не дождусь!{w} Вижу, верно угадал!"
    else:
        me "Значит, когда я вчера вам рассказывал о своём видении, это была чушь и попытка отвильнуть от зарядки, а сегодня выясняется, что вы про неё знаете!"
        me "Нехорошо, Ольга Дмитриевна, нехорошо."
    show mt rage panama pioneer with dspr
    mt "Не смей так разговаривать со старшими!"
    "Лицо вожатой приобрело нездоровый лиловый цвет, а глаза готовы были выскочить из орбит."
    "На секунду мне показалось, что её сейчас удар хватит, но Ольга судорожно перевела дыхание и кое-как взяла себя в руки."
    show mt angry panama pioneer with dspr
    mt "Поговорим об этом позже, а сейчас марш на ужин!"
    "Тяжело дыша от злости, я гордо задрал голову и прошествовал мимо разярённой вожатой в ворота.{w} Та пошла следом за мной, намереваясь, судя по всему, конвоировать до самой столовой."
    stop music fadeout 5
    window hide
    scene bg ext_square_sunset
    show mt angry panama pioneer at fleft
    play ambience ambience_camp_center_evening fadein 1
    with dissolve
    window show
    play sound eat_horn fadein 2
    "Сигнал к ужину настиг нас на площади. К этому времени я уже немного поостыл."
    stop sound fadeout 2
    th "Вообще, если подумать, то надо сначала поужинать, а потом уже ругаться с Ольгой, или с кем там ещё придётся."
    dreamgirl "Ну наконец-то ты за ум взялся! Мало ли как оно может обернуться - вдруг тебя в карцер решат посадить, так хоть наешься напоследок."
    "Под сомнительные напутствия подсознания и недовольное пыхтение вожатой за спиной я направился было в сторону столовой."
    "Однако Ольга, похоже, к этому времени тоже немного успокоилась, потому что заявила вдруг относительно вменяемым голосом:"
    mt "Семён, погоди-ка! Зайдём к умывальникам сначала. Не хочу, чтобы ты весь отряд распугал."
    "Я окинул взглядом свою одежду и руки."
    th "Мда-а… Надо думать, лицо у меня примерно такое же чистое. Так не только отряд, так весь лагерь распугать можно."
    dreamgirl "Не понимаешь ты своего счастья! Вся столовая была бы в твоём распоряжении!"
    th "Кто-то только что мне про карцер втирал, кажется? Сгинь!"
    scene bg ext_washstand_day
    show mt angry panama pioneer at fleft
    with dissolve
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    window show
    "Под конвоем вожатой я дошёл до умывальников и наскоро умылся."
    stop sound_loop
    play sound sfx_close_water_sink
    pause(1)
    "Вообще, после сегодняшней беготни я с удовольствием ополоснулся бы до пояса, пусть и ледяной водой, но устраивать для вожатой сеанс стриптиза не собирался. Просто из принципа.{w} Не заслужила."
    "Отыскав в кармане носовой платок, я кое-как вытерся им и вопросительно посмотрел на Ольгу.{w} Та критически оглядела меня и поморщилась, но ничего не сказала, лишь кивнула в направлении столовой."
    window hide
    scene bg ext_dining_hall_near_sunset
    show mt angry panama pioneer at fleft
    with dissolve
    window show
    "Уже подойдя к столовой, я подумал, что помимо умывания можно было бы и рюкзак отнести домой, но связываться с  вожатой не хотелось."
    "Под любопытные взгляды пионеров я вошёл в столовую."

# Неиспользуемый фрагмент спуска через люк и бункер
#
# "И она направилась обратно к люку."
# смена сцены
# скрип люка
# темнота
# Сборщику: Необходимо будет проверить, что оброненный Шуриком фонарь уже используется ранее.
# "После яркого дневного света в шахте под люком мне показалось так же темно, как и в прошлый раз.{w} Решив не дожидаться, пока глаза привыкнут к темноте, я включил трофейный фонарь."
# туннель (со светом фонарика, если есть)
# show uv upset
# uv "Не свети в глаза."
# "Юля недовольно сощурилась, и я поспешил отвести фонарь в сторону."
# uv "Не понимаю, зачем вам эти светящие штуки. Здесь и так всё хорошо видно."
# me "Ну, видимо, у людей не такое хорошее зрение."
# темнота
# "Тем не менее я выключил фонарь и постоял несколько секунд, слепо тараща глаза."
# me "Тогда ты веди меня за руку, пока я к темноте не привыкну, хорошо?"
# uv "Хорошо!"
# "Впрочем, на этот раз Юле недолго пришлось вести меня за собой - очень быстро я начал различать свет далёкого светильника, да и идти по знакоймой дороге было не в пример проще."
# show uv normal
# "Несколько минут, и мы оказались у знакомой уже двери."
# гермодверь
# "Открыть гермодверь, из-за которой я, словно Железный Дровосек, чуть не отрубил сам себе ногу, теперь не составило никакого труда."
# бункер
# "Не задерживаясь в бункере, мы Задерживаться в бункере необходимости не было и мы просто прошли дальше."
# "Некоторое время мы шли в молчании. Я не знал, о чём спросить кошкодевочку, и она тоже не проявляла желание разговаривать."

