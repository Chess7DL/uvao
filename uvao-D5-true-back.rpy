label alt_day5_uvao_true_back:
# Д5 - Поход наверх. Тру рут.
#
# <---строки ниже удалить при сборке!>
    $ day_time()
    $ persistent.sprite_time = "day"
    scene bg int_catacombs_living with dissolve
    show uv normal at center
    window show
# <---строки выше удалить при сборке!>
    # TODO: звуки и музыка
    "Внутренний замок ничем не отличался от внешнего - кнопка в стене и колесо на двери. Наверное, предполагалось, что в бункере всегда находятся как минимум двое."
    dreamgirl "А если со вторым случится что-то? Возможно, у тебя просто не хватает ума догадаться, как открыть дверь в одиночку?"
    th "А мне и незачем! Второй раз я сюда лезть не собираюсь."
    "Отшив навязчивое не в меру подсознание, я кивнул Юле:"
    me "Давай, жми!"
    "Замок щелкнул, я завертел колесо, и бронированная дверь повернулась на исполинских петлях."
    window hide
    scene bg int_catacombs_door with fade
    show uv normal at cright with dissolve
    window show
    "За дверью никого не было, как и ожидалось. Не зная точно, найти этот люк в кабинете было довольно сложно. Но какого черта здесь было нужно Шурику, хотел бы я знать?"
    uv "Дверь только запри. Я запомнила, как открывать."
    th "Как это она будет её открывать, в одиночку-то?"
    "Похоже, это соображение её ничуть не волновало. Ну ладно. Преодолевая инерцию нескольких центнеров металла, я вернул дверь на место и закрутил кремальеру в положение \"Закр.\"."
    window hide
    scene bg int_catacombs_entrance
    show uv normal at cright with dissolve
    window show
# При спуске было обозначено, что в туннеле очень мало источников света (два?). Как ГГ заметит, что Юля на него поглядывает?
    "Без лишних разговоров мы дошли до люка с лесенкой. Юля иногда поглядывала на меня искоса, будто бы собираясь что-то спросить, но почему-то помалкивала.{w} Молчал и я. Мне было о чем подумать."
    me "Я первый полезу. Вдруг там Шурик, или еще кто-нибудь - заметит тебя, потом растреплет всем."
    show uv smile at cright with dspr
    "Почему-то это предложение развеселило ее, она радостно заулыбалась."
    uv "Хорошо! Ты первый, а я потом, если никого нет!"
    "Я пожал плечами и начал восхождение, очень скоро стукнувшись макушкой об закрытый люк."
    "Держась за лестницу одной рукой и уперевшись спиной в стенку шахты, я навалился на крышку и осторожно приподнял её, стараясь не скрипеть петлями."
    window hide
    stop ambience
    $ day_time()
    $ persistent.sprite_time = "day"
    scene int_old_building_day_uvao with flash
    window show
    "После темноты подземелий свет ножом резал глаза.{w} Проморгавшись, я огляделся и, стараясь не поднимать лишнего шума, откинул крышку люка."
    "Выбравшись наверх, я прислушался и выглянул в коридор.{w} Ни Шурика, ни кого-либо ещё видно не было."
# Вообще, про не хватившихся - лишнее, ИМХО
#    "Не было видно и кого-то еще, значит в лагере нас с ним пока что не хватились."
    me "Вроде никого."
    uv "Ага. Я никого не чую."
    show uv normal at cright with dissolve
    "Юля ловко вылезла следом за мной."
    window hide
    scene int_old_building_day_uvao with fade
    show uv normal at cright with dissolve
    window show    
    "Мы вышли на крыльцо и некоторое время просто жмурились от солнца. Сколько же времени я там проторчал, под землей?{w} Ох ёлки, уже обед скоро!"
    th "Вот за обедом и переговорю с Виолой."
    dreamgirl "Ага, разговоры на опасные темы лучше вести в людном месте. Чтобы свидетелей побольше, и живых щитов, если вдруг что-то пойдет не так…"
    me "Ладно, Юль. Мне пора бежать."
    show uv sad with dspr
    uv "Ты уходишь?"
    dreamgirl "Так просто ты от нее не отделаешься, братец."
    me "Да. Вожатая не любит, когда я опаздываю, да и обед скоро."
    uv "А что будет, если опоздаешь?"
    show uv grin with dspr
    "Юля игриво улыбнулась и слегка потянулась. Платье с готовностью облегло все изгибы её тела."
    dreamgirl "На больное давит, чертовка.{w} Соберись, тебе еще с Виолой беседовать."
    me "В принципе…{w} Ну…"
    "Промямлил я, глядя на аппетитные выпуклости девочки."
    "Впрочем, мне все-таки удалось взять себя в руки."
    me "Она строгая. Чуть что – накажет.{w} И ладно, если что-то легкое, а то вон, вчера гирлянды собирать заставила, а это часа на три работы.{w} А ведь тогда не получится встретиться с тобой…"
    "Юля вздохнула."
    show uv smile with dspr
    uv "Да? Ну ладно.{w} А что такое гирлянды?"
    me "Ну это… Огоньки такие на деревьях. Их на праздники вешают."
    show uv laugh with dspr
    uv "А! Это которые разноцветные и светящиеся?{w} Я как-то хотела взять один, но он укусил меня и погас, а вместе с ним и много-много остальных…{w} Жалко. А зачем они?"
    show uv smile with dspr
    "Я снова бросил взгляд на экран телефона, потом на Юлю."
    me "Если хочешь, можешь меня проводить. Я попробую объяснить, зачем они нужны и как устроены."
    dreamgirl "Ты спятил?"
    th "Виола сказала, чтобы… Как ее? Визуализация!{w} В общем, чтобы я поддерживал с ней контакт. На такую поблажку стоит пойти, иначе она может обидеться."
    dreamgirl "Ты уж как хочешь, но психолог из тебя, как из меня – Посол Доброй воли."
    "Я мысленно хмыкнул."
    show uv laugh with dspr
    uv "А можно? Давай! Я как раз знаю короткую дорожку…"
    th "А вот это очень кстати."
    "Я закинул рюкзак на плечо, Юля схватила меня за руку и потащила куда-то в лес - в сторону от натоптанной тропинки."
    window hide
    scene bg ext_path2_day with fade
    show uv normal at cright with dspr
    window show
    "Какое-то время мы шли молча. Я постоянно оглядывался, опасаясь появления Шурика.{w} Юля явно скучала и решила первой развеять тишину."
    uv "Ты кого ищешь все время?"
    me "Шурика. Не хотелось бы с ним столкнуться нос к носу."
    "Юля посмотрела наверх и пару раз шумно вздохнула."
    uv "Нет, я никого не чую…{w} Мне кажется, мы его здесь не увидим."
    "Какой-то огонек блеснул в глазах Юли, хотя мне могло и показаться."
    window hide
    scene bg ext_polyana_day with flash
    show uv normal at cright with dspr
    window show
    "На мгновение меня ослепил солнечный свет. Мы вышли в самый центр заросшей полянки, которую я до этого не видел."
    me "А где это мы?"
    uv "Я люблю это место. Тут очень много грибов после дождя!"
    "Она ловко запрыгнула на поваленное дерево и заглянула под него."
    show uv smile far with dspr
    uv "Вот! Нашла!"
    "Лицо ее озаряла детская радостная улыбка, а в ладошке устроился маленький подберезовик."
    me "Я, конечно, понимаю, что грибы это хорошо, но лучше все-таки поторопиться…"
    show uv normal far with dspr
    "Улыбка Юли начала медленно сходить на нет."
    me "Но, мы можем сюда потом вернуться, если хочешь."
    show uv smile far with dspr
    uv "Да! Очень хочу! Поможешь мне с запасами!"
    dreamgirl "Многоходовочкой попахивает…"
    "Она не церемонясь схватила меня за предплечье и вновь потащила в чащу."
    window hide
    scene bg ext_path2_day with fade
    show uv smile close at cright with dspr
    window show
    "Следуя за Юлей, я довольно ловко уворачивался от веток, но то и дело спотыкался о торчащие корни. Юля же ловко преодолевала все препятствия и продолжала меня тянуть дальше."
    "Создавалось впечатление, что если бы мы шли не по глухому лесу, а по аллее парка, то она вообще бежала бы вприпрыжку."
    "Задумавшись, я не заметил торчащий корень…"
    # *Звук удара и потрясем ЦГ-шку*
    play sound2 sfx_bodyfall_1
    me "Тьфу ты, черт!" with vpunch
    show uv guilty close at cleft with dspr
    uv "Ты не ушибся?"
    "Я перевернулся на спину и встретился глазами с наклонившейся надо мной Юлей. Ее каштановые волосы щекотали мне лицо."
    me "Я…{w} Вроде в порядке."
    "Она протянула мне руку, но я поднялся сам, отряхнул форму и огляделся."
    uv "Точно все хорошо?"
    me "Да ничего страшного, даже не ушибся вроде.{w} В любом случае у нас в лагере есть медсестра, уж она-то быстро на ноги поставит в случае чего…"
    "На миг передо мной появилось серьезное лицо Виолы, а в голове эхом прозвучали ее слова."
    "«…Последствия могут быть весьма тяжёлыми…»"
    "«…закончится очень печально. Для тебя - как минимум…»"
    uv "Эй!"
    "Перед лицом промелькнула юлина ладонь."
    show uv normal close at cleft with dspr
    uv "Тогда пойдем? А то сидишь…"
    me "Да я просто задумался…"
    show uv normal far at cleft with dspr
    "Юля уже ушла вперед, гордо неся пушистый хвост.{w} Я поднял рюкзак, пристроил его поудобнее на плече и побежал за ней, на этот раз внимательнее смотря под ноги."
    window hide
    scene bg ext_path_day with fade
    show uv smile at cright with dspr
    window show    
    "Вскоре мы вышли на тропинку. Здесь можно было уже идти рядом, и я принялся было объяснять Юле принцип работы гирлянды, но у той возникали все новые и новые вопросы, так что занятие это было нелегким."
    "В итоге я сдался и решил не углубляться в рассказы про электричество и про то, откуда оно берется. Лес тем временем редел."
    uv "Интересно!{w} Ой! Дальше я не хочу идти. Еще слишком светло."
    me "Я тогда потороплюсь. Спасибо за все, Юля."
    uv "И тебе спасибо! Так интересно слушать! Про гирлянды и про это… Илитричество…{w} Приходи еще!"
    me "Все, я побежал. Наверняка опоздал! До встречи."
    "Я постарался ей улыбнуться. Вышло как-то не особенно удачно, по крайней мере, мне так показалось, но Юля улыбнулась в ответ и махнула рукой."
    "Выйдя из чащи, я стряхнул листву с рюкзака и обобрал с одежды прицепившийся репей. Полуденное солнце приветливо встретило меня теплыми лучами."
    hide uv with dissolve
    "Я оглянулся. Лес отсюда казался густым и темным. Юли уже не было видно."
    dreamgirl "Какое душевное прощание. Может вам стоило обняться, поцеловаться или…"
    th "Вот тебя только не хватало!"
    window hide
    scene bg ext_houses_day with fade
    $ renpy.pause(1)
    scene bg ext_square_day with fade
    $ renpy.pause(1)
    scene bg ext_house_of_mt_day with fade
    window show
    "Пейзаж сменялся быстро, я был поглощен раздумьями и в уме прикидывал примерный план отчета перед Виолеттой. Вскоре показался уже привычный мне треугольный домик."
    scene bg int_house_of_mt_day
    "Ольги Дмитриевны не было видно, и я вошел, закрыв за собой дверь."
    play sound eat_horn fadein 2
    "В этот же момент раздался горн, призывавший пионеров всех возрастов трапезничать."
    stop sound fadeout 2
    "Особо не церемонясь, я бросил рюкзак возле своей тумбочки и, выйдя из домика, направился к столовой."
    return