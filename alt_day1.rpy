init 2:
    $ alt_day1_foreveralone_un = False
    $ alt_un_nicebook = False
    $ alt_un_stop_her = False
    $ alt_sl_met = False
    $ karma = 0
    $ lp_el = 0
    $ lp_mi = 0
    $ lp_sl = 0
    $ lp_un = 0
    $ date_un = 0
    $ lp_us = 0
    $ lp_dv = 0
    $ alt_day1_dv_fight = False
    $ semen_name = 0
    $ semen_panique = False
    $ semen_str = 10
    $ dv_wet = False
    $ run_el = 0
    $ dv_help = 0
    $ keys_take = 0
    $ alt_headshot = False
    $ alt_day1_sl_conv = False
    $ alt_day1_chase_uvao = False
    $ alt_day1_genda_investigation = False # Флаг входа на ЮВАО-рут
    
label alt_day1_start:
    $ make_names_unknown()
    $ alt_chapter(1, u"Пробуждение")
    $ day_time()
    $ persistent.sprite_time = "day"

    play music music_list["no_tresspassing"] fadein 2
    $ volume (0.7,'sound_loop')
    play sound_loop sfx_cooling_engine_loop fadein 2

    scene bg black
    window show
    "Я проснулся от не по-зимнему яркого и жаркого солнца, бьющего прямо в глаза. "
    window hide

    scene bg int_bus with fade2
    window show
    "Я не обратил на это внимания, так как спросонок не слишком чётко воспринимал окружающую реальность."
    "Первая реакция была самой ожидаемой — меня буквально подбросило в кресле и понесло на выход."
    th "Я же свою остановку проехал!"
    "Однако там, где я ожидал увидеть дверь, был ещё один ряд кресел. "
    "Стояли они как-то не так, как это должно быть в маршрутных автобусах."
    "Кресла протянулись парами вдоль салона, освобождая узкий проход посередине, напоминая рейсовые «Икарусы» из моего далекого детства. "
    "И провалиться мне на месте, если это не «Икарус». "
    show blinking
    "Я обессиленно свалился в кресло. "
    "Здрасьте, приехали! "
    "Что происходит вообще?"
    show blinking
    "И где, чёрт возьми, моя музыка?"
    "Внутриканальные наушники совершенно необъяснимым образом вывалились из ушей и болтались на проводе поверх пальто…"
    "Поймав беглецов, я попытался водворить их на место, однако, они, даже не задержавшись, мгновенно вывалились наружу."
    "Неужели поломались?"
    "Час от часу не легче."
    "И будто издеваясь, плеер показал максимум зарядки и даже что-то зашумел в динамиках, для меня теперь бесполезных. "
    "Пора выбираться отсюда."
    "На подгибающихся ногах я побрёл в сторону сиденья водителя. "
    "И когда добрался — я ущипнул себя что было сил."
    window hide

    scene bg int_bus:
        zoom 1.0 xalign 0.5 yalign 0.4
        linear 6.0 zoom 2.0 xalign 0.5 yalign 0.4
    with dissolve
    window show
    "Потому что изменение автобуса и яркое солнце ещё можно как-то объяснить, но как объяснить {w}ТО, ЧТО Я ПОПАЛ В ЛЕТО?!"
    "Боль немного отрезвила, и желание кататься в истерике по салону само собой сошло на нет."
    "Сбросив пальто, я стал продвигаться наружу."

    window hide
    stop sound_loop fadeout 2
    $ volume(1.0,'sound_loop')
    stop music fadeout 3

    scene white with dissolve
    $ renpy.pause(0.5)
    scene bg ext_road_day with dissolve2
    play ambience ambience_camp_entrance_day fadein 2
    window show
    "Везде, куда ни глянь, бушевала неукротимая зелень самых сочных оттенков и небо было такого прозрачного оттенка, который бывает только летом."
    th "ЛЕТО!?!"
    window hide

    scene bg black with fade
    window show
    "В виски больно толкнулась кровь, будто споласкивая подзабытые впечатления."
    window hide

    scene anim intro_14 with fade2
    play sound sfx_intro_bus_transition
    $ renpy.pause(3, hard=True)
    scene anim intro_15 with fade
    $ renpy.pause(3, hard=True)
    scene anim intro_16 with fade
    $ renpy.pause(3)

    window show
    " Я начал вспоминать.{w} Урбанистический пейзаж за окном ЛиАЗа сменился пригородом, а после и вовсе полями."
    "Долгая-долгая дорога, унесшая за собой поля, леса, озёра, степи… "
    if herc:
        "И вишенкой на торте - ведь я же уже выходил из автобуса! {w}И ничего я не проспал."
        "Правда, воспоминания об этом почему-то смазаны… {w}Я прекрасно помню, что выходил - а что дальше?"
        th "Если я ничего не перепутал - я, кажется, вышел на смену, поздоровался с Зинкой. А что дальше?"
        "Дальше в голове зияло слепое пятно."
        th "Кажется, я уснул - и либо во сне был перенесён сюда…"
        th "…либо продолжаю видеть сон!" with hpunch
        "Впрочем, вторая оплеуха и щипок за ногу ситуацию не исправили."
        th "Я сплю? Я спал?"
    if loki:
        th "Так. Думать. Вспоминать."
        th "Я сидел в скайпе, договаривался о каких-то сроках, и на всплывшее в правом нижнем углу экрана жёлтое окошко даже внимания не обратил."
        th "А там оказалась она. Ксана."
        th "Что дальше? Кажется, мы условились встретиться - при этом она поклялась, что это последний раз, и она навсегда пропадёт из моей жизни."
        "Дальше в воспоминаниях значилась обширная дыра, почему-то отдающая могильным холодом."
        "Но то, что в автобус я сел... И то, что автобус этот был не «Икарусом», а самым что ни на есть ЛиАЗиком, я помнил абсолютно точно!"
        "Опасливо потыкав пальцем память, я решил оставить всё до лучших времён."
        "Хотя бы до момента, когда более-менее сориентируюсь."
        th "Ещё раз! Я закрыл глаза - я уснул?"
    window hide
    
    stop ambience fadeout 3

    scene cg d1_uv_2 with fade2

    window show
    "И если я спал — почему я всё помню сейчас?"
    "Я ведь спал, я точно знаю — ко мне опять приходила та девочка и спрашивала, пойду ли я с ней."
    "И вот я здесь."
    "Что же это за девочка? {w}  И что ей может быть нужно от меня?"
    "Мысли о ней спугнули боль, и от висков оттащили там-тамы. Но всё-таки, кто она?"
    window hide

    $ renpy.pause(2)

    scene ext_bus1 with fade2
    play ambience ambience_day_countryside_ambience fadein 5
    window show
    "Это она привезла меня сюда?"
    "Вот так, получив согласие, взяла и привезла в сон? {w} Разве такое возможно?"
    "Голос…"
    "Он доносился словно издалека, и я едва-едва мог разобрать отдельные слова. "

    scene cg d1_uv_2 with flash2
    "Её губы почти касаются моего уха, и от тепла дыхания и щекотно, и приятно."
    dreamgirl "«…вспомни…»"
    dreamgirl "«…жалуйста… един…»"
    dreamgirl "«…шанс…»"
    window hide

    scene bg ext_road_day with dissolve
    window show
    if loki:
        "Остро, больно кольнуло где-то в грудине - так, что я схватился за грудь."
        "Фантомная боль?"
        "Скорее, воспоминание."
        "Я куда-то ехал, но доехал ли - не помню!"
        "Вместо этого я оказался здесь."
        "Но там, где закончились силы памяти - началась епархия ощущений."
        "Холод. Мороз. Боль в хрустящих костях."
        "Медленное угасание сознания."
        th "Нет!" with vpunch
        th "У меня же встреча в парке Горького, куда, блин, я ещё заехал?!"
        "Я бросил взгляд на телефон - московское время значилось как 15 часов."
    else:
        "И тут меня как толкнуло. {w}У меня же телефон есть! {w}Сейчас наберу родителей, и все узнаю."
        "Нащупав в кармане смартфон, я извлёк его из чехла и уставился на экран. "
        "Время — 2 часа дня, дата — 18 июля, а вот с годом затык — почему-то отображается нулями. Похоже, опять дата слетела."
        "Ладно, это всё ерунда, надо набрать родителей."
        "Последний номер. "
        "Ничего удивительного, кроме них там было считанное количество других номеров. Из числа тех, кого я забыл удалить. "
        play sound sfx_cellular_phone_error fadein 0
        "Глухо."
        window hide
        $ renpy.pause(3)
        window show  
        "Я отнял телефон от уха и проверил качество приёма, однако вместо хотя бы минимального сигнала, там горел жирный красный крест — потеря сигнала оператора. "
        th "Куда же вы меня завезли, интересно."
        "Я решил проверять до конца."
        "Вайфай. {w}Ожидаемо не нашло."
        "Радиоканалы. PTT рация. {w} Bluetooth. {w} Глухо."
        "Смежные частоты. {w} Глухо."
        "Ладно, дальше."
        $ volume (0.2,'sound_loop')
        play sound white_noise fadeout 2
        "Я с грехом пополам пристроил наушники в ушах и переключил плеер в режим радиосканера. "
        "FM-диапазон. {w} Глухо. {w} АМ-диапазон. {w} Глухо."
        stop sound
        th "Как на северном полюсе!"
        "GPS?"
        "Я с замиранием сердца попытался провести синхронизацию со спутником, чтобы хотя бы примерно определиться, куда меня занесло."
        me "Мать вашу!"
        "Спутников не обнаружено."
        th "Это что, какая-то шутка?!"
        "И вот тут я по-настоящему испугался. {w}Для того, чтобы сигнал спутника отвалился, должно было произойти что-то крайне нехорошее. {w}Даже не знаю что. {w}Все спутники посбивали к чертям? Или закрыли всю систему глобального позиционирования?"
    
    "Мне вдруг перестало хватать воздуха и я, в панике, сделал несколько шагов на подгибающихся ногах в сторону дороги."
    menu:
        "Сесть и успокоиться":
            $ semen_str = semen_str + 1
            $ karma = karma + 10
            window show
            "Вздохнув, я убрал телефон в карман. Похоже, связи с внешним миром нет и не намечается, придётся наслаждаться летом."
            if loki:
                th "Куда бы я ни стремился - я безнадёжно опоздал."
                th "Оно, может, и к лучшему."
            "Я посмотрел вокруг ещё раз."
            "Что-то было не так. {w}Неправильно. {w}И дело не в том, что я из зимы в лето попал. {w}Хотя и это тоже. {w}В первую очередь это краски! "
            "Да, те самые краски, родом из детства, чистейших тонов."
            "Природа вокруг будто и не знала никогда экологических катастроф, выброса различной дряни в воздух и прочих «нежностей» от человечества." 
            "Здесь всё было зелёное, яркое, сочное — будто вымытое."
            window hide
            stop ambience fadeout 2
            jump alt_day_1_sl_pos

        "Бежать в панике":
            $ semen_str = semen_str - 1
            $ semen_panique = True
            stop music fadeout 3
            window show
            "Надо бежать отсюда!"
            window hide
            jump alt_day_1_lostcon

        "Попробовать угнать автобус":
            scene ext_bus1 with fade2
            $ karma = karma - 10
            window show
            "В замке зажигания ключей не оказалось."
            "Ну да, глупо было бы надеяться."
            "Вздохнув, я обогнул автобус…"
            scene bg ext_camp_entrance_day with flash
            extend "…и застыл."
            "По ту сторону автобуса обнаружились до боли знакомые ворота."
            "Совёнок."
            if loki:
                "Те самые, что я недавно видел во сне."
                th "Неужели я не впервые здесь?"
                "В грудь опять больно кольнуло, и я поспешил сесть, наплевав даже на то, что ступеньки не очень чистые."
                "Почему-то на душе было так тихо и так мирно, как не было уже лет десять."
                "Эдакая крапивинская благодать, щедро сдобренная кружащейся головой - воздух-то какой здесь!"
                "Его же пить можно."
                
            else:
                "Чего и следовало ожидать."
                "Длинная кирпичная стена, уходящая сколько хватало глаз, два гипсовых пионера у входа и номер местного маршрута — 410. "
                th "Сегодня 410-й явно задерживается." 
                "На секундочку в груди вдруг защемило тем самым предчувствием, которое присуще лишь дням, что идут перед днями рождения и поездками куда-то очень далеко."
                "Голова немного закружилась от напоённого кислородом воздуха, и я присел на ступеньку, глупо улыбаясь."
            scene bg ext_road_day with dissolve
            window hide
            stop ambience fadeout 2
            jump alt_day_1_sl_pos

label alt_day_1_sl_pos:
    $ alt_chapter(1, u"Первое знакомство")
    $ day_time()
    $ persistent.sprite_time = "day"
    scene ext_bus1 with fade
    window show
    sl "Привет!"
    "Нарушил молчание девичий голос."
    show sl normal pioneer with flash
    play music music_list["take_me_beautifully"] fadein 1
    "Я поднял голову. {w}Девочка. {w}В пионерской форме. {w}Ожидаемо, после эдакого-то путешествия." 
    th "Не удивлюсь, если тут ещё и драконы с хоббитами водятся."
    "Лицо разглядеть трудно из-за того, что она стоит против солнца, однако, золотистые волосы и фигурку-«закачаешься» я отметить успел."
    "Если спросите меня — так слишком роскошную для какой-то там пионерки."
    "Я молча разглядывал её снизу вверх. {w}Что мне ещё оставалось?"
    "Однако, терпения незнакомки не хватило надолго."
    sl "Ты так и будешь молчать?"
    "Нетерпеливо спросила она, становясь прямо передо мной, так, что я, наконец, смог разглядеть её лицо. "
    if persistent.un_7dl_bad:
        "Почему-то хмурящееся - будто она силилась узнать меня, или я кого-то напоминал ей."
    "Тонкие черты лица, огроменные голубые глазищи (не иначе, посводила с ума половину парней в окрестностях!), и ощущение, которое я уже и не надеялся встретить в людях — какая-то открытость, искренность."
    "Тепло и доброта, присущие только детям."
    "Впрочем, на вид ей можно было дать с равным успехом и двенадцать (открытый, душевный взгляд) и семнадцать (если судить по тому, что обтягивала белая пионерская рубашка). "
    "И как раньше я был готов глупо улыбаться мыслям о девочке из сна, так и сейчас я сидел и, глупо улыбаясь, тонул в её глазах."
    if loki:
        "И как-то вдруг потеряла значение и стародавная история, и Ксана с её дурацкой меркантильностью, и..."
    th "Поздравляю, Семён!"
    "Выдал я себе мысленного леща."
    th "Ты поплыл. {w}И по кому поплыл? {w}По пионерке! {w}Да она же несовершеннолетняя!"
    window hide
    stop ambience fadeout 1

    menu:
        "Ответить":
            $ lp_sl = lp_sl + 1
            $ karma = karma + 10
            window show
            "Приступ социопатии опять приморозил язык к нёбу, и я с ужасом понял, что вот сейчас обижу эту девочку!"
            "Просто потому, что не смог выдавить и слова. Это ненормально! {w} Уймись, возьми себя в руки. {w} Вспомни, что ты гораздо старше её."
            th "Не помогает! Что делать, думай! У тебя меньше секунды!"
            play sound sfx_punch_medium
            with vpunch
            "Я заметался в панике и на автомате выписал самому себе оплеуху. {w}Кажется, сработало — тормоза немного расслабились, и язык обрёл подвижность."
            me "З-здравствуй."
            "Ответил я, поднимаясь."
            me "П-прости."
            sl "Ты почему ещё здесь?"
            "Она нахмурилась."
            sl "Автобус же давно приехал. "
            me "Извини."
            "Ещё раз зачем-то извинился я."
            jump lost_con

        "Промолчать":
            $ lp_sl = lp_sl - 1
            show sl surprise pioneer at cleft
            "Незнакомка выглядела обескураженной моим молчанием."
            "Она наклонилась надо мной и аккуратно потрясла за плечо."
            sl "Ты хорошо себя чувствуешь?"
            sl "Ты почему молчишь?"
            jump lost_con

    window hide
    with fade

    window show

label lost_con:
    me "Я… {w}это… {w}уснул! {w}Вот."
    show sl laugh pioneer at center with dspr
    sl "И спал в автобусе всё это время?"
    "Незнакомка от души рассмеялась."
    sl "Ну ты и соня! Как тебя зовут?"
    "Кто бы мог подумать, что ко мне подойдёт познакомиться такое вот чудо. Нет, понятно, что безо всякой задней мысли, но всё-таки…"
    me "Моё имя Семён."
    "Я попытался улыбнуться, но получилась какая-то жалкая ухмылка."
    show sl normal pioneer with dissolve
    sl "Тебя что-то насмешило?"
    "Она опять хмурится, а моё сердце ёкает в унисон."
    me "Н-нет… просто…"
    sl "Ясно. {w}Ты так странно себя ведёшь, Семён. Что-то не так?"
    "Я отвёл глаза. И что прикажете ей отвечать? Что я приехал из зимы на ЛиАЗе, а попал в лето на «Икарусе»? {w}Да она меня первая в психушку сдаст!"
    me "Да нет. Просто перегрелся немного на солнышке. Я здесь впервые, так что…"
    sl "Ну тогда тебе надо к вожатой! Она тебе всё расскажет. {w}Смотри. {w}Сейчас пойдёшь прямо-прямо до площади, не перепутаешь, там Генда стоит, там повернёшь налево, увидишь домики, ну а дальше сориентируешься."
    "Она махнула рукой, будто я знал, что там за воротами. "
    me "Я… Это… "
    sl "Всё понял?"
    "Нифига я не понял."
    me "Да."
    "Но не признаваться же в этом!"
    sl "А мне пора. Добро пожаловать в «Совёнок»!"
    window hide
    if lp_sl >= 1:
        jump day1_convince
    else:
        hide sl with dissolve
    jump alt_day_1_arrival

label alt_day_1_lostcon:
    window hide
    play ambience ambience_camp_entrance_day fadein 2
    play sound sfx_slavya_run
    with None
    scene bg ext_road_day with flash:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat
    with dissolve
    window show
    "Быстро прикинув в уме, я торопливо бросился в сторону, откуда, судя по всему, приехал автобус."
    window hide
    window show
    "…"
    window hide
    $ persistent.sprite_time = 'day'
    scene bg ext_road_day 
    with dissolve2
    play ambience ambience_ext_road_day fadein 3
    window show
    play sound breath fadein 3
    "…"
    "Физкультура закаляет тело и дух, позволяет очистить разум и найти верное решение."
    "Не мой случай."
    "Я едва осилил сто метров, когда дыхалка приказала долго жить, и я остановился, пыхтя как паровоз."
    th "И что ж я маленьким не сдох?"
    "Обожжённое горло пыталось поймать хоть немного кислорода из плывущего раскалённого воздуха, стелющегося над асфальтом."
    "В глазах потемнело от кислородного передоза, плюс сыграла жара."
    stop sound
    "Я покачал головой, пытаясь немного проморгаться и убрать пелену с глаз."
    th "Далеко убежал. Молодец."
    stop ambience fadeout 2
    "И тут небо над головой завертелось, и я потерял сознание."
    "…"
    "…"
    scene anim prolog_1
    sl "Эй, соня, поднимайся!"
    "Меня привёл в чувство девичий голос."
    "Неужели тот самый? {w} Нет, не думаю."
    "Во всяком случае, он не очень похож на тот, что я помню."
    "Я медленно открыл глаза. Кошмар никуда не делся — я всё так же лежал в зимней одежде на обочине дороги, а вокруг цвело лето."
    scene bg ext_road_day with flash
    voice "Давай, поднимайся! Ольга Дмитриевна тебя уже обыскалась!"
    "Я огляделся в поисках голоса."
    show sl normal pioneer with dissolve
    "Девочка. В пионерской одежде. Ну да, следовало ожидать. Маразм крепчал."
    "Я издал нервный смешок, а девочка нахмурилась."
    sl "С тобой всё в порядке?"
    jump lost_con

label day1_convince:
    menu:
        "Попытаться остановить":
            $ karma = karma + 10
            if semen_str >= 10:
                me "Прости пожалуйста, а ты не могла бы меня проводить? Кажется… Я ничего не понял."
                $ alt_day1_sl_conv = True
                if not (herc or loki):
                    $ slavya_convoy = True
                    $ plthr = u"Дрищ"
                $ lp_sl = lp_sl + 1
                sl "Ладно!"
                scene bg ext_camp_entrance_day
                show sl smile pioneer at center 
                "Она схватила меня за руку и потащила к воротам."
                "Мне лишь оставалось перебирать ногами, чтобы поспеть за своей деятельной провожатой."
                jump alt_day_1_arrival
            else:
                sl "Прости, но мне и правда надо бежать. Разберешься!"
                "Она скрылась за воротами."
                "Я пожал плечами."
                me "Сами разберёмся!"
                jump alt_day_1_arrival

        "Дать уйти":
            hide sl with dissolve
            jump alt_day_1_arrival

        "Сказаться больным":
            $ karma = karma - 10
            $ alt_day1_sl_conv = True
            if not (herc or loki):
                $ slavya_convoy = True
                $ plthr = u"Дрищ"
            me "А это не очень горит?"
            "Я оттянул ворот свитера."
            me "Кажется, я не очень хорошо себя чувствую."
            show sl normal pioneer with dissolve
            sl "Правда? Что ж ты сразу не сказал?"
            sl "Давай я провожу тебя в медпункт."
            me "Медпункт? Бррр."
            "Я поёжился."
            "Не люблю врачей. И не доверяю им."
            sl "Не бойся, у нас лучшая медсестра, она мигом поставит тебя на ноги!"
            me "Знаешь… Давай лучше начнём с вожатой."
            me "Волнуется, поди…"
            "Я выдохнул несколько раз."
            show sl laugh pioneer
            sl "Это да!"
            "Она рассмеялась."
            sl "Она же вместе с тобой на автобусе приехала. А ты всё не идёшь, и не идёшь."
            me "Ну всё, мне лучше. Пошли?"
            sl "Пошли!"
            jump alt_day_1_arrival

label alt_day_1_arrival:
    $ alt_chapter(1, u"Прибытие")
    play music music_list["my_daily_life"] fadein 1
    play ambience ambience_camp_center_day fadein 1
    scene bg ext_clubs_day with dissolve
    
    window hide
    $ set_mode_nvl()
    window show
    "Пионерские лагеря… Как же, как же. Припоминаю нечто подобное. В конце концов, я из поколения последних октябрят, и о таких вещах как пионерлагеря, знаю не понаслышке — успел съездить целых два раза, прежде чем их преобразовали в детские оздоровительные."
    "Перед глазами как наяву — ты выпрыгиваешь из электрички в Зеленогорске, смотришь на часы и матюкаешься, глядя на табло."
    "Последний автобус укатил пятнадцать минут назад, и до вечера ещё три часа, которые надо чем-то занять. Впрочем, это ерунда, эти часы удивительным образом выпадают из памяти, и в результате ты прорываешься в салон 499-го, и, спустя какой-то час уже выпадаешь на остановке, откуда за рощу ведёт извилистая тропинка."
    "Ты идёшь по ней, неторопливо осматриваясь по сторонам, по правую руку остаётся частный сектор, а по левую уже очень скоро можно разглядеть нарядные домики голубых и зелёных цветов."
    "Кто-то называет их «корпусами», кто-то «дачами», а для меня они всегда были домом. {w}Ведь дом — там, где твоё сердце, правда?"
    nvl clear
    window hide

    $ renpy.pause(3)

    window show
    "Вообще, довольно странная девочка. {w}Ни имени, ни чего другого не сказала. {w}Восприняла меня в зимней одежде как нечто, само собой разумеющееся. {w}Не иначе, у них тут «Икарусы» каждую неделю выгружают нового Семёна в теплом пальто."
    "И весь этот балаган с головной болью, сменой климата и разговорами во сне, похоже, волнует только меня."
    "А этим… {w}пионерам {w}- им и так есть чем заняться. Например, доподлинно реконструировать культуру ушедшей эпохи. {w}Помнится, был такой фильм, кажется, что-то там советского периода, не то парк, не то сад. Примерно такая же ситуация. "
    "Разве что здесь мне вряд ли дадут сыграть в Трумэна и добраться до изнанки декораций. "

    "Я подтянул сваливающиеся штаны. Для полноты эффекта осталось только встретить Ленина на броневичке. Хотя там обещали какого-то Генду. Что за птица этот Генда, мне, честно сказать, было неизвестно, поэтому посмотреть стоило. "
    
    nvl clear
    window hide
    scene bg ext_clubs_day with dissolve
    with fade
    stop music fadeout 4
    $ set_mode_adv()

    if alt_day1_sl_conv:
        show sl normal pioneer with dissolve
        jump day1_safe
    else:
        jump day1_alone

label day1_safe:
    $ persistent.sprite_time = 'day'
    window show
    sl "Побежали!"
    hide sl
    "Нетерпеливая девочка буквально вихрем протащила меня по лагерю!"
    scene bg ext_clubs_day:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat (12)
    with dissolve
    extend "Мимо клубов…"
    scene bg ext_square_day:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat (12)
    with dissolve
    extend "…мимо площади…"
    scene bg ext_houses_day:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat (12)
    with dissolve
    extend "…домиков…"
    scene bg ext_house_of_mt_day
    "…и наконец остановилась у какого-то треугольного домика. К этому моменту одежду на мне уже можно было выжимать."
    window hide
    jump alt_day_1_mod_tan

label day1_alone:
    window show
    "Вообще, мне здесь уже нравится. Чем-то напоминает голоштанное детство в сосновых борах северо-западного региона."
    "Всё новенькое, свежее. Бетонные плиты, которыми вымощена дорожка, ещё не побиты, хотя тут и там уже успела пробиться зелень."
    "Пройдя немного по дороге, я заметил небольшое одноэтажное здание."
    "Едва ли это могла быть вожатская, однако, осмотреться стоило."
    "Судя по вывеске, это здание кружков по интересам. {w}Или, как претенциозно заявляли авторы вывески, «клубы»."

    "Я уже хотел было двигаться дальше, когда дверь открылась, и оттуда вышла невысокая девочка в пионерской форме, сидящей ещё ладнее, чем на первой незнакомке — хотя мне казалось, что такого и быть не может."
    play sound sfx_open_door_clubs fadein 0
    with None
    show un normal pioneer far at left 
    with dissolve 
    "Однако в отличие от первой незнакомки, на лице новой девочки не было ни улыбки, ни позитивного настроения."
    "Она казалась подавленной и чём-то напомнила мне то, что я вижу каждый день в отражении на протяжении последних четырёх-пяти лет: затяжной депрессии честное зерцало."
    "Не знаю, что могло довести до такого состоянии априори жизнерадостный в таком возрасте организм, но я невольно тут же проникся солидарностью к грустной девочке."
    "Захотелось подойти и погладить её по голове, чтобы хоть как-то утешить."
    th "Сразу, как только пропадёт привычный уже ступор."
    "Ну нельзя же быть такой расстроенной!"
    if persistent.un_7dl_bad and not persistent.un_7dl_good:
        show un scared pioneer with dspr
        "Она застыла как громом поражённая - огромные глаза стали ещё больше, мгновенно увлажнившись, и на самом дне их заплескался страх!"
        th "Я что, напугал её?!"
    elif persistent.un_7dl_good:
        show un shy pioneer with dspr
        "Её лицо показалось смутно знакомым, но я хоть убей не мог вспомнить никого с такими чертами."
        th "Да уж, такую красоту я бы точно не забыл!"
        "Девочка смущённо потупилась в пол и несмело улыбнулась…"
        th "Она что, мне улыбнулась?"
        "Я мгновенно забыл обо всём, включая невероятно красивую пионерку у ворот, и самым банальным образом залип."
        "…и тут же, не обнаружив ответной реакции, спрятала улыбку, оставляя меня гадать - а не почудилось ли это вовсе?"
    else:
        "Однако, она подняла глаза, и у нас произошло некое сцепление взглядов, а девочка замерла, будто от испуга."
        with None
        show un surprise pioneer far at left 
        with dspr

    th "Ну да, так всё и начинается - сначала прибываем из зимы на «Икарусе», затем западаем со скоростью одна привязанность в минуту."
    th "А дальше как обычно - весёлый дом с жёлтыми стенами, вежливые санитары и рубашка от  Кардена с завязками на спине."
    "Я стоял практически в таком же ступоре, тоже застыв, раздумывая, стоит ли подойти с риском спугнуть, или предоставить инициативу ей? "
    "Чёртова социофобия, будто мало я мекал и блеял у входа. Что же теперь делать, а? Опять бить себе по морде?"
    "Всё это время, пока я стоял в раздумьях, девушка с каким-то потаённым интересом смотрела на меня."
    "Интересная внешность, необычная прическа. Даже волосы… Фиолетовые? Ээээ…"

    show us grin sport far at right with dissolve
    play sound sfx_bush_leaves
    $ renpy.pause(2)
    play music music_list["i_want_to_play"] fadein 5
    show us grin sport at right with dissolve
    "И когда я уже почти решился на то, чтобы заговорить, наше трогательное рандеву прервали — соседние кусты затрещали, и на дорогу выскочил кто-то!"
    "Кто-то невысокий, в алой футболке с надписью «СССР» и самой проказливой мордашкой из тех, что мне когда-либо попадались."
    "Рыжие до красноты волосы, стянутые двумя хвостами, торчащими абы как, и такие же бездонные глазищи, как и…"
    "Да как у всех мною здесь встреченных! {w}Вырастет — будет раскрасавица. {w}Если до той поры не прибьёт кто-нибудь ненароком. "
    "Издалека она казалась совсем маленькой, и по возрасту определённо была младше и той… хм… пионерки? Пускай пионерки. Так вот, мелкая была определённо моложе и пионерки у ворот, и этой, на пороге «клубов»." 

    "Я уже совсем было собрался наплевать на возможные «меканья» и подойти спросить дорогу, как вдруг «СССР» подскочила к грустной девочке и начала что-то говорить, размахивая руками в лучших итальянских традициях."
    "«Грустяша» же в свою очередь смутилась, потупила взгляд и ничего не сказала."
    "Я бы, наверное, и дальше наблюдал за их занимательным диалогом, но «СССР» вдруг достала из кармана что-то и начала трясти этим перед лицом «Грустяши»."

    "«Чем-то» оказался кузнечик. Нет. Саранча. Огромная! Сказать по правде, я бы и сам напугался, если бы мне в лицо ткнули чем-то подобным." 

    window hide
    stop ambience fadeout 2

    scene cg d1_grasshopper with dissolve
    window show
    "«Грустяша» не разочаровала!"
    un "Иииииии-ииииии!"
    "Завизжала она и моментально убежала куда-то в сторону, где должен находиться Ленин, а находится почему-то какой-то Генда."
    "Проще говоря, в сторону площади."
    $ persistent.sprite_time = "day"
    scene bg ext_clubs_day
    with None
    show us grin sport at left :
        linear 2.0 xalign 2.0 
    with dspr
    with dissolve
    "«СССР» подмигнула мне, хитро улыбнулась и бросилась за ней. "
    window hide

    scene bg ext_clubs_day with dissolve
    play ambience ambience_camp_center_day fadein 1
    stop music fadeout 2
    window show
    "Восхитительное начало дня."
    th "Мало того, я попал непонятно куда и непонятно зачем, так еще и дети в пионеров играют."
    th "И всё это — судя по флоре и особо крупным представителям фауны — во многих тысячах километров от дома."
    "Я имею в виду, если здесь водятся {i}такие{/i} кузнечики, то каких размеров остальные насекомые?"
    "А вдруг это вообще другая реальность? Параллельный мир, например. Где время текло медленнее."
    "В конце концов, если некому Генде ставят памятники, я хотя бы краем уха должен был что-нибудь слышать об этом деятеле."
    window hide

    scene bg ext_square_day
    "И ведь насколько эта самая реальность ярче моей!"
    "Сейчас, сравнивая, кажется, будто мне приснился бесцветный сон, или я просто выдумал всё, что со мной происходило до автобуса."
    "Ложная память."
    "И что теперь делать? "
    "Только сейчас я начал понимать, что со мной произошло что-то совершенно невероятное, по нервной системе шибануло, до дрожи в руках, откатом всего пережитого."
    if not loki:
        "Моя девушка — тогда, когда она ещё была — в последнюю нашу ссору назвала меня тормозом. {w}И теперь я думаю, что у неё были все основания так считать. "
    else:
        "Ксана — тогда, когда она ещё имела право голоса в моём доме — постоянно жаловалась на то, что я слишком эмоционально холоден. {w}Мол, именно из-за этого она и…"
    "Так что не бесстрашие виной моему спокойному восприятию, а то, что до меня просто не дошло. "
    "А вот если бы дошло сразу…"
    if semen_panique:
        "Впрочем, я и так показал свои «лучшие» качества."
    else:
        "Меня передёрнуло от отвращения при мысли о возможной картине со мной, катающимся по траве в тихой истерике. Кошмар."


    scene bg ext_houses_day with dissolve
    window show
    "Заметив по ту сторону площади небольшие домики, я свернул туда."
    "Однако, не так уж важно, победил я страх или не понял, что надо бояться — вести надо себя достойно и соответствовать высокому званию представителя моей реальности."
    "Я шёл по мощёной дорожке, жмурился от солнца и с интересом рассматривал «бочки» — полукруглые корпуса, с одной дверью и одним окном, накрытые листом гофрированного железа."
    "Для пионеров слишком шикарно. {w}Лично мне приходилось ютиться в бараке на сорок человек, разбитых на четыре спальни — девочки в левом крыле, мальчики в правом."
    "Поэтому мнение о быте пионеров у меня сложилось несколько отличное от того, что я вижу здесь. {w}Во всяком случае, спален-общежитий здесь нет, и это уже неплохо." 
    "Что же касается распорядка…"
    stop music fadeout 2
   
    play sound sfx_punch_medium
    with vpunch

    "ХЛОП!"
    play music music_list["that_s_our_madhouse"]
    "Я не успел додумать — от сильного хлопка между лопаток я потерял равновесие и растерял остатки мыслей. "
    me "Что за…"
    dv "Вот он! Нашла!"
    
    show dv smile pioneer2 with dissolve
    "Я не успел среагировать, когда мою шею поймали сложным захватом, не дающим пошевелиться или даже вдохнуть свободно."
    "Понятно, чем это чревато, приходилось уже опаздывать на смену."
    "Скосив глаза назад, я увидел, что меня держит ещё одна невысокая девочка, однако, обладающая довольно развитой мускулатурой — ну, насколько это возможно в шестнадцать лет, чтобы не выглядеть совсем уж Шварценеггером."
    "Крепкая, но, как и все девочки её возраста, весящая чуть больше, чем ничего. Так что можно…"
    if herc:
        $ alt_day1_dv_fight = True
        $ lp_dv = lp_dv + 1
        "Вспомнив, чему меня учили на занятиях, я сделал шаг назад и перехватил рыжую за талию."
        "Однако вместо выхода на болевой приём, я просто перебросил её через плечо…"
        "… И побежал!"
        dv "Ох!"
        show dv surprise pioneer2 at left 
        dv "Ты что делаешь, псих?! Пусти."
        "Она явно не ожидала такой реакции, и колотила кулаками мне по спине, пытаясь освободиться."
        me "Да иди!"
        scene bg ext_path_day
        show dv surprise pioneer2 at left 
        "Не пробежав и пятидесяти метров, я аккуратно ссадил её на землю и делано охлопал ладони."
        me "Я могу идти?"
        "Девочка промолчала, видимо, до сих пор пребывая в лёгком шоке, и я, восприняв молчание знаком согласия, развернулся и собрался было уходить…"
        us "Лови его!"
        show us smile sport far at center 
        "Раздался уже знакомый голос мелкой. Я обернулся — она, пыхтя, старательно волокла пожарное ведро, в котором плескалась вода."
        me "А мне здесь нравится!"
        "Я подмигнул рыжей-старшей и тут же смазал лыжи, спасясь от мелкой террористки с ведром."
        hide us with dissolve
        hide dv with dissolve
    elif loki:
        $ lp_dv = lp_dv + 1
        $ karma = karma - 10
        play sound sfx_punch_medium
        with vpunch
        me "Эй… {w}кх… {w}отпусти, полоумная… {w}Задушишь."
        dv "Ничего!"
        play sound sfx_punch_medium
        with vpunch
        "Держащая меня бандитка и не подумала ослаблять захват."
        me "Я… {w}Сам… {w}За опоздание."
        dv "Правда? {w}Ты хочешь выписать себе штрафную?"
        dv "А не врёшь?"
        "Она засомневалась, но увидеть как человек сам себя обливает из пожарного ведра, ей, похоже, очень хотелось."
        play sound sfx_punch_medium
        with vpunch
        me "Так… {w}Поймаешь… {w}Если… {w}Вру." 
        dv "Ладно."
        "Она ослабила захват и кивнула уже знакомой «СССР», волочащей красное коническое ведро, до краёв налитое мутной водой из пожарной бочки."
        dv "Наш новичок хочет выписать сам себе штрафную! Дадим ему этот шанс!"
        "Заявила бандитка, и я, наконец, смог разглядеть её как следует."
        th "А она ничего!"
        "Рыжие волосы, не красные, как у «СССР», а, скорее, медные."
        "Янтарные - до красноты - глаза, выражение некой надменности - в общем, то, что передо мной самая настоящая сорвиголова, я распознал сразу."
        us "Ты уверена?"
        show us laugh2 sport at right with dissolve
        dv "Нет."
        "Рыжая-старшая облизала губы."
        dv "Но ведь мы его поймаем если что, верно?"
        show us smile sport at right with dissolve 
        us "Точно!" 
        us "Сейчас я…"
        "Я взял ведро в руку…"
        me "Вторая степень унижения!"
        play sound sfx_water_emerge
        "…и одной огромной волной окатил сразу обеих!"
        $ lp_dv = lp_dv + 1
        show dv shocked pioneer2 at center
        show us fear sport at right
        me "Ну как вам моя штрафная?!"
        "Расхохотался я и, развернувшись, мгновенно смазал лыжи."
        us "Обманщик! Лови обманщика!"
        dv "Держи его!"
        jump day1_chase1
    else:
        jump d1_humil
        
label day1_chase1:
            $ persistent.sprite_time = 'day'
            with dissolve
            window show
            scene bg ext_square_day with flash:
            "Топот за спиной не смолкал, однако, на кураже я выжал из своих ног такую скорость, какую уже давно не выдавал. "
            "Истово, от души, хохоча, я пересёк площадь в другую сторону."
            scene bg ext_square_day with flash:
                pos (0,0) 
                linear 0.1 pos (-5,-5) 
                linear 0.1 pos (5,5) 
                pos (0,0) 
                linear 0.1 pos (0,-5) 
                linear 0.1 pos (0,5) 
                repeat
            with dissolve
            "Помахал Генде…"
            scene bg ext_aidpost_day with flash:
                pos (0,0) 
                linear 0.1 pos (-5,-5) 
                linear 0.1 pos (5,5) 
                pos (0,0) 
                linear 0.1 pos (0,-5) 
                linear 0.1 pos (0,5) 
                repeat
            with dissolve
            "…пробежал мимо какого-то здания с флагом. Красный крест? Медпункт?"
            scene bg ext_dining_hall_away_day with flash:
                pos (0,0) 
                linear 0.1 pos (-5,-5) 
                linear 0.1 pos (5,5) 
                pos (0,0) 
                linear 0.1 pos (0,-5) 
                linear 0.1 pos (0,5) 
                repeat
            with dissolve
            "Краем глаза отметил столовую…"
            stop music fadeout 3
            "…и выскочил на пристань."
            window hide
            jump alt_day_1_dock

label d1_humil:
    if not (herc or loki):
        $ alt_sp_wet = True
    $ lp_dv = lp_dv - 1
    "Я дёрнулся, что было сил."
    "Однако, после сегодняшнего утра и пробежки в зимней одежде сил оставалось маловато."
    dv "Ульянка, бегом!"
    $ meet('us','Ульяна')
    show us laugh2 sport at right
    us "Бегу-бегу!"
    "Уже знакомый мне детский голосок запыхался так, будто она несла что-то…{w}тяжёлое?"
    me "Аааа… Отпустите меня, гады!"
    "Я забился, пытаясь вырваться, уже понимая, что сейчас произойдёт."
    "Тщетно!"
    "Рыжая вражина только сильнее сдавила мою шею, перекрывая остатки кислорода."
    play sound sfx_water_emerge
    pause(1)
    "Оставалось лишь смириться и принять унижение."
    "Очередное унижение в новом коллективе. Казалось бы, причём здесь любовь к людям."
    dv "Добро пожаловать в лагерь!"
    "Смех на два голоса, вдвойне обидный от того, что смеются надо мной."
    hide dv
    hide us
    "Немного обсохнув на ближайшей скамеечке, я поднялся и двинулся куда глаза глядят."
    "И очень скоро моему взгляду открылось озеро."
    stop music fadeout 3
    window hide
    jump alt_day_1_dock

label alt_day_1_dock:
    $ alt_chapter(1, u"Пристань")
    scene bg ext_boathouse_day with fade
    play ambience ambience_boat_station_day fadein 2
    window show
    "Здесь так мирно."
    if loki or herc:
        "Кажется, здесь можно укрыться от погони. Во всяком случае, пока."
    else:
        "Плещущая в берег вода дарила ощущение покоя."
    "Прогрохотав тяжёлыми зимними ботинками по доскам настила, я ушёл к самому краю настила на понтонах."
    "Присмотрев себе местечко, сбросил с себя мокрый свитер и повесил его на поручень." 
    "Поискал по карманам сигареты, не нашёл. Ну и ладно."
    th "Ощущение, что я что-то забыл…"
    th "Пальто! В нём же и паспорт, и пара сотен на проезд! Как я мог забыть его в автобусе!"
    if alt_sp_wet:
        "Впрочем, после того тёплого приёма, что мне устроили в лагере, идти, честно говоря, никуда не хочется."
    elif herc or loki:
        "Однако, возвращаться за ним — идея не из самых разумных. Меня же сто процентов где-то на территории поджидают две обормотки с вёдрами. Ибо месть!"
    th "Возьму таймаут."
    "Я сидел, задумчиво глядя на поверхность воды — где-то за пятьдесят метров была провешена буйковая завеса, чтобы пионеры далеко не заплывали."
    "А дальше — за прихотливым изгибом, выдающим в нашем водоёме речку, просто слишком широкую, была видна пойма, содержащая в себе два острова, а на узкой части её стягивала едва видимая отсюда стальная нитка железной дороги."
    "Сейчас по ней, кстати, куда-то уже спешил тепловоз, волочащий за собой ряд товарных вагонов, и убаюкивающие синкопы перестука колёс далеко разносились по спокойной воде."
    "В принципе…Если долго-долго плыть, за буйки, чуть дальше, то, возможно, появится способ запрыгнуть в товарняк, благо, они всегда сбрасывают скорость на болотинке…"
    "И укатить? Уехать поездом до ближайшего города, а там прорываться."
    "Но это в том лишь случае, если мне вдруг кровь из носу понадобится сбежать отсюда."

    sl "Так тебе не сюда!"
    "Раздался за спиной знакомый уже голос."
    show sl smile swim with dissolve
    play music music_list["take_me_beautifully"] fadein 1
    "Я обернулся. Передо мной стояла та первая девочка, которую я встретил у ворот."
    sl "Я же тебе сказала от площади на домики ориентироваться, а ты куда пошёл?"
    menu:
        "Сами ноги к тебе привели!":
            if herc or loki:
                "От души улыбнулся я, глядя на приветливую незнакомку."
                $ lp_sl = lp_sl + 1
                me "Это не я, это случайно получилось."
            else:
                me "Случайно…"
        "Да я не понял нифига":
            "Сказать по правде, на вопрос я бы сейчас ответить не смог — пионерская форма на девочке сменилась купальником, и в голове царила восторженно-звенящая пустота. "
    sl "Ой, я же так и не представилась. Меня Славя зовут! Вообще, полное имя Славяна, но меня все Славей зовут."
    $ meet('sl','Славя')
    $ alt_sl_met = True
    "Она поощрительно кивнула."
    show sl smile swim with dspr
    sl "И ты тоже зови."
    me "Что…"
    "Я с трудом вернулся к действительности."
    me "А… Да… Славя. А я…"
    if alt_sp_wet:
        "Я был несколько растерян от её внезапного появления, да и тугой купальник, обрисовывающий то, что надо обрисовывать, вносил определённую ноту разлада. "
    sl "Ладно. Я уже заканчиваю здесь. Ты подожди меня здесь минуту, я переоденусь, и вместе пойдём к Ольге Дмитриевне, хорошо?"
    hide sl with dissolve
    "Я кивнул. После этих слов она куда-то убежала, а я, наконец, догадался стянуть тяжёлые зимние ботинки, связать их шнурками как в детстве, чтобы перекинуть через плечо, и сесть на мостки, окунув ноги в воду."
    "Тёплая."
    "Меланхолично водя ногами в тёплой воде, я размышлял о сегодняшних событиях."
    
    "По большому счёту, все, кого я сегодня встретил, вели себя в высшей степени нормально — девочка, напугавшаяся саранчи, бойкая Славя, даже те две бандитки — все вели себя так, как и положено детям в пионерском лагере."
    "Следов какого-то коварного заговора обнаружить не удалось, как и зловещих целей того, кто меня забросил сюда."
    sl "Пошли?"
    show sl smile pioneer with dspr
    me "Пошли…"
    "Цапнув свитер с поручня, ответил я."
    "Её отношение ко мне… Сказать по правде, я уже и забыл, когда обо мне кто-то просто заботился."
    "Тем теплее было на душе от её попыток помочь. Однако, всё это достаточно подозрительно, не так ли?"
    stop ambience
    scene bg ext_houses_day with dissolve
    show sl normal pioneer with dspr

    if alt_sp_wet:
        "Мы вернулись к месту моего недавнего позора, лишь убедившего меня в том, что в этом лагере друзей у меня нет. Нееееет."
        "Я окинул взглядом печальную картину и на секунду призадумался — а что, если бы я мог открутить время назад…{w} Как бы я поступил?"
        "Красное пожарное ведро лежало здесь же."
        "Покачав головой, Славя подняла его и повесила на крючок ближайшего противопожарного щита."
        "Я с подозрением проводил её взглядом."
        me "Здесь меня и облили… Две рыжие гадины."
        show sl sad pioneer with dissolve
        sl "Не злись на них. Ульянка и Алиса тут что-то вроде лагерной совести."
        me "Подружки-попрыгушки?"
        "Съязвил я."
        "А Славя серьёзно кивнула."
    elif herc:
        "Место, чуть не ставшее свидетелем моего позора."
        "Сказать по правде, не ожидал от себя такой прыти!"
        "Я бы слукавил, если бы сказал, что мне не понравилась ситуация."
        me "Здесь меня облить хотели."
        sl "Вот как? Кто?"
        me "А откуда я знаю. Две какие-то рыжие, одна постарше, другая помладше, обе с хвостами."
        show sl laugh pioneer with dissolve
        sl "Это Ульянка и Алиса. Они подруги, вот вместе и балуются."
        sl "А ты с ними справился, значит? Как?"
        me "Вспомнил, что девушек надо носить на руках."
        "Улыбнулся я."
    elif loki:
        "Мы вернулись обратно к тому месту, откуда я начал свой забег до пляжа."
        "Зло в лице двух рыжих вредин было заборото, а я не смог сдержать улыбки."
        sl "Чему ты улыбаешься, Семён? {w} Только не говори что мне, ты на меня даже не смотришь."
        "Здесь же валялось коническое красное ведро, которое Славя, покачав головой, подобрала и повесила на крюк ближайшего противопожарного щита."
        me "Просто я в курсе, откуда здесь это ведро, и почему вода разлита."
        sl "Ты что-то знаешь?"
        me "Ну… Скажем так, я поступил с другими людьми так, как они хотели поступить со мной."
        sl "Ты облил Ульянку?!"
        $ meet('us','Ульяна')
        show sl laugh pioneer
        "Она рассмеялась."
        me "Вообще, их было двое. Там вторая рыжая была какая-то…"
        sl "Двачевская наша. Кто же ещё. Алиска всегда с Ульяной безобразничают."
        "Мы свернули возле ближайшего домика, и, пройдя по импровизированной улице, уткнулись в ещё один домик, необычность которого сразу бросалась в глаза. "
    else:
        "Мы шли мимо домиков, когда Славя вдруг остановилась и сказала."
        sl "А ты знаешь… Ведь у нас опоздавших новичков обычно обливают…"
        me "То есть ты меня, по сути, спасла?"
        "Я не знал что сказать."
        me "Ну тода спасибо…{w=.4} Наверное…"
        sl "Да…"
    stop music fadeout 2
    stop ambience fadeout 2
    window hide
    jump alt_day_1_mod_tan

label alt_day_1_mod_tan:
    $ alt_chapter(1, u"Вожатая")
    scene bg ext_house_of_mt_day with fade
    show sl normal pioneer
    with dissolve
    play music music_list["everyday_theme"] fadein 2

    window show
    "В отличие от «бочек», этот домик был треугольным, будто сняли крышу с дома, положили на землю, и под ней поселились."
    "Это, да ещё и отвоёвывающие себе пространство кусты сирени, сложились в удивительной красоты натюрморт, достойный кисти любого художника. "
    sl "Что стоишь? Пойдём!"
    if alt_sl_met:
        "Вывела меня из раздумий Славя."
    else:
        "Вывела меня из размышлений девочка."
    window hide
    play ambience ambience_camp_center_day fadein 1
    window show
    mt "…издеваться над Леной, Ульяна!"
    $ meet('us','Ульяна')
    if herc and alt_day1_dv_fight:
        th "О! Ульяна! Не та ли мелкая? Если так… {w}За ней кое-какой должок!"
    elif loki and not alt_day1_sl_conv:
        th "Никак, уже где-то обсохнуть успела? Не девочка, а метеор."
    else:
        th "Лена. Ульяна. Сразу два имени. Вот это прогресс."
    "Перед домиком стоял разложенный шезлонг, куда я немедленно забрался"
    if alt_day1_sl_conv:
        extend "."
    else:
        extend ", прихватив по дороге крайне удачно лежащую шишку."
    if alt_sl_met:
        "Славя села в соседний и спокойно улыбнулась в ответ на вопрошающий взгляд."
    else:
        "А она осталась стоять, смотря на меня сверху вниз и задумчиво меня разглядывая…"
        "Я поёжился и вспомнил вдруг — я же её имени не спросил!"
        me "Кхм…"
        "Я покраснел."
        me "Прости пожалуйста, я понимаю, что вопрос крайне несвоевременный…"
        me "Не могла бы ты назвать своё имя? Мы так и не познакомились толком."
        show sl laugh pioneer at center with dissolve
        "Она рассмеялась."
        sl "Твоя правда, извини."
        sl "Меня все в лагере называют Славей."
        $ meet('sl','Славя')
        me "Как «Слава», только мягче?"
        show sl serious pioneer
        sl "Полное имя Славяна."
        "Кажется, ей не очень понравились мои вопросы…"
        me "Очень красивое имя."
        "Как легко сделать что-то хорошее, ничего не делая! Похвалил имя — и молодец!"
        show sl shy pioneer 
        sl "Ты тоже меня зови Славей. Мне так привычнее."
        "Она, наконец, села рядом."
    th "Похоже, у вожатой дел невпроворот. Впрочем, судя по вздорному детском голоску, который я слышу… Ничего удивительного."
    play sound sfx_open_door_kick
    with vpunch
    show us smile sport at right
    "Через мгновение дверь распахнулась и оттуда выскочила девочка в футболке «СССР»."
    if herc and alt_day1_dv_fight:
        "Похоже, что Ульяна — это она. Пробежать ей я дал, но бросил в спину шишкой, попав строго между лопатками."
        $ meet('us','Ульяна')
        show us dontlike sport at right with dissolve
        us "Эй! Ты что творишь?"
        "Она обиженно посмотрела на меня."
        me "Воду таскать не надорвалась?"
        us "Уууу…"
        "Она почесала спину и, нехорошо посмотрев на меня, убежала."
    elif loki and not alt_day1_sl_conv:
        "Жертву обливания я узнал сразу и подмигнул ей. Она же в ответ только недобро прищурилась. Явно замыслила какую-то пакость."
        me "Привет, мелкая! Как купание?"
        show us dontlike sport at right with dissolve
        us "…"
        "Она развернулась и исчезла за домиками."
    else:
        $ meet('us','Пионерка')
        us "Иди-иди!"
        show us grin sport at right
        us "Тебя уже ждут."
        show us grin sport at left :
            linear 2.0 xalign 1.0 
        "Она подмигнула мне и убежала за домики."
        me "Это Лена или Ульяна?"
        $ meet('us','Ульяна')
        sl "Ульянка."
        "Рассмеялась Славя."
        us "Наша отрядная гордость и краса. Не спрашивай, почему."
    hide us with dissolve
    if alt_day1_sl_conv:
        "Вслед за ней вышла девочка с неописуемо печальным выражением лица. Что же такого Ульяна с ней натворила?!"
    else:
        "Вслед за ней вышла и «Грустяша». Лена?"
        if persistent.un_7dl_good:
            "Почему-то я сразу решил, что это она. {w}Не знаю, почему."
            "Наверное, потому, что ей это имя подошло бы идеально."
    show un normal pioneer far at left with dspr
    sl "Лена, не обижайся на неё. Ульянка просто так играет."
    $ meet('un','Лена')
    "Приободрила Славя."
    stop ambience fadeout 2
    th "Значит, Лена. Что ж. Очень приятно."
    if persistent.un_7dl_good:
        me "Ну! Что я говорил!"
    un "Да я не…"
    show un normal pioneer at left with dspr
    "Она сделала пару шагов и только тут заметила меня."
    show un shy pioneer at left with dissolve
    "…и мгновенно запнулась на полуслове."
    "Кажется, я смутил её."
    "Жарко покраснев, девочка уставилась в пол и постаралась как можно быстрее скрыться из виду."
    "А я обернулся вслед и проводил её задумчивым взглядом."
    th "Интересно, она всегда такая, или это моё тлетворное влияние?"
    hide un with moveoutleft
    show sl normal pioneer at center with dspr
    sl "Нас ждут, пошли!"
    play sound sfx_knock_door7_polite
    $ renpy.pause(2)
    mt "Открыто!"
    window hide

    stop ambience
    play sound sfx_open_dooor_campus_1
    play ambience ambience_int_cabin_day fadein 1

    scene bg int_house_of_mt_noitem_day
    show mt normal pioneer
    with dissolve

    window show
    "Не слушая моих возражений, блондинка затащила меня в домик."
    "Внутри он выглядел именно так, как и ожидалось — две кровати, столик, стулья, пара шкафчиков и постер с несравненным Жаном Марэ в роли Фантомаса."
    "Ничего сверхъестественного, хотя по части бардака, этот домик влёгкую мог потягаться с моей квартирой."
    "Возле окна стояла девушка примерно моего возраста и что-то читала в ежедневнике. {w}И, разумеется, как и всех прочих представителей прекрасного пола, встреченных мной тут, природа не обделила её ни внешностью, ни фигурой. "
    $ meet('mt','Ольга Дмитриевна')
    th "Хоть это радует — помирать буду в компании прекрасных дам. Если буду, конечно."
    mt "А вот и ты."
    mt "Когда я выходила из автобуса, ты ещё спал. Неужели только проснулся?"
    me "Эм… Ну, да. "
    mt "Всё с тобой ясно, засоня."
    show mt smile pioneer
    mt "Меня зовут Ольга Дмитриевна, и я твоя вожатая. "
    "Она улыбнулась, сводя всё к шутке. Ну а я решил подыграть фарсу."
    me "Вожатая, значит. А я, значит, пионэр?"
    show mt angry pioneer at center with dissolve 
    mt "Не коверкай слова, Семён."
    mt "Ты теперь часть отряда, а значит, я должна сделать из тебя образцового пионера."
    me  "Да вы на меня посмотрите."
    "Я уже откровенно издевался."
    me "Я что, похож на пионэра?"
    show sl normal pioneer at right
    "Ольга замолчала на пару минут, задумчиво изучая моё лицо, и будто этого было недостаточно, вокруг меня прошлась Славя."
    sl "Поправьте меня, если я ошибаюсь… Но похож."
    mt "Я тоже так считаю."
    me "Ладно, девочки."
    "Я решил сворачивать самодеятельность."
    me "Вы пошутили, я тоже посмеялся, пора заканчивать концерт."
    show sl serious pioneer at right
    sl "Семён, ты заговариваешься. Может, доктора позвать?"
    mt "Хорошая мысль, Славя. Приведи, пожалуйста, доктора Виолу."
    "Кивнув, Славя исчезла за дверью."
    hide sl with dissolve
    me "Да какого доктора Виолу?! Вы что, тут все с ума посходили! Уймитесь, это уже несмеш…"
    "Я осёкся, когда Ольга, двумя быстрыми шагами пересекла помещение, и, взяв меня за плечо сильными пальцами, развернула носом к зеркалу."
    stop music fadeout 3
    me "Ох…"
    me "…ты ж…"
    me "…ё…"
    play sound sfx_open_cabinet_2
    play music music_list["revenga"] fadein 3
    window hide
    scene d1_dahell:
        pause 0.5 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (5,0) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (5,0) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (5,0) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (5,0) 
        linear 0.05 pos (0,0) 
    with dissolve
    window show
    
    "Я отказывался верить тому, что увидел в зеркале."
    "Из-под чёлки взирал дрищ лет семнадцати, будто немного уменьшенный относительно меня настоящего."
    "А я-то всё гадал, почему сваливаются штаны, и ботинки ощутимо великоваты."
    "Загадка с наушниками также решилась мгновенно."
    "Оставалась сущая малость."
    me "ЭТО ЧТО, Я?"
    scene bg int_house_of_mt_noitem_day
    show mt normal pioneer at center
    window show
    "Потолок закружился и начал заваливаться назад."
    window hide
    with None
    show blinking
    with dissolve
    $ renpy.pause(3.5)
    me "Не знаю, что вы со мной сделали, но верните мне — меня! Вы! Я…"
    play sound sfx_punch_medium
    with vpunch
    scene anim prolog_1
    with fade3
    window show
    "Наконец, пол подпрыгнул и со всей силы врезал мне в лоб."
    stop music fadeout 5
    mt "… с ним такое? Славя, доктор когда будет?"
    sl "Через пять минут, Ольга Дмитриевна. Она сказала, ей какое-то лекарство надо взять."
    mt "Быстрее бы."
    "Я помотал головой и попытался подняться."
    "К несчастью, силы совершенно оставили меня, и всё, что мне оставалось — это безучастно пялиться в потолок."
    scene bg int_house_of_mt_noitem_day
    "Я ощутил, как мои пальцы кто-то осторожно сжал."
    sl "Семён, ты только не волнуйся, хорошо?!"
    sl "Я уже доктора позвала. Она у нас лучше всех, мигом тебя на ноги поставит."
    cs "Пионерка, не утешай мальчика, это может повредить его самооценке."
    "Раздался в комнате новый голос."
    "О, у нас новое действующее лицо. Местный бестиарий блещет разнообразием."
    "Итак, кто же?"
    scene bg int_house_of_mt_noitem_day
    show mt normal pioneer far at left
    show sl normal pioneer far at right
    with dissolve
    play music music_list["eternal_longing"] fadein 5
    with None
    show cs normal
    with dspr
    cs "Виолетта Церновна Коллайдер."
    with None
    show cs grin
    with dspr
    "Она подмигнула."
    cs "Но для тебя можно просто {i}Виола{/i}… пионер."
    $ meet('cs','Виола')
    show cs normal close with zoomin
    "Её руки споро бегали по моему телу, проверяя рефлексы, лимфатические узлы, дыхание… Хотя я готов был поклясться, что Виола меня… лапает!"
    "Именно так. Несмотря на все заверения в профессионализме медсестры."
    show sl angry pioneer at right
    "Я густо покраснел. А Славя, видя мою реакцию, кажется, о чём-то догадалась и подозрительно посмотрела на медсестру."
    sl "Виолетта Церновна, что вы можете сказать о состоянии Семёна?"
    cs "Нормальный здоровый пионер. {w}Немного на солнце перегрелся плюс акклиматизация. {w}Пару дней пусть отсыпается и меньше на солнце сидит."
    "Помахав у меня под носом ваткой с нашатырём, резюмировала Виола."
    cs "Сейчас придёт в себя, напоите бульоном, я распоряжусь на кухне. Вечером можно кефир и пару булочек."
    cs "И никаких ранних подъёмов, верно… пионер?"
    "Она многозначительно посмотрела на меня и медленно-медленно подмигнула ещё раз."
    me "Верно. Для меня это вредно."
    "Закончив осмотр, она одарила меня ещё одним многозначительным взглядом, так, что я поспешил оправить футболку — {w} и исчезла за дверью."
    hide cs
    stop music fadeout 3
    show sl normal pioneer at right
    mt "Превосходно. Семён, придёшь в себя, и марш на бельевой склад, притащишь себе матрац и постельное бельё. Пионер должен самостоятельно ухаживать за своей постелью."
    "Я посмотрел на Славю."
    sl "Встретимся там!"
    "Убедившись в том, что моему здоровью ничего не угрожает, Славя улыбнулась и последовала вслед за медсестрой."
    hide sl 
    stop ambience
    play ambience ambience_camp_center_day fadein 1

    show mt normal pioneer at center
    mt "Пойду и я. Располагайся, вечером по расписанию ужин, не забудь."
    me "Ээээ… А что там с бульоном?"
    mt "Заправишь кровать и сходишь в столовую."
    "Безапелляционно заявила вожатая."
    mt "Справишься?"
    me "Ну… да."
    mt "Вот и хорошо. Увидимся за ужином, Семён."
    hide mt with dissolve
    "Она выпорхнула за дверь вслед за Славей."
    "Пальто я благополучно посеял в автобусе, так что дело было за свитером - я тут же избавился от него, повесив на спинку кровати и снял ботинки, поклявшись себе либо раздобыть нормальную обувь по погоде, либо ходить босиком."
    "Я остался в одной футболке «Indigo Denim», и призадумался, не слишком ли это будет вызывающе — носить такие вещи в советском лагере."
    "После секундных раздумий, решил, что футболка летом будет уместнее свитера. Даже с буржуйской надписью на груди."
    th "В конце концов, мелкая, вон, в эсэсэрке бегает, и ничего. Чем я хуже? Буду причащать массы к плетёному хлопку." 
    window hide
    jump alt_day_1_elektron

label alt_day_1_elektron:
    $ alt_chapter(1, u"Электроник")
    scene bg ext_houses_day
    play music music_list["my_daily_life"] fadein 3
    with dissolve
    window show
    "Что ж, ответы на вопросы можно поискать и на сытый желудок."
    "Как в той самой пословице про то, что русский на голодный желудок ни думать, ни делать не хочет."
    "А на сытый, понятно, не может."
    "Я шёл по дорожке, и тут меня окликнули."
    el "Эй!"
    "Я оглянулся."
    "Надо же."
    "Первое существо мужского пола, которое я сумел встретить. "

    show el normal pioneer with dissolve
    "Встрёпанный парень с россыпью мелких веснушек. Лицо классическое среднее. Семён у нас уже есть, значит, либо Василий, либо Сергей, угу."
    el "Привет!"
    "Он протянул руку."
    el "Рад познакомиться, Я — Электроник! Настоящий. Можешь меня так и звать. "
    "Настоящий Электроник. {w=.3}Ко-{w=.3}неч-{w=.3}но. {w}И под рубашкой прячет вилку на 220."
    "Косплейщик, что ли? Не скудеет земля русская идиотами."
    $ meet('el','Электроник')
    me "Понятно…"
    el "А Ульянка меня ещё Сыроежкой называет. "
    "Судя по недовольному выражению лица, ему это прозвище категорически не нравилось."
    th "Кстати, довольно метко. Такого гриба ещё поискать."
    el "Фамилия у меня такая. Сыроежкин."
    me "Я так и подумал. Ты только это… не волнуйся, ладно?"
    "К сожалению, его рукопожатие было достаточно твёрдым, и я не успел отобрать руку. А он уже переключился на новую тему."
    el "Как же не волноваться? Нам тебе постель притащить надо, а ещё столько в лагере показать. Пошли быстрее."
    "Он развернулся вправо, и не смотря, иду ли я за ним, устремился в сторону площади."
    el "А это правда, что ты в зимней одежде приехал?"
    "Я внимательно оглядел себя. Футболка, джинсы, да ещё и босиком…"
    me "Брехня."
    el "Вот и я так подумал! Это Ульянка опять сплетни распускает!"
    me "Ульянка — это та…"
    el "Мелкая, рыжая, проказливая!"
    if alt_day1_sl_conv:
        el "Ей вообще веры мало, но девочки могут и поверить."
    else:
        el "Она говорит, приехал в тулупе, ругается и ведёт себя отвратительно."
    
    scene ext_shed_day
    "Спустя пару минут мы уже подходили к серому сарайчику с огромными воротами с торцевой стороны."
    show el grin pioneer with dspr  
    el "Ты не думай, это просто снаружи так. Внутри там изоляция и всё чисто. Там ведь не только бельё, там ещё инвентарь всякий, и лыжи, и мячи… Вооот."
    me "Лыжи? У вас тут что, ещё и зимой заезды бывают?!"
    show el angry pioneer at center with dissolve
    el "Мне пока не везло. Говорят, здесь очень весело в Новый Год. Наряжают Генду, и ёлку из угодий привозят, огромную, голубую. Хотел бы я на это посмотреть."
    me "Какие твои годы. Увидишь ещё."
    "Он уже начал откровенно доставать своей болтологией, и я поспешил напомнить ему о цели прогулки."
    me "Веди к кастелянше или кто там у вас бельём заведует."
    stop music fadeout 3
    "Мы обошли здание и приблизились к распахнутой настежь двери, через которую было видно полутёмное помещение с трёхэтажными деревянными полками, на которых лежали свёрнутые в рулон матрасы, а в качестве начинки были подушка и тонкое шерстяное одеяло."
    sl "Ой, ребята, подождите. Сейчас я выйду."
    play music music_list["take_me_beautifully"] fadein 1
    show sl shy sport at center 
    show el normal pioneer at left
    "Несколько минут спустя из-за полок появилась встрепанная и покрасневшая Славя, одёргивающая одежду. "
    el "Эм… Я чего-то не знаю?"
    "Осторожно поинтересовался Электроник."
    "Я же стоял в ступоре, как и всякий раз, когда случайно оказывался втянут в чужие тайны."
    show el normal pioneer at left with dspr
    me "Я определённо чего-то не понимаю. Можно же было просто привести меня сюда и…"
    sl "…И проконтролировать бульон, выдачу формы, глажку рубашки, установку на довольствие и целую кучу других тонких моментов?"
    show sl laugh sport at center
    "Славя рассмеялась, увидев мои квадратные глаза."
    me "Упс…"
    sl "Извинения приняты. Забирайте постель, какая нравится."
    stop music fadeout 3
    hide sl
    "Несколько минут спустя, сбросив постель в домике вожатой, я уже двигался в сторону площади."
    window hide

label alt_day_1_meeting:
    $ alt_chapter(1, u"Экскурсия")
    scene bg ext_square_day
    show el normal pioneer at left
    with dissolve
    play music music_list["my_daily_life"] fadein 3
    window show
    el "Начнём экскурсию с площади. Там все наши достопримечательности, Генда и…"
    me "И…"
    "Я скептически приподнял бровь."
    el "И…"
    "Он потерянно озирался, и вдруг просиял."
    el "И — Лена!"
    hide el  with dissolve
    show el laugh pioneer at cleft with dspr
    el "Смотри, Семён. Лена!"
    show un normal pioneer at cright   with dspr
    th "Не спорю, Лена — девушка видная. Но называть её достопримечательностью…" 
    "Я подавил смешок."
    me "А она в курсе, как ты её только что окрестил?"
    el "Конечно."
    show un surprise pioneer at cright   with dspr
    "Он подскочил к девочке и затараторил."
    el "Лена, привет! Это новенький! Семён, знакомься."
    with dissolve
    th "И что ей говорить?!"
    "В панике заметались мысли."
    me "Привет.{w} Мы, вроде, уже заочно знакомы."
    show un shy pioneer at cright   with dspr
    un "Да…"
    show un shy pioneer at cright with dspr
    "Она на секунду оторвалась от книжки, взглянула на меня — и тут же, покраснев до самых ушей, спряталась за книжку, изо всех сил стараясь не замечать нашего присутствия."
    "Я невольно улыбнулся, настолько её поведение напоминает моё собственное."
    "Нет, краснеть-то я не стану — староват уже для таких дел."
    "Но в целом попытка спрятаться от собеседника мне близка и понятна."
    $ renpy.pause(3)
    "Впрочем, судя по картинке в зеркале, моя старость осталась на морозных улицах города. "
    show el normal pioneer at cleft
    el "Ладно. Пошли дальше, тут ещё куча достопримечательностей ждёт."
    "Вот и поговорили. {w}Похоже, Электронику достаточно поздороваться с человеком, чтобы считать его чуть ли не другом. {w}То ли дело я, который забывает имя человека через пять секунд после того, как оно было произнесено вслух."
    me "Приятно было познакомиться, Лена!"
    "Крикнул я через плечо, увлекаемый дальше неуёмным гидом."
    "Лена вздрогнула и, хотя мне это казалось фантастичным, покраснела ещё сильнее."
    hide un  with dissolve
    window hide

    me "Какие ещё достопримечательности покажешь?"
    el "Сейчас, сейчас…"
    scene bg ext_dining_hall_away_day with fade
    window show
    "Мы уже подходили к новому объекту интереса. Как несложно было догадаться по приземистым очертаниям, перед нами была столовая. "
    el "А здесь у нас…"
    me "Да-да, я уже догадался. Здесь мы принимаем органическую пищу. "
    if not (slavya_convoy or alt_day1_sl_conv):
        "Рыжая бандитка была здесь же, и на последнюю реплику отреагировала сдержанным смешком."
    else:
        "На крылечке стояла девочка с рыжими волосами, сколотыми заколками в два хвоста. Мою плоскую остроту она восприняла одобрительно и даже усмехнулась."
    "Видимо, мои шутки от Евгения Ваганыча будут обладать определённой популярностью."

    scene bg ext_dining_hall_near_day
    el "А это…"
    show dv normal pioneer2 far at left  with dspr
    "Электроник перешёл на шёпот, который не услышал бы только глухой."
    el "Наша Алиса. Она всегда в плохом настроении."
    $ meet('dv','Алиса')
    "Девочка-пожар, девочка-брызги…"
    me "Такая злая?"
    el "Да нет, просто она какая-то…{w}Фамилия Двачевская, но только попробуй назвать её ДваЧе…."
    window hide

    play music music_list["awakening_power"] fadein 2
    
    show dv angry pioneer2 far at left  with dspr
    dv "Ты что-то сказал, гриб?!"
    show el scared pioneer at center   with dspr
    "Алиса мгновенно спрыгнула с крыльца и бросилась в нашу сторону."
    el "Ээээ… Ладно. Дальше сам как-нибудь!"
    me "Что?"
    "С умным лицом спросил я у пустого воздуха."
    "Электроника и след простыл, а я почему-то замер и наблюдал за приближающейся погибелью."
    hide el  with dissolve
    window hide
    

    menu:
        "Побежать за ним":
            $ lp_el = lp_el + 1
            $ run_el = 1
            stop music fadeout 3
            window show
            "Решив, что лучше не дразнить гусей, я припустил вслед за Электроником и скоро поравнялся с ним."
            window hide
            $ persistent.sprite_time = "day"
            scene ext_shed_day
            with dissolve
            play ambience ambience_camp_center_day fadein 2
            window show
            "Немного подумав, он резко вывернул в сторону складов."
            "Однако, не доходя до них немного, прямо с тропинки прыгнул в густую траву."
            "Я, борясь с душащим меня хохотом, спрятался рядом."
            "Озлобленная Алиса{w}, не смотря по сторонам, {w}пронеслась по дорожке и скрылась за складом."
            "Выждав несколько минут для успокоения, Электроник облегчённо выдохнул и выбрался из травы."
            show el smile pioneer at center   with dspr
            el "Кажется, оторвались."
            "Видок у него был тот ещё. Похоже, нырок в траву произошёл несколько не так, как он планировал."
            el "Думает, знает, где меня искать. Сейчас к клубу повернёт, будет меня там караулить. А я туда не пойду. {w} Чревато!"
            th "Чем может быть чревато попасть в засаду Двачевской, я уже догадался, поэтому сочувствующе кивнул."
            el "Ничего, до ужина доживём, а там уже опасность минует. Ну, во всяком случае, должна."
            "Он с неудовольствием оглядел испачканную травяным соком одежду."
            el "Весь изгваздался. Ладно, ты пока сходи куда-нибудь, а я пойду себя в порядок приведу. За ужином увидимся. "
            stop ambience fadeout 2
            hide el
            "Немного подумав, я направился в сторону, откуда слышались упругие удары по мячу."
            "Похоже, там играли в футбол."
            jump soccer_d1

        "Ничего не делать":
            $ lp_dv = lp_dv + 1
            $ run_el = 0
            show dv angry pioneer2 at left   with dissolve
            window show
            "Поравнявшись со мной, она одарила меня особо злым взглядом из своего арсенала."
            dv "С тобой ещё потолкуем!"
            if loki and not alt_day1_sl_conv:
                me "Принесёшь мне ещё одно ведро?"
                $ karma = karma - 10
                "Я с хохотом увернулся от мелькнувшей в воздухе ладошки."
                "Она со злости плюнула и, развернувшись, умчалась вслед за Электроником."
            elif herc and alt_day1_dv_fight:
                me "Понравилось на руках кататься?"
                $ karma = karma + 10
                "Я был готов сграбастать нахалку в охапку, и кажется, она прочитала это намерение в моих глазах."
                "Она со злости плюнула и, развернувшись, умчалась вслед за Электроником."
            else:
                me "А что я? Я ничего."
                "Я безмятежно пожал плечами."
                "К жесту добавилась улыбка. {w} Почему-то виноватая."
                "Не удостив меня ответом, Двачевская кинулась за Электроником."
            stop music fadeout 5
            hide dv  with dissolve

            window hide

            window show
            th "Судя по всему, дальше меня никто не будет доставать, и время до ужина можно посвятить блаженной праздности."
            "Я наобум выбрал направление и отправился в сторону, откуда раздавались упругие удары по мячу — там явно играли в футбол."
            window hide

label soccer_d1:
            $ persistent.sprite_time = "day"
            scene ext_playground2 
            with dissolve
            play ambience ambience_soccer_play_background fadeout 2
            play music music_list["went_fishing_caught_a_girl"] fadein 2
            window show
            "Некоторое количество времени спустя я добрался до искомого поля и, расположившись на стоявших по периметру поля скамейках, задумчиво наблюдал за матчем. "
            "Матч был в самом разгаре, и я поневоле засмотрелся."
            "Некогда у меня получалось неплохо обрабатывать мяч, однако, приоритеты спорт в себя никогда не включали."
            "Дети резво носились по полю, однако даже из постоянно двигающейся толкучки выделялась одна, самая быстрая, самая непоседливая."
            "Играли 5 на 6."
            th "Ульянка…{w} Чего и следовало ожидать."
            scene ext_playground2
            "Под ноги подкатился мяч, и я, приняв его на ногу, запулил обратно. "
            play sound sfx_soccer_ball_kick
            extend "Мда… {w}Не лучший мой удар. "
            show us laugh sport far at center    with dissolve   
            us "Эй ты!"
            "Крикнула Ульянка."
            us "Пошли играть!"
            "Я задумался."
            th "А почему бы, собственно и нет?"
            th "С другой стороны, босиком и в тёплых брюках играть как-то…"
            me "Давай в другой раз!"
            show us dontlike sport
            us "Ты скучный!"
            "Припечатав глаголом напоследок, она вернулась к игре."
            hide us  with dissolve
            "Я слез со скамейки и ради разнообразия, обратил внимание на стоящие тут же турнички."
            scene ext_playground2:
                xalign 0.5 yalign 0.5 zoom 1.0
                linear 2.0 zoom 1.5 xalign 0.55 yalign 0.55
            "В моё время очень модно было слово «воркаут», означающее самое тривиальное - любовь повисеть на турничке."
            "Я, посмотрев пару роликов на ютубе, даже стал ходить на свой турник во дворе, однако, быстро забросил это дело."
            "А ведь раньше кое-что смогал! Надо посмотреть, что за тело мне подсунули, и способно ли оно подтянуться хотя бы десяток раз."
            "Пальцы, непривычные к нагрузкам, никак не хотели укладываться правильно. "
            "Потом куда-то увело центр тяжести. {w}Наконец, наорав на себя, я подпрыгнул и повис на перекладине, ровно держа спину, и…"
            "Раз!"
            "Два!"
            "Три!"
            us "Четыре! Пять!"
            with dissolve
            "Раздался хор за спиной."
            if semen_str > 10:
                "Вздрогнув от неожиданности, я, тем не менее, продолжил подтягивания, пока не остановился на цифре «10»."
                scene bg ext_playground_day
                show us smile sport
                us "Эй, и это что, всё?"
                me "Ну, так а больше и не надо!"
                me "Впрочем, ты всегда можешь забраться на турник и переубедить меня!"
            else:
                "Вздрогнув от неожиданности, я разжал руки и шлёпнулся на землю."
                play sound sfx_punch_medium
                with vpunch
                us "Ууууу…"
                "Разочарованно загудели незаметно подтянувшиеся болельщики."
                scene bg ext_playground_day
                th "Так и до инфаркта довести недолго."
                "Недовольно подумал я, поднимаясь."
                show us smile sport
                us "Дальше будешь подтягиваться?"
                me "Не буду. Я вам не бесплатное шоу."
                show us dontlike sport
                us "Да ты просто больше не можешь! Слабак!"
                if loki and not alt_day1_sl_conv:
                    me "Будешь обзываться — оболью ещё раз!"
                    "Ульянка хотела было ответить что-то обидное, но её прервали сигналы бессмертного «бери ложку, бери хлеб»." 
                else:
                   "Можно подумать, счастье — в количестве подтягиваний."
                   "Поняв, что на «слабо» я не ведусь, Ульянка хотела было вернуться к игре, но её прервал сигнал горна."
            play music eat_horn fadein 3 
            us "Бегом до столовой! Кто последний, тот тухлый помидор!"
            hide us with dissolve
            "Дети загалдели, и, толкаясь, бросились по направлению к столовой."
            $ renpy.pause(1)
            stop music fadeout 3
            stop sound_loop
            stop sound
            stop ambience
            if run_el == 1:
                th "А Электроник всё ещё не пришёл."
                th "Может он и не придёт вовсе?"
                "Я уже собрался было идти, как тут ко мне подбежал Электроник."
                stop music fadeout 2
                show el smile pioneer at center   with dspr
                el "А-а-а. Дождался таки. Ну и хорошо. Побежали в столовую, пока не съели всё!"
                "Мы направились в столовую. Благо, я теперь знал, где она."
            else:
                "А я неторопливо двинул за ними."
            window hide
            stop ambience
            jump alt_day1_supper

label alt_day1_supper:
    $ alt_chapter(1, u"Ужин")
    $ day_time()
    $ persistent.sprite_time = "sunset"
    scene bg ext_dining_hall_away_sunset 
    with dissolve

    stop sound fadeout 2

    if run_el == 0:    
        window show
        "Ульянка успела первой, но её ожидаемо завернула вожатая, уже стоящая на крыльце."
        stop music fadeout 2
        show mt normal pioneer far at center    with dissolve
        mt "Марш руки мыть!"
        show us normal sport far at left    with dissolve
        us "Ну Ольга Дмитриевна!"
        mt "Ульяна!"
        us "Ладно, ладно. Иду."
        hide us with dissolve
        "Я, с огромным удовольствием наблюдавший всю сцену, даже вздрогнул, когда вожатая окликнула меня."
        mt "Семён!"
        me "Да?" 
        mt "Руки мыл?"
        me "Нет."
        mt "Так иди мой, потом жду в столовой." 
        me "Понял."
        hide mt
        "Мой желудок был абсолютно солидарен с вожатой и поддержал её решение самым жизнерадостным бурчанием."
        window hide
        stop ambience fadeout 3
        "Спустя пять минут я, отряхивая ладони от холодной — ледяной! — воды, поднялся на крылечко и вместе с вожатой зашёл внутрь."
        jump alt_day_1_dining_room
    else:
        "Заскочив по дорожке к умывальникам и сполоснув руки холодной до ломоты в пальцах водой, мы зашли в помещение столовой."
    $ persistent.sprite_time = "day"
    scene bg int_dining_hall_people_day 
    with dissolve
    window hide
    stop music fadeout 3
    stop ambience fadeout 1
    jump alt_day_1_dining_room

label alt_day_1_dining_room:
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_day  with fade

    

    window show
    "Что ж… Столовая как столовая."
    "Длинные столы на двенадцать человек, выстроившиеся у стен, одиночные столы по другую сторону прохода, чтобы обслуживающий персонал с тележками мог подобраться и сервировать или убрать со стола."
    "Кафель на полу и стенах, стулья из нержавеющей стали и колонки под потолком на случай, если на улице будет дождь."
    "Наособицу стояли небольшие столики - судя по всему, для персонала. За одним из них я разглядел какую-то фигуристую девицу примерно моего возраста."
    dreamgirl "Бывшего."
    th "Что?"
    dreamgirl "Бывшего возраста."
    th "А ты ещё кто такой?"
    dreamgirl "Твоя интуиция, балда."
    "Приглядевшись, я узнал в ней медсестру, осматривавшую меня."
    th "Виолетта, кажется?"
    "Виола приветственно кивнула, перехватив мой взгляд."
    
    show mt normal pioneer at right with dissolve
    mt "Семён, сейчас тебе место найдём."
    show mt normal pioneer at left with dissolve
    "Вожатая подвела меня к паре столов, которые, судя по всему, принадлежали моему отряду и, немного подумав, указала пальцем на свободное место рядом с Ульянкой."
    mt "Здесь и будешь сидеть… "
    show dv normal pioneer2 at right with dissolve
    show mt angry pioneer at left with dissolve
    extend "Двачевская, стоять!"
    "Она чуть ли не из воздуха добыла рыжую молнию, несущуюся к законному пюре с котлетой."
    show dv surprise pioneer2 at right with dspr
    dv "Чего ещё?"
    mt "Ты во что одета? "
    dv "Эмм… В форму? "
    if loki:
        if not alt_day1_sl_conv:
            "Я подавил смешок — после инцидента при входе в лагерь, Алиса переоделась, но сделала это по-своему."
        "Расстёгнутая рубашка, завязанная узлом на животе, демонстрирующая отсутствие всех лишних предметов одежды под ней, пионерский галстук, завязанный на запястье."
        "Именно так и должен ходить настоящий пионер."
        mt "Немедленно приведи в порядок форму!"
        dv "Пффф… Как скажете."
        "Она неторопливо потянула узел рубашки…"
        hide dv with dissolve
        show mt normal pioneer at left
        me "Отвернись к стенке, горе луковое!"
        "Не выдержал я."
        me "Иначе саботируешь ужин у половины лагеря."
        show dv normal pioneer at right
        "Алиса последовала моему совету, и, поправив форму, уселась по ту сторону прохода, между делом окинув меня косым взглядом."
        show dv normal pioneer at cright
        hide dv with dissolve
    elif herc:
        "Мой смешок привлёк внимание и вожатой, и Алисы. Причём, если первая, судя по смеющимся глазам, прекрасно поняла игру, в которую я играю…"
        "То последняя приняла всё за чистую монету."
        "Она упёрла руки в бока и жеманно так потянулась, подчёркивая связанной узлом рубашкой то, что хотела подчеркнуть. "
        me "Кажется, кто-то перепутал столовую с сельской дискотекой…"
        "Вроде, шёпотом, но услышало почему-то половина столовой."
        "Двачевская вспыхнула."
        th "Ого, ей, оказывается, знакома такая вещь как смущение?!"
        "И выскочила вон из столовой, уже второй раз за сегодня испепеляя меня взглядом."
        hide dv with dissolve
        show mt normal pioneer at left
        me "Надеюсь, я не сказал лишнего."
        show dv normal pioneer at right
        "Алиса вернулась через пол-минутки, уже приведя в порядок форму и даже частично разгладив замятости от узла."
        "Одарив меня крайне нехорошим взглядом, она заняла своё место."
        show dv normal pioneer at cright
        hide dv with dissolve
    elif alt_sp_wet or slavya_convoy:
        "Я подавил смешок. Похоже, понятие формы для Алисы коренным образом отличалось от общепринятого."
        "Крайне вызывающе надетая рубашка, держащаяся лишь за счёт узла чуть ниже груди, галстук, напульсником повязанный на запястье, и два рыжих хвоста-молнии."
        "И правда, что же не так с формой?"
        "Именно так и должен ходить настоящий пионер."
        mt "Немедленно приведи в порядок форму!"
        dv "Пффф… Как скажете."
        "Она неторопливо потянула узел рубашки…"
        show dv normal pioneer2 at right
        show mt angry pioneer at left
        mt "Алиса!"
        dv "Ну что ещё!"
        "Ольга Дмитриевна немного подумала и махнула рукой."
        show mt normal pioneer at cleft
        show dv normal pioneer2 at cright
        with dissolve
        mt "Ладно, иди так."
        hide dv with dissolve

    me "Ольга Дмитриевна, у меня глупый вопрос."
    "Обратился я к вожатой, понимая, что сидеть рядом с Ульянкой крайне чревато."
    mt "Все глупые вопросы — после ужина. Садись ешь давай."
    "Видимо, я исчерпал квоту глупых поступков на сегодня. Делать нечего, пришлось садиться. В конце концов, какая разница с кем сидеть, если мне важнее содержимое тарелки, а не окружение."
    hide mt with dissolve
    us "Эй, псст!"
    show us smile sport with dissolve
    me "Чего тебе?"
    us "Почему играть с нами не стал?"
    me "Не та одежда была."
    us "А, ну тогда ладно, садись ешь."
    "Она подмигнула, а я повернулся к своей тарелке и обнаружил, {w=.5}что котлета пропала!"
    "Прикинув хрен к носу, и сделав поправку на уже сложившуюся репутацию егозы, я обернулся к ней. "
    me "Котлету."
    us "В большой семье клювом не щёлкают."
    me "Ах ты маленькая…"
    
    
    menu:
        "Потребовать котлету.":
            $ karma = karma + 10
            $ lp_us = lp_us + 1
            "Я грозно посмотрел на неё и протянул тарелку."
            if loki:
                me "Улечка, солнце моё. Давай не будем делать глупостей и вернём котлеты их владельцам, а?"
                me "А не то я тебе сейчас это пюре на голову надену!"
            elif herc:
                me "Мелкая, не упрямься и верни что взяла."
            else:
                me "Ульяна. Котлету. {w=.6} ЖИВО."
            us "Да нет у меня её, сам смотри!"
            "Она показала на свою тарелку, где оставалось меньше половины пюре, и, разумеется, ни одной котлеты."
            me "Ты… Съела…"
            us "Так, стоп."
            "Она мгновенно схватила мою тарелку и пропала."
        "Смириться.":
            me "А и подавись… Не особо-то и хотелось."
            us "Ну вот…"
            "Она, казалось, сама расстроилась из-за пропажи."
            us "Ты не огорчайся, сейчас что-нибудь организуем."
            "Она мгновенно схватила мою тарелку и пропала."
        "Отнять тарелку.":
            $ karma = karma - 10
            "Резкое движение рукой — и у меня на одну тарелку больше."
            show us dontlike sport
            us "Эй! Отдай! Это моё!"
            me "Тебе напомнить про семью и клюв?"
            us "Ну и ладно."
            show us dontlike sport
            "Она сложила руки на груди."
            us "Желаю тебе объесться этим пюре. Всё равно котлеты там нет."
            me "Как нет?!"
            "Я сунул нос в тарелку, чтобы убедиться в правдивости слов Ульянки."
            "И в самом деле — котлеты не было."
            me "Мелкая, ты не в курсе, что поедание чужих котлет не только негигиенично, но ещё и опасно?"
            show us normal sport
            us "Что?"
            me "По уху тебе дам, вот что!"
            show us smile sport 
            us "Ты не огорчайся, сейчас что-нибудь организуем."
            "Она мгновенно схватила мою тарелку и пропала."

    hide us with dspr
    "Замечательно. За один ужин я лишился и мяса, и гарнира."
    th "Тебе уже кто-нибудь говорил, что ты осёл? Жизнь — боль."
    "Однако, спустя несколько минут Ульянка вернулась с тарелкой, на которой уже лежала горячая котлета."
    us "Питайся!"
    show us grin sport with dissolve
    "Она поставила еду передо мной."
    us "И помни мою доброту!"
    hide us with dissolve
    me "Спасибо большое!"
    "От души поблагодарил я, отметив краем сознания, что моя благодетельница уже куда-то смылась."
    th "Ну и ладно. Сейчас важнее всего — правильное и здоровое питание. "
    "То, что я с утра ничего не ел, дало о себе знать, и я с душой ткнул вилкой в котлету, готовясь сделать из благолепия на тарелке мою любимую тюрю вперемешку из мяса, пюре и горошка. "
    window hide

    scene cg d1_food_normal with dissolve
    $ renpy.pause(1)
    window show
    "Я уже почти было откусил благословенный ароматный кусок мяса, как вдруг заметил на тарелке шевеление."
    window hide

    scene cg d1_food_skolop with dissolve
    play sound sfx_suspence_bang
    "Переведя взгляд на тарелку, я отпрыгнул назад, запуская стул в стенку."
    me "Мать честная, оно что, живое?!!"
    play music music_list["awakening_power"] fadein 2

    scene cg d1_food_skolop:
        zoom 1.0 xalign 0.5 yalign 0.5
        linear 1.0 xalign 0.6 yalign 0.8 zoom 2.0
    $ renpy.pause(1)
    window show
    "Это что, сколопендра? Личинка чужого? Ещё какая-то инопланетная гадина?"
    "Я с вскриком шлёпнулся на кафельный пол…"
    window hide

    scene bg int_dining_hall_people_day with fade
    window show
    "Неведомая тварь неторопливо развернулась и, выбрав направление, всё ускоряясь, понеслась…"
    "Ко мне!"
    me "Ааа-а-а-а-а-а-а-а-а!"
    play sound sfx_chair_fall
    with vpunch
    "С громким криком я взял разбег, передвигаясь исключительно на карачках, и даже преуспел, пока не финишировал об стену."
    "…а инопланетная мерзость целеустремлённо двигалась за мной! "
    play sound sfx_punch_washstand
    with vpunch

    "Шутки шутками, но для моего голодного рассудка это оказалось чересчур."
    "{i}И каждому воздастся по грехам его!{/i}"
    me "{b}УЛЬЯНААААА!!!{/b}"
    stop ambience fadeout 1
    $ renpy.pause(1)
    "Во внезапно опустившейся на столовую тишине мой крик раздался особенно громко."
    "И, судя по донесшемуся от двери довольному хохоту, достиг ушей адресата. "
    stop music fadeout 2
    show us laugh2 sport with dspr
    play music music_list["eat_some_trouble"]
    "Вот только увидев выражение моего лица, она расхохоталась уже в полный голос!"
    us "Ах-ха-ха-ха-ха-хаа!"
    hide us with dspr
    "Рыжий метеор унёсся сквозь двери, справедливо взимая фору."
    window hide

    scene bg black with fade
    jump alt_day_1_chase

label alt_day_1_chase:
    $ alt_chapter(1, u"Погоня")
    scene bg ext_dining_hall_near_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    play sound_loop sfx_run_forest fadein 1
    play music music_list["went_fishing_caught_a_girl"] fadein 2
   
    window show
    if not (slavya_convoy or alt_day1_sl_conv):
        "Уже в который раз за этот сумасшедший день я бежал по этому странному лагерю."
    "Маленькая дрянь выскочила с отрывом в десять метров… Но её это не спасёт!"
    "Я выскочил на крыльцо и огляделся."
    "На дорожке к площади мелькнула и скрылась за поворотом какая-то невысокая тень."
    "Одновременно с этим затрещали кусты на юго-востоке по направлению в сторону футбольного поля."
    window hide

    scene bg ext_dining_hall_away_day with dissolve
    menu:
        "Бежать на площадь!":
            scene bg ext_square_day with dissolve
            window show
            th "Сколь-ко мож-но у-же бе-гать!"
            "Волка ноги кормят, но я сегодня что-то всё больше бегаю, чем кормлюсь."
            "За весь день я даже воды не попил, откуда ж силам взяться. А ещё эта мерзавка."
            "Я ещё не очень хорошо придумал, что сделаю с Ульянкой, когда её догоню, но это будет очень медленно и мучительно."
            window hide
            stop ambience fadeout 1
            scene bg ext_path_day with dissolve
            $ renpy.pause (1)
            play ambience ambience_forest_day fadein 1
            play sound breath fadein 3
            window show
            "Пробежав весь лагерь насквозь, я начал понимать, что где-то разминулся с жертвой."
            "Я остановился, со злостью ударив кулаком по дереву, и, согнувшись, попробовал восстановить дыхание."
            if not (herc or loki):
                "Где же мы с ней разминулись? Я попытался восстановить события."
                "Сначала тень, метнувшаяся от столовой, потом площадь с Гендой, потом... да и все. Куда угодно могла шмыгнуть!"
                "Следопыт из меня получился не очень. Самому бы выбраться..."
                $ alt_day1_chase_uvao = True
            window hide
            jump nocatch

        "Бежать на спортплощадку!":
            window show
            "То, что я выбрал верное направление, стало понятно уже через десять секунд."
            "Красная футболка с надписью «СССР» мелькала среди зелени заметным пятном."
            "И я прибавил ходу, не обращая внимания на возмущённо ноющие ноги."
            window hide
            scene bg ext_playground_day 
            with dissolve
            "Впрочем, долго их игнорировать не удалось бы, и я почувствовал, что ещё немного, и начну сдавать."
            "Скорости было недостаточно, дыхалки было недостаточно."
            "К несчастью, демонёнок в алой футболке, казалось, не знал устали и лишь наращивал темп."
            "К моменту, когда я выбежал на поле, она уже успела миновать половину, и теперь полным ходом двигалась ко спасению."
            "Я в отчаянии огляделся, и мой взгляд наткнулся на крайне удачно попавшийся мячик."
            stop ambience
            stop sound_loop
            menu:
                "Пнуть мячик.":
                    $ karma = karma - 10
                    $ alt_headshot = True
                    me "Сбежать захотела?"
                    "Я буквально упивался злорадством."
                    me "Чёрта с два!"
                    "Я взял короткий разбег, ударил на силу, отправляя круглый снаряд строго между лопаток беглянки."
                    if herc and alt_day1_dv_fight:
                        "Прямо в то же место, куда парой часов ранее засветил шишкой."
                        th "Похоже, для Ульянки мой приезд означает перемены к худшему."
                    else:
                        pass
                    "Нога заныла от неправедного отношения, однако, на такие глупости мне было плевать."
                    play sound sfx_soccer_ball_kick
                    us "Оххх!"
                    "Ульянку буквально снесло с ног, и, зарыв носом в траву, пару метров проволокло вперёд."
                    me "Мавр сделал дело, мавр может уходить."
                    "Я раскланялся воображаемым трибунам и со всем возможным достоинством удалился."
                    jump alt_day1_headshot
                "Не пинать":
                    $ lp_us = lp_us + 1
                    "Ульянка скрылась из виду."
                    "Я ещё некоторое время продолжал преследование, пока дыхалка не запросила пощады."
                    if semen_str < 10:
                        "Это, однако, лучше моего утреннего результата. {w} Начинаю набивать руку?"
                    else:
                        pass
                    jump nocatch

label alt_day1_headshot:
    scene bg ext_path_day with fade
    play music music_list["take_me_beautifully"] fadein 1
    window show
    "Пробормотав отходную молитву по безвременно почившему ужину, я направился куда глаза глядят, и уже очень скоро уткнулся в знакомые ворота с пятиконечной звездой. "
    "Как это странно, когда подъездной пятачок у лагеря совмещён с автобусной остановкой. "
    scene bg ext_no_bus_sunset with dissolve
    th "По опыту знаю, что ради лагерей никто никогда не станет останавливать транспорт, значит, нужен либо частный сектор, либо деревня, либо предприятие."
    "Вот и думай теперь…"
    "Сорвав травинку, я закусил сочную зелень, и усевшись прямо здесь на бетонный бордюр, стал подводить итоги дня."
    "Смирившись с тем, что ложусь спать без ужина. Не впервой."
    "Водителя сегодня я так и не встретил, несмотря даже на то, что обошёл практически весь лагерь."
    "Это значит, что он скорее всего живёт где-то неподалёку."
    "Или, если уж совсем вдаваться в чертовщину, вполне могло быть, что автобус был вовсе без водителя."
    "Это значит, что вне графика отсюда крайне трудно выбраться."
    if not alt_day1_sl_conv:
        "Разве что…"
        scene bg ext_boathouse_day
        show prologue_dream
        with fade
        window show
        "На поезде…"
        window hide
        window show
    "Я подумаю об этом завтра."
    stop sound_loop fadeout 2
    stop ambience fadeout 2
    $ renpy.pause(1)
    window hide
    jump slavya_saviour
    
label nocatch:
    scene bg ext_path_sunset with fade2
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    window show
    "Время было уже позднее, и делать что-то в потёмках не хотелось совершенно."
    th "Если я ничего не перепутал, то здесь можно будет выйти к клубам…"
    th "Заблудиться ещё не хватало. Будет апофеоз неудачника."
    window hide
    scene bg ext_clubs_sunset with fade
    play ambience ambience_camp_center_evening fadein 2
    window show
    "В темноте, да ещё и с тыла — попробуй узнай здание, которое видел раз в жизни."
    "К счастью, чутьё меня не подвело, и тропинка привела меня точнёхонько к зданию клуба."
    if alt_day1_sl_conv:
        "Я сегодня так и не заглянул туда. Может, там можно будет хотя бы разобраться с годом."
    else:
        "К тому самому месту, где я встретил Лену и Ульяну…{w} с саранчой."
        "Вот тяжело было сделать выводы, наверное…"
        "Не надо было брать еду из рук у маленькой мерзавки. "
        "Короче, я либо ей понравился, либо нет."
        "И оба варианта сулят баааальшие проблемы."
        "Вздрогнув от пришедших в голову картин, я зашагал в сторону ворот…"
    stop sound_loop fadeout 2
    window hide
    jump slavya_saviour

label slavya_saviour:
    $ alt_chapter(1, u"Спасительница")
    $ sunset_time()
    scene bg ext_no_bus_sunset with dissolve
    $ persistent.sprite_time = "night"
    show sl normal pioneer with dissolve
    "У ворот уже кто-то стоял."
    me "Кто здесь?!"
    "Я вздрогнул от неожиданности, а неизвестный произнёс знакомым уже голосом:"
    sl "Привет! А что ты тут делаешь так поздно?"
    "Я опять вздрогнул от звуков её громкого голоса. Нервы ни к чёрту, покурить бы. Хотя бы китайской марки «Цюзые»."
    sl "Я видела, ты за Ульяной побежал."
    if alt_headshot:
        th "Интересно, сколько она знает…"
        sl "Не догнал?"
        me "Ну…"
        "Не в силах врать этой девочке, я пожал плечами."
        me "В целом… Не догнал."
        show sl smile pioneer with dissolve
        sl "Ничего удивительного. {w} Никто не может. {w}Не ты первый. "
        "Я пожал плечами. Можно сбежать от человека, но не от его мести."
    else:
        "Я угрюмо уставился на асфальт. Она теперь тоже издеваться будет?"
        sl "Не догнал?"
        me "Нет."
        "Удивлённый её участием, я немного растаял."
        me "Потерял… Где-то."
        show sl smile pioneer with dissolve
        sl "Ничего удивительного. {w} Никто не может. {w}Не ты первый. "
        "Ничего, Земля планета маленькая…"

    sl "Ты, наверное, голодный? Поужинать-то тебе не удалось."
    play sound sfx_stomach_growl
    "Я хотел было прочитать ей гневную и гордую отповедь в духе «не надо со мной как с маленьким ребёнком», однако мечтательная китовая песнь желудка расставила все точки."
    show sl laugh pioneer with dissolve
    "Славя снова рассмеялась."
    sl "Пошли покормим тебя."
    me " А как же режим, график и всё такое? Столовая закрыта."
    sl "Да ничего, у меня ключи есть, что-нибудь тебе найдём. "
    me "Извини за нескромный вопрос, но откуда?"
    show sl normal pioneer with dissolve
    sl "Оттуда! Я же помощница вожатой, забыл?"
    "Она начала проявлять нетерпение."
    sl "Мы идём?"
    me "Да, конечно. {w}Спасибо. "
    "От такого предложения грех было отказываться."
    window hide

    $ persistent.sprite_time = "night"
    scene bg ext_square_night 
    with dissolve

    "Когда мы вышли на площадь, уже окончательно стемнело, а Славя, почему-то смутившись, сказала:"
    show sl normal pioneer at center   with dissolve
    sl "Слушай, мне надо соседку предупредить, что я немного задержусь."
    sl "А то она паникерша такая, обязательно что-нибудь себе придумает."
    "Ангелы не ходят в туалет, да-да, помню."
    sl "Ты пока иди вперёд и подожди меня около столовой, а я скоро буду. Хорошо?"
    me "Ладно…"
    "Я кивнул и побрёл в сторону столовой."
    window hide

    $ persistent.sprite_time = "night"
    scene bg ext_dining_hall_away_night 
    with dissolve

    window show
    "В темноте столовая выглядела совершенно иначе, чем при дневном свете, и если бы не пара дежурных лампочек, у меня бы в жизни не появилось желания подходить к этому зданию."

    play sound sfx_alisa_picklock

    "Однако, на крыльце уже кто-то был и, судя по сосредоточенному пыхтению, пытался забраться внутрь."
    window hide

    $ persistent.sprite_time = "night"
    scene bg ext_dining_hall_near_night 
    with dissolve
    window show
    "Безуспешно."
    "Я, стараясь быть как можно более шумным, поднялся на крыльцо и тактично прочистил горло."
    show dv normal pioneer at center   with dissolve
    
    if alt_headshot and not slavya_convoy:
        "А…"
        "Я осёкся и отступил на шаг."
        "Тут-то меня и прикопают. Если мелкая наябедничала подружке о том, как я поставил ей хедшот мячом, дела мои скверны, ох и скверны."
    else:
        me "Кхе-кхе."
        "Взломщик обернулся."
        "Алиса, кто ж ещё."
        me "Воруем?"

    dv "Это ты."
    "Она немного постояла, видимо, о чём-то раздумывая, а потом зашипела:"
    dv "Ну чего стоишь пенёчком, помогай давай!"
    me "Помочь тебе дверь взломать?"
    if herc:
        "Я вопросительно приподнял бровь, а потом, догадавшись, что мои ужимки в темноте незаметны, подбавил в голос сарказма."
        me "Может сразу стекло расхерачим и стены сломаем?"
    elif loki:
        me "Ты уверена? Может, не надо?"
        me  "Это ведь не хиханьки с обливаниями, может и по загривку прилететь."
    else:
        me "Не думаю, что это хорошая идея — вламываться в столовую."
        "Я постарался подпустить в голос как можно больше убедительности."
        me "Может, не стоит?"
    show dv smile pioneer at center with dspr
    dv "Что?"
    "Несмотря на злость, она, казалось, пребывала в некотором ступоре."
    dv "Сам жрать не хочешь, что ли? Ульянка тебя с ужином прокатила."
    me "Я-то голодный. Но вскрывать дверь… Может, не стоит? "
    dv "Ну и сиди дальше на диете."
    "Она фыркнула и вернулась к ковырянию замка английской булавкой. "
    "Я призадумался. Вроде бы ничего, кроме плохого, от этой девчонки я сегодня не видел."
    if alt_sp_wet:
        "Сначала она обливает."
    elif slavya_convoy or alt_day1_sl_conv:
        "С первых же минут знакомства смотрит на меня волком."
    elif loki or herc:
        "Сначала она пытается меня облить."
    window hide
    window show
    if run_el == 0:
        "Потом хочет дать по уху."
    else:
        "Потом шипит на меня."

    "А сейчас втягивает в сомнительное предприятие со взломом замка."
    "Можно же сейчас просто отойти в сторону подождать, пока придёт Славя, а уж Славя сто процентов вломит домушницу вожатой."
    "Или нет… "
    "Поняв, что совесть не позволяет мне быть безучастным свидетелем, и удивляясь самому себе, я подал голос:"
    me "Слушай."
    dv "Чего тебе?"
    me "Ты бы отошла. А то сюда Славя направлялась."
    show dv angry pioneer at center   with dspr
    dv "Что?! Что ж ты сразу не сказал! Я сваливаю!"
    "Она подскочила к перилам, как я после ужина, и уже готовилась прыгнуть через них." 
    dv "А на тебе уже второй должок!" 

    menu:
        "Помочь":
            $ lp_dv = lp_dv + 1
            $ karma = karma + 10
            $ dv_help = 1
            "Однако, я успел поймать её за плечо"
            if not alt_day1_sl_conv:
                extend ", в очередной раз подивившись тому, насколько же она сильная."
            else:
                extend ", между делом подивившись заигравшим под пальцами мышцам."
            me "Да погоди ты, торопыга."
            show dv rage pioneer at center   with dspr
            dv "Отпусти!"
            me "Да перестань ты орать!"
            "Зашипел я."
            me "Я сейчас всё равно есть пойду. Могу и тебе что-нибудь захватить. Булок там, ещё какой дряни. Будешь?"
            show dv shocked pioneer at center with dspr
            dv "И быть тебе обязанной?"
            show dv rage pioneer at center   with dspr
            dv "Я пас!"
            hide dv with dissolve
            "Алиса скрылась в темноте, а я пожал плечами."
            "Колхоз — дело добровольное. "

        "Не помогать":
            $ dv_help = 0
            window show
            "С этими словами Алиса скрылась в ночи."
            hide dv with dissolve
            me "Дело сделано."
            "Помогать ей я не хочу, но и подставлять не желаю. Пусть будет так."
    window hide
    with fade

    window show
    "И отступил назад — со стороны тропинки уже слышались шаги, там шла Славя. "
    "Она выглядела взволнованной."
    show sl normal pioneer at center   with dissolve

    stop ambience fadeout 2

    sl "Всё хорошо?"
    me "Да более чем."
    "Я немного напряжённо улыбнулся."
    me "Сейчас меня покормят, что может быть лучше?"
    show sl laugh pioneer with dissolve
    "Славя рассмеялась и, поискав на связке, обнаружила нужный ключ, после чего отворила дверь в столовую."
    play sound sfx_open_door_clubs fadein 0
    sl "Заходи."
    "Она, кажется, не обратила внимания на мои актёрские потуги. {w}Или вежливо притворилась, что всё хорошо."
    window hide

    $ persistent.sprite_time = "night"
    scene bg int_dining_hall_night 
    with dissolve

    play music music_list["a_promise_from_distant_days"] fadein 5

    window show
    "В столовой мы прошли до самой раздачи, и Славя, усадив меня, скрылась в недрах кухни."
    show sl normal pioneer at center   with dissolve
    sl "Подожди здесь, ладно? Я тебе сейчас что-нибудь принесу."
    me "Бу сделано."
    "Я откинулся на спинку и стал ждать свою спасительницу."
    hide sl  with dissolve
    window hide

    show cg d1_sl_dinner 
    with dissolve

    window show
    "Через пару минут она появилась с большим блюдом булочек и поставила его на стол."
    "Потом на столе оказалось несколько старинных кефирных пирамидок — сто лет таких не пробовал!"
    sl "Там ещё кастрюлька с бульоном, но она совсем остыла."
    me "А и чёрт с ним. И так поем."
    sl "Приятного аппетита."
    me "Спасибо. "
    "Не сказать, чтобы ужин отличался разнообразием. Хотя не мне с моими дошираками воротить нос. "
    "Пока я ел, Славя внимательно, изучающе разглядывала меня - так, что под конец ужина я даже почувствовал себя немного неловко."
    "Что вообще странно, этой девочке в считанные часы удалось подобрать ключик к моему сердцу, и я уже не считаю её посторонним прохожим."
    "Как ей это удалось? Может, дело в её отношении к окружающим?"
    me "У меня кефир на носу остался?"
    "Я потёр кончик носа, вызывая её смех. "

    show cg d1_sl_dinner_0  with dspr

    sl "Нет. Просто…"
    "Она улыбнулась."
    sl "Я хотела спросить, как тебе здесь, в лагере? Первый день."
    me "Спорно."
    "Не слишком красиво спрашивать у человека, которого за шкирку выдернули из родной реальности и переместили куда-то, где он даже не знает, как вести себя, как это ему нравится на новом месте…"
    "Но чёрт знает. Может, это я только здесь белая ворона, а для них этот и есть их родная реальность?"
    "Я немного покатал эту мысль и счёл её справедливой."
    "Почему бы и нет. Кто бы сюда меня ни переместил, ему гораздо проще использовать что-то существующее, чем создавать мир с нуля, верно?"
    sl "Ничего."
    "Она совершенно правильно расценила моё молчание."
    sl "Ещё привыкнешь."
    show cg d1_sl_dinner  with dspr

    "Славя мечтательно уставилась в окно, а я отрицательно покачал головой."
    th "Не думаю, что я когда-нибудь привыкну к этому месту. "
    th "Впрочем, ей-то и невдомёк…"
    th "Или она хочет, чтобы я думал, что ей невдомёк. {w}И дальше играем в игру — я знаю, что она знает, что я знаю, что она знает."
    me "Я раньше был в лагерях, так что есть с чем сравнивать."
    "Надо было как-то прерывать повисшее молчание."
    sl "Правда?"
    "Без особого интереса спросила она."
    me "Правда."
    "Я чуть было не выдал нечто в духе «девять лет жизни катался», но это не слишком удачно соотнеслось бы с тем, на сколько я выгляжу."
    "Она выглядела по-настоящему влюблённой в это место, и внутри шевельнулось что-то, не дающее остаться равнодушным или выдать нечто нейтральное."
    "Почему-то очень захотелось сделать или сказать что-нибудь, что заставило бы эту непосредственную девочку улыбнуться."
    me "И я хочу сказать, здесь очень…"
    "В конце концов, это для меня «совок», «ретро» и «село кукуево», а для них? {w}Может, это и есть их единственная жизнь."
    "Если, конечно, это понятие уместно…"
    sl "Как?"
    "Она отвернулась от окна и внимательно изучала меня. {w}Так, будто от моего ответа зависело что-то очень-очень важное. "
    me "Здесь мирно."
    "Улыбнулся я."
    me "Как будто оказался в том месте, какое всегда искал."
    "Я наконец успешно отбился от «кавайно», «няшно», «прикольно» и целой когорты других дурацких синонимов милоты."
    show cg d1_sl_dinner_0  with dspr
    sl "Пожалуй."
    "Славя облегчённо улыбнулась, будто я правильно ответил на важный вопрос."
    sl "Я очень рада, что ты так думаешь."
    me "Почему?"
    sl "Здесь почему-то не всем нравится.  Даже ты запаниковал… тогда, у вожатой."
    "Она замолчала, поняв, что сказала лишнего."
    "А я сидел с испариной, прокатившейся по спине, и мучительно старался поймать глоток воздуха."
    "Даже то, как меня встретила Двачевская, не шло ни в какое сравнение с тем, что произошло сейчас, когда совершенно незнакомая мне девочка признаётся в симпатии. "
    me "А тебе?"
    "Молчание стало нестерпимым, захотелось деть куда-то руки… Но чем есть тогда?"
    sl "Мне?"
    me "Нравится здесь?"
    sl "Я здесь отдыхаю."
    "Призналась она."
    me "Значит, не стоит обращать внимание на других. "
    sl "А я и не обращаю особо."
    "Славя рассмеялась."
    "Кажется этот разговор уводил меня далеко-далеко, совершенно не туда, куда я стремился. "
    sl "Ты волнуешься."
    "Это было сказано утвердительным тоном."
    me "Что? Почему?"
    sl "Ты жуёшь слишком сосредоточенно. "
    me "Прости…"
    sl "Ничего."
    "Никак не получалось воспринимать её как кого-то чужого."
    "Хотя бы чуть больше осторожности в расспросах и ответах. "
    "Я же язык жестов изучал, кое-что могу сказать по позе и осанке человека."
    "Но здесь и сейчас напротив меня сидел кто-то настолько искренний и открытый, что все попытки анализировать его заканчивались неиллюзорными угрызениями совести."
    "Как и большинство людей, которых я здесь встретил."
    th "Они честные и нормальные."
    th "Обычные люди в обычной обстановке с небольшой поправкой на седьмой-восьмой десяток двадцатого века. "
    "Все они были {b}нормальными{/b}, такими, какими и должны быть люди в моём понимании, со своими достоинствами и недостатками."
    "А Славя была ещё и…{w} милой?"
    "Я украдкой посмотрел на неё, не зная что сказать, а она перехватила взгляд."

    show cg d1_sl_dinner  with dspr

    sl "Извини, сегодня так забегалась, не успела тебе лагерь показать. "
    me "Ты же прислала Электроника. Он мне всё и показал."

    show cg d1_sl_dinner_0  with dspr

    sl "Прямо всё-всё?"
    "Она улыбнулась так, что я, смутившись, спрятал глаза."
    me "Да кто ж его знает. Я здесь первый день, может, что и пропустил. "
    sl "Хорошо, и что сегодня уже видел?"
    me "В порядке очереди?"
    sl "Да."
    me "Ладно."
    "Я почесал затылок."
    me "Сначала достопримечательность — Генда, потом Лена, за ней — столовая, злая Алиса и футбольное поле. "
    sl "А пляж?"
    if alt_day1_sl_conv:
        me "Да как-то не довелось."
    else:
        me "Я почти пришёл. Ты меня там перехватила, помнишь?"
        sl "Да…"
    "Она смущённо вцепилась в косу."
    sl "Но ты туда обязательно сходи. Или хочешь, давай, вместе сходим?"
    me "Ну… Ладно. Давай. "
    "Естественность и непринуждённость Слави плавно дожигали мои годовые запасы смущения и откровенно уже пугали, когда мне вдруг пришло в голову то, что сформировалось в процессе жевания."
    "Вдруг для них всё так и должно быть?"
    "Вдруг этот мир непонятен и пугающ только для меня, а для них он…{w} родной?"
    "Быть может, меня зашвырнуло в прошлое — хотя я не помню такого прошлого, да и Генды, если уж на то пошло. "
    me "А можно задать глупый вопрос?"
    sl "Нет."
    me "Ну пожа-а-алуйста."
    sl "Ладно. Какой у тебя вопрос?"
    me "Какой сейчас год?"
    sl "Вот уж действительно неумный вопрос. Неужели сам не знаешь?"
    me "Мне бы хотелось, чтобы это сказала ты… Если не слишком сложно."
    sl "Не сложно."
    window hide

    stop music fadeout 5

    $ persistent.sprite_time = "night"
    scene bg int_dining_hall_night 
    show sl smile pioneer at center 
    with dissolve

    window show
    "Она встала из-за стола."
    sl "Уже поздно. Сам дорогу до Ольги Дмитриевны найдёшь?"
    me "Конечно, найду. Но зачем мне к ней?"
    sl "Спать не собираешься?"
    me "Собираюсь, но…"
    show sl laugh pioneer at center   with dspr
    "Наверное, со стороны я выглядел действительно крайне глупо, потому что Славя рассмеялась."
    sl "Или забыл, куда тебя поселили?"
    me "Логично…"
    show sl smile pioneer at center   with dspr
    sl "Ладно, я побежала тогда.{w} Спокойной ночи!"
    hide sl with dissolve
    me "Сладких…"

    "Я начал собираться в домик, прихватил с собой остатки пиршества — несколько булочек и пирамидки с кефиром. Схомячу ближе к ночи."

    th "Странно, почему это Славя так внезапно…"
    "Свет фонаря с улицы отразился в чём-то металлическом, висящем на двери."
    th "Славина связка ключей! {w}Как это она такую вещь важную забыла?"
    "Я было собрался догонять спасительницу, но в какую сторону догонять, не догадался."
    "А стучаться во все домики подряд среди ночи показалось не самой разумной затеей. "
    window hide

    menu:
        "Взять ключи":
            $ karma = karma - 10
            window show
            $ keys_take = 1
            th "Ладно."
            "Я старательно закрыл дверь в столовую и спустил связку в карман."
            th "При возможности отдам, а пока пусть будут у меня для большего спокойствия."
            window hide
        "Не трогать":
            $ keys_take = 0
            window show
            th "Хотя, с другой стороны, зачем они мне…"
            window hide

    $ persistent.sprite_time = "night"
    scene bg ext_dining_hall_near_night 
    with dissolve

    play ambience ambience_camp_center_night fadein 5

    window show
    "Ночь была… Необычной. Нет, не так. Она была непривычной."
    "Мне, человеку, прожившему всю жизнь в северо-западном округе и всем другим видам отдыха предпочитавшему Карельский перешеек, были диковаты и необычайная темнота, и влажный воздух, и стрекот сверчков."
    "Северная летняя ночь куда тише. А эта меня пугает. "
    "Надо срочно последовать совету Слави и добраться до домика вожатой. "
    if dv_help == 1:
        play music music_list["orchid"]
        "Я проверил дверь в столовую ещё раз, и тут в кустах за спиной что-то зашуршало."    
        "Мне стало отчётливо не по себе. "    
        "Булочки поместились в кармане, а вот кефир пришлось отложить на стоящую тут же скамеечку."
        "Распределив добычу, я стал оглядываться в поисках подходящего оружия."
        with vpunch
        play sound sfx_scary_sting
        "И тут меня кто-то похлопал по плечу."
        "Это было довольно неожиданно, и, кажется, реакция моя была не самой вменяемой."
        me "Ааааа, машу вать, кто здесь! Не подходите, я буду драться!"
        "Я отскочил в сторону и резко обернулся. Там стояла…"
        stop music fadeout 1
        show dv laugh pioneer at center   with dspr
        me "Алиса? Ты меня угробить решила?! Тебе чего надо?"
        "Она чуть ли не в голос хохотала, довольная результатами своей проделки."
        dv "Это не я чего, это ты чего разорался? {w}Принёс еду?"
        me "Едуууу?"
        "Переспросил я, борясь с нарастающим желанием придушить мерзавку."
        me "Ты же сказала, что не хочешь быть в долгу!"
        show dv smile pioneer at center   with dspr
        dv "Я сказала — я передумала. Так принёс?"
        me "Две булочки с изюмом устроят?"
        show dv shy pioneer at center   with dspr
        dv "Ну ты даёшь. А где всё остальное?"
        me "СЪЕЛ!"
        me "Я не ужинал, помнишь?"
        dv "Ладно, что-нибудь ещё есть?"
        me "Полтора литра кефира хватит?"
        dv "О, давай."
        "Пирамидки мгновенно исчезли. "
        me "Могла бы и предупредить… Что всё-таки примешь предложение."
        "Я помассировал гудящие виски. От происходящего у меня жутко разболелась голова."
        me "И не подкрадываться так."
        show dv smile pioneer at center   with dspr
        dv "Ну извини, не знала что ты такой трус. А покормить меня была твоя идея. А принёс непонятно что."
        if keys_take == 1:
            $ lp_dv = lp_dv + 1        
            me "В общем, так."
            "Мне начала надоедать эта пикировка."
            me "Здесь ключи от столовой. Я открываю, ты берёшь сколько надо, и разбегаемся."
            dv "Не, хватит и того, что есть."
        me "Что-нибудь ещё? "
        dv "Нет, ничего…"
        show dv grin pioneer at center   with dspr
        dv "Иди спи. Спокойной ночи, трус. "
        hide dv with dissolve
        "Если Алисе в ту сторону, значит, мне в диаметрально противоположную."
        "Например, ещё раз посмотрю на Генду. Вот сколько ни смотрю, нигде звоночек не звонит. Борода, пижонские очки, пиджак. Чёрт знает."
    
    window hide

label day_1_lena:
    $ alt_chapter(1, u"Вечер")
    $ night_time()
    $ persistent.sprite_time = "night"
    scene bg ext_square_night with dissolve
    window show
    "Я уселся на лавочку и ещё раз прокрутил в голове всё, что произошло со мной сегодня, от высадки из автобуса до засады у столовой. "
    "И как ни старался я, как ни прикидывал — но не мог найти в действиях окружающих какого-либо злого умысла. "
    if not alt_day1_sl_conv:
        "Даже две рыжие бандитки действовали только ради собственного развлечения и не преследовали каких-либо злых целей."
    "Моя смерть, внутренние органы или электронный счёт никого не интересовали. "
    th "Автобус, лагерь, девочки…"
    "Почистив силиконовые амбушюры полой футболки, я уже совсем было собрался немного приобщить местный мир к последним достижениям электронной музыки, как вдруг моё внимание привлёк тихий шорох с соседней скамейки. "
    "Девочка. {w}Читает книгу."
    scene d1_un_book0 with dissolve
    play music music_list["lets_be_friends"] fadein 5
    th "Лена."
    "Я подумал, что сегодня она была единственной, с кем я не успел перекинуться и парой слов, и будет невежливо оставлять ситуацию неизменной."
    menu:
        "Подойти":
            scene d1_un_book with dissolve
            $ lp_un = lp_un + 1
            window hide
            window show
            "Я встал и подошёл к ней поближе."
            "Девочка продолжала читать, не обращая на меня особого внимания."
            me "Кхм."
            "Ноль внимания, фунт презрения."
            me "Лена, кажется?"
            "Молчание."
            th "Да она меня игнорирует, похоже."
            "Я шумно прочистил горло."
            me "Привет. Что читаешь?"
            scene bg ext_square_night
            show un surprise pioneer at center
            with dspr
            "От удивления и неожиданности она аж подпрыгнула. "
            me "Ох… Прости."
            "Я постарался успокаивающе улыбнуться и, похоже, преуспел."
            me "Я совсем не хотел тебя пугать."
            show un shy pioneer at center   with dspr
            un "Ничего…"
            "Даже в полутьме был заметен румянец, которым мгновенно покрылись её щёчки."
            me "Так что читаешь?"
            "Она подняла книгу с коленей и показала обложку."
            "На белом фоне алой вязью было написано «Унесённые ветром»."
            "Я улыбнулся."
            "Правильно, что ещё может читать девочка её возраста в пионерском лагере?"
            show un normal pioneer at center   with dspr
            "Не Ницше же."
            window hide
        "Не подходить":
            if persistent.un_7dl_good or persistent.un_7dl_bad:
                $ alt_day1_foreveralone_un = True
                th "И что я ей скажу?"
                "Я-таки дочистил наушники и сдобрил тёплый вечер ударными гитарными рифами."
                "Ништяк!"
                "Немного посидев, девочка встала и ушла."
                "Вот она, старая школа!"
                hide un with dissolve
                jump alt_day1_sleep
            else:
                th "Лучше не беспокоить её. {w}Пойду-ка я."
                "Я уже совсем было собрался тихонечко убраться, когда девочка подняла глаза от книги."
                th "Она меня заметила!"
                window hide
                scene bg ext_square_night
                show un normal pioneer far at center
                with dissolve
                window show
                "Ничего не оставалось, как подойти поздороваться."
                me "П-привет."
                "Она молча кивнула."
                me "Интересно?"
                "Пальцы, указывающие на книгу, почему-то мелко дрожали."
                "Не говоря ни слова, девочка подняла книгу так, чтобы я мог разглядеть в свете фонарей алый корешок с золотыми буквами."
                th "Митчелл. «Унесённые ветром»."
                "Прочитал я."
    menu:
        "Похвалить книгу":
            $ alt_un_nicebook = True
            $ lp_un = lp_un + 1
            $ karma = karma + 10
            window show
            me "У тебя очень хороший вкус."
            "Поколение интернетов знает понемногу всё и не знает ничего толком."
            "Но уж пару цитат выдать смогу."
            me "Скарлетт О-Хара. Я подумаю об этом завтра. "
            me "Люди больше волнуются лишь о тех, кто дарит им заботы."
            un "Человек не может двигаться вперёд, если его душу разъедает боль воспоминаний."
            "Подхватила она, и тут же, смутившись, опустила глаза."
            me "Замечательная книжка. "
            un "Да…"
            "Она захлопнула книгу и отложила в сторону."
            me "Уже уходишь?"
            un "Да… Пойду спать."
            menu:
                "Дать ей уйти.":
                    $ karma = karma + 10
                    me "Спокойной ночи."
                    un "Спокойной ночи."
                    "Она поднялась со скамейки, поколебалась с секунду, а потом развернулась и растаяла в ночи."
                    hide un with dissolve
                    th "Всё как обычно, вот что значит старая школа."
                    "Такие девушки как она, не интересуются такими парнями как я. Всё логично. Нас либо френдзонят, либо игнорят." 
                    "Спасибо, хоть поговорила со мной."
                    jump alt_day1_sleep
                "Задержать.":
                    $ alt_un_stop_her = True
                    $ lp_un = lp_un + 1
                    $ karma = karma - 10
                    me "Подожди!"
                    "Я отчаянно искал слова понимая, что она же сейчас уйдёт!"
                    "Девочка неуверенно замялась, но осталась на месте."
                    me "А кроме Митчелл, ты что ещё любишь читать?"
                    "Не придумал ничего умнее, но хоть что-то."
                    un "Ну… Я разные книги читаю."
                    "Она немного помолчала."
                    un "А ты?"
                    me "Я?"
                    "И правда, а что я?" 
                    "Рассказать ей о том, что последние пару лет читаю со смарта золотую коллекцию юмористического фэнтези нулевых в триста книжек?"
                    "Или вспомнить Бушкова, Головачёва, Булычёва — они, вроде, уже творили в это время? "
                    "А, была не была!"
                    me "Знаешь, как-то один жанр не выберу. Приключений, фантастика, прочая художественная литература. Имена назвать трудно."
                    un "Что-нибудь посоветуешь?"
                    me "Стругацких посоветую. Чем старше становлюсь, тем больше граней в их творчестве открываю. Рекомендую."
                    un "Фантастика?"
                    "Она замолчала."
                    show un sad pioneer at center   with dspr
                    un "Не люблю фантастику."
                    me "Не просто фантастика, а социально-философская фантастика!" 
                    me "Тех же «гадких лебедей» возьми — вот о чём там? "
                    me "О какой-то там школе и детях-индиго? О сверхспособностях? "
                    me "Нет, там — о людях! О том, как ведут себя люди, оказавшиеся в необычных ситуациях."
                    un "Никогда не слышала о такой книге."
                    me "Ну как же, она же в 67-м написана…"
                    show un normal pioneer at center   with dspr
                    "Лена пожала плечами, вопросительно глядя на меня."
                    "Так, надо срочно менять тему, пока меня не забрали на опыты как пришельца из будущего!"
                    me "Не суть. Там у них много чего интересного." 
                    jump un_stay
                "Сменить тему" if alt_day1_chase_uvao:
                    th "Я ж забыл, о чем вообще с ней собирался поговорить!"
                    me "Лена, я вот еще что спросить хотел…"
                    un "О чем?"
                    me "Генда этот, весь день на него смотрю… ты не знаешь кто это? Чем знаменит?"
                    $ alt_day1_genda_investigation = True
                    show un surprise pioneer at center
                    "На меня посмотрели странно. Как на ребенка, спросившего «Тетя, а что это за большая светящаяся штука в небесах?»."
                    "Повисла неловкая тишина"
                    un "Я, пожалуй, пойду. Поздно уже…"
                    hide un with dissolve
                    "Она поднялась со скамейки и ушла, украдкой на меня поглядывая."
                    th "Вот теперь я прочно занял роль местного психа."
                    "Вдруг позади меня хрустнула ветка. Я обернулся и, кажется, успел заметить чью-то тень!"
                    "Секунда - и как ничего не бывало."
                    th "Наверно, малышня…"
                    "Вздохнув, я поднялся и побрел в сторону домиков."
                    jump alt_day1_sleep
        "Спросить о книге.":
            me "Никогда не читал. Говорят, это дамское чтиво…{w=.7} Хоть я не слишком понимаю, что это может значить."
            me "Интересно?"
            un "Местами."
            if herc:
                $ karma = karma + 10
                $ lp_un = lp_un + 1
                me "Ты не очень-то разговорчивая. Мне это нравится."
                me "Не сочтёшь наглостью, если я спрошу…"
                me "Чем, помимо книг ты увлекаешься?"
                show un shy pioneer
                "Лена смутилась."
                un "Всяким… разным…"
                "Неохотно прошептала она."
                me "Разным… А это разное можно делать вдвоём?"
                un "Что?"
                "Она подняла на меня глаза, в которых застыл немой вопрос."
                me "Вдвоём… Ну… {w} Вместе!"
                me "Понимаешь?"
                un "Понимаю."
                "Прошептала она."
                th "Здесь и сейчас, мадемуазель, позвольте мне предложить вам мою дружбу, плечо и жилетку."
                th "Соглашайтесь, мадемуазель. И эта неуютная вселенная станет на одного счастливого человека светлее. "
                th "Простое «Да» устроит. Ведь счастье — это та же святость, ты находишься в гармонии с целым миром от того, что счастлив."
                th "Я не претендую на святость, я лишь хочу, чтобы вы стали моим другом. Хотя бы им для начала."
                th "А дальше… Кто знает."
                show un smile3 pioneer
                un "Рисовать."
                "Наконец нарушает молчание она."
                un "Я люблю рисовать. И можно делать это вместе."
                "Хотел бы я это увидеть. Впрочем…"
                jump ask_date_lena
            elif loki:
                $ lp_un = lp_un + 1
                me "Я не большой фанат чтения. Предпочитаю действие."
                me "Но если ты советуешь почитать книжку…"
                me "Я прислушаюсь."
                th "Когда тебе шестнадцать, слово «люблю» ещё кажется святым и незатасканным."
                th "И признание человеку в том, что он тебе нравится, частенько способно развернуть жизнь целиком."
                th "Сначала мы думаем о том, что это может значить."
                th "Потом мы думаем, почему можем кому-то нравиться. {w=.5}Хотя это-то самое простое — нравятся просто так. Нипочему."
                th "А потом происходит непоправимое, и человек занимает место в наших мыслях."
                th "Так что можешь считать меня последним подонком, но ты мне нравишься. И я не знаю, что с этим делать."
                th "Поэтому я закидываю взгляд в небо и приветливо щурясь ласковым звёздам говорю:"
                me "Знаешь… Ты мне нравишься…"
                me "У меня такое впервые… {w=.3} ты прости за некоторую корявость."
                show un shy pioneer
                un "…"
                th "Я просто призна{b}ю{/b}сь тебе в этом, и теперь это уже наша общая проблема."
                me "Я не тороплю тебя с выводами. Просто… Если вдруг тебе станет одиноко…"
                th "Молодец. Предложил девочке сесть с тобой рядом в столовой."
                th "Что дальше? Предложишь выпить воды из-под крана и покатаешь на метро?"
                un "Это так…"
                me "Понимаю."
                "Её слова, мнение вдруг стали необъяснимо важны и весомы. Мне хотелось услышать, что она скажет в ответ…"
                "И не важно, положительным он будет или отрицательным. Значение имеет лишь то, что общение продолжается."
                th "С момента встречи у ворот и до текущего момента. И дай бог, чтобы завтра, послезавтра и — сколько мы тут пробудем?"
                th "Я хочу, чтобы общение не заканчивалось. Редкие реплики как негранённые алмазы, румянец на щеках как свидетельство искренности."
                un "Я должна… Мне надо подумать."
                me "Только недолго. Ночи хватит?"
                "Она снова смолкает, и через некоторое время разворачивается в мою сторону."
                un "Я думаю, хватит."
                jump ask_date_lena
            else:
                $ lp_un = lp_un + 1
                me "Митчелл. Хм… Про…"
                "Я прочистил горло. Почему-то очень тяжело выговаривались самые простые слова. Мне, завзятому цинику, не подчинялся язык спросить, про любовь ли книжка."
                me "Про… гм… про войну, да?"
                un "Про людей."
                "И про то, как некоторые сами не знают чего хотят."
                "Что ж, во всяком случае, это достаточно честный ответ."
                "И всё-таки, о чём можно заговорить с девушкой, которую видишь второй раз в жизни, а впервые увидел за несколько часов до того?"
                "Я знаю больше двух тысяч глупых шуток, анекдотов и дежурных фраз. Я могу издеваться над человеком часами и не разу не повториться. "
                "Но стоит лишь зайти речи о том, что касается меня лично, каких-то отношений — и я прячусь в свою скорлупку, где уже ждут своей очереди бутылка водки и пистолет."
                "И я не виню тебя ни в чём, и не буду винить, если ты откажешься. Я представил себя на твоём месте: кто-то незнакомый вломился в дом и претендует на мою благосклонность и внимание."
                jump ask_date_lena

label ask_date_lena:
    "Она уже готовилась уходить, когда я остановил её вопросом."
    me "Лена, можно тебя кое о чём попросить?"
    show un normal pioneer
    un "О чём?"
    menu:
        "Пригласить куда-нибудь":
            $ date_un = 1
            $ lp_un = lp_un + 1
            me "Я тут новенький. Ничего не знаю."
            un "…"
            "Осталось только добавить «Воооот» — и можно спиртовать уродца в одной кунсткамере с Электроником."
            me "Мне бы хотелось изучить лагерь получше. Понять, куда стоит ходить. Чем заниматься."
            un "…"
            me "Если тебе не очень трудно… Не могла бы ты…"
            "Я делаю глубокий вдох как перед нырком в глубокую холодную воду."
            me "Составить мне компанию."
            un "Ты хочешь, чтобы я показала тебе лагерь?"
            show un shy pioneer at center   with dspr
            me "Ну… да!"
            "Не говорить же ей о том, что мне просто хочется, чтобы компанию мне составила именно она."
            un "Н-ну… Я не знаю…"
            me "Пожалуйста. Тебе же не слишком сложно?"
            "Тормоза на мгновение ослабли от пьяного ночного воздуха, и я поспешил вколотить первый гвоздь в крышку гроба."
            me "Просто мне хочется провести с тобой немного времени. Пообщаться, понимаешь?"
            show un smile pioneer
            "Она опять покраснела, но в этот раз выглядит как-то… Иначе?"
            th "Если верить языку жестов, она совсем не против моей компании."
            me "Так что скажешь?"
            un "Я не…"
            me "Подожди! Не спеши отказывать! Прошу."
            me "Подумай хотя бы до завтра, и тогда скажешь мне решение. Ладно?"
            "Она пожала плечами и закрыла книжку."
            hide un with dissolve
        "Свести всё к шутке":
            me "Да нет, ни о чём особенном."
            me "Считай это шуткой."
            un "Да?"
            "Она как-то угасла."
            un "Хорошо."
            "Идиот. {w}Считать шуткой следует всю мою жизнь. {w=.3}С момента зачатия. Ну нельзя же быть настолько феерически бестолковым!"
            hide un with dissolve
label un_stay:
    un "Ладно…"
    show un grin pioneer at center   with dspr
    un "Я пойду, хорошо? "
    me "Да."
    "Машинально посмотрев время на экране смарта, отреагировал я."
    me "Поздновато…"
    "Она пристально посмотрела на телефон в моей руке, что-то хотела спросить, но передумала."
    un "Спокойной ночи."
    me "И тебе — сладких снов. "
    hide un  with dissolve
    "Она скрылась в темноте, а я ещё долго сидел и задумчиво смотрел в направлении, куда она ушла."
    th "Вроде бы, ничего особенного, типичный образ самой обыкновенной застенчивой пионерки. Но…"
    "Но… Лена прочно заняла место в списке козырей, которыми играл лагерь против моей старой жизни."
    hide un with dissolve

label alt_day1_sleep:
    stop music fadeout 5
    "…"
    "Ночь была тихой, пионеры, похоже, разошлись, и в темноте были лишь видны светлые квадраты окон их корпусов."
    stop ambience fadeout 2
    "А сверху на всё это улыбались тёплые звёзды. Я добрёл до знакомого домика и постучался в дверь."
    play sound sfx_knock_door2
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_house_of_mt_night
    with dissolve
    window show
    mt "Открыто!"
    window hide
    play sound sfx_open_dooor_campus_1
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night
    show mt smile pioneer far
    with fade
    $ renpy.pause (1)
    play ambience ambience_int_cabin_night fadein 1
    window show
    "И я вошёл."
    "Ольга Дмитриевна приветливо улыбнулась мне."
    "Она сидела на кровати справа и бегло записывала какие-то мысли в солидного вида тетрадь в кожаном переплёте."
    "Не иначе, про Семёна, который гуляет допоздна и заставляет нянчиться с ним лучшую пионерку отряда!"
    "Я уже начал было что-то говорить, но она жестом оборвала меня и указала на соседнюю кровать, где лежала скатка и постельное бельё."

    show mt normal pioneer with dissolve
    mt "Садись."
    "Она продолжала писать."
    mt "У тебя, конечно, целая куча вопросов. Но давай мы оставим увлекательные викторины на завтра."
    me "Но как же…"
    mt "Успокойся."
    " Прервала меня она."
    mt "Сейчас это не важно."
    "Она задумчиво посмотрела на меня."
    mt "Вещей, вижу, у тебя нет."
    mt "Не страшно, подберём что-нибудь."
    mt "Форму тебе Славя завтра тоже даст, не волнуйся. "
    me "Но я не…"
    "Мои робкие протесты вынудили её отложить ручку и строго посмотреть мне в глаза."
    mt " А мы все здесь не…"
    mt "Спать будешь там, где сидишь, можешь начинать заправляться."
    me "Что?!"
    "Я растерялся."
    th "Да, помнится, Славя говорила что-то в этом духе, но я не обратил внимания."
    th "Потому что ну не может же вожатая и правда жить с пионером!"
    mt "Ты не маленький, стесняться меня нечего."
    "Глаза у неё были зелёные, бесовские, и какая-то сжавшаяся в моём затылке пружина понемногу распускалась, уже не норовя забрызгать окружающих содержимым моей черепной коробки."
    "Мало-помалу я начал клевать носом, а вожатая улыбнулась."
    show mt smile pioneer with dspr
    mt "Вот и славно."
    play sound sfx_click_2

    scene bg int_house_of_mt_night2 with dissolve
    window show
    "И уже через три минуты я блаженно вытянулся на прохладных простынях."
    mt "Спокойной ночи."
    "Я окончательно провалился в сон."
    window hide

    scene bg black with fade2
    stop music fadeout 2
    $ renpy.pause (2)
    jump alt_day_2_start