# Эвент c танцами на крыше
# Общий для трех веток - от озера, от телепорта, и от пляжа, после того как сдали Славю и Лену в медпункт
# Флаги: на хентай - alt_uvao_D5_hentai, на вход от озера - alt_uvao_D6_lake, на телепорт - alt_uvao_d6_teleport
label alt_uvao_d6_roof_dance:
    scene bg ext_dining_hall_near_night
    
    "Юля подвела меня к внутреннему углу дворика."
    uv "Вот! Здесь можно залезть."
    me "Здесь?! Да тут же отвесная стена! Как я тут залезу?"
    uv "Глупый. Смотри, я научу!"
    "Юля как-то уцепилась пальцами за кирпичи в стенах, и, упираясь то в одну стену, то в другую, стала ловко взбираться наверх. Вскоре сверху послышался голос:"
    uv "Понял? Вот так надо. Попробуй!"
    if alt_uvao_D5_hentai: #(Если хентай = от озера или телепорта)
        "Ох, сомнительно мне что-то."
        "Я не питал иллюзий относительно собственной ловкости и силы. Десяток подтягиваний - это мой предел!"
        "Тем не менее, я постарался повторить этот акробатический этюд. Одно хорошо - я вряд ли поднимусь на заметную высоту, прежде чем рухну вниз. Ну что ж, попробуем! Раз, два, и трииии…"
        window hide
        $ renpy.pause(3.0)
        window show
        "Все оказалось не настолько сложным. Тело будто действовало само!"
        "Я легко переносил вес тела то на одну, то на другую сторону,  будто прирожденный паркурщик, и вскоре моему взору открылась терра инкогнита столовской крыши."
        scene bg ext_roof
        show uv smile at left
        uv "Молодец! Я же говорила, это легко!"
        "Юля сидела на краю, свесив ноги."
        "Цепляясь за что попало, я перевалился через край крыши и распластался на рубероиде."
    else: #(Если без хентая = от пляжа, или тоже от озера)
        me "У меня когтей нету, чтоб как ты лазить! Сейчас…"
        "Я прошелся вдоль стены туда-сюда, в поисках какого-то способа… О, водосточная труба! Хлипковата, правда, но, надеюсь, скобы выдержат…"
        window hide
        #TODO: Топот по железным ступенькам
        $ renpy.pause(3.0)
        window show
        scene bg ext_roof
        show uv normal close at left
        "Пара минут напряженного сопения, и пальцы заскребли по кровле. Юля уцепилась в мою руку и стала тянуть, упираясь пятками в желоб."
        if not alt_uvao_d6_teleport: 
            dreamgirl "Тянем-потянем, вытащить не можем… Зато можешь ставить галочку - лазил к девушке по водосточной трубе. Чек'д."

    "Расслабленно полежать на крыше мне не дали."
    show uv smile at left
    uv "Вставай, не лежи! Показывай, как танцевать!"
    th "Это что, мне надо будет сейчас, перед ней, демонстрировать хореографические умения? Да еще на трезвую голову?!"
    th "Лучше уж съешьте меня сразу, без позора… "
    if not alt_uvao_d6_teleport: 
        dreamgirl "Да не тупи ты. Никто не ждет от тебя выступления на золотую медаль, ей и сравнивать-то не с чем."
        "Все равно, во всем теле ощущалась стеснительная деревянность. Просто-таки излучая неловкость, я сначала долго выбирал кусок крыши поровнее, потом отгребал в стороны ветки и листья… "
    else:
        "Чтобы немного оттянуть неизбежное, я сначала долго выбирал кусок крыши поровнее, потом отгребал в стороны ветки и листья… "
    "Юля терпеливо ждала, когда ж я перестану заниматься ерундой и наконец посвящу ее в таинство искусства танца. Поводов потянуть время больше не оставалось."
    me "Ну, значит, вот… Танцевать - это значит двигаться под музыку… Под медленную музыку обычно танцуют вдвоем…"
    if not alt_uvao_d6_teleport: 
        dreamgirl "Ты еще лекцию о роли танца в истории человечества прочитай, олух! Меньше слов, больше движений, давай-давай! Это энергичный танец!"
    else:
        th "Что ж я несу-то…"
    "Я покосился на площадь - там уже кто-то взял на себя роль главного зажигалы, выплясывая что-то невообразимое в середине танцпола, а остальные пионеры постепенно присоединялись к топтанию - и попытался спародировать пару движений."
    me "Ну, в общем, это делается как-то… так."
    #TODO: типа-анимацию спрайта? Качание туда-сюда, например
    "Юля немедленно повторила."
    me "Молодец. Потом вот так."
    "Снова повторила. И снова в точности - ловкости и внимательности ей было не занимать."
    if not alt_uvao_d6_teleport: 
        dreamgirl "Эй, подруга, посмотри на меня, делай как я, делай-делай как я!"
    "Мало-помалу я и сам начал получать удовольствие от наших танцев. Мы выписывали все более сложные па, изобретали свои, доселе никому не ведомые движения - кто б увидел, помер со смеху, но нам было весело."
    "Юля оказалась великолепной партнершей, ловила все на лету и даже начала сама что-то придумывать - я едва успевал за ее прыжками! Явно врожденный талант."
    uv "А как под медленную музыку танцуют? По-другому? Покажи!"
    "Спросила она в промежутке между композициями."
    me "Ладно. Как будет медленная музыка - покажу."
    if not alt_uvao_D5_hentai:
        "Дожить бы ещё до этого момента - она меня совсем загнала!"
        if alt_uvao_D6_lake:
            dreamgirl "Не жалуйся, а пользуйся моментом исправить вчерашний фейл. Сначала танцы на крыше, потом прогулки при луне… и тебя снова пригласят в бункер под бочок."
            "Снова ты о своём…"
            dreamgirl "Не, серьёзно, если б ты её на озере не поцеловал - был бы безнадежным тормозом. А так - не совсем безнадёжный."
        else: #От пляжа
            "После такого дня вообще надо не по крышам лазить, а лежать без сознания!"
            dreamgirl "Так сходи к медсестре за таблетками, будешь лежать и пускать слюни в компании двух прелестных девушек." 
            dreamgirl "Себя-то не обманывай - вся эта утомительная котовасия тебе почему-то пришлась по душе, а ворчишь ты только по привычке. Инерция мышления."

    "Музыка стихла, и с площади донеслось:"
    mi "А теперь, девочки, наконец-то настал наш черёд! Приглашайте мальчиков, и учтите, мальчики, отказываться нельзя! Ой, я опять слишком много болтаю, всё-всё, запускаю музыку!"
    # TODO: "Кошку и дракона", только приглушить звук немного
    show uv laugh
    uv "Медленная! Показывай, как вдвоём танцуют!"
    "Я рассмеялся и с шутовским поклоном подхватил протянутую руку."
    th "Обещал - значит, придётся показывать."
    "Рука занимает свое место на талии удивлённой партнёрши, первый шаг с левой - вальсируем!"
    th "Надо же, ещё помню, как это делается."
    scene cg d6_uv_roofdance
    "Вряд ли я смог бы повторить это на людной площади, но у нас много свободного места, а посторонних взглядов нет совсем."
    "После первых скомканных движений общий ритм  наконец-то найден, и мы плавно и размеренно движемся как единое целое. Юля, видимо, напрыгавшись, с легким вздохом положила голову мне на плечо, и так и замерла." 
    "Пушистое ушко мягко уткнулось мне в щёку, разгоняя волны приятной щекотки, и тепло прижавшегося тела вызывало чувство… не знаю, счастья?"
    "Может быть. Уже невообразимо давно со мной не приключалось ничего столь восхитительно милого и в то же время захватывающего, как Юля."
    "Музыка стихла, и мы остановились вместе с ней."
    uv "Танцевать - здорово!"
    "Шепнула Юля мне на ухо, не торопясь разрывать объятия."
    "Я же мог только кивнуть - голова слегка кружилась, то ли от танцев и от близости юной девушки, то ли от всех сегодняшних переживаний. Вздохнув, я шагнул назад…"
    scene bg ext_roof
    show uv smile close at cleft
    "Непонятно откуда взявшаяся бутылка выскользнула из-под ноги и, подпрыгивая по прошлогодней хвое, перекатилась за край крыши."
    #TODO: Дзынь! 
    "Внизу послышались торопливые грузные шаги."
    ba "А ну стоять! Кто там лазит?"
    show uv surprise close
    "Саныч! Мы тут же присели и притаились, едва дыша. Юля смешно прижала уши, пытаясь выглянуть из-за края крыши, мне даже пришлось потянуть ее обратно."
    "А физрук внизу продолжал топать и грозить всеми мыслимыми карами гипотетическим пионерам. На что он рассчитывает, что у нас совесть проснется и мы вылезем с повинным видом?"
    ba "…утром узнаю кто - простой уборкой не отделаетесь! Я вам… ох… ж… блинчатый кар-р-рась!"
    "Поток ругательств резко оборвался, и медведь, судя по звукам, похромал в сторону скамейки."
    if not (alt_uvao_D6_lake or alt_uvao_d6_teleport):  #(Если от пляжа)
        #ОД беседует с Виолой, Славя отдыхает - некому дотащить до медпункта
        ba "Чтоб вас… только-только новые кеды купил… гадский носор-р-рог…"
        "Посидев немного, он с трудом поднялся и медленно, стараясь не напрягать раненую ногу, похромал куда-то в сторону своей вотчины."
        window hide
        $ renpy.pause(2.0)
        window show
        show uv smile close
        "Всё стихло, и мы вылезли из укрытия, переводя дух."
        "С площади донеслось объявление о том, что этот танец - последний."
        me "Юль, давай спускаться. Сейчас все начнут расходиться, нас кто-нибудь заметит."
        show uv upset close
        uv "Уже всё? Жалко…"
        "Она явно была расстроена."
        show uv normal
        me "Увы, танцы заканчиваются. Пора."
        hide uv with easeoutbottom
        "Вздохнув, но смирившись с неизбежным, она подошла к краю крыши… и просто спрыгнула на землю! Во даёт!"

    else: #(От озера или телепорта)
        #ОД помогает довести Саныча
        mt "Борис? Что случилось?"
        ba "Да эти… чтоб их… кто-то стекло разбил, а я пошел посмотреть и напоролся. Новые кеды, блин!"
        "Голос Ольги Дмитриевны стал более обеспокоенным."
        mt "Ой, да тут не о кедах думать надо. Виола! Виола! Иди скорее сюда!"
        cs "Что за спешка… {i}комсомольцы{/i}? Опять без трудностей не можете?"
        mt "Снова ты со своими шуточками! Смотри, как Борис себе ногу рассадил!"
        cs "И вправду. Давай-ка его в медпункт… Под обе руки - взяли! Гордись, Бориска, когда еще доведется сразу с двумя дамами пообниматься…"
        "Постепенно голоса удалились, и мы вылезли из укрытия, переводя дух."
        show uv smile close
        uv "Давай дальше танцевать!"
        "Немедленно предложила Юля."
        me "Только давай больше ничего с крыши не сбрасывать."
        "И мы танцевали до самого закрытия дискотеки. А когда стихла музыка, мы долго сидели на краю крыши и смотрели на звезды."
        "Наверное, полагалось бы целоваться, или даже искать укромное местечко для двоих, но…"

    return
