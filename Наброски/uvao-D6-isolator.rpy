###############
# Д6 Беспалевная ветка
# Разговор в домике и заключение в изоляторе. 
# TODO: спрайты, звуки, музыка.
#
# Используется флаг alt_uvao_D4_viola_morning
# Используется флаг alt_day2_sl_keys_kept
# Используется флаг alt_day1_sl_keys_took
# Используется флаг alt_uvao_D5_evening_sl
# Используется флаг alt_day2_us_escape
# Используется флаг alt_uvao_D6_trust_uv
# Используется флаг alt_uvao_D6_ask_uv_keys
# Устанавливает флаг alt_uvao_D6_trust_uv
# Устанавливает флаг alt_uvao_D6_ask_uv_keys
#
label alt_day6_uvao_isolator_house:
    window hide
    scene bg int_house_of_mt_sunset
    play ambience ambience_int_cabin_day fadein 4
    play music music_list["what_do_you_think_of_me"] fadein 4
    window show
    "Я зашёл в наш с вожатой домик."
    me "Вы меня искали, Ольга Дмитриевна?"
    "Вожатая лежала на кровати с книгой в руках. Интересно, что читают вожатые? Не любовные же романы? Хотя с нашей станется."
    show mt normal pioneer with dissolve
    mt "Да, Семён. У меня к тебе серьёзный разговор."
    "Сказала Ольга Дмитриевна, откладывая книгу в сторону. Я присмотрелся к обложке. Так я и думал. \"Унесённые ветром\", Маргарет Митчелл. После дамского чтива серьёзные разговоры самое то."
    mt "Семён."
    "Мягко начала вожатая, принимая сидячее положение."
    mt "Я ведь всё равно узнаю, где ты был вчера. Лучше расскажи всё сам. Как говорится, признание своей вины смягчает наказание."
    me "Опять начинается?"
    "Возмутился я. Впрочем не очень сильно."
    show mt angry pioneer with dspr
    mt "Да, Семён, да. Начинается. Это моя обязанность - знать всё обо всех."
    show mt normal pioneer with dspr
    mt "А также моя обязанность заботиться обо всех. Вот ты отсутствовал половину дня."
    mt "Усталый, голодный. А потом твои родители с меня спрашивать будут ведь, почему их мальчик так исхудал."
    me "Да не был я голодный."
    "Возмутился я."
    me "Мы с Юлей булочками перекусили…"
    show mt surprise pioneer with dspr
    "И тут же зажал себе рот рукой. Блин! Что же я наделал?!"
    mt "Это кто такая?"
    "Нахмурилась вожатая."
    me "Девочка одна знакомая."
    "Вяло попытался отбрыкиваться я. Но вожатую было уже не остановить."
    show mt angry pioneer with dspr
    mt "Семён, не ври мне хоть в этот раз. Я знаю всех детей в этом лагере. Никакой \"Юли\" среди них нет."
    th "Что же делать? Эх, была – не была. Да и что мне теперь уже остаётся, загнанному в угол?"
    if not alt_uvao_D4_viola_morning: #в 4 дне проговорились ОД
        me "Помните, я вам рассказывал про своё видение - девочка с кошачьими ушами и хвостом?{w} Так вот она на самом деле существует. Да-да!"
    else: # в 4 дне с утра беседовали с Виолой
        me "Я про легенду лагерную - девочку-кошку. Мне Виола рассказала тогда, и я её встретил!"
    me "Её зовут Юля. Она живёт в катакомбах под старым корпусом, ловит мышей и делает запасы грибов на зиму.{w} Вчера я ходил к ней в гости провёл там всю первую половину дня."
    show mt surprise pioneer with dspr
    "Видимо это невероятное объяснение произвело впечатление на вожатую, потому что глаза её округлялись всё больше и больше с каждой моей фразой. А в конце этой пространной речи она смотрела на меня уже с неподдельным испугом. Более того – я мог бы поклясться, что вожатая была в ужасе!"
    show mt angry pioneer with dspr
    mt "Семён! Ты, с этой…{w} кошкой!{w} Никогда не смей больше подходить к ней! Она опасна!"
    mt "Ты с ней контактировал! Ты мог пострадать!"
    "Кажется она немного успокоилась."
    mt "Сейчас мы немедленно пойдём к доктору Виоле. Тебя надо обследовать."
    show mt normal pioneer far with dissolve
    "И она вскочила с кровати, направившись в сторону двери."
    me "Да со мной всё нормально!"
    "Но к сожалению, слова мои не возымели никакого эффекта. Пришлось встать и пойти вслед за вожатой."
    window hide
    scene bg ext_house_of_mt_day
    show mt normal pioneer at cright
    with dissolve
    window show
    "Вожатая буквально проволокла меня по лагерю, от домика "
    window hide
    scene bg ext_square_day
    show mt normal pioneer at cright
    with dissolve
    window show
    extend "через площадь "
    window hide
    scene bg ext_aidpost_day
    show mt normal pioneer at cright
    with dissolve
    window show
    extend "к медпункту. Все мои попытки объяснить, что поводов для паники и спешки нет, были мастерски проигнорированы."
    window hide
    play music music_list["eternal_longing"] fadein 5
    play sound sfx_open_door_1
    scene bg int_aidpost_day
    show cs normal glasses_thru at cleft #очки на глазах
    show mt normal pioneer at cright #без панамы, ибо неслась от домика. Может заодно shocked?
    show el surprise pioneer far at fleft behind cs
    with dissolve
    window show
    "В строение с красным крестом мы вломились, не утруждая себя буржуазными пережитками в виде стука в дверь."
    cs "Вообще-то неплохо бы стучаться."
    "Сказала Виола, не оборачиваясь. Лежащий на кушетке Электроник попытался смущенно натянуть рубашку…"
    # Кого обрабатывать Виолой будем - Шурика или Электроника после его очередного общения с Женей? У Электроника есть голый спрайт, наденем ему одни штаны для приличия... --перегаррет
    cs "Лежи, лежи… {i}пионер.{/i} Я ещё не закончила обработку твоих… ран."
    mt "Виола, у нас ЧП."
    cs "Пять минут подождёт?"
    mt "Нет."
    cs "Ладно, пионер. Свободен. Впредь будь осторожнее."
    hide el with dissolve
    "Выпущенный на свободу Сыроежкин не заставил просить себя дважды - оделся в две секунды и испарился. В другое время я б полюбопытствовал, где это он умудрился так интересно пораниться, но сейчас…"
    cs "Ну и с чего вдруг такая спешка? Что случилось?"
    mt "Кошка наша случилась. Помнишь, Семён вчера с утра куда-то пропал? А он, оказывается, все утро с ней по старым шахтам бегал!"
    show cs normal glasses with dspr
    "Виола пристально посмотрела на меня, покусывая дужку очков."
    cs "Ну, пионер… Это правда?"
    th "Может, соврать? Скажу, что все придумал, чтобы вожатую позлить… Да какого черта, вообще?!"
    me "Да, правда!"
    "Виола неопределённо хмыкнула, Ольга же снова напустилась на меня."
    show mt angry pioneer at cright with dspr
    mt "Вот! Слышала?! И что теперь с ним делать? Это же… это… просто катастрофа!"
    cs "Пока ещё ничего страшного не произошло, так что не сгущай краски, Ольга."
    mt "Не сгущай?! У меня тут шестьдесят человек детей под опекой, ты понимаешь, чем это все грозит?!"
    th "Это она к чему вообще?…"
    cs "Понимаю, понимаю. Ну и что ты предлагаешь делать? Звонить и срочно всех эвакуировать? Пока повода нет."
    mt "Когда повод будет - будет уже поздно!"
    th "Какая ещё эвакуация?!"
    cs "И тем не менее, панику разводить рано. Ты ведь понимаешь, общая эвакуация - это необратимо? Объект будет загажен с концами, только закрывать."
    mt "А что делать-то?!"
    cs "Снимать штаны и бегать!{w} У нас всего два дня осталось. Закроем смену как обычно, к следующему периоду все устаканится."
    mt "А с ним-то что предлагаешь делать?"
    cs "Да ничего. Выпороть его, конечно, не мешало бы, но это не наш метод.{w} Посидит тут, завтра вместе со всеми сядет в автобус и прости-прощай."
    me "Да вы… это же… Вы не имеете права!"
    "От подобной перспективы я даже стал заикаться, но мои возмущённые тирады не произвели ровно никакого впечатления."
    cs "Помолчи, пионер. Ты даже не понимаешь, что происходит, и чем чреваты твои эскапады с аномалией."
    me "С кем? Это с Юлей-то?!"
    mt "Это он её так окрестил. Юля её зовут, говорит…"
    me "Не я окрестил, а она сама так представилась!"
    cs "Даже так? Выбрала она себе имя, ничего не скажешь…"
    cs "Впрочем, сейчас мы не об этом.{w} Семён, здесь у нас кое-что посерьёзнее обычного пионерского лагеря, как ты понял. Весьма важный научный проект, мирового уровня."
    cs "И мне совершенно не улыбается все закрывать по вине бестолкового попаданца.{w} Посидишь в изоляторе сегодня-завтра, ничего с тобой не случится."
    if alt_uvao_D4_viola_morning:
        cs "Надо было тебе, пионер, сразу мне все рассказать. Сам виноват."
    me "Эээ… Попаданца?"
    cs "Ну да. Жаргонное название для пришельцев из параллельного мира."
    me "Так что же получается… "
    extend "ВЫ ВСЁ ЗНАЛИ?! " with vpunch
    extend "С самого начала знали, что я никакой не пионер?!"
    cs "Не кричи, пионер. Да, мы двое знали. И делали вид, чтобы избежать лишних вопросов, и ещё по некоторым причинам."
    me "И по каким же?"
    cs "Вечером расскажу. Если хорошо себя вести будешь."
    "Она отперла дверь изолятора и приглашающе взмахнула рукой."
    dreamgirl "Ваш номер, сэр! Не угодно ли чаю? Кофе? Девочек?"
    cs "К сожалению, придётся тебе тут поскучать. Вроде там какие-то журналы оставались, Трофимов не забрал. А обед тебе кто-нибудь принесёт."
    window hide
    hide cs
    hide mt
    with dissolve
    play sound sfx_open_door_kick 
    with fade
    play music music_list["trapped_in_dreams"] fadein 4
    window show
    "Дверь захлопнулась, щелчок замка утвердил мой новый статус арестанта. Из-за двери донеслось - "
    mt "Виола, дай чего-нибудь, а то меня аж трясёт. А мне ещё с концертом этим…"
    cs "Валерьянки? Сейчас…"
    "Что-то стукнуло, звякнуло, булькнуло…"
    mt "Что ж за смена-то такая… Все она, чтоб её… Мало того, что Шурик едва жив остался, так ещё и это…{w} Виола, ты уверена, что до эффекта не дойдёт?"
    cs "Уверена. Может он вообще - {i}того{/i}? Пока мы ходим…" # намекает, что Семен котопультируется оттуда.
    th "Он? {i}Того{/i}? О чем это они?"
    "Я повалился на кровать. Простыни источали крепкий больничный запах - стерильность, стерильность и ещё раз стерильность."
    dreamgirl "Ну что, заловили нас с тобой, волк{b}и{/b} позорные! Ты из блатняка что-нибудь знаешь? \"Владимирский централ\" смогёшь?{w} Если будем достаточно громко завывать, кто-нибудь удивится, что тут происходит."
    th "Или Виола вколет какого-нибудь успокоительного. Карательная психиатрия, знаешь ли…{w} Хотя может хоть они меня от тебя избавят…"
    dreamgirl "О, как высоко меня оценили! Это льстит."
    "Повалявшись немного и погрузившись в бездны отчаянья, я взялся за журналы - \"Юные Техники\" за январь и февраль 85-го года. К сожалению, повесть Кира Булычева обрывалась на самом интересном месте, а все остальное мне было малоинтересно."
    "Делать было абсолютно нечего. От скуки я начал обдумывать планы побега, но, как оказалось, дверь была довольно прочной, а окна были забраны решётками."
    if (alt_day2_sl_keys_kept or alt_day1_sl_keys_took == 1):
        "Даже всесильный артефакт, открывающий любые двери в лагере, не мог помочь - замочной скважины с моей стороны не было."
    th "Это же пионерлагерь, а не СИЗО, не колония малолетних! Зачем здесь решётки-то на окнах?!"
    dreamgirl "По-видимому, специально для таких вот - как она назвала? - попаданцев. Расслабьтесь, граф, ещё не время копать подкоп ложкой."
    play sound sfx_knocking_door_outside
    "Моё заточение нарушил сначала щелчок замка внешней двери, а затем стук в мою дверь."
    me "Да-да! Входите пожалуйста, прошу вас!"
    "Ответил я, даже не пытаясь скрыть сарказм."
    play sound sfx_open_door_1
    show sl normal pioneer with dissolve
    sl "Привет, Семён. Как ты тут?"
    me "Замечательно! Веселюсь от души."
    "Яд моих слов можно было сцеживать в мензурку. Впрочем, Славя никак не отреагировала."
    sl "А я тебе покушать принесла. Обед сегодня очень вкусный!"
    "И вправду, в руках она держала авоську с металлическими посудинами. Из-под крышек просачивался умопомрачительный запах. Я внезапно ощутил, насколько же я проголодался!"
    play sound sfx_stomach_growl
    me "О! Это очень кстати!"
    "Я забрал у неё судки и расставил на столе. Солянка! Густая, нажористая, как в лучших домах! И макароны с котлетой."
    dreamgirl "Одно хорошо, кормят на здешней зоне от пуза!"
    "Я не нашёл, чем возразить, и зачерпнул первую ложку, самую вкусную, самую полную. Славя присела на кушетку возле двери, пристально глядя на меня."
    dreamgirl "Смотри, а выход все же пасёт. Может, её - того? И на волю?" # "Того" - у каждого человека разное. Лучше уточнить, кмк -R
    "Я чуть не подавился."
    th "ТОГО?!" with hpunch
    dreamgirl "Ну вон, под кроватью железное судно. Увесистая вещь, должно быть. Такой по башке огреть…"
    th "…"
    th "Ты что несёшь, идиота кусок?! На мокруху меня подбива… тьфу."
    stop music fadeout 4
    "Ощущение, проявившееся у меня в голове, больше всего походило на громогласное лошадиное ржание. Зарёкшись ещё хоть как-то реагировать на выходки внутреннего придурка, я проглотил все, что у меня было во рту и повернулся к Славе."
    me "Очень вкусно! Спасибо тебе."
    show sl smile pioneer with dspr
    sl "На здоровье!{w} Послушай, Семён…{w} А хочешь, я тебя выпущу?"
    play music music_list["you_lost_me"] fadein 4
    me "Что?"
    sl "Хочешь, я тебя отсюда выпущу?"
    " - повторила она, как для идиота."
    th "Славя просто так возьмёт - и нарушит порядок? Невероятно!"
    if alt_uvao_D5_evening_sl:
        extend " Вчерашние странности продолжаются…"
    me "В чем подвох?{w} Это на тебя не похоже."
    show sl smile2 pioneer with dspr
    sl "Может быть, ты просто плохо меня знаешь?{w} Так что? Ты согласен?"
    me "То есть ты просто так возьмёшь и выпустишь меня? Без всяких условий?"
    sl "Ну не совсем.{w} Одно условие все-таки будет."
    me "Ага, я так и подозревал.{w} И что я буду должен взамен?"
    sl "Пообещай мне сделать кое-то для меня."
    me "Что именно?"
    sl "Не могу сейчас сказать. Просто пообещай."
    me "Кота в мешке, значит…"
    sl "Не волнуйся, ничего невыполнимого я от тебя не потребую. Просто… одну вещь."
    menu:
        "Согласиться":
            me "Но ведь… меня сразу искать начнут. Что мне, пешком по шоссе убегать?"
            if alt_day2_us_escape:
                me "Или опять поезд ловить?"
            sl "Да не обязательно. Тебе всего-то до вечера надо спрятаться, а там я тебя найду."
            me "И что, потом вместе сбежим? Как романтично…"
            show sl laugh pioneer with dspr
            "Славя рассмеялась."
            sl "Там все увидишь! Ну давай, выходи скорее, а то кто-нибудь придёт."
            show sl normal pioneer with dspr
            th "Ну, хватит нерешительности. Пора действовать! Кстати, надо обед захватить с собой. Мало ли, сколько партизанить придётся…"
            "Я шустро сложил судки обратно в авоську и пошёл вслед за Славей."
            sl "Я выгляну, если никого нет - позову тебя."
            play sound sfx_close_door_1
            "- сказала она, запирая дверь в пустой изолятор."
            play sound sfx_open_door_1
            sl "Выходи, никого нет!"
            window hide
            scene bg ext_aidpost_day
            show sl normal pioneer
            with dissolve
            window show
            me "Так, что дальше? Прятаться в лесу?"
            # уходим на дикий пляж, где тонул в Славя-руте. Там же рядом Унылкин схрон
            sl "Дальше - через забор за футбольным полем, и выходи к реке, там заброшенный пляж есть. Там никто искать не будет."
            sl "А я тебя там потом найду."
            # Перелезаем забор, проливаем на себя солянку из кастрюли в силу косорукости. Пичаль-беда.
            # Отмываемся от супа на пляже. Встречаемся с Юлей, купаемся, делим обед на двоих. Шорох, Юля прячется.
            # Приходит Славя и толкает речь про то, что все ненастоящее, что это Семен все придумал, и он должен проснуться и взять ее с собой. Семен охреневает
            # Приходит Лена, видит Славю, сцена ревности, мерянье тем, кому Семен больше нужен. Семен охреневает вторично.
            # К этому балагану присоединяется Юля. 
            jump alt_day6_uvao_isolator_sl_escape
        "Отказаться":
            pass
    th "Очень уж это подозрительно. Не может всё быть так просто."
    th "Ей что-то от меня надо, о чем я впоследствии определённо пожалею."
    me "Нет, спасибо. Я не люблю, когда со мной играют втёмную."
    show sl surprise pioneer with dspr
    me "Я лучше подожду, пока Виола вернётся. Все это - просто огромное недоразумение!"
    dreamgirl "Нас выпустят, это точно!" 
    # Славя удивлена, что Семён не ломанулся приключаться, в  собственном вымышленном мире-то
    sl "Но, Семён… почему?"
    sl "Ты разве за этим сюда приехал, чтобы скучать взаперти?!"
    me "Поскучаю, ничего страшного."
    sl "Я не понимаю… "
    "Она собиралась ещё что-то сказать, но я перебил."
    me "Благодарю за предложение, но нет. Подумать только, самая примерная активистка, помощница вожатой предлагает мне удрать из лагеря!"
    show sl angry pioneer with dspr
    "Кажется, это её задело за живое. Она вскочила, кипя от злости…"
    sl "Ладно же! Вот и сиди тут один, как сыч!"
    hide sl with dissolve
    play sound sfx_open_door_kick
    "…и стремительно вышла, не забыв захлопнуть и запереть дверь."
    th "И что это такое сейчас было?"
    th "Сначала запирают.{w} Намекают на какую-то катастрофу из-за Юли.{w} Потом приходит Славя и предлагает просто выпустить."
    th "Да что за херня здесь происходит?!" with hpunch
    "Машинально черпая ложкой суп, я почти не чувствовал вкуса."
    #th "Ну давай разберём по частям мною сказанное." # Не стареющая классика
    th "Давай попробуем разложить всё по полочкам."
    dreamgirl "Не то чтоб это была твоя сильная сторона, но попытка не пытка."
    th "Намёки Ольги про какую-то непонятную опасность…"
    if not alt_uvao_D4_viola_morning:
        th "А позавчера утром она просто наорала на меня, стоило мне упомянуть о хвостатой тени!"
        th "От реальной опасности не будут так отмахиваться!"
    else:
        th "А Виола, наоборот, была не против, чтобы я поискал это лагерное привидение."
    stop music fadeout 4
    th "А теперь - какая-то сплошная паника.{w} Научный проект на грани катастрофы, грядёт эвакуация лагеря… {i}Эффект{/i} какой-то ещё…"
    th "Психоз Шурика - это тоже Юля виновата, что ли? Да он всегда ненормальный был!"
    window hide
    play music music_list["memories"] fadein 4
    scene anim prolog_1
    show prologue_dream
    show uv normal behind prologue_dream
    with dissolve
    window show
    "Юля.{w} Смешная, наивная девочка-кошка…"
    "Перед мысленным взором всплыли воспоминания."
    window hide
    show bg ext_house_of_mt_day behind prologue_dream
    show uv dontlike close at center behind prologue_dream
    with dissolve
    window show
    "Первая встреча в окошке домика…"
    window hide
    hide bg ext_house_of_mt_day
    hide uv
    show bg ext_no_bus_sunset behind prologue_dream
    show uv smile at center behind prologue_dream
    with dissolve
    window show
    "Вечерний перекус булочками…"
    window hide
    hide bg ext_no_bus_sunset
    hide uv
    show int_old_building_day_uvao behind prologue_dream
    show uv normal at center behind prologue_dream
    with dissolve
    window show
    "Беседа в старом лагере…"
    window hide
    hide bg int_old_building_day_uvao
    hide uv
    show bg int_catacombs_living behind prologue_dream
    show uv smile close at center behind prologue_dream
    with dissolve
    window show
    "Странный бункер… "
    if alt_uvao_D5_hentai:
        extend "И наша животная страсть на пыльных матрасах."
    window hide
    hide bg int_catacombs_living
    hide uv
    show bg int_mine_crossroad behind prologue_dream
    show uv normal at center behind prologue_dream
    with dissolve
    window show
    "Короткое путешествие по заброшенным шахтам…"
    window hide
    scene anim prolog_1
    show prologue_dream
    show uv normal behind prologue_dream
    with dissolve
    window show
    th "Юля, конечно, странная…{w} эээ…{w} странное существо, но ожидать от неё каких-то катаклизмов… По-моему, она вполне безобидна." # котоклизмов, ага
    dreamgirl "Если, конечно, не считать той вожатой, с которой Юля {i}поговорила{/i}… Как она сказала - «Она почему-то бегала и кричала»?{w} Кажется, очень похоже на Шурика."
    dreamgirl "В общем, даже и не знаю, что тебе посоветовать."
    menu:
        "Поверить Ольге и Виоле":
            $ alt_uvao_D6_trust_uv = False
        "Юля - не опасна!":
            $ alt_uvao_D6_trust_uv = True
    window hide
    stop music fadeout 4
    scene bg int_aidpost_day with dissolve
    play music music_list["mystery_girl_v2"] fadein 4
    window show
    #TODO: DEBUG + MUSIC
    "От глубоких размышлений меня отвлекла чья-то тень в окне. Я обернулся."
    show uv normal far at center with dissolve
    uv "Привет. Это у тебя котлета? А мне?"
    "Это было так неожиданно, что я отшатнулся назад."
    if alt_uvao_D6_trust_uv:
        me "Ой…{w} Юля! Ты меня напугала."
        uv "Не пугайся. А чего ты там сидишь?"
        me "Да вот видишь, меня тут заперли!"
        show uv surprise2 far at center with dspr
        uv "Заперли?! А почему?"
        me "Да вот не пойму. Наверное, чтобы я с тобой не гулял."
        show uv laugh far at center with dspr
        "Юля рассмеялась, будто я ей анекдот рассказал."
        show uv smile far at center with dspr
        uv "Глупые они! Я тебя всегда найду!"
        "Глядя на её гордую довольную улыбку, я и сам рассмеялся."
        me "Ну да, нашла. Что дальше делать будем?"
        uv "Вылезай оттуда, раз я тебя нашла. И котлету принеси."
        dreamgirl "Ты сегодня нарасхват, я смотрю."
        me "Так говорю же, не могу. Меня тут заперли."
        th "Интересно, сможет ли она раздобыть ключи?"
        dreamgirl "А чем тебя тогда не устроило предложение Славяны? Уже полчаса как бегал бы на свободе."
        th "Тем, что это была Славяна, очевидно же."
        me "Юля, а ты сможешь раздобыть ключи?"
        show uv normal far at center with dspr
        $ alt_uvao_D6_ask_uv_keys = True
        uv "Ключи? А зачем?"
        me "Чтобы отпереть дверь! И выйти!"
        uv "А ты выйди так."
        me "Как?"
        uv "Так."
        "Чувствовать себя идиотом, не понимающим простейших вещей - бесценно."
        me "Как {i}так{/i}-то?!"
        uv "Ну {i}так{/i}!{w} Просто захоти и выйди."
        me "Ну вот хочу я выйти, и что? Дверь-то все равно заперта."
        uv "Ты не так хочешь. Надо сильно захотеть. И тогда выйдешь. Попробуй!"
        "Сильно захотеть… Мне казалось, я уже давно забыл, что это такое."
        uv "Попробуй! Можешь глаза закрыть, чтобы лучше хотелось."
        show blink
        "Захотеть… Ну вот хочу я на волю.{w} Очень хочу.{w} К Юле."
        th "Хочу к Юле!{w} К Юле хочу!{w} Прямо сейчас!"
        if alt_uvao_D5_hentai:
            "Под плотно зажмуренными веками заплясали пятна света. Звон в ушах, на самой грани слышимости…"
            "И совершенно неожиданный порыв ветра встрепал волосы."
            "Я открыл глаза."
            window hide
            scene bg ext_aidpost_day
            show unblink
            show uv smile close at center with dissolve
            window show
            
            # Пытаемся выяснить у Юли, что произошло. Юля объясняет как все само собой разумеющееся
            # "Котлету забыл! Ну ладно, в столовой еще найдем"
            # Убегаем в леса, дичаем
            jump alt_day6_uvao_isolator_uv_escape
        else:
            dreamgirl "Хотеть, как говорится, не вредно, вредно не хотеть."
            "Я постоял еще примерно полминуты с зажмуренными глазами, и мое терпение лопнуло."
            window hide
            scene bg int_aidpost_day
            show unblink
            show uv surprise far at center with dissolve
            window show
            "Юля была удивлена и растеряна."
            uv "Не получается… Непонятно. Должно получаться!"
            me "Должно? Ничерта подобного!"
    else:
        uv "Ты чего? Испугался? Это же я!"
        me "Ээ… Привет."
        "Я все равно старался держаться подальше от неё."
        uv "А ты чего там сидишь?"
        me "Заперли меня. Из-за тебя, между прочим!"
        show uv guilty far at center with dspr
        uv "Почему из-за меня?"
        me "Не знаю… Ничего не объяснили, говорят только, что с тобой - опасно."
        uv "Не бойся! С тобой ничего не будет."
        me "А с остальными? Они тут чуть было не собрались весь лагерь эвакуировать!"
        uv "Эва-куировать?"
        me "Ну увозить, то есть."
        uv "Нет! Не надо никого увозить! Это плохо!"
        uv "Скажи им!"
    stop music fadeout 4
    hide uv with dissolve
    play sound sfx_bush_leaves
    "Наш увлекательный разговор прервали чьи-то голоса. Юля немедленно спряталась в кустах позади медпункта."
    play sound sfx_open_door_1
    play music music_list["eternal_longing"] fadein 4
    "Кто-то открыл дверь в моё узилище. Наш безумный доктор."
    show cs normal at left with dissolve
    cs "Ага, ты ещё тут, пионер…"
    me "Да вот, знаете ли, решил все-таки дождаться объяснений. Да вы заходите, не стесняйтесь, будьте как дома!"
    show mt angry pioneer at center
    show sl serious pioneer far at right behind mt
    with dissolve
    "Сарказм не попал в цель. Виола зашла, а следом за ней и Ольга Дмитриевна со Славяной."
    "Расположившись напротив меня, Ольга и Виола принялись неодобрительно сверлить меня взглядом. Славя же, наоборот, держалась поодаль и старательно смотрела в другую сторону."
    th "Обижается, что я не согласился, что ли?"
    cs "Ну и как прикажешь это понимать, Семён?"
    "Я собирался требовать ответов, докапываться до истины, вскрывать тайные причины происходящего… но внезапное обвинение непонятно в чем выбило почву из-под ног."
    me "Эээ… понимать что?"
    cs "Твоё поведение."
    me "А… что с ним не так?"
    cs "Не догадываешься?"
    me "Ну… нет."
    "Я лихорадочно перебирал гипотетические прегрешения, которые мог совершить за сегодня." 
    dreamgirl "Это они про твою беседу через решётку с Юлей?"
    th "Да не. Вроде бы не успели заметить…"
    mt "Если ты думаешь, что твоё происхождение защищает тебя от ответственности, то ты глубоко ошибаешься! У нас есть все необходимые полномочия для поддержания порядка на вверенном нам объекте!"
    dreamgirl "Вах, какой канцелярит! Просто песня!"
    "Я молчал и тупо хлопал глазами. Это какой-то бред…"
    mt "А поначалу ты казался мне таким приличным человеком! Как ты мог даже помыслить такое, Семён?"
    me "Да о чём вы вообще?!"
    cs "О твоей попытке изнасиловать Славяну."
    $ renpy.music.set_volume(0, .5, channel="music")
    # Ту-ду-дум!
    "От такого нелепого и чудовищного обвинения я несколько секунд беззвучно открывал и закрывал рот."
    $ renpy.music.set_volume(1, 10, channel="music")
    me "А… Э… То есть… Это… Я… Славю…?{w} ДА ВЫ СОВСЕМ С УМА ПОСХОДИЛИ?!"
    cs "А как это тогда называется? Она принесла тебе обед, ты попытался затащить её и повалить на койку. И если б ей не удалось вырваться…"
    me "Это все враньё! Ничего этого не было! Славя, скажи ведь!"
    show sl serious pioneer at right with dspr
    "Она смотрела на меня странным взглядом, даже без тени обычной теплоты."
    sl "Все таки и было."
    me "Да ты… Да это же… Какого черта, Славя?!"
    "Я обернулся к Виоле и Ольге."
    me "Если хотите знать, как все было, то вот вам! Славя принесла мне обед, а потом вдруг предложила убежать. Я отказался. Вот и все!"
    "Они недоуменно переводили взгляд с меня на Славяну и обратно, а я решил добавить:"
    me "Я понимаю, что вы мне не верите. Она - помощница вожатой, а я - какой-то непонятный “попаданец”, но…"
    me "Если б я её тащил силой - должны остаться следы - синяки там или ссадины!"
    me "Вместо того, чтоб молча отпихиваться, можно было заорать - кто-нибудь бы услышал!"
    me "И вообще, почему она рассказала это только сейчас, а не сразу же, как убежала?!"
    dreamgirl "Браво, Шерлок. У нас неоспоримое алиби! Ещё бы понять, зачем ей все это…"
    show mt surprise pioneer at center behind sl
    show sl angry pioneer at right
    with dspr
    mt "Э… Славя?"
    sl "Что?!"
    show mt normal pioneer at center behind sl with dspr
    mt "Так что {b}на самом деле{/b} здесь произошло? Кто врёт - Семён или ты?"
    "Она вскочила, вся красная от ярости."
    sl "А если я, что тогда? Накажете? В характеристику впишете?"
    mt "Славя, Славя, что с тобой?"
    sl "Со мной - ничего! Я просто хочу выжить! Стать настоящей!"
    cs "О чем ты говоришь?"
    sl "А то вы не знаете! Может вы и смирились с ролью кукол, но я не это не согласна!" 
    show mt surprise pioneer behind sl with dspr
    # cs есть удивленная? А то она упорно держит покерфейс
    "Мы недоуменно переглянулись, все трое."
    cs "Кукол? Какие ещё куклы?"
    sl "Да не притворяйтесь, Виолетта Церновна. Я слышала ваш вчерашний разговор с Ольгой Дмитриевной."
    cs "Да? Это какой же?"
    sl "Тот самый. О том, что весь наш лагерь - это выдуманный мир. И я, кстати, знаю, кто его придумал!"
    cs "И кто же? Покажи мне этого демиурга, у меня есть что ему сказать…"
    "Славя вытянула руку, едва не ткнув пальцем мне в лоб."
    sl "Он!"
    me "Я?!"
    dreamgirl "Ну не я же…"
    th "А ты не врёшь?"
    dreamgirl "Да-да, и часовню тоже я развалил… Нет, чувак, я тут не при чем."
    sl "А кто же ещё? Едва приехал, весь лагерь начал вокруг тебя крутиться, все с тобой носятся…"
    sl "Встретили его, как почётного гостя, поселили в домике вожатой, ужин пропустил - так перекус организуем, заскучал - вот тебе карточный турнир, вот кружки по интересам, вот тебе дискотека внеочередная…"
    sl "Неужели это все, потому что ты - посольский сынок? Прямиком с Кубы к нам заехал, в зимнем пальто - ну да, там же такие морозы!"
    me "Да что ты несёшь?"
    cs "Подожди, Славя. Не знаю, что ты там подслушала и на какие мысли это тебя навело, но они точно не имеют ничего общего с реальностью…"
    sl "С какой реальностью, Виолетта Церновна?! Он наиграется в пионеров и уйдёт, а мы…"
    sl "Не знаю что с нами будет - может просто исчезнем, а может - будем стоять на полке, как прочитанная книга, пока ему не придёт в голову снова её открыть, снова встретить меня у ворот лагеря…"
    sl "А я этого не хочу! Не хочу, чтоб за меня решали, кого мне встречать, а кого метлой гонять! Я хочу - быть настоящей!"
    me "Это как - настоящей?"
    sl "Это значит - не быть милой куклой с этикеткой \"добрая активистка\".{w} Это значит  - в твоём мире, не знаю уж откуда ты к нам приехал…"
    show cs normal far at fleft
    show mt surprise pioneer far at cleft
    show sl cry pioneer at cright with dissolve # надо расшакалить плачущую эмоцию под close
    "Она порывисто схватила меня за руку, в огромных глазах стояли слезы."
    sl "Семён… ты, наверно, злишься на меня, что я веду себя не по сценарию… но, пойми, я уже не могу."
    sl "Наверно, ты плохой писатель, раз не можешь сладить с собственным персонажем{w}, или очень хороший - раз смог наделить меня свободой воли…"
    sl "Пожалуйста… выпусти меня… наружу…"
    "Я ошеломлённо таращил глаза."
    me "Славя… Что ты себе выдумала… Я тут не при чем! Я сам сюда попал непонятно как, против воли!"
    uv "Неправда. Ты сам захотел прийти."
    play music music_list["mystery_girl_v2"] fadein 4
    show cs normal at fleft
    show sl surprise pioneer close at cleft
    show uv upset far at right
    show mt surprise pioneer at left behind sl
    with dissolve
    "Все обернулись. В дверях стояла Юля, опираясь на косяк двери."
    "Реакция на её визит была неоднозначной. Ольга Дмитриевна побледнела и попятилась назад, Виола тоже старалась не приближаться и не делать резких движений. Славя удивлённо разглядывала странное существо."
    # Если бы это был какой-нибудь телесериал - это идеальный момент для конца очередной серии.
    # (Если верил Юле и попросил ключи):
    if alt_uvao_D6_trust_uv:
        "А я очень ей обрадовался."
        me "Юля!"
        cs "Юля… вот значит как…"
        show sl surprise pioneer far at cleft behind uv
        show uv upset close normal at cright
        "Я подбежал и схватил её за руку."
        me "Хорошо, что ты пришла!"
        "Меня вусмерть достали все эти тайны, непонятные намёки… Всем от меня что-то надо!"
        uv "Да, я пришла. За тобой."
        me "Отлично! Пойдём?"
        "Славя сделала было шаг ко мне, пытаясь удержать…"
        cs "Славяна! Нет! Пусть уходят!"
        uv "Да. Не надо, он не поможет. И ты - не кукла."
        sl "Но… не понимаю…"
        uv "Потом поймёшь."
        "Держась за руки, мы выбежали из медпункта. Всё, их сумасшествие меня больше не касается!"
        # Уходим в берлогу спать. Без танцев, без хентая - Юля не в настроении из-за всей катавасии.
        jump alt_uvao_D6_sleep_with_uv
    # Если поверил исследователям и испугался Юлю:
    "Я же не знал, что и подумать. Её, наверное, следовало опасаться, но я не мог поверить, что она может мне навредить."
    me "Привет, Юля."
    cs "Юлия? Но как?!"
    uv "Ты Виола? Я почему-то тебя помню... Только ты была другой."
    sl "Кто это? С ушами…"
    me "Это Юля. Она живет в старом лагере и таскает всякую еду из столовой."
    cs "А еще она - визуализация здешней аномалии."
    show sl serious pioneer with dspr
    sl "И вы будете говорить, что это всё - не выдумка?! Что в обычном пионерском лагере бегает такая вот… девочка с хвостом, и никто не пытается её поймать?"
    uv "Некоторые пытаются. Как ты."
    sl "Я?!"
    cs "Подожди, Славя. Юля, зачем ты пришла?"
    uv "Вы громко кричали. Ругались. Это плохо. Я пришла, чтобы вы не ругались."
    sl "Не из-за чего тут ругаться! Семён, ты должен меня забрать отсюда!{w} Пожалуйста!"
    uv "Он не сможет."
    sl "Тебя не спросили! Брысь под лавку, кисонька! А то хвост оторву!"
    show uv dontlike far at cright behind sl
    show cs fear at fleft
    show mt angry at left behind sl
    with dissolve
    # все пугаются - Юлю обидели!
    # TODO: ОД и Виолу тоже в шоке
    cs "Славяна!"
    mt "Славя! Немедленно замолчи! Сейчас же!"
    sl "Нет, не замолчу! Не замолчу! Вы можете и дальше этот дурацкий сценарий разыгрывать, а мне надоело!"
    # Нужно больше истерики и криков!
    "Вопли вгрызались мне в мозг, как призрачные свёрла, и проклятый сосед-с-перфоратором не собирался останавливаться… Я сжал гудящую голову ладонями, пытаясь удержать шатающуюся реальность."
    menu:
        "Это все какой-то бред!":
            "Непослушная вселенная продолжала вращаться, все быстрее и быстрее. Крики и прочие звуки потонули в ватном шуме. Последним ощущением был вскрик и чьи-то пальцы, больно вцепившиеся мне в руку."
            # Уходим на нейтрал
            jump alt_uvao_neutral_end
        "Хватит!":
            pass
    play music music_list["just_think"] fadein 4
    show uv upset far at cright behind sl
    show cs normal at fleft
    show mt surprise at left behind sl
    with dissolve
    th "Хватит…{w} Пожалуйста, хватит…{w} Хватит!"
    me "ХВАТИТ!" with hpunch
    # TODO: звук БАМЦ - есть у нас?
    "Удивительно, но они послушались. Наверно, помогла пустая кастрюля из-под супа, которую я швырнул об пол."
    me "А теперь давайте попробуем внести немного логики в этот бред и разобраться, что все-таки происходит."
    me "Вам всем от меня что-то нужно, но проблема в том, что я нихрена не понимаю - что именно. Давайте я начну."
    "Я помолчал, собираясь с мыслями."
    me "Итак. Да, я действительно попал сюда из другого мира. Зимой 2014 года я сел в самый обычный автобус, задремал, а проснулся здесь, на сиденье Икаруса. Я понятия не имею как и почему я сюда попал…"
    uv "Ты захотел, вот и попал."
    me "Юля, не перебивай! Так вот, я не знаю, как я сюда попал, и как мне вернуться обратно. Да, мне уже несколько лет снился сон, в котором девочка, похожая на Юлю, звала меня пойти с ней. Наверное, в этот раз я согласился."
    uv "Я же говорю, захотел."
    me "Вот, вкратце, как я вижу происходящее. Никакой лагерь я не придумывал, за это уж я могу ручаться. Теперь давай ты, Славя. Только, пожалуйста, факты, без домыслов и истерик. Как для идиота, простыми словами."
    sl "Попробую. Начнём с того, что я не помню жизни до лагеря."
    show mt surprise pioneer with dspr
    mt "Что? Как?"
    sl "Да, именно так. Я знаю, что где-там у меня есть дом, родители, братья, сестры, друзья… но я их не помню."
    mt "Но почему ты не рассказала?"
    sl "О чем? Как вы себе это представляете?"
    mt "Ну…"
    sl "И я не хотела, чтоб меня начали лечить. Поэтому я притворялась нормальной - кажется, у меня хорошо получалось."
    mt "Странности были, но я списывала все на хлопоты с лагерем… Надо было мне не так тебя нагружать."
    sl "Да нет, ничего… А потом - вчера - я подслушала ваш с Виолой разговор. О параллельных мирах, о созданных воображением \"карманах реальности\", и много ещё о чём… И сделала выводы."
    cs "Девочка, если уж берёшься подслушивать - слушай внимательно! Это были просто умствования, \"что было бы, если бы\"! А вот почему ты ничего не помнишь - это загадка…"
    me "Ладно, Виола, давайте теперь вы. К тому же вы все равно обещали мне все рассказать."
    cs "Да, думаю, в этой ситуации…"
    mt "Виола! А как же…"
    cs "…в {b}этой{/b} ситуации можно немного нарушить правила и играть в открытую."
    "Не передать, как я счастлив!"
    cs "Начнём, пожалуй, с азов. Тебе, Славяна, тоже будет полезно послушать."
    "Наконец-то!"
    cs "Итак. Как вы уже поняли, существуют параллельные миры. В обычных условиях они существуют независимо, никак друг на друга не влияя. Но иногда, по неизвестным причинам, человек из одного мира может попасть в другой."
    cs "Как правило, этот человек должен быть слабо привязан к своему миру. Зона перехода обычно открывается в местах, обладающих, так сказать, хорошим эмоциональным фоном - курорты, санатории,  ну и пионерлагеря, все в таком духе. Человек внезапно возникает здесь, а там, должно быть, пропадает."
    cs "В течение нескольких дней он либо адаптируется к местным условиям и остаётся здесь навсегда, либо так же бесследно пропадает - скорее всего, возвращается обратно."
    cs "Уехать своим ходом из зоны перехода до окончания адаптации он не может - его либо перенесёт обратно в зону перехода, либо, опять же, он исчезнет из нашего мира."
    me "А Юля? Кто она такая? Тоже, что ли, пришелец из параллельного мира?"
    uv "Я - это я, глупый! Ты же уже спрашивал!"
    cs "Сейчас дойду и до неё. Обычные зоны, или межпространственные аномалии, как их принято называть, работают краткосрочно, только на период адаптации субъекта перехода. Но также существуют стабильные зоны, как, например, эта."
    cs "В них помимо обычных странностей аномалии, проявляются вот такие существа, подобные твоей подруге."
    me "Что, все с кошачьими ушами и хвостом?"
    cs "По-разному. Есть мнение, что на их внешний вид как-то влияет сам попаданец. По крайней мере, он может общаться с ними без особого вреда для здоровья."
    me "А остальные?"
    cs "А у остальных проявляется весь спектр психических заболеваний. Психозы, депрессии, мании, фобии, чем дальше - тем хуже."
    me "И, значит, Шурик…"
    cs "Скорее всего. Можешь сам уточнить."
    me "Юля, это правда? Это из-за тебя Шурик умом тронулся?"
    uv "Я его прогнала. Он ко мне пришёл и хотел меня палкой ударить! Я не люблю, когда меня ловят."
    "Ну и дела…"
    me "Виола, так почему, если Юля представляет такую опасность, лагерь по-прежнему работает?"
    cs "Эмоциональный фон должен быть благоприятным. Если закрыть лагерь, или даже заменить всех отдыхающих на сотрудников - будет уже не то, переход может закрыться. А этим мы рисковать никак не можем - это единственный объект такого рода. Если мы поймём механизм работы этих переходов - это же будет переворот в науке!"
    dreamgirl "А если в процессе пару-тройку психически неустойчивых подростков придётся успокоительными обкалывать -  ну что ж, наука требует жертв!"
    me "Это все, конечно, занимательно… но что будет со мной?"
    cs "Пока не знаю. В целом у тебя неплохие шансы на то, чтобы остаться здесь. Но не стопроцентные. А ты-то сам чего хочешь?"
    "Я? Чего же я хочу…"
    "Было непривычно себе в этом признаваться, но вариант \"чтобы меня просто оставили в покое\" мне больше не подходил. Эта неделя, хоть и донельзя насыщенная событиями, заставила чувствовать себя более живым, чем пара последних лет."
    me "Знаете, поиск ответов мне понравился. Я был бы не прочь заниматься этим на постоянной основе."
    cs "Ага. Это можно было бы организовать. По приезду сделаем тебе паспорт, а дальше подашь документы как обыкновенный абитуриент."
    # Надо название. Учебный центр при НИИ СПГС?
    sl "А что будет со мной?!"
    cs "Тебе я бы тоже рекомендовала подать документы к нам."
    sl "Виола, плевать я хотела на документы. Что со мной случилось?"
    cs "Это тебе надо у психиатра узнавать…"
    show sl scared pioneer with dspr
    # Славя пугается - карательная психиатрия же!
    cs "Хотя есть у меня одна догадка. Семён, спроси у своей… подруги… не связана ли она как-то с этим."
    me "А почему бы вам самой не спросить у неё? Может быть, вы уже будете к ней относиться не только как к объекту исследования, но и к полноценной личности? Она вполне этого заслуживает!"
    "Почему-то такое обращение к Юле меня сильно задело. Несмотря ни на что, я считал ее живой девочкой."
    cs "Да, действительно… Эээ… Юля, ты как-нибудь связана с тем, что произошло со Славяной?"
    uv "Она хотела меня поймать. Гонялась за мной по лесу."
    show sl surprise pioneer with dspr
    sl "Я? Когда это? Я ничего такого не помню…"
    uv "Недавно. Семён тогда ещё не приехал. Я хотела посмотреть, как ты на озере танцуешь, а ты меня заметила. И стала ловить."
    sl "Ой. Извини, я не подумала… Наверное…"
    uv "Ладно. Не поймала ведь."
    cs "Как я и думала. Есть все шансы, что, удалившись от аномалии, твоя амнезия пропадёт сама собой. А если нет - по приезду в город уже проконсультируемся со специалистами…{w} А пока - пропишу тебе таблеточку, и часов десять здорового сна."
    show sl smile pioneer
    sl "Ох, поспать было бы просто замечательно. Спасибо вам…"
    hide uv with dissolve
    play sound sfx_wind_gust
    "Шорох, порыв сквозняка… Я обернулся - Юли уже и след простыл."
    "Эх. А я собирался с ней ещё поболтать."
    dreamgirl "Успеешь ещё… поболтать. Это точно не последняя ваша встреча."
    th "Думаешь?"
    dreamgirl "Не думаешь, а знаешь."
    play music music_list["eternal_longing"] fadein 2
    "Тем временем Виола достала из шкафчика ничем не примечательный пузырёк с таблетками и налила в стакан воды."
    cs "Вот. Выпей, и сразу же в постель."
    sl "Хорошо…"
    me "Давай я тебя провожу. Если, конечно, я больше не изолирован от общества…"
    "Я вопросительно посмотрел на Виолу."
    show cs smile at fleft with dspr
    cs "Да нет, можешь идти, пионер. Проводи Славяну… главное, не переутомляйтесь там, ей сейчас нельзя."
    show sl shy pioneer at cleft with dspr
    me "Снова вы за своё, Виола. Славя, я тебя снаружи подожду."
    jump alt_uvao_D6_take_sl_home
    
# Провожаем Славю домой
label alt_uvao_D6_take_sl_home:
    window hide
    play sound sfx_open_door_1
    play music music_list["so_good_to_be_careless"] fadein 4
    scene bg ext_aidpost_day with fade
    window show
    "Я поспешил покинуть это царство психиатрии и наркотических веществ. Подожду лучше на свежем воздухе!"
    "Через несколько секунд и Славя выпорхнула из дверей вслед за мной."
    show sl smile pioneer close at center with dissolve
    me "Ну что, пойдём?"
    sl "Да, пора."
    window hide
    scene bg ext_square_day with fade
    show sl normal pioneer close at center with dissolve
    window show
    "Мы вышли к площади, где Электроник и Алиса, под руководством Мику, подключали аппаратуру для дискотеки."
    dreamgirl "Хорош молчать. Про погоду что-нибудь скажи, хотя бы…"
    sl "Пойдёшь потом на танцы?"
    me "Не знаю. Может быть."
    sl "А я вот все просплю… Так жалко! Могли бы потанцевать."
    me "Ничего, ещё успеем. Особенно, если вместе учиться будем."
    sl "Да, наверное…"
    "Лекарство, кажется, уже начинало действовать, её походка становилась немного неуверенной. Подхватив под локоть, я скорее повёл её к домикам - иначе скоро придётся тащить на руках."
    window hide
    scene bg ext_houses_day with fade
    show sl smile pioneer close with dissolve
    window show
    sl "Ты прости меня, Семён… Я таких глупостей натворила сегодня. Не знаю даже, как завтра в глаза смотреть буду - и тебе, и остальным."
    me "Да ничего."
    sl "Не знаю даже, чего я хотела этим добиться. Прости. Я такая дура."
    window hide
    scene bg ext_house_of_sl_day with fade
    show sl smile pioneer close with dissolve
    window show
    "А вот и её дом. Она отперла дверь, потом обернулась и вдруг обняла меня."
    sl "Спасибо тебе."
    "Я почувствовал, как тёплые губы коснулись щеки, и ту же секунду Славя от меня отдалилась."
    show sl shy pioneer close with dissolve
    sl "Одной глупостью больше, одной меньше - какая разница. Извини, я уже с ног валюсь."
    me "А… да, спокойной ночи!"
    hide sl with dissolve
    "Она скрылась в домике, а я так и стоял перед дверью, дуб дубом."
    dreamgirl "Очнись, герой-любовник. Поцелуй в щёчку - не такое уж достижение."
    "Да, действительно. Пойду, что ли, на площадь, если не танцевать, так хоть музыку послушаю…"

    # Уходим на вечерний эвент
