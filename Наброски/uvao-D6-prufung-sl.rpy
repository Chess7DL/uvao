label alt_day6_uvao_prufung_sl:
    scene bg ext_stage_big_day
    "Со сцены ещё издалека доносилось странное копошение - последние приготовления к концерту были в самом разгаре."
    "Какая-то малышня толпилась у первого ряда разодетая в старые жёлтые костюмы, повидавшими не одну смену. Кажется, изображали они утят."
    "Мимо меня прошагал с крайне озадаченным видом взмыленный Электроник со странной тяжеленной бандуриной в руках, завёрнутой в плотную тряпку."
    th "Он что? Тоже в этом священнодействии собрался участвовать? Или как дармового переносчика тяжестей привлекли?"
    "Проводив его взглядом до самой сцены и прикидывая, что же именно он мог так бережно нести, я наконец увидел то, зачем пришёл сюда."
    scene bg ext_stage_near_clear
    "Славя с явно недовльным видом о чём-то активно спорила с Мику."
    show mi upset voca with dissolve
    show sl angry pioneer with dissolve
    sl "Ты ведь понимаешь, что Ольга Дмитриевна это так не оставит. Делай как знаешь - но я тебя предупредила."
    mi "Славичка, ну дай мне хоть раз за три недели одеться как я хочу!"
    "Недоумённо пригляделся и, наконец, обратил внимание сине-зелёное обтягивающее платье Мику."
    "Конечно для моего времени в нём не будет ничего необычного, но сейчас оно смотрелось явным перебором для местных пуританских нравов."
    show mi dontlike voca with dissolve
    mi "Это же специальное, сценическое. Нельзя же выступать как клуша!"
    "Взгляд Мику был очень обиженным - что-то, а свои экстравагантные наряды она, похоже, очень любила."
    "Чувствуя, что этот балаган может быть бесконечным, как и все споры о морали, я всё-таки решил подать голос:"
    me "Славя. Можно тебя?"
    "Лицо блондинки словно просияло. Она в два шага подлетела ко мне и возбуждённо затараторила."
    sl "Ты что-то хотел, Сёмушка?"
    show mi normal voca with dissolve
    sl "Что-то случилось или тебе что-то нужно? Ты говори скорее."
    me "Славя, я это..."
    sl "Или ты не знаешь где сесть. Я уже нам заняла места - там хорошо видно. Хочешь провожу?"
    dreamgirl "Опять двадцать пять. Заткни её и как можно быстрее."
    dreamgirl "Сил моих нет этих «Сёёёёмушек» слушать."
    "Набрав побольше воздуха в грудь, я медленно произнёс, чеканя каждое слово:"
    me "Славя, прекрати. Нам нужно серьёзно поговорить. Отойдём."
    show sl sad pioneer with dissolve
    "Славя растеряно тряхнула своими длиннющими косами и недоумённо спросила:"
    sl "К-куда?"
    sl "Ты видишь - я же занята сейчас, Сёмушка."
    show sl smile pioneer with dissolve
    "Впрочем, она тут же вернула свою фирменную улыбочку на место."
    sl "Давай после концерта. Ты ведь помнишь про наш разговор?"
    sl "Погуляем потом вместе и поговорим, о чём только захочешь."
    show sl smile2 pioneer with dissolve
    sl "Может и чем-то {i}важным{/i} займёмся..."
    #феффект
    me "Да прекратишь ты, наконец, или нет?!"
    show sl sad pioneer with dissolve
    me "Что сегодня с тобой такое?!"
    "Не дав сказать Славе и слова, я схватил её за руку и приказным тоном рявкнул:"
    me "Пошли, живее."
    show mi laugh voca with dissolve
    mi "Какой нетерпеливый у тебя сегодня кавалер, Славичка."
    me "Мику, у тебя там концерт? Вот и занимайся."
    show mi sad voca with dissolve
    "Я с ненавистью глянул на замолчавшую вдруг японку, от чего та сделала пару шагов назад, развернулся и потащил ничего не понимавшую Славю прочь от сцены."
    dreamgirl "А хорошо ты их уделал. Наконец-то ведёшь себя как мужик, а не половая тряпка!"
    dreamgirl "Встречайте - впервые на арене: типичный альфач, Семён «буйный психопат» Персунов."
    "В этой ситуации только этой проклятой шизофрении и могло быть весело."
    hide mi
    scene bg ext_stage_big_day
    "Когда мы отошли уже на почтительное расстояние от сцены активисточка вырвала руку и взволнованно прервала молчание:"
    sl "Что происходит, Сёмушка?! Куда ты меня тащишь?!"
    sl "Да объясни ты хоть что-то уже!"
    me "Нет. Это ты прекрати Славя."
    me "Что это ещё за выходки? Что ты задумала?! Признавайся!"
    "Моё негодование, как снежный ком, сорвалось с вершины и, набирая силу с каждым словом, было способно раздавить любого кто попадётся ему на пути."
    sl "К-какие в-выходки? О чём ты?"
    me "Не прикидывайся! Ты же не собака, чтобы бегать за мной, радостно виляя хвостом. Чего тебе от меня нужно?"
    sl "К-каким хвостом?"
    me "А как мне всё это понимать по твоему? Или ты сбрендила?"
    me "Чего это ты плакала вчера на пляже, а потом стала туман наводить своими вопросами?"
    if alt_uvao_true:
        if not alt_uvao_D6_CS_vetrov:
            me "А на кормёжке с чего вдруг такая приветливая была?"
        me "Я уже молчу про все эти недоэротические намёки на пляже про купание голышом."
    me "Ну и главный твой номер, прима балерина. Все эти «Сёёёёмушки» и «важные дела» за обедом."
    me "Не верю я тебе."
    me "Что ты со мной задумала сделать?! Чем я прогневал твоё кардинальское величество?!"
    "Сказать что Славя стояла ошарашенной - значит ничего не сказать. Её всю потряхивало, глаза уставились в пол и чувствовалось, что ещё немного и она будет готова разрыдаться."
    sl "Сёмушка, миленький мой, ты мне очень нравишься. Что мне сделать, чтобы мне поверил?"
    sl "У нас ведь так мало времени осталось. Вот я и решила..."
    "Она не закончила мысль. Было видно, что каждое слово даётся ей с большим трудом."
    th "А вдруг я ошибся?... Может, она и не врёт?"
    "Настала теперь и моя очередь впадать в ступор."
    dreamgirl "Да ты посмотри на неё! У неё же зрачки по пять копеек, как после здоровой дозы метамфетамина."
    dreamgirl "Сомневаюсь, что она вообще хоть как-то соображает, что говорит."
    sl "Сёмушка, вчера я поняла, что ты особенный. Что только ты мне и нужен, только с тобой я смогу всё..."
    sl "Но как нам быть вместе, если завтра уже расстанемся? Ты ведь такой замкнутый - сам и не сделаешь первый шаг."
    sl "А я ведь девушка. За что ты так со мной?"
    sl "Я ведь ничего плохого тебе не сделала, а ты меня {i}величеством{/i} называешь..."
    "Я стоял и не мог ответить ничего вразумительного, а Славя смотрела на меня умоляющими глазами и всё продолжала:"
    sl "Почему ты меня боишься? Я ведь обычная девушка."
    sl "И у меня тоже есть свои чувства."
    sl "Давай хотя бы попробуем, Сёмушка. Ну пожалуйста."
    #"Славя вдруг заглянула мне прямо в глаза."
    sl "Возьми меня с собой. Я готова ради этого отправиться куда угодно, только бы рядом c тобою быть."
    "Её взгляд был туманным - казалось, что для неё теперь это вопрос жизни и смерти. Не было больше той уверенной в себе паладинши: каждое её действие выражало только бесконечное отчаяние."
    me "Ты... ты это серьёзно?"
    sl "Да, Сёмушка."
    "Отступать было уже слишком поздно, да и способен ли я был к другому решению?"
    "Я никогда и никому не доверял прежде, а Славя в моих глазах была всегда слишком самодостаточной, чтобы ей ни с того ни с сего понадобился кто-то близкий."
    th "Делай, что хочешь, но сейчас мне мозги запудрить у тебя точно не получится."
    me "Зачем ты мне врёшь?"
    "Негромко и с явными нотками злобы начал я."
    me "Меня уже достала эта чушь, Славя!"
    me "Сейчас разве весна? Или ты на всех как мартовская кошка кидаешься? Нет, это не про тебя."
    me "Посмотри на меня: конченый неудачник, который шарахается от любого шороха и бежит от близких контактов с людьми."
    me "Такая как ты ни за что на свете не станет марать честь своего блестящего мундира об это серое и унылое убожество."
    sl "Неправда, ты не такой! Ты хороший!"
    me "Как бы не так, Славя! Я падаль рода человеческого - обычная дешёвка."
    me "Чего тебе от меня надо?! Хватит со мной в игры играть!"
    "Славя стояла с мертвенно-бледным лецом, а по её щекам тонкой струйкой потекли слёзы."
    show sl cry pioneer with dspr
    th "Театр одного актёра продолжается..."
    "У меня так и не было ни единого основания верить в искренность её чувств."
    "Она немного помялась и, не прекращая рыдать, произнесла:"
    sl "Я не могу без тебя жить, Семён. Ты мне очень нужен."
    me "Опять свою шарманку про любовь заводишь? Надоело, Славя! Давай, что-нибудь пооригинальнее, наконец."
    "Она немного уняла слёзы и тихо ответила:"
    sl "Помоги мне, Семён. Я не понимаю, что со мной..."
    dreamgirl "Ну неужели?! Подсекай её срочно!"
    "Я решил тоже несколько умерить свой пыл и попытался успокоиться - диалог в кои-то веки начал отдавать конструктивом."
    me "Хорошо, Славя. И чем же я могу быть тебе полезен?"
    "Было видно как на её руках пробегают мурашки, а саму активисточку заметно потряхивает от волнения, но она всё же продолжила:"
    sl "Я не знаю кто я и откуда..."
    dreamgirl "Станция «Жёлтый дом». Конечная. Поезд дальше не идёт: просьба освободить вагоны."
    me "Что ты опять несёшь, милая? Как это ты не знаешь кто ты? Что за бред!"
    "От этих слов на её лице горе сменилось искренним раздражением - ещё пара глупых фраз и на меня набросятся с кулаками."
    show sl serious pioneer with dspr
    sl "Послушай меня, пожалуйста, и не перебивай."
    sl "Бред говоришь? Я не помню ничего, что было до этого проклятого лагеря - ясно это тебе?"
    sl "Я вечная помошница нашей ленивой вожатой - девочка-метла, у которой нет ни чувств, не воспоминаний."
    sl "Шурх-шурх, Семён. «Шурх-шурх» - это и есть вся я!"
    sl "А вокруг пустота, понимаешь? Как будто ничего и никогда не существовало. Я даже дома своего родного не помню!"
    me "А я здесь причём, Славя?"
    me "Тебе к доктору нужно. А лучше сразу в дурку. Сходи к Виоле - она-то тебя точно пристроит, куда следует."
    show sl angry pioneer with dspr
    "Славя с неприкрытой злобой сжала кулаки."
    sl "Я же просила не перебивать. Быть может я ничего и не помню, но точно знаю, что клеймо больной на голову мне ни к чему."
    sl "Я хочу делать что-то хорошее в этом мире, Семён - оставить после себя след. Зачем мне отталкивать людей от себя репутацией сумасшедшей дуры?!"
    dreamgirl "Ты посмотри-ка: и впрямь паладинша. И откуда столько идеалов в голову себе запихнула? Сектантша коммунистического покроя, прям."
    me "Так при чём тут я, Славя? Объясни мне наконец."
    sl "Я подслушала вчера разговор Виолы и вожатой о тебе: про аномалию, про параллельные миры, про будущее."
    sl "И тогда всё встало на свои места. Этот мир, этот лагерь - всё ненастоящее."
    sl "И я ненастоящая!"
    "Она перешла почти на крик и снова заплакала."
    show sl cry pioneer with dspr
    sl "Это твой, твой мир, Семён. Ты создал его таким. Ты Создатель!"
    dreamgirl "Без доктора тут не обойтись. «Подумала» она и «решила» - смешно."
    dreamgirl "Доказательства, интересно, будут?"
    sl "Я не хочу быть игрушкой, слышишь? Не хочу быть куклой или картонкой!"
    sl "Сделай же меня настоящей, Сёмушка! Я жить хочу, понимаешь? По-настоящему жить!"
    sl "Ты же особенный! Ты можешь! Это ведь ты меня придумал такой."
    sl "Возьми меня с собой! Дай мне шанс не быть пустышкой в чьём-то воображении!"
    "Славя закрыла лицо руками и громко зарыдала. Сил сдерживаться похоже у неё совсем не осталось."
    th "Большего я от неё теперь точно не добьюсь."
    dreamgirl "А тебе разве мало сказанных слов? Совсем мыслей собственных нет?"
    menu:
        "А, может быть, она права?":
            "До сих пор я не принимал в расчёт этой такой простой и в то же время логичной теории."
            "Все эти проколы, попаданцы, параллельные миры и правда выглядят слишком уж запутанными и непохожими на реальность."
            "Почему история этого мира частично совпадает с моим, но только с определённого момента начинаются расхождения?"
            "Почему здесь говорят со мной на том же самом русском языке без каких бы то ни было отличий от моего родного дома?"
            dreamgirl "Глубоко копаешь. А разве мелочи тебя не смущают?"
            th "Да взять хотя бы этого истукана на площади - уж слишком он кажется мне подозрительно знакомым."
            th "А сами девочки... Какому всемогущему рандому стоит помолиться чтобы тебя окружали школьницы с внешностью фотомоделей?"
            dreamgirl "Если хорошо вспомнить все их округлости..."
            th "Вот именно. Не знаю сколько белка нужно есть, чтобы такое отрастить и нигде лишнего при этом не отложилось."
            "Я огляделся по сторонам."
            th "Да даже природа. Я всё понимаю - образцовый пионерский лагерь, зона отдыха и всё такое." 
            th "Но тут даже сирень - обычная самая сирень - цветёт отчего-то в середине июля. И это ещё учитывая, что мы явно сильно южнее моего промозглого края."
            dreamgirl "Забавный ты. Получается, хвостатая твоя подружка тебя и не смущает, что ли?"
            th "Я до сих пор толком не понимаю, что она такое... А после фотографии совсем в догадках теряюсь."
            if alt_uvao_D5_hentai:
                dreamgirl "Не понимаешь, но за хвостик подёргать её совсем не испугался."
            else:
                dreamgirl "Не понимаешь, но лезть с ней в странные бункеры не испугался."
            th "Ты мне это до конца жизни вспоминать будешь?"
            dreamgirl "Почему нет, раз уж ты себе титул местного демиурга решился примерить."
            th "Но ведь так похоже! Как будто я во сне каком-то своём - где всё идеальное."
            dreamgirl "Идеальное, говоришь? Так что же ты бежишь от этого идеального, поджав хвост, когда тебе всё на блюдечке несут да с голубой каёмочкой?"
            dreamgirl "В своём мире был не пойми кем и даже тут в роли самоназначенного божка - чуть что прячешь голову в песок."
            th "Да потому что я не понимаю как оказался здесь!"
            dreamgirl "Но создал этот мир всё равно ты?"
            "Я окончательно запутался и закрыл глаза от напряжения."
            "В голове прокручивались снова и снова все события последних дней..."
            "Моё пробуждение в автобусе..."
            "... обморок в домике вожатой..."
            "... Юля около столовой..."
            "... мой поход в бункер..."
            "... и наконец, эта истерика Слави."
            "Голова была уже готова взорваться от новой попытки найти хоть сколько-то правдоподобный ответ. В конце концов, я не выдержал."
            me "Ааа! Как я устал от этих проклятых загадок!"
            me "Я понимаю только одно - я здесь лишн..."
            "Мой возглас прервался на полуслове. Какая-то большая вспышка мелькнула на долю секунды перед глазами и сознание отключилось."
            if alt_uvao_D5_hentai:
                jump alt_uvao_ending_neutral_bad
            else:
                jump alt_uvao_ending_neutral
        "Слишком всё это странно.":
            "Мне всегда было тяжело смотреть, как рядом кто-то плачет - это что-то на уровне инстинктов, которые мне так и не удалось изжить за годы отчуждения от общества."
            "Но версия Слави выглядела откровенно бредовой, чтобы в неё верить."
            th "Быть может она и не врёт, что ничего не помнит, но оставлять её в таком состоянии опасно. Мало ли какую глупость ещё себе выдумает?"
            dreamgirl "Ты её с кем-то другим не попутал? Когда человека прижмут, каким бы он не был, он очень на многое способен."
            dreamgirl "С этой буйнопомешанной нужно что-то делать и быстро. Пока она за тобой с дубиной в поисках виноватого во всех грехах гоняться не начала."
            th "Думаешь и до такого дойдёт?"
            dreamgirl "Это врядли. Но решать что-то нужно и побыстрее."
            if alt_uvao_D6_CS_vetrov:
                menu:
                    "Мне поможет Виола.":
                        th "Уж кто-кто, а медсестра скорее всего сможет найти нормальное объяснение всем этим славиным причудам и провалам в памяти."
                        th "На худой конец таблеточками успокоительными накормит да спать уложит."
                        dreamgirl "Как ты эту, психическую, дотащишь туда? Она же сказала, что к врачам ни ногой."
                        th "Придётся подыграть ей, а дальше пусть Виола сама разбирается."
                        "Славя по-прежнему плакала навзрыд рядом со мной: даже удивительно, что это нисколько не помешало моим рассуждениям."
                        "Привести её хоть сколько-то в чувства сейчас было главной задачей - я взял её за локти и, привлекая внимание, начал медленно говорить:"
                        me "Славя, успокойся. Ничего страшного не случилось пока."
                        "Она наконец убрала руки и показала мне свои красные зарёванные глаза."
                        sl "Ч-что?"
                        me "Я хочу тебе помочь."
                        "Её по прежнему заметно трясло, но слёзы прекратились."
                        sl "Ты сделаешь м-меня настоящей?"
                        me "Я не знаю, как это работает, Славя, но знаю, кто нам сможет помочь и всё объяснить."
                        me "Ты говорила, что подслушала вчера Виолетту, так?"
                        sl "Д-да."
                        me "Значит, она хорошо понимает, что нужно делать, чтобы ты стала настоящей. Ты же хочешь этого?"
                        sl "Конечно. М-мы пойдём сейчас к ней?"
                        "Я изобразил на лице самую добрую улыбку какую только мог."
                        th "Не хватало только, чтобы сейчас она опять сбежала."
                        me "Мы пойдём и она поможет. Ты ведь веришь мне?"
                        sl "Верю..."
                        "Я аккуратно обхватил её за плечи, прижал к себе и неспешно повёл в сторону медпункта."
                        jump alt_day6_uvao_interrogation_sl_cs
                        #Далее проводим Славю к Виоле, где Виола найдёт в Славя-версии нестыковки (а именно, прокол с попаданцем и тем, что Славя напрочь забыла инструктаж о них). Прибежит ОД перепуганная, т.к. Мику ей рассказала что Семён куда-то увёл Славю и понеслось...                                    
                    "Попытаться успокоить Славю.":
                        pass
            else:
                pass
            "От её плача мне в голову не приходило ни одной стоящей мысли, потому мне ничего не оставалась как, несколько помявшись, неуверенно подойти и обнять Славю."
            dreamgirl "А ты неплох. Сейчас лучшим успокоительным будет пара хороших развратно-поступательных."
            dreamgirl "Погладь её где нужно, пообещай исполнения всех желаний и она твоя!"
            "Я смутился - Славя была так близко, её кожа была горячей, а от учащенного дыхания так соблазнительно вздымалась её грудь."
            th "Нужно успокоиться... Нужно успокоиться..."
            "Несмотря на всю опасность, что могла таить Славя, я почувствовал какой-то прилив сил и осторожно погладил её по голове."
            me "Славя, Славичка. Ты меня слышишь?"
            "Не ответив, она доверительно уткнулась носом в мою грудь и прижалась чуть плотнее."
            me "Успокойся. Я никуда не сбегу и ничего плохого тебе не сделаю."
            me "Я не знаю, почему ты так решила, но для меня ты самая настоящая."
            sl "Но я ведь ничего не помню."
            "Тихим успокаивающим тоном я продолжил:"
            me "Расскажи мне про то, что помнишь. Ты ведь уже третью неделю здесь?"
            sl "Д-да."
            "Подняв наконец голову, она мутным взгядом посмотрела на меня своими зарёванными раскрасневшимися глазами."
            sl "Я помню, к-как мы выходили из автобуса."
            me "Расскажи мне об этом. Там было что-то забавное?"
            sl "Ты же в-видел сегодняшнее платье Мику?"
            me "Видел."
            sl "Она в нём приехала и не хотела сменять на пионерскую форму... Ольга тогда её в домик даже запереть грозилась... пока не переоделась..."
            sl "Смешно было..."
            "Славя опять замолчала. Пусть медленно, но она всё-таки успокаивалась."
            sl "Я ведь выступать сейчас на концерте должна... Плохо подводить Ольгу."
            "Оказалось, что наша активисточка могла отличиться даже здесь."
            me "У тебя был номер?"
            sl "Да... Я песню должна была петь."
            me "А какую?"
            sl "Русскую, народную... Мне мама на родительский день костюм привезла, жалко не надену такую красоту."
            dreamgirl "Что-то показания трещат по швам. Она ведь говорила, что ничего не помнит кроме лагеря."
            me "Мама?"
            "Славя удивлённо посмотрела на меня."
            sl "Д-да. А ч-что?"
            me "Ты же не помнишь..."
            sl "Ой..."
            "Всё с таким трудом обретённое спокойствие вновь было потеряно, и Славя снова громко зарыдала."
            sl "Да что я за дура-то такая... Истеричка сумасшедш-шая... бешеная..."
            me "Славя, прекрати... Не плачь..."
            me "Ну забегалась - работаешь ведь много - голову напекло пока подметала. С кем не бывает..."
            me "Славя?.."
            "Она вновь ушла в себя и прекратила обращать внимание на внешние раздражители."
            dreamgirl "Молодец, эффективно успокоил. Теперь её до вечера из этого состояния не вытащишь."
            dreamgirl "Ты её даже до медпункта за успокоительным не доведёшь - она же последние силы сейчас потеряет."
            th "Опять что-то решать. Как же я устал."
            dreamgirl "Жить овощем у двадцатичетырёхдюймовика было куда интереснее, да?"
            dreamgirl "Сам довёл человека - сам и расхлёбывай."
            menu:
                    "Мне поможет Ольга.":
                        #TODO-текст
                        #Говорим Славе, что сейчас вернёмся и просим подождать.
                        #Идём сами к Оленьке, которая уже на эстраде детей рассаживает.
                        #Получаем втык, что так поздно явились.
                        #Просим отойти и рассказываем про багло в голове Слави.
                        #Ольга шокиерт. Поручает физруку сидеть с детьми на концерте и быстро-быстро с ГГ к Славе.
                        #Под белы рученьки вдвоём ведём Славю к Виоле.
                        jump alt_day6_uvao_aidpost_sl_concert
                        #Приводим Славю к Виоле. ОД куражится, что всё закрыть к чертям и прочее, т.к. второй случай помешательства за 2 дня и т.д.
                    "Лучше сбежать пока не очухалась.":
                        #TODO-текст
                        #Делает улыбочку и говорит - ну вот всё же хорошо.
                        #Ты настоящая. Пора на концерт.
                        #Славя отправляется подавленная на концерт, не понимая ничего.
                        #ГГ думает.
                        #Нужно понять что творится в этом легере, а лечение симптоматики не приводит к реальному излечению.
                        #Стараясь не привлекать внимание сваливает с эстрады.
                        #Идут размышлизмы о причинах сего чуднОго явления.
                        #Белена, лебеда - нет?
                        #Крокодил, дезоморфин, барбитураты - нет!
                        #Пришельцы, рептилоиды, госдеп - нет.
                        #Кошочка!!!!
                            if alt_uvao_D6_CS_vetrov:
                                menu:
                                    "Пойти к Виоле.":
                                        jump alt_day6_uvao_enquiry_cs
                                        #Приходим к Виоле и намёками-намёками пытаемся прознать о влиянии Кошочки. Виола бесится и требует выкладывать правду. Приводим Славю вместе с ОД.
                                    "Искать Юлю.":
                                        jump alt_day6_uvao_seek_uv
                                        #Шарашимся по лагерю до дискотеки и ищем Юлю. Встречаем Ульяну, которая пытается затащить на дискотеку, но отнекиваемся.
